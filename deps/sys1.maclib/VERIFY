         MACRO                                                          00250000
&NAME    VERIFY &RPL=,&ACTION=                                     @LSC 00278500
.*                                                                      00307000
.* $MAC(VERIFY):                                                        00310500
.*                                                                      00314000
.*PROPRIETARY V3 STATEMENT                                              00316100
.*LICENSED MATERIALS - PROPERTY OF IBM                                  00318200
.*"RESTRICTED MATERIALS OF IBM"                                         00320300
.*5695-DF1                                                              00322400
.*COPYRIGHT 1985, 2011  IBM CORP.                                  @L1C 00325200
.*END PROPRIETARY V3 STATEMENT                                          00328000
.*                                                                      00331500
.* STATUS = z/OS 1.13 DFSMS                                        @L1C 00335000
.*                                                                      00338500
.* $LS=DFP,HDP2210,,STLAYS: MVS/XA DFP RELEASE 2.1.0  7/13/84           00339900
.* $L1=64BIT,JDZ1D10,20101116,SVLWER: 64 bit message               @L1A 00341300
         GBLC  &SYSAM64    AMODE64=YES set by SYSSTATE macro       @L1A 00342700
         GBLC  &SYSASCE    ASCENV=AR or ANY set by SYSSTATE        @L1A 00344100
         SYSSTATE TEST     Test how macros are to expand           @L1A 00345500
         AIF   ('&SYSAM64' EQ 'YES').E0    GO IF AMODE64=YES       @L1A 00346900
         AIF   (('&SYSASCE' NE '') AND ('&SYSASCE' NE 'P')).E1     @L1A 00348300
         AIF   ('&RPL' NE '').OK1                                  @LSC 00350000
         IHBERMAC 24                                                    00400000
         MEXIT                                                          00450000
.OK1     ANOP                                                      @LSC 00483300
&NAME    DS    0H                                                  @LSA 00516600
         AIF   ('&RPL'(1,1) EQ '(').RPLR                                00550000
         LA    1,&RPL                   LOAD RPL ADDRESS           @LSC 00585000
         AGO   .OK2                                                @LSC 00620000
.RPLR    AIF   ('&RPL' EQ '(1)').OK2    SKIP LOAD IF RPL PTR IN R1 @LSC 00655000
         LR    1,&RPL(1)                LOAD RPL ADDRESS           @LSC 00690000
.OK2     AIF   ('&ACTION' EQ '').OK3    ACTION NOT SPECIFIED       @LSA 00732000
         AIF   ('&ACTION'(1,7) EQ 'REFRESH').CCT    ACTION=REFRESH @LSA 00774000
         MNOTE 12,' ACTION KEYWORD MUST HAVE ''REFRESH'' AS PARAMETER.' 00816000
.*                                      ACTION NOT REFRESH         @LSA 00858000
         MEXIT                                                          00900000
.CCT     AIF   (K'&ACTION EQ 7).OK4     COUNT IS 7                 @LSA 00935000
         MNOTE 12,' ACTION KEYWORD MUST HAVE ''REFRESH'' AS PARAMETER.' 00970000
.*                                      ACTION NOT REFRESH         @LSA 01005000
         MEXIT                                                          01040000
.OK3     LA    0,6                      VERIFY REQUEST ONLY        @LSC 01075000
         AGO   .CONT                    SKIP OVER                  &LSA 01110000
.OK4     LA    0,20                     VERIFY REFRESH REQUEST     @LSA 01145000
.CONT    L     15,24(1)                 LOAD ACB ADDR FROM RPL+24  @LSC 01180000
         L     15,8(15)                 LOAD VSAM INTERFACE RTN ADR@LSC 01215000
         BASR  14,15                    LINK TO INTERFACE ROUTINE       01265000
         MEXIT                                                          01315000
.E0      IGGERMAC 203,VERIFY   THE VERIFY MACRO DOES NOT SUPPORT   @L1A 01365000
         MEXIT                   SYSSTATE AMODE64=YES              @L1A 01415000
.E1      IGGERMAC 204,VERIFY   THE VERIFY MACRO DOES NOT SUPPORT   @L1A 01465000
         MEND                    SYSSTATE ASCENV=AR OR ANY         @L1A 01515000
