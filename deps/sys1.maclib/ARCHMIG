         MACRO                                                          00001000
.********************************************************************** 00002000
.*                                                                    * 00003000
.* $MAC(ARCHMIG) COMP(SERVICE) PROD(DFHSM):                           * 00004000
.* MACRO NAME = ARCHMIG                                               * 00005000
.* FUNC = SERVICE                                                     * 00006000
.* SUBFUNC = MWE                                                      * 00007000
.*                                                                    * 00008000
.* DESCRIPTIVE NAME = REQUEST DFHSM MIGRATE A DATA SET.               * 00009000
.*                                                                    * 00010000
.********************************************************************** 00011000
.*PROPRIETARY V3 STATEMENT                                            * 00012000
.*LICENSED MATERIALS - PROPERTY OF IBM                                * 00013000
.*"RESTRICTED MATERIALS OF IBM"                                       * 00014000
.*5650-ZOS                                                            * 00015000
.*COPYRIGHT IBM CORP. 1989, 2015                                      * 00016000
.*END PROPRIETARY V3 STATEMENT                                        * 00017000
.********************************************************************** 00018000
.*                                                                    * 00019000
.* STATUS = VERSION 2, RELEASE 5, LEVEL 0                             * 00020000
.*                                                                    * 00021000
.* FUNCTION = PERFORM DFHSM SVC 109 TO MIGRATE THE SPECIFIED          * 00022000
.*            DATA SET.                                               * 00023000
.*                                                                    * 00024000
.* NOTES = SEE BELOW                                                  * 00025000
.*                                                                    * 00026000
.*   DEPENDENCIES = NONE                                              * 00027000
.*                                                                    * 00028000
.*   RESTRICTIONS = NONE                                              * 00029000
.*                                                                    * 00030000
.*   REGISTER CONVENTIONS =                                           * 00031000
.*                                                                    * 00032000
.*            REGISTER 13 MUST POINT TO A STANDARD SAVE AREA          * 00033000
.*            WHEN MACRO IS ISSUED.                                   * 00034000
.*                                                                    * 00035000
.*            REGISTERS 0, 1, 14 AND 15 WILL BE DESTROYED.            * 00036000
.*                                                                    * 00037000
.*   PATCH LABEL = NONE                                               * 00038000
.*                                                                    * 00039000
.* MODULE TYPE = MACRO                                                * 00040000
.*                                                                    * 00041000
.*   PROCESSOR = BAL                                                  * 00042000
.*                                                                    * 00043000
.* INPUT =                                                            * 00044000
.*          DSN      (REQUIRED) SPECIFY THE ADDRESS OF A 44-BYTE AREA * 00045000
.*                   CONTAINING THE NAME OF THE DATA SET TO BE ACTED  * 00046000
.*                   ON, LEFT JUSTIFIED AND PADDED WITH BLANKS.       * 00047000
.*                                                                    * 00048000
.*          WAIT     (OPTIONAL) SPECIFY YES!NO WHETHER APPLICATION    * 00049000
.*                   DESIRES TO WAIT UNTIL DFHSM HAS PROCESSED THIS   * 00050000
.*                   REQUEST.  DEFAULT FOR THIS PARAMETER IS NO.      * 00051000
.*                                                                    * 00052000
.*          MIGLVL   (OPTIONAL) SPECIFY ML1!ML2 FOR THE MIGRATION     * 00053000
.*                   LEVEL TO MIGRATE THE DATA SET.  DEFAULT FOR      * 00054000
.*                   THIS PARAMETER IS ML1.                           * 00055000
.*                                                                    * 00056000
.*          FORCML1 (OPTIONAL) SPECIFY YES!NO TO INDICATE IF      @01A* 00057000
.*                  ML1DAYS(0) SHOULD BE OVERRIDDEN FOR SMS       @01A* 00058000
.*                  MANAGED DATA SETS. IF YES, THE DATA SET WILL  @01A* 00059000
.*                  MIGRATE TO ML1 WITHOUT COMPACTION             @01A* 00060000
.*                                                                    * 00061000
.*          TEST     (OPTIONAL) SPECIFY YES!NO WHETHER MACRO IN TEST  * 00062000
.*                   MODE.  TEST=YES DENOTES THAT THE MWE SHOULD BE   * 00063000
.*                   BUILT BUT THAT NO DFHSM SVC IS ISSUED.  DEFAULT  * 00064000
.*                   FOR THIS PARAMETER IS NO.                        * 00065000
.*                                                                    * 00066000
.*          SNAPDCB  (OPTIONAL) SPECIFY THE ADDRESS OF AN OPEN DCB    * 00067000
.*                   USED FOR SNAP PROCESSING OF THE MWE AREA UPON    * 00068000
.*                   RETURN FROM DFHSM SVC PROCESSING.                * 00069000
.*                                                                    * 00070000
.*          WKAREA   (REQUIRED) SPECIFY THE ADDRESS OF A 200 BYTE     * 00071000
.*                   WORK AREA TO CONTAIN INFORMATION SPECIFIED IN    * 00072000
.*                   THIS MACRO.                                      * 00073000
.*                                                                    * 00074000
.*          TRANSITION (OPTIONAL) SPECIFY YES!NO WHETHER THE DATA @TXA* 00075000
.*                    SET SHOULD GO THROUGH TRANSITION PROCESSING @TXA* 00076000
.*                                                                    * 00077000
.*          MOVE (OPTIONAL) SPECIFY YES!NO WHETHER THE DATA     1P@TXA* 00078000
.*                    SET SHOULD GO THROUGH MOVE PROCESSING     1P@TXA* 00079000
.*                                                                    * 00080000
.*          SMSAREA (OPTIONAL) SPECIFY THE ADDRESS OF DATA AREA 2P@TXA* 00081000
.*                    TO CONTAIN THE OPTIONAL SMS PARAMETERS    2P@TXA* 00082000
.*                    FOR CLASS TRANSITION PROCESSING           2P@TXA* 00083000
.*                                                                    * 00084000
.*          CLOUD   (OPTIONAL) SPECIFY THE ADDRESS OF DATA AREA   @CWA* 03347000
.*                  TO CONTAIN THE NAME OF THE CLOUD              @CWA* 03348000
.*                                                                    * 03349000
.* OUTPUT =   REGISTER 15 WILL CONTAIN MWE RETURN CODE                * 00085000
.*            REGISTER 0  WILL CONTAIN MWE REASON CODE                * 00086000
.*                                                                    * 00087000
.*            INVALID REQUESTS NOT SENT TO DFHSM WILL HAVE            * 00088000
.*            THE FOLLOWING RETURN CODES:                             * 00089000
.*                                                                    * 00090000
.*            402 - LOCATE ERROR ON MIGRATE REQUEST                   * 00091000
.*            806 - LINK FAILURE - UNABLE TO LINK TO ARCGIVER         * 00092000
.*                                                                    * 00093000
.* CHANGE ACTIVITY =                                                  * 00094000
.* $K0=HSMKD,250,090888,THP5: PROGRAMMABLE DFHSM REQUEST          @KDA* 00095000
.*                            PROCESSOR                           @KDA* 00096000
.* $01=OW18064,130,011996,THY2: ALLOW SPECIFICATION OF FORCML1    @01A* 00097000
.* $TX=HSMTX,220,080113,THI9-9: Storage Tiers Phase 2             @TXA* 00098000
.* $TX-HSMTX,220,060414,THI9-9: Storage Tiers Phase 2           1P@TXA* 00099000
.*                          MOVE parameter of MIGRATE command   1P@TXA* 00100000
.* $TX-OA47700,230,040415,THI9-9: Storage Tiers Phase 2         2P@TXA* 00101000
.*                            Add the support of optional SMS   2P@TXA* 00102000
.*                            parameters of MIGRATE command     2P@TXA* 00103000
.*                            for transition processing         2P@TXA* 00104000
.* $CW=OA50667,230,070716,THI9-9: Cloud Support                   @CWA* 03998000
.********************************************************************** 00105000
&NAME    ARCHMIG &DSN=,&WAIT=NO,&MIGLVL=ML1,&TEST=NO,                  X00106000
               &WKAREA=,&SNAPDCB=,&FORCML1=NO,&TRANSITION=NO,   1P@TXC X00107000
               &MOVE=NO,&SMSAREA=,&CLOUD=                         @CWC  04139960
         LCLA  &A                                                       00109000
         LCLB  &ERROR                                                   00110000
         LCLC  &H                                                       00111000
         LCLC  &L1                                                      00112000
         LCLC  &L2                                                      00113000
&L1      SETC  'ARC'.'&SYSNDX'.'A'                                      00114000
&L2      SETC  'ARC'.'&SYSNDX'.'B'                                      00115000
.*                                                                      00116000
.* CHECK THE PARAMETERS SPECIFIED WITH THE HMIGRATE FUNCTION            00117000
.*                                                                      00118000
         AIF   ('&DSN' NE '').MIG1                                      00119000
         MNOTE 8,'REQUIRED PARAMETER, DSN, NOT SPECIFIED'               00120000
&ERROR   SETB  1                                                        00121000
.MIG1    ANOP                                                           00122000
         AIF   ('&WKAREA' NE '').MIG11                            @CWC  04869990
         MNOTE 8,'REQUIRED PARAMETER, WKAREA, IS NOT SPECIFIED'         00124000
&ERROR   SETB  1                                                  @TXA  00125000
.MIG11   ANOP                                                     @CWA  04902070
         AIF   ('&CLOUD' EQ '').MIG2                              @CWA  04902140
         AIF   ('&TRANSITION' EQ 'YES').MIG12                     @CWA  04902210
         AGO   .MIG13                                             @CWA  04902280
.MIG12   MNOTE 8,'CLOUD IS MUTUALLY EXCLUSIVE WITH TRANSITION=YES PARAMX04902350
               ETER'                                              @CWA  04902420
&ERROR   SETB  1                                                  @CWA  04902490
.MIG13   AIF   ('&MOVE' EQ 'YES').MIG14                           @CWA  04902560
         AGO   .MIG15                                             @CWA  04902630
.MIG14   MNOTE 8,'CLOUD IS MUTUALLY EXCLUSIVE WITH MOVE=YES PARAMERER'  04902700
*                                                                 @CWA  04902770
&ERROR   SETB  1                                                  @CWA  04902840
.MIG15   AIF   ('&FORCML1' EQ 'YES').MIG16                        @CWA  04902910
         AGO   .MIG17                                             @CWA  04902980
.MIG16   MNOTE 8,'CLOUD IS MUTUALLY EXCLUSIVE WITH FORCML1=YES PARAMETEX04903050
               R'                                                 @CWA  04903120
&ERROR   SETB  1                                                  @CWA  04903190
.MIG17   AIF   ('&MIGLVL' NE 'ML1').MIG18                         @CWA  04903260
         AGO   .MIG19                                             @CWA  04903330
.MIG18   MNOTE 8,'CLOUD IS MUTUALLY EXCLUSIVE WITH MIGLVL PARAMETER IS X04903400
               NOT EQUAL TO ML1'                                  @CWA  04903470
&ERROR   SETB  1                                                  @CWA  04903540
.MIG19   AIF   ('&SMSAREA' EQ '').MIG9                            @CWA  04903610
         MNOTE 8,'CLOUD IS MUTUALLY EXCLUSIVE WITH SMSAREA PARAMETER'   04903680
*                                                                 @CWA  04903750
&ERROR   SETB  1                                                  @CWA  04903820
.MIG2    ANOP                                                     @TXA  00126000
         AIF   ('&TRANSITION' EQ '').MIG6                       1P@TXC  00127000
         AIF   ('&TRANSITION' EQ 'NO').MIG6                     1P@TXC  00128000
         AIF   ('&TRANSITION' EQ 'YES').MIG3                      @TXA  00129000
         MNOTE 8,'TRANSITION PARAMETER SPECIFIED INCORRECTLY'   1P@TXC  00130000
&ERROR   SETB  1                                                1P@TXA  00131000
.MIG3    ANOP                                                   1P@TXA  00132000
         AIF   ('&MOVE' NE 'YES').MIG4                          1P@TXA  00133000
         MNOTE 8,'TRANSITION AND MOVE ARE MUTUALLY EXCLUSIVE PARAMETERSX00134000
               '                                                1P@TXA  00135000
&ERROR   SETB  1                                                  @TXA  00136000
.MIG4    ANOP                                                     @TXA  00137000
         AIF   ('&MIGLVL' EQ 'ML2').MIG5                        1P@TXA  00138000
         AIF   ('&FORCML1' NE 'YES').MIG6                       1P@TXA  00139000
.MIG5    ANOP                                                   1P@TXA  00140000
         MNOTE 8,'TRANSITION IS MUTUALLY EXCLUSIVE WITH MIGLVL=ML2 AND X00141000
               FORCML1=YES PARAMETERS'                            @TXA  00142000
&ERROR   SETB  1                                                        00143000
.MIG6    ANOP                                                   1P@TXA  00144000
         AIF   ('&MOVE' EQ '').NOERR                            1P@TXA  00145000
         AIF   ('&MOVE' EQ 'NO').NOERR                          1P@TXA  00146000
         AIF   ('&MOVE' EQ 'YES').MIG7                          1P@TXA  00147000
         MNOTE 8,'MOVE PARAMETER SPECIFIED INCORRECTLY'         1P@TXA  00148000
&ERROR   SETB  1                                                1P@TXA  00149000
.MIG7    ANOP                                                   1P@TXA  00150000
         AIF   ('&MIGLVL' EQ 'ML2').MIG8                        1P@TXA  00151000
         AIF   ('&FORCML1' NE 'YES').NOERR                      1P@TXA  00152000
.MIG8    ANOP                                                   1P@TXA  00153000
         MNOTE 8,'MOVE IS MUTUALLY EXCLUSIVE WITH MIGLVL=ML2 AND FORCMLX00154000
               1=YES PARAMETERS'                                1P@TXA  00155000
&ERROR   SETB  1                                                1P@TXA  00156000
.NOERR   ANOP                                                   2P@TXC  00157000
         AIF   ('&SMSAREA' EQ '').MIG9                          2P@TXA  00158000
         AIF   ('&TRANSITION' EQ 'YES').MIG9                    2P@TXA  00159000
         MNOTE 8,'SMSAREA PARAMETER CAN BE ONLY SPECIFIED IF TRANSITIONX00160000
               =YES'                                            2P@TXA  00161000
&ERROR   SETB  1                                                2P@TXA  00162000
.MIG9    AIF   (NOT &ERROR).BUILD                               2P@TXA  00163000
         MEXIT                                                          00164000
.*                                                                      00165000
.* AT THIS POINT ALL THE PARAMETERS HAVE BEEN PROPERLY SPECIFIED.       00166000
.* NOW BUILD THE CONTROL BLOCK AND CALL THE PROCESSING ROUTINE.         00167000
.*                                                                      00168000
.BUILD   ANOP                                                           00169000
         AIF   ('&WKAREA'(1,1) EQ '(').WREG                             00170000
&NAME    LA    15,&WKAREA                                               00171000
         AGO   .CLRWKA                                                  00172000
.WREG    ANOP                                                           00173000
&A       SETA  K'&WKAREA-2                                              00174000
&H       SETC  '&WKAREA'(2,&A)                                          00175000
&NAME    LR    15,&H                                                    00176000
.CLRWKA  ANOP                                                           00177000
         XC    0(200,15),0(15)     CLEAR THE WORK AREA                  00178000
         B     *+12                BRANCH AROUND IDENTIFIER             00179000
         DC    CL8'DFHSMREQ'                                            00180000
         MVC   0(8,15),*-8         SET THE IDENTIFIER                   00181000
         B     *+12                BRANCH AROUND FUNCTION NAME          00182000
         DC    CL8'HMIGRATE'       FUNCTION NAME                        00183000
         MVC   8(8,15),*-8                                              00184000
.XWAIT   AIF   ('&WAIT' NE 'YES').XLVL                                  00185000
         OI    16(15),X'40'        WAIT WAS SPECIFIED                   00186000
.XLVL    AIF   ('&MIGLVL' NE 'ML2').FORCE                         @01C  00187000
         OI    16(15),X'10'        ML2 WAS SPECIFIED                    00188000
.FORCE   AIF   ('&FORCML1' NE 'YES').XTEST                        @01A  00189000
         OI    17(15),X'10'        FORCML1 WAS SPECIFIED          @01A  00190000
.XTEST   AIF   ('&TEST' NE 'YES').XTRNS                           @TXC  00191000
         OI    16(15),X'02'        TEST WAS SPECIFIED                   00192000
.XTRNS   AIF   ('&TRANSITION' NE 'YES').XMOVE                   1P@TXC  00193000
         OI    19(15),X'08'        TRANSITION WAS SPECIFIED       @TXA  00194000
.XMOVE   AIF   ('&MOVE' NE 'YES').XCLOUD                          @CWC  06384990
         OI    19(15),X'04'        MOVE WAS SPECIFIED           1P@TXA  00196000
.XCLOUD  AIF   ('&CLOUD' EQ '').XDSN                              @CWA  06393000
         OI    19(15),X'02'        CLOUD WAS SPECIFIED            @CWA  06396000
.XDSN    ANOP                                                           00197000
         AIF   ('&DSN'(1,1) EQ '(').DREG                                00198000
         LA    1,&DSN              GET ADDR OF DSN                      00199000
         ST    1,20(15)            STORE IN PARM LIST                   00200000
         AGO   .XSNAP                                                   00201000
.DREG    ANOP                                                           00202000
&A       SETA  K'&DSN-2                                                 00203000
&H       SETC  '&DSN'(2,&A)                                             00204000
         ST    &H,20(15)           STORE ADDR OF DSN                    00205000
.XSNAP   ANOP                                                           00206000
         AIF   ('&SNAPDCB' EQ '').XSMS                          2P@TXC  00207000
         AIF   ('&SNAPDCB'(1,1) EQ '(').SREG                            00208000
         LA    1,&SNAPDCB          GET ADDRESS OF SNAP DCB              00209000
         ST    1,188(15)           STORE IN PARM LIST                   00210000
         AGO   .XSMS                                            2P@TXC  00211000
.SREG    ANOP                                                           00212000
&A       SETA  K'&SNAPDCB-2                                             00213000
&H       SETC  '&SNAPDCB'(2,&A)                                         00214000
         ST    &H,188(15)          STORE DCB ADDRESS IN PARM LIST       00215000
.XSMS    ANOP                                                   2P@TXA  00216000
         AIF   ('&SMSAREA' EQ '').CLOUD                           @CWC  07309990
         AIF   ('&SMSAREA'(1,1) EQ '(').SREG1                   2P@TXA  00218000
         LA    1,&SMSAREA          GET ADDRESS OF SMS AREA      2P@TXA  00219000
         ST    1,96(15)            STORE IN PARM LIST           2P@TXA  00220000
         AGO   .GIVER                                           2P@TXA  00221000
.SREG1   ANOP                                                   2P@TXA  00222000
&A       SETA  K'&SMSAREA-2                                     2P@TXA  00223000
&H       SETC  '&SMSAREA'(2,&A)                                 2P@TXA  00224000
         ST    &H,96(15)           STORE SMS ADDR IN PARM LIST  2P@TXA  00225000
         AGO   .GIVER                                             @CWA  07340800
.CLOUD   ANOP                                                     @CWA  07341600
         AIF   ('&CLOUD' EQ '').GIVER                             @CWA  07342400
         AIF   ('&CLOUD'(1,1) EQ '(').SREG2                       @CWA  07343200
         LA    1,&CLOUD          GET ADDRESS OF CLOUD AREA        @CWA  07344000
         ST    1,100(15)         STORE IN PARM LIST               @CWA  07344800
         AGO   .GIVER                                             @CWA  07345600
.SREG2   ANOP                                                     @CWA  07346400
&A       SETA  K'&CLOUD-2                                         @CWA  07347200
&H       SETC  '&CLOUD'(2,&A)                                     @CWA  07348000
         ST    &H,100(15)        STORE CLOUD ADDR IN PARM LIST    @CWA  07348800
.GIVER   ANOP                                                           00226000
         LR    1,15                PARAMETER ADDRESS TO REG 1           00227000
         LINK  EP=ARCGIVER,ERRET=&L1                                    00228000
         L     15,192(1)           LOAD RETURN CODE                     00229000
         L     0,196(1)            LOAD REASON CODE                     00230000
         B     &L2                                                      00231000
&L1      LA    15,806              INDICATE LINK FAILED                 00232000
&L2      EQU   *                                                        00233000
         MEXIT                                                          00234000
         MEND                                                           00235000
