         MACRO                                                                  
         IST6MCRS &CONVID=,&CONMODE=,&BRANCH=,&ECB=,&EXIT=,&OPTCD=,    X        
               &INVALP1=,&INVALP2=,&CONXMOD=,&QUALIFY=,&VTRINA=,       X        
               &VTRINL=,&VTROUTA=,&VTROUTL=              @I1C @L1C @D1C         
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCRS                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: CONTROL = RESETRCV PROCESSOR                */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1994.       */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */         
.* /*      CONTROL=RESETRCV PARAMETER ON THE APPCCMD MACRO.          */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS:                                              */         
.* /*      IST6MCUA,IST6MCUP,IST6MCUC                                */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: NONE                                     */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*    05/27/86 - ZERO OUT THE QUALIFY FIELD               @P030472*/         
.* /*                                                                */         
.* /* $MAC(IST6MCRS),COMP(API),PROD(VTAM): LU6.2 FUNCTION REQUEST    */         
.* /*                                                                */         
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */         
.* /* ---- -------- ------- ------ ------  ------------------------- */         
.* /* $Q1= P064461  HVT3303 890116 460025: INVALID LIST SPLIT        */         
.* /* $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION          */         
.* /* $I1= FF@@@MST APPCFDX 911118 879315: APPC Full-Duplex support  */         
.* /* $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services     */         
.* /* $Y1= P114809  APPCRR  940518 576146: DCE Security Services     */         
.* /* $L2= KA@DR006 APPCRR  940713 576146: Vector Format             */         
.* /* $D1= ID@@@MST FPAPI   950310 143576: Fast Path API for APPC    */         
.* /*                                                                */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
.* A-000000-999999                                             @R495810         
.*                                                                              
         GBLB  &ISTGEN                                                          
         GBLB  &ISTRS                                                           
         AIF   (&ISTRS EQ 1).RSINVKW                                            
*                                                                               
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                              
*                                                                               
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"               
*                                                                               
*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1993.                      
*             ALL RIGHTS RESERVED.                                              
*                                                                               
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                         
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY                      
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                          
*                                                                               
*             SEE COPYRIGHT INSTRUCTIONS.                                       
*                                                                               
&ISTRS   SETB  1                                                                
.RSINVKW ANOP                                                                   
         IST6MCUC RESETRCV,CONTROL,INVALKW=&INVALP1                             
         IST6MCUC RESETRCV,CONTROL,INVALKW=&INVALP2                             
.*                                             CHECK INVALID APPCCMD            
.RSID    ANOP                                                                   
         AIF   ('&CONVID' EQ '').RSVECT                            @L1C         
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X        
               KW=CONVID                                                        
.*                                             PUT CONVID IN APPC AREA          
.RSVECT  ANOP                          ENSURE BOTH PARMS ARE CODED @L1A         
.*                                     OR BOTH ARE NOT CODED       @L1A         
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).RSVOUT    @D1C         
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).RSERVI     @D1C         
         IST6MCUA KWVAL=&VTRINA,LOC=RPL6VAIA-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_ADDRESS                      @D1C         
.*                                     STORE VECTOR AREA INPUT ADDRESS          
.*                                                                 @L1A         
         IST6MCUA KWVAL=&VTRINL,LOC=RPL6VAIL-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2                 @D1C         
.*                                     STORE VECTOR AREA INPUT LENGTH           
.*                                                                 @L1A         
         AGO   .RSVOUT                                             @D1C         
.RSERVI  ANOP                                                      @L1A         
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C         
.RSVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED       @D1A         
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).RSCOMKW @D1A         
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).RSERVO   @D1A         
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X        
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                @D1A         
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X        
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                @D1A         
         AGO   .RSCOMKW                                            @D1A         
.RSERVO  ANOP                                                      @D1A         
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A         
.RSCOMKW ANOP                                                                   
         IST6MCUP CONMODE=&CONMODE,OPTCD=&OPTCD,ECB=&ECB,              X        
               EXIT=&EXIT,BRANCH=&BRANCH,CONXMOD=&CONXMOD,             X        
               CKCMODE=YES,        CHECK CONMODE & CONXMOD PARAMETERS  X        
               CKSYN=YES,          ASY/SYN IS VALID FOR OPTCD PARAMETERX        
               CKBUFF=NO           BUFFLST/NBUFFLST IS INVALID     @I1C         
.*                                 OPTCD PARAMETER                              
.RSQFY   ANOP                                                      @L1A         
         AIF ('&QUALIFY' EQ '').RSNQUAL                       @L1A @Y1C         
         AIF ('&QUALIFY' EQ 'NULL').RSNQUAL                        @L1A         
         IST6MCER 5,QUALIFY,&QUALIFY    INVALID QUALIFY VALUE      @L1A         
         AGO   .RSREQ                                              @L1A         
.RSNQUAL ANOP                                                      @L1A         
         AIF   (NOT &ISTGEN).CEXIT                                 @L1A         
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6NQUA                      @L1A         
.*                                     SET QUALIFY TO NULL         @L1A         
.RSREQ   ANOP                                                                   
         AIF   (NOT &ISTGEN).CEXIT                                              
         MVI   RPL6REQ-ISTRPL6X(14),RPL6RSRV                                    
*                                PUT RESETRCV REQUEST CODE IN APPC AREA         
.CEXIT   MEND                                                                   
