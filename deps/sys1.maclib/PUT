*%/*                                                               @L2A 00002000
*********************************************************************** 00004100
*PROPRIETARY V3 STATEMENT                                             * 00009200
*LICENSED MATERIALS - PROPERTY OF IBM                                 * 00014300
*"RESTRICTED MATERIALS OF IBM"                                        * 00019400
*5695-DF1                                                             * 00024500
*COPYRIGHT 1992, 2011 IBM CORP.                                       * 00030900
*END PROPRIETARY V3 STATEMENT                                         * 00037300
* FUNCTION: Write a logical record for QSAM, VSAM, VTAM or         @L3A 00039200
*      subsystem.                                                  @L3A 00041100
* STATUS = z/OS 1.13 DFSMS                                         @L3A 00043000
*$L1=31BIT,JDZ1110,,SJPLRK:  INVOCABLE IN 31-BIT MODE              @L1A 00043500
*$L2=PLX  ,JDZ1110,930930,SJPLMET: PL/X SUPPORT                    @L2A 00044000
*$L3=64BIT,JDZ1D10,20100303,SVLJH: 64 bit message                  @L3A 00044500
*********************************************************************** 00045100
         MACRO                                                          00050000
&NAME    PUT   &DCB,&AREA,&RPL=                                         00100000
         GBLC  &SYSAM64    AMODE64=YES set by SYSSTATE macro       @L3A 00240900
         GBLC  &SYSASCE    ASCENV=AR or ANY set by SYSSTATE macro  @L3A 00242600
         SYSSTATE TEST     Test how macros are to expand           @L3A 00244300
         AIF   ('&SYSAM64' EQ 'YES').E0    GO IF AMODE64=YES       @L3A 00246000
         AIF   (('&SYSASCE' NE '') AND ('&SYSASCE' NE 'P')).E1     @L3A 00247700
         AIF   ('&DCB' NE '').NJES      NON-JES MACRO EXPANSION         00250000
         AIF   ('&RPL' NE '').JES       JES MACRO EXPANSION             00300000
         IHBERMAC 06                                                    00350000
         MEXIT                                                          00400000
.NJES    ANOP                                                           00450000
&NAME    IHBINNRA &DCB,&AREA                                            00500000
         SLR   15,15               CLEAR REGISTER FOR ICM          @L1A 00537500
         ICM   15,7,49(1)          LOAD PUT ROUTINE ADDRESS        @L1C 00575000
         BASR  14,15               LINK TO PUT ROUTINE             @L3C 00612500
         MEXIT                                                          00650000
.JES     ANOP                                                           00700000
&NAME    DS    0H                                                       00750000
         AIF   ('&RPL'(1,1) EQ '(').RPLR                                00800000
         LA    1,&RPL              LOAD RPL ADDRESS                     00850000
         AGO   .AREG                                                    00900000
.RPLR    LR    1,&RPL(1)           LOAD RPL ADDRESS FROM REGISTER       00950000
.AREG    LA    0,X'01'             INDICATE PUT IN REGISTER             01000000
         L     15,24(1)            LOAD ACB ADDR FROM RPL               01050000
         L     15,8(,15)           LOAD VSAM INTERFACE MOD ADDR    @L3C 01100000
         BASR  14,15               LINK TO INTERFACE ROUTINE       @L3C 01150000
         MEXIT                                                     @L3A 01200000
.E0      IGGERMAC 203,PUT    THE PUT MACRO DOES NOT SUPPORT        @L3A 01250000
         MEXIT                 SYSSTATE AMODE64=YES                @L3A 01300000
.E1      IGGERMAC 204,PUT    THE PUT MACRO DOES NOT SUPPORT        @L3A 01350000
         MEXIT                 SYSSTATE ASCENV=AR OR ANY           @L3A 01400000
         MEND                                                           01500000
**/ PUT: MACRO KEYS(0:1(DCB_NAME,DCB_ADDR),0:1(AREA_NAME,      /*@L2A*/ 01550000
*   AREA_ADDR),0:1(RPL_NAME,RPL_ADDR),RECORD_ADDR,SYNAD_NAME); /*@L2A*/ 01600000
* ANS('? '!!MACLABEL!! ' PUTP '!!MACKEYS!!';') RESCAN SKIP;    /*@L2A*/ 01650000
*  %END PUT;                                                   /*@L2A*/ 01700000
