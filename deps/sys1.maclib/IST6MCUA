         MACRO                                                                  
         IST6MCUA &KWVAL=,&LOC=,&BASEREG=,&KW=,&LEN=               @L1C         
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCUA                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: ADDRESS-DATA PARAMETER PROCESSOR            */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1994.       */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      CALLED BY OTHER MACROS TO STORE A PARAMETER VALUE         */         
.* /*      IN APPROPRIATE LOCATION  IN RPL OR APPC AREA. THE         */         
.* /*      PARAMETER VALUES MAY BE IN THE FORM OF A SYMBOLIC         */         
.* /*      NAME OR REGISTER NOTATION.                                */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*     KWVAL   -- CONTAIN THE PARAMETER VALUE                     */         
.* /*     LOC     -- CONTAIN THE LOCATION (OFFSET) IN THE RPL OR     */         
.* /*                APPC AREA FOR THIS PARAMETER                    */         
.* /*     BASEREG -- BASE REGISTER FOR RPL OR APPC AREA ADDRESSING   */         
.* /*     KW      -- CONTAINS A CHARACTER STRING DESCRIBING THE      */         
.* /*                KEYWORD, USED TO RECOGNIZE SPECIAL HANDLING     */         
.* /*                REQUIRED BY KEYWORD TYPES AND IN GENERATED      */         
.* /*                COMMENTS.  LENGTH KEYWORDS (SUCH AS AREALEN,    */         
.* /*                RECLEN, VTRINL) MUST HAVE A CHARACTER STRING    */         
.* /*                VALUE ENDING IN "LENGTH" TO BE CORRECTLY        */         
.* /*                HANDLED IF THEIR SPECIFIED VALUE > X'FFF'.) @Y1C*/         
.* /*     LEN     -- LENGTH OF FIELD TO BE SET (DEFAULT IS 4)        */         
.* /*                (VALID LENGTHS ARE 4 AND 2)                 @L1A*/         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS: NONE                                         */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: IST6MCER                                 */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*                                                                */         
.* /*  $MAC(IST6MCUA),COMP(API),PROD(VTAM): ADDRESS-DATA PARAMETER   */         
.* /*                                       PROCESSOR                */         
.* /*                                                                */         
.* /*  FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS         */         
.* /*  ---- -------- ------- ------ ------  ----------------         */         
.* /*  $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION         */         
.* /*  $KF= S4N@@CLD HATAPPN 910629 692313: CP-CP Sessions           */         
.* /*  $L1= KA@DR006 APPCRR  940714 879315: Vector format            */         
.* /*  $Y1= P118546  FREEDOM 950405 879315: Handle length values     */         
.* /*                                       larger than X'FFF'       */         
.* /*                                                                */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
.* A-000000-999999                                             @R495810         
.*                                                                              
         GBLB  &ISTGEN                                                          
         GBLB  &ISTUA                                                           
         LCLA  &LN                                                 @L1A         
         LCLA  &KWLN                                               @Y1A         
         AIF   (&ISTUA EQ 1).UASTART                                            
*                                                                               
*  IST6MCUA - ACF/VTAM                                                          
*                                                                               
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                              
*                                                                               
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"               
*                                                                               
*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1994.                      
*             ALL RIGHTS RESERVED.                                              
*                                                                               
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                         
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY                      
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                          
*                                                                               
*             SEE COPYRIGHT INSTRUCTIONS.                                       
*                                                                               
&ISTUA   SETB  1                                                                
.UASTART ANOP                                                                   
&KWLN    SETA  K'&KW                                                            
&LN      SETA  4                       DEFAULT FIELD LENGTH        @L1A         
         AIF   ('&LEN' EQ '').UAKW     CHECK FOR LENGTH PASSED     @L1A         
&LN      SETA  &LEN                    USE SPECIFIED FIELD LENGTH  @L1A         
         AIF   ((&LN EQ 2) OR (&LN EQ 4)).UAKW       VALID LENGTHS @L1A         
         IST6MCER 5,LEN,&LEN           ERROR IN INVOKING MACRO     @L1A         
         MEXIT                                                     @L1A         
.UAKW    AIF   ('&KWVAL'(1,1) NE '(').CKKW   BR IF NON REG FORM                 
         AIF   (NOT &ISTGEN).UAEND                                              
         AIF   (&LN GT 2).STREG                                    @L1A         
         STCM  &KWVAL(1),3,&LOC.(&BASEREG)                                      
         MNOTE '*                                     STORE 2-BYTE &KW'         
         MEXIT                                                     @L1A         
.STREG   ST    &KWVAL(1),&LOC.(&BASEREG)                                        
         MNOTE '*                                           STORE &KW'          
         MEXIT                                                                  
.CKKW    ANOP                                                                   
.*   CHECK FOR KEYWORDS WHOSE VALUES REPRESENT A LENGTH, NOT AN ADDRESS         
         AIF   ((&KWLN GE 6) AND ('&KW'(&KWLN-5,6) EQ 'LENGTH')).LNFLD X        
                                                                   @Y1A         
.*   CHECK FOR KEYWORDS HAVING SPECIAL HANDLING                                 
         AIF   ('&KW'(1,6) NE 'CONVID').CKKW1                                   
         AIF   (NOT &ISTGEN).UAEND                                              
         MVC   &LOC.(4,&BASEREG),&KWVAL                                         
         MNOTE '*                                           STORE &KW'          
         MEXIT                                                                  
.CKKW1   ANOP                                                                   
         AIF   ('&KW'(1,4) NE 'CGID').CKKW2                        @KFA         
         AIF   (NOT &ISTGEN).UAEND                                              
         MVC   &LOC.(4,&BASEREG),&KWVAL                                         
         MNOTE '*                                           STORE &KW'          
         MEXIT                                                                  
.CKKW2   ANOP                                                                   
         AIF   ('&KW'(1,3) NE 'CID').CKKW3                                      
         IST6MCER 8,ARG                                                         
         MEXIT                                                                  
.CKKW3   ANOP                                                                   
.*   GENERAL ADDRESS-TYPE FIELD                                                 
         AIF   (NOT &ISTGEN).UAEND                                              
         LA    15,&KWVAL            SET VALUE IN REGISTER                       
         AGO   .STVAL                                              @Y1A         
.LNFLD   ANOP                                                      @Y1A         
         AIF   (NOT &ISTGEN).UAEND                                 @Y1A         
         CNOP  0,4                  FORCE WORD CONSTANT ALIGNMENT  @Y1A         
         AIF   (&LN GT 2).LDL4                                     @Y1A         
         B     *+8                  BRANCH AROUND VALUE CONSTANT   @Y1A         
         DC    AL4(&KWVAL*65536)    HALFWORD VALUE IN HI-ORDER PART OF +        
                 WORD CONSTANT (DONE TO FORCE RANGE-CHECK OF SPECIFIED +        
                 VALUE), OVERFLOW ERROR WHEN VALUE EXCEEDS 32767.  @Y1A         
         ICM   15,3,*-4             GET 2-BYTE VALUE IN REGISTER   @Y1A         
         AGO   .STVAL                                              @Y1A         
.LDL4    ANOP                                                      @Y1A         
         B     *+8                  BRANCH AROUND VALUE CONSTANT   @Y1A         
         DC    AL4(&KWVAL)          WORD CONTAINING VALUE          @Y1A         
         L     15,*-4               GET VALUE IN REGISTER          @Y1A         
.STVAL   AIF   (&LN GT 2).STL4                                     @Y1C         
         STCM  15,3,&LOC.(&BASEREG)                                             
         MNOTE '*                                     STORE 2-BYTE &KW'         
         MEXIT                                                     @L1A         
.STL4    ST    15,&LOC.(&BASEREG)                                  @Y1C         
         MNOTE '*                                            STORE &KW'         
.UAEND   MEND                                                                   
