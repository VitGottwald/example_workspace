         MACRO                                                          00050000
&NAME    GETLINE &PARM=,&UPT=,&ECT=,&ECB=,&INPUT=,&TERMGET=,&ENTRY=,   *00100000
               &SUBSTACK=,&MF=                                @PBY0239  00150000
.********************************************************************** 00200000
.*                                                                    * 00250000
.*COPYRIGHT:                                                          * 00280000
.*        5685-025 COPYRIGHT IBM CORP 1986,                           * 00310000
.*        THIS PRODUCT CONTAINS RESTRICTED MATERIALS OF IBM,          * 00340000
.*        REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083       * 00370000
.*                                                                    * 00400000
.*STATUS:                                                             * 00450000
.*                                                                    * 00483300
.*        JBB2367  TSO/E                                              * 00516600
.*                                                                    * 00550000
.*FUNCTION:                                                           * 00600000
.*   THE GETLINE MACRO EXAMINES PARAMETERS SPECIFIED IN THE GETLINE   * 00650000
.*   MACRO INSTRUCTION AND BUILDS A PARAMETER BLOCK TO BE USED BY THE * 00700000
.*   GETLINE SERVICE ROUTINE (REFER TO SECTION 3.48.9.1 FOR THE       * 00750000
.*   PARAMETER BLOCK FORMAT).                                         * 00800000
.*   IN THE LIST FORM, THE GETLINE MACRO GENERATES THE GETLINE        * 00850000
.*   PARAMETER BLOCK CONSISTING OF ONE WORD OF CONTROL (CONTROL,      * 00887500
.*   TINPUT, AND SUBSTACK OPTIONS) AND THE ADDRESS OF THE INPUT       * 00925000
.*   BUFFER.                                                  @PBY0239* 00962500
.*   IN THE EXECUTE FORM OF THE GETLINE MACRO, CODE IS GENERATED      * 01000000
.*   WHICH WILL RESET THE PARAMETER BLOCK IF REQUIRED AND LOAD THE    * 01050000
.*   ADDRESSES OF THE UPT, ECT, ECB, AND PARAMETER BLOCK INTO THE     * 01100000
.*   SERVICE ROUTINE PARAMETER LIST IF SPECIFIED. A LINK MACRO WILL   * 01150000
.*   BE GENERATED TO INVOKE THE GETLINE SERVICE ROUTINE UNLESS AN     * 01200000
.*   ENTRY POINT IS SPECIFIED. IF AN ENTRY POINT IS SPECIFIED, THE    * 01250000
.*   CODE TO ENABLE A BRANCH AND LINK WILL BE GENERATED.              * 01300000
.*                                                                    * 01350000
.*ENTRY POINTS:                                                       * 01400000
.*   GETLINE MACRO CALL - ONLY ENTRY POINT.                           * 01450000
.*                                                                    * 01500000
.*INPUT:                                                              * 01550000
.*   PARAMETERS SPECIFIED IN THE GETLINE MACRO FORMAT (FOR FORMAT     * 01600000
.*   SPECIFICATIONS REFER TO SECTION 3.48.9.1).                       * 01650000
.*                                                                    * 01700000
.*OUTPUT:                                                             * 01750000
.*   IN THE L-FORM, A 2-WORD PARAMETER BLOCK (REFER TO SECTION        * 01800000
.*   3.48.9.1) CONSISTING OF CONTROL WHICH IS SET BY THE MACRO AND    * 01850000
.*   ADDRESS OF THE INPUT BUFFER.                                     * 01900000
.*   IN THE E-FORM, CODE WHICH RESETS CONTROL IN THE PARAMETER BLOCK, * 01950000
.*   PLACES THE ADDRESSES OF THE SERVICE ROUTINE PARAMETER BLOCK,     * 02000000
.*   UPT, ECT, AND ECB IN THE SERVICE ROUTINE PARAMETER LIST AS       * 02050000
.*   SPECIFIED, AND INVOKES THE SERVICE ROUTINE.                      * 02100000
.*                                                                    * 02150000
.*EXTERNAL REFERENCES:                                                * 02200000
.*   LINK MACRO.                                                      * 02250000
.*                                                                    * 02300000
.*EXITS,NORMAL:                                                       * 02350000
.*   IN THE L-FORM, RETURN TO CALLER.                                 * 02400000
.*   IN THE E-FORM, CODE IS GENERATED TO INVOKE THE GETLINE SERVICE   * 02450000
.*   ROUTINE ACCORDING TO THE STANDARD LINKAGE CONVENTIONS (REFER TO  * 02500000
.*   SECTION 3.48.1.5).                                               * 02550000
.*                                                                    * 02600000
.*EXITS,ERROR:                                                        * 02650000
.*   INVALID INPUT.                                                   * 02700000
.*                                                                    * 02750000
.*TABLES/WORK AREAS:                                                  * 02800000
.*   SERVICE ROUTINE PARAMETER LIST - A TABLE OF POINTERS TO THE UPT, * 02850000
.*   ECT, ECB, AND SERVICE ROUTINE PARAMETER BLOCK.                   * 02900000
.*   SERVICE ROUTINE PARAMETER BLOCK - A CONTROL TABLE SET ACCORDING  * 02950000
.*   TO THE SPECIFIED MACRO PARAMETERS AND THE ADDRESS OF THE INPUT   * 03000000
.*   BUFFER.                                                          * 03050000
.*                                                                    * 03100000
.*ATTRIBUTES:                                                         * 03150000
.*   SERIALLY REUSABLE.                                               * 03200000
.*                                                                    * 03250000
.*NOTES:                                                              * 03300000
.*   DATE OF CURRENT RELEASE :                                        * 03350000
.*      09/01/86                                              @PBY0239* 03400000
.*   PREVIOUS RELEASE DATES :                                         * 03450000
.*      01/22/70                                              @PBY0239* 03475000
.*      12/12/69                                                      * 03500000
.*      10/22/69                                                      * 03550000
.*                                                                    * 03600000
.*CHANGE ACTIVITY:                                                    * 03605000
.*  @PBY0239   TSO/E RELEASE 3 SUPPORT FOR RUNNING CLIST THROUGH THE  * 03610000
.*             TSO SERVICE FACILITY. THESE CHANGES SUPPORT THE NEW    * 03615000
.*             SUBSTACK OPTION.                                       * 03620000
.*             THE INPUT AND TERMGET OPTION SECTIONS OF THE EXECUTE   * 03625000
.*             FORM WERE REWRITTEN TO MAKE THE MACRO MORE READABLE    * 03630000
.*             AND THE SUBSTACK OPTION WAS ADDED TO THE EXECUTE FORM. * 03635000
.*             THE LIST FORM WAS COMPLETELY REWRITTEN TO MAKE IT MORE * 03640000
.*             READABLE AND TO ADD THE SUBSTACK OPTION.               * 03645000
.*                                                                    * 03650000
.*             SEE THE GETLINE SERVICE ROUTINE FOR FURTHER DESCRIPTION* 03700000
.*             OF THIS NEW OPTION.                            @PBY0239* 03750000
.*                                                                    * 03800000
.********************************************************************** 03850000
         LCLA  &COUNT                    LOOP COUNTER         @PBY0239  03900000
         LCLB  &B1,&B2,&CHECK1,&CHECK2,&NOLOAD,&REGLOAD       @PBY0239  03950000
.*                                                                      04000000
         AIF   (T'&MF EQ 'O').ERR1       NO MF PARAMETER                04050000
         AIF   (N'&SYSLIST EQ 0).MF1     POSITIONAL PARAMETERS INVALID  04100000
         AIF   (T'&SYSLIST(1) NE 'O' OR N'&SYSLIST NE 1).ERR7           04150000
.MF1     AIF   ('&MF' EQ 'L').LSTFORM    LIST FORM            @PBY0239  04200000
.********************************************************************** 04300000
.*                                                                    * 04350000
.*                         EXECUTE FORM                               * 04400000
.*                                                                    * 04450000
.********************************************************************** 04500000
         AIF   ('&MF(1)' NE 'E').ERR1                                   04550000
         AIF   (T'&NAME EQ 'O').E2                                      04600000
&NAME    DS    0H                                                       04650000
.********************************************************************** 04662500
.*                         MF PARAMETER                       @PBY0239* 04675000
.********************************************************************** 04687500
.E2      AIF   (T'&MF(2) EQ 'O').ERR1                                   04700000
         AIF   ('&MF(2)' EQ '(1)').E3                                   04750000
         AIF   ('&MF(2)'(1,1) EQ '(').WARN1                   @PBY0239  04778500
.E2A     LA    1,&MF(2)                  LOAD PARM LIST ADDR            04807000
.********************************************************************** 04835500
.*                         UPT PARAMETER                      @PBY0239* 04864000
.********************************************************************** 04892500
.E3      AIF   (T'&UPT EQ 'O').E4        UPT OMITTED                    04921000
         AIF   ('&UPT'(1,1) EQ '(').E3A                                 04950000
         LA    14,&UPT                   LOAD UPT ADDR                  05000000
         ST    14,0(0,1)                 STORE UPT ADDR IN PARM LIST    05050000
         AGO   .E4                                                      05100000
.E3A     ANOP                                                           05150000
         ST    &UPT(1),0(0,1)            STORE UPT ADDR IN PARM LIST    05185700
.*                                                            @PBY0239  05221400
.********************************************************************** 05257100
.*                         ECT PARAMETER                      @PBY0239* 05292800
.********************************************************************** 05328500
.E4      AIF   (T'&ECT EQ 'O').E5        ECT OMITTED                    05364200
         AIF   ('&ECT'(1,1) EQ '(').E4A                                 05400000
         LA    14,&ECT                   LOAD ECT ADDR                  05450000
         ST    14,4(0,1)                 STORE ECT ADDR IN PARM LIST    05500000
         AGO   .E5                                                      05550000
.E4A     ANOP                                                           05600000
         ST    &ECT(1),4(0,1)            STORE ECT ADDR IN PARM LIST    05641600
.********************************************************************** 05683200
.*                         ECB PARAMETER                      @PBY0239* 05724800
.********************************************************************** 05766400
.E5      AIF   (T'&ECB EQ 'O').E6        ECB OMITTED                    05808000
         AIF   ('&ECB'(1,1) EQ '(').E5A                                 05850000
         LA    14,&ECB                   LOAD ECB ADDR                  05900000
         ST    14,8(0,1)                 STORE ECB ADDR IN PARM LIST    05950000
         AGO   .E6                                                      06000000
.E5A     ANOP                                                           06050000
         ST    &ECB(1),8(0,1)            STORE ECB ADDR IN PARM LIST    06085700
.*                                                            @PBY0239  06121400
.********************************************************************** 06157100
.*                         PARM PARAMETER                     @PBY0239* 06192800
.********************************************************************** 06228500
.E6      AIF   (T'&PARM EQ 'O').E7       PARM OMITTED                   06264200
         AIF   ('&PARM'(1,1) EQ '(').E6A                                06300000
         LA    14,&PARM                  LOAD PARM ADDR                 06325000
         ST    14,12(0,1)                STORE PARM ADDR IN PARM LIST   06350000
&NOLOAD  SETB  1                         SET FLAG TO INDICATE THAT REG  06375000
.*                                       14 CONTAINS THE PARM ADDRESS   06400000
.*                                                            @PBY0239  06425000
         AGO   .E7                                                      06450000
.E6A     ANOP                                                           06500000
         ST    &PARM(1),12(0,1)          STORE PARM ADDR IN PARM LIST   06522900
.*                                                            @PBY0239  06545800
&REGLOAD SETB  1                         SET FLAG TO INDICATE THAT A    06568700
.*                                       REGISTER CONTAINS THE PARM     06591600
.*                                       ADDRESS              @PBY0239  06614500
.********************************************************************** 06637400
.*           INPUT, SUBSTACK, AND TERMGET PARAMETERS          @PBY0239* 06660300
.********************************************************************** 06683200
.E7      ANOP                                                 @PBY0239  06706100
         AIF   (N'&INPUT EQ 0 AND N'&SUBSTACK EQ 0                     *06729000
               AND N'&TERMGET EQ 0).E11  DON'T UPDATE ANYTHING          06751900
.*                                                            @PBY0239  06774800
         AIF   (&NOLOAD).INPUT           LOAD PARM BLOCK?               06797700
.*                                                            @PBY0239  06820600
         AIF   (&REGLOAD).LOADREG        LOAD PARM BLOCK REGISTER?      06843500
.*                                                            @PBY0239  06866400
         L     14,12(0,1)                LOAD PARM BLOCK ADDRESS        06889300
.*                                                            @PBY0239  06912200
         AGO   .INPUT                                                   06935100
.*                                                            @PBY0239  06958000
.LOADREG LR    14,&PARM(1)               LOAD PARM BLOCK ADDRESS        06980900
.*                                                            @PBY0239  07003800
.********************************************************************** 07026700
.*                      INPUT PARAMETER                       @PBY0239* 07049600
.********************************************************************** 07072500
.INPUT   ANOP                                                 @PBY0239  07095400
.*   IF THE OPERANDS ARE OMITTED DON'T UPDATE THEM.           @PBY0239  07118300
         AIF   (N'&INPUT EQ 0).E8                             @PBY0239  07141200
         AIF   (N'&INPUT GT 2).ERR2      TOO MANY OPERANDS    @PBY0239  07164100
&COUNT   SETA  N'&INPUT                  INITIALIZE LOOP COUNTER        07187000
.*                                                            @PBY0239  07209900
.LOOP1   ANOP                                                 @PBY0239  07232800
.*   IF ONE OF THE OPERANDS IS OMITTED USE THE DEFAULT VALUE. @PBY0239  07255700
         AIF   (T'&INPUT(&COUNT) EQ 'O').DEC1                 @PBY0239  07278600
         AIF   ('&INPUT(&COUNT)' EQ 'ISTACK').DEFLT2  USE DEFAULT       07301500
.*                                                            @PBY0239  07324400
         AIF   ('&INPUT(&COUNT)' EQ 'TERM').SET1      DON'T USE DEFAULT 07347300
.*                                                            @PBY0239  07370200
         AIF   ('&INPUT(&COUNT)' EQ 'LOGICAL').DEFLT1 USE DEFAULT       07393100
.*                                                            @PBY0239  07416000
         AIF   ('&INPUT(&COUNT)' NE 'PHYSICAL').ERR3  INVALID OPERAND   07438900
.*                                                            @PBY0239  07461800
&B1      SETB  1                   INDICATE "PHYSICAL" OPTION @PBY0239  07484700
.DEFLT1  ANOP                                                 @PBY0239  07507600
         AIF   (&CHECK1).ERR3      CHECK FOR "PHYSICAL" AND "LOGICAL"   07530500
.*                                                            @PBY0239  07553400
&CHECK1  SETB  1                   BEING USED TOGETHER - CAN'T BE       07576300
.*                                                            @PBY0239  07599200
         AGO   .DEC1                                          @PBY0239  07622100
.SET1    ANOP                      INDICATE "TERM" OPTION     @PBY0239  07645000
&B2      SETB  1                                              @PBY0239  07667900
.DEFLT2  ANOP                                                 @PBY0239  07690800
         AIF   (&CHECK2).ERR3      CHECK FOR "TERM" AND "ISTACK"        07713700
.*                                                            @PBY0239  07736600
&CHECK2  SETB  1                   BEING USED TOGETHER - CAN'T BE       07759500
.*                                                            @PBY0239  07782400
.DEC1    ANOP                                                 @PBY0239  07805300
&COUNT   SETA  &COUNT-1                  DECREMENT LOOP COUNTER         07828200
.*                                                            @PBY0239  07851100
         AIF   (&COUNT NE 0).LOOP1                            @PBY0239  07874000
         MVI   0(14),B'00&B1.&B2.0000'   SET CONTROL FIELD              07896900
.*                                                            @PBY0239  07919800
&B1      SETB  0                                              @PBY0239  07942700
&B2      SETB  0                                              @PBY0239  07965600
&CHECK1  SETB  0                                              @PBY0239  07988500
&CHECK2  SETB  0                                              @PBY0239  08011400
.********************************************************************** 08034300
.*                      SUBSTACK PARAMETER                    @PBY0239* 08057200
.********************************************************************** 08080100
.E8      ANOP                                                 @PBY0239  08103000
         AIF   (T'&SUBSTACK EQ 'O').E9       IF OMITTED, USE DEFAULT    08125900
.*                                                            @PBY0239  08148800
         AIF   ('&SUBSTACK' EQ 'NO').DEFLT3  USE DEFAULT      @PBY0239  08171700
         AIF   ('&SUBSTACK' NE 'YES').ERR4   INVALID OPERAND  @PBY0239  08194600
&B1      SETB  1                             INDICATE "SUBSTACK" OPTION 08217500
.*                                                            @PBY0239  08240400
.DEFLT3  ANOP                                                           08263300
         MVI   1(14),B'&B1.0000000'      SET SUBSTACK OPTION BIT        08286200
.*                                                            @PBY0239  08309100
&B1      SETB  0                                              @PBY0239  08332000
.********************************************************************** 08354900
.*                      TERMGET PARAMETER                     @PBY0239* 08377800
.********************************************************************** 08400700
.E9      ANOP                                                 @PBY0239  08423600
.*   IF THE OPERAND IS OMITTED DON'T UPDATE IT.               @PBY0239  08446500
         AIF   (N'&TERMGET EQ 0).E11                          @PBY0239  08469400
         AIF   (N'&TERMGET GT 2).ERR5    TOO MANY OPERANDS    @PBY0239  08492300
&COUNT   SETA  N'&TERMGET                INITIALIZE LOOP COUNTER        08515200
.*                                                            @PBY0239  08538100
.LOOP2   ANOP                                                 @PBY0239  08561000
.*   IF ONE OF THE OPERANDS IS OMITTED USE THE DEFAULT VALUE. @PBY0239  08583900
         AIF   (T'&TERMGET(&COUNT) EQ 'O').DEC2               @PBY0239  08606800
         AIF   ('&TERMGET(&COUNT)' EQ 'EDIT').DEFLT5 USE DEFAULT        08629700
.*                                                            @PBY0239  08652600
         AIF   ('&TERMGET(&COUNT)' EQ 'ASIS').SET2   DON'T USE DEFAULT  08675500
.*                                                            @PBY0239  08698400
         AIF   ('&TERMGET(&COUNT)' EQ 'WAIT').DEFLT4 USE DEFAULT        08721300
.*                                                            @PBY0239  08744200
         AIF   ('&TERMGET(&COUNT)' NE 'NOWAIT').ERR6 INVALID OPERAND    08767100
.*                                                            @PBY0239  08790000
&B1      SETB  1                   INDICATE "NOWAIT" OPTION   @PBY0239  08812900
.DEFLT4  ANOP                                                 @PBY0239  08835800
         AIF   (&CHECK1).ERR6      CHECK FOR "NOWAIT" AND "WAIT"        08858700
.*                                                            @PBY0239  08881600
&CHECK1  SETB  1                   BEING USED TOGETHER - CAN'T BE       08904500
.*                                                            @PBY0239  08927400
         AGO   .DEC2                                          @PBY0239  08950300
.SET2    ANOP                      INDICATE "ASIS" OPTION     @PBY0239  08973200
&B2      SETB  1                                              @PBY0239  08996100
.DEFLT5  ANOP                                                 @PBY0239  09019000
         AIF   (&CHECK2).ERR6      CHECK FOR "EDIT" AND "ASIS"          09041900
.*                                                            @PBY0239  09064800
&CHECK2  SETB  1                   BEING USED TOGETHER - CAN'T BE       09087700
.*                                                            @PBY0239  09110600
.DEC2    ANOP                                                 @PBY0239  09133500
&COUNT   SETA  &COUNT-1                  DECREMENT LOOP COUNTER         09156400
.*                                                            @PBY0239  09179300
         AIF   (&COUNT NE 0).LOOP2                            @PBY0239  09202200
         MVI   2(14),B'100&B1.000&B2'    SET TINPUT OPTION FIELDS       09225100
.*                                                            @PBY0239  09248000
.********************************************************************** 09270900
.*                      ENTRY PARAMETER                       @PBY0239* 09293800
.********************************************************************** 09316700
.E11     AIF   (T'&ENTRY EQ 'O').E12                                    09350000
         AIF   ('&ENTRY' EQ '(15)').E11B                                09391600
         AIF   ('&ENTRY'(1,1) EQ '(').WARN2                   @PBY0239  09433200
.E11A    LA    15,&ENTRY                 LOAD EP ADDR         @PBY0239  09474800
.E11B    BALR  14,15                     BRANCH TO SERVICE ROUTINE      09516400
.*                                                            @PBY0239  09558000
         MEXIT                                                          09600000
.********************************************************************** 09608300
.*                      STANDARD LINKAGE                      @PBY0239* 09616600
.********************************************************************** 09624900
.E12     L     15,16(0,0)                LOAD CVT POINTER               09633200
         TM    348(15),B'10000000'       IS GETLINE LOADED ?            09641500
         BNO   IKJ@&SYSNDX               NO - BRANCH TO LINK            09649800
         L     15,348(0,15)              YES - BALR TO GETLINE          09658100
         BALR  14,15                                                    09666400
         B     IKJ$&SYSNDX                                              09674700
IKJ@&SYSNDX LINK  EP=IKJGETL                                            09683000
IKJ$&SYSNDX DS    0H                                                    09691300
         MEXIT                                                          09700000
.********************************************************************** 09750000
.*                                                                    * 09800000
.*                             LIST FORM                              * 09850000
.*                                                                    * 09900000
.********************************************************************** 09950000
.LSTFORM DS    0F                                             @PBY0239  09985100
.********************************************************************** 10020200
.*                      INPUT PARAMETER                       @PBY0239* 10055300
.********************************************************************** 10090400
.*       AIF   (N'&INPUT GT 2).ERR2      TOO MANY OPERANDS    @PBY0239  10125500
.*   IF THE OPERANDS ARE OMITTED USE THE DEFAULT VALUES.      @PBY0239  10160600
&COUNT   SETA  N'&INPUT                  INITIALIZE LOOP COUNTER        10195700
.*                                                            @PBY0239  10230800
         AGO   .DECIDE3                                       @PBY0239  10265900
.LOOP3   ANOP                                                 @PBY0239  10301000
.*   IF ONE OF THE OPERANDS IS OMITTED USE THE DEFAULT VALUE. @PBY0239  10336100
         AIF   (T'&INPUT(&COUNT) EQ 'O').DEC3                 @PBY0239  10371200
         AIF   ('&INPUT(&COUNT)' EQ 'ISTACK').DEFLT7  USE DEFAULT       10406300
.*                                                            @PBY0239  10441400
         AIF   ('&INPUT(&COUNT)' EQ 'TERM').SET3      DON'T USE DEFAULT 10476500
.*                                                            @PBY0239  10511600
         AIF   ('&INPUT(&COUNT)' EQ 'LOGICAL').DEFLT6 USE DEFAULT       10546700
.*                                                            @PBY0239  10581800
         AIF   ('&INPUT(&COUNT)' NE 'PHYSICAL').ERR3  INVALID OPERAND   10616900
.*                                                            @PBY0239  10652000
&B1      SETB  1                   INDICATE "PHYSICAL" OPTION @PBY0239  10687100
.DEFLT6  ANOP                                                 @PBY0239  10722200
         AIF   (&CHECK1).ERR3      CHECK FOR "PHYSICAL" AND "LOGICAL"   10757300
.*                                                            @PBY0239  10792400
&CHECK1  SETB  1                   BEING USED TOGETHER - CAN'T BE       10827500
.*                                                            @PBY0239  10862600
         AGO   .DEC3                                          @PBY0239  10897700
.SET3    ANOP                      INDICATE "TERM" OPTION     @PBY0239  10932800
&B2      SETB  1                                              @PBY0239  10967900
.DEFLT7  ANOP                                                 @PBY0239  11003000
         AIF   (&CHECK2).ERR3      CHECK FOR "TERM" AND "ISTACK"        11038100
.*                                                            @PBY0239  11073200
&CHECK2  SETB  1                   BEING USED TOGETHER - CAN'T BE       11108300
.*                                                            @PBY0239  11143400
.DEC3    ANOP                                                 @PBY0239  11178500
&COUNT   SETA  &COUNT-1                  DECREMENT LOOP COUNTER         11213600
.*                                                            @PBY0239  11248700
.DECIDE3 AIF   (&COUNT NE 0).LOOP3                            @PBY0239  11283800
&NAME    DC    B'00&B1.&B2.0000'         SET CONTROL FIELD    @PBY0239  11318900
&B1      SETB  0                                              @PBY0239  11354000
&B2      SETB  0                                              @PBY0239  11389100
&CHECK1  SETB  0                                              @PBY0239  11424200
&CHECK2  SETB  0                                              @PBY0239  11459300
.********************************************************************** 11494400
.*                      SUBSTACK PARAMETER                    @PBY0239* 11529500
.********************************************************************** 11564600
         AIF   (T'&SUBSTACK EQ 'O').DEFLT8   IF OMITTED, USE DEFAULT    11599700
.*                                                            @PBY0239  11634800
         AIF   ('&SUBSTACK' EQ 'NO').DEFLT8  USE DEFAULT      @PBY0239  11669900
         AIF   ('&SUBSTACK' NE 'YES').ERR4   INVALID OPERAND  @PBY0239  11705000
&B1      SETB  1                         INDICATE "SUBSTACK" OPTION     11740100
.*                                                            @PBY0239  11775200
.DEFLT8  ANOP                                                 @PBY0239  11810300
         DC    B'&B1.0000000'            SET SUBSTACK OPTION BIT        11845400
.*                                                            @PBY0239  11880500
&B1      SETB  0                                              @PBY0239  11915600
.********************************************************************** 11950700
.*                      TERMGET PARAMETER                     @PBY0239* 11985800
.********************************************************************** 12020900
         AIF   (N'&TERMGET GT 2).ERR5    TOO MANY OPERANDS    @PBY0239  12056000
.*   IF THE OPERAND IS OMITTED USE THE DEFAULT VALUES.        @PBY0239  12091100
&COUNT   SETA  N'&TERMGET                INITIALIZE LOOP COUNTER        12126200
.*                                                            @PBY0239  12161300
         AGO   .DECIDE4                                       @PBY0239  12196400
.LOOP4   ANOP                                                 @PBY0239  12231500
.*   IF ONE OF THE OPERANDS IS OMITTED USE THE DEFAULT VALUE. @PBY0239  12266600
         AIF   (T'&TERMGET(&COUNT) EQ 'O').DEC4               @PBY0239  12301700
         AIF   ('&TERMGET(&COUNT)' EQ 'EDIT').DEFLT10 USE DEFAULT       12336800
.*                                                            @PBY0239  12371900
         AIF   ('&TERMGET(&COUNT)' EQ 'ASIS').SET4    DON'T USE DEFAULT 12407000
.*                                                            @PBY0239  12442100
         AIF   ('&TERMGET(&COUNT)' EQ 'WAIT').DEFLT9  USE DEFAULT       12477200
.*                                                            @PBY0239  12512300
         AIF   ('&TERMGET(&COUNT)' NE 'NOWAIT').ERR6  INVALID OPERAND   12547400
.*                                                            @PBY0239  12582500
&B1      SETB  1                   INDICATE "NOWAIT" OPTION   @PBY0239  12617600
.DEFLT9  ANOP                                                 @PBY0239  12652700
         AIF   (&CHECK1).ERR6      CHECK FOR "NOWAIT" AND "WAIT"        12687800
.*                                                            @PBY0239  12722900
&CHECK1  SETB  1                   BEING USED TOGETHER - CAN'T BE       12758000
.*                                                            @PBY0239  12793100
         AGO   .DEC4                                          @PBY0239  12828200
.SET4    ANOP                      INDICATE "ASIS" OPTION     @PBY0239  12863300
&B2      SETB  1                                              @PBY0239  12898400
.DEFLT10 ANOP                                                 @PBY0239  12933500
         AIF   (&CHECK2).ERR6      CHECK FOR "EDIT" AND "ASIS"          12968600
.*                                                            @PBY0239  13003700
&CHECK2  SETB  1                   BEING USED TOGETHER - CAN'T BE       13038800
.*                                                            @PBY0239  13073900
.DEC4    ANOP                                                 @PBY0239  13109000
&COUNT   SETA  &COUNT-1                  DECREMENT LOOP COUNTER         13144100
.*                                                            @PBY0239  13179200
.DECIDE4 AIF   (&COUNT NE 0).LOOP4                            @PBY0239  13214300
         DC    B'100&B1.000&B2'          SET TINPUT OPTION FIELDS       13249400
.*                                                            @PBY0239  13284500
         DC    B'00000000'               SET TINPUT OPTION FIELDS       13319600
.*                                                            @PBY0239  13354700
         DC    A(0)                      INPUT BUFFER ADDRESS           13389800
.*                                                            @PBY0239  13424900
         MEXIT                                                          13460000
.ERR1    MNOTE 8,'INVALID OR OMITTED MF PARAMETER'                      13500000
         MEXIT                                                          13550000
.ERR2    MNOTE 8,'INVALID NUMBER OF OPERANDS FOR INPUT KEYWORD'         13600000
         MEXIT                                                          13650000
.*                                                            @PBY0239  13683300
.ERR3    MNOTE 8,'INVALID OPERAND USED FOR INPUT KEYWORD'               13716600
         MEXIT                                                          13750000
.*                                                            @PBY0239  13775000
.ERR4    MNOTE 8,'INVALID OPERAND USED FOR SUBSTACK KEYWORD - USE YES O*13800000
               R NO ONLY'                                               13825000
         MEXIT                                                          13850000
.*                                                            @PBY0239  13883300
.ERR5    MNOTE 8,'INVALID NUMBER OF OPERANDS FOR TERMGET KEYWORD'       13916600
         MEXIT                                                          13950000
.*                                                            @PBY0239  13983300
.ERR6    MNOTE 8,'INVALID OPERAND USED FOR TERMGET KEYWORD'             14016600
         MEXIT                                                          14050000
.*                                                            @PBY0239  14054500
.ERR7    MNOTE 8,'POSITIONAL PARAMETERS ARE NOT ALLOWED'                14059000
         MEXIT                                                          14063500
.*                                                            @PBY0239  14068000
.WARN1   MNOTE 0,'PARAMETER LIST ADDR IN MF PARAMETER IS NOT IN REG 1'  14072500
         AGO   .E2A                                                     14077000
.*                                                            @PBY0239  14081500
.WARN2   MNOTE 0,'ENTRY POINT ADDR IN ENTRY PARAMETER IS NOT IN REG 15' 14086000
         AGO   .E11A                                                    14090500
.*                                                            @PBY0239  14095000
         MEND                                                           14100000
