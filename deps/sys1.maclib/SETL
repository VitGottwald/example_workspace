*********************************************************************** 00050000
*PROPRIETARY V3 STATEMENT                                             * 00100000
*LICENSED MATERIALS - PROPERTY OF IBM                                 * 00150000
*"RESTRICTED MATERIALS OF IBM"                                        * 00200000
*5695-DF1                                                             * 00250000
*COPYRIGHT 1992, 2011 IBM CORP.                                       * 00300000
*END PROPRIETARY V3 STATEMENT                                         * 00350000
*                                                                  @L1A 00358300
* FUNCTION: Obsolete QISAM macro used only for emulation with VSAM @L1A 00366600
* CHANGE ACTIVITY                                                       00374900
* OY54004 - 4/27/92 - MODIFY CONTINUATION CHARACTER FOR DBCS            00383200
* $L1=64BIT,JDZ1D10,20101116,SVLWER: 64 bit message                @L1A 00391500
*********************************************************************** 00400000
         MACRO                                                          00450000
&NAME    SETL  &DCB,&TYPE,&LL                                           00500000
         LCLB  &B(7)                                                    00650000
         LCLA  &CTR                                                     00700000
         LCLC  &A                                                       00750000
         GBLC  &SYSAM64    AMODE64=YES set by SYSSTATE macro       @L1A 00758300
         GBLC  &SYSASCE    ASCENV=AR or ANY set by SYSSTATE macro  @L1A 00766600
         SYSSTATE TEST     Test how macros are to expand           @L1A 00774900
         AIF   ('&SYSAM64' EQ 'YES').E5    GO IF AMODE64=YES       @L1A 00783200
         AIF   (('&SYSASCE' NE '') AND ('&SYSASCE' NE 'P')).E6     @L1A 00791500
         AIF   ('&DCB' EQ '').E1                                        00800000
         AIF   ('&TYPE' EQ '').E2                                       00850000
         AIF   ('&LL' EQ '' AND '&TYPE'(1,1) NE 'B').E3                 00900000
&NAME    IHBINNRA &DCB,&LL     Load registers 1 and 0                   00950000
&A       SETC  '&TYPE'                                                  01000000
&B(1)    SETB  ('&A' EQ 'K' OR '&A' EQ 'KD' OR '&A' EQ 'KX' OR '&A' EQ +01050000
               'KDX' OR '&A' EQ 'KCD' OR '&A' EQ 'KCX' OR '&A' EQ 'KCDX+01100000
               ' OR '&A' EQ 'KC')                                       01150000
&B(1)    SETB  (&B(1) OR '&A' EQ 'KH' OR '&A' EQ 'KDH' OR '&A' EQ 'KXH'+01200000
                OR '&A' EQ 'KDXH' OR '&A' EQ 'KCDH' OR '&A' EQ 'KCXH' O+01250000
               R '&A' EQ 'KCDXH' OR '&A' EQ 'KCH')                      01300000
&B(2)    SETB  ('&A' EQ 'KCD' OR '&A' EQ 'KCX' OR '&A' EQ 'KCDX')       01350000
&B(2)    SETB  (&B(2) OR '&A' EQ 'KC')                                  01400000
&B(2)    SETB  (&B(2) OR '&A' EQ 'KCDH' OR '&A' EQ 'KCXH' OR '&A' EQ 'K+01450000
               CDXH' OR '&A' EQ 'KCH')                                  01500000
&B(3)    SETB  ('&A' EQ 'I' OR '&A' EQ 'ID' OR '&A' EQ 'IX' OR '&A' EQ +01550000
               'IDX')                                                   01600000
&B(4)    SETB  ('&A' EQ 'B' OR '&A' EQ 'BD' OR '&A' EQ 'BX' OR '&A' EQ +01650000
               'BDX')                                                   01700000
&B(5)    SETB  ('&A' EQ 'KD' OR '&A' EQ 'KDX' OR '&A' EQ 'KCD' OR '&A' +01750000
               EQ 'KCDX' OR '&A' EQ 'ID' OR '&A' EQ 'IDX' OR '&A' EQ 'B+01800000
               D' OR '&A' EQ 'BDX')                                     01850000
&B(5)    SETB  (&B(5) OR '&A' EQ 'KDH' OR '&A' EQ 'KDXH' OR '&A' EQ 'KC+01900000
               DH' OR '&A' EQ  'KCDXH')                                 01950000
&B(6)    SETB  ('&A' EQ 'KX' OR '&A' EQ 'KDX' OR '&A' EQ 'KCX' OR '&A' +02000000
               EQ 'KCDX' OR '&A' EQ 'IX' OR '&A' EQ 'IDX' OR '&A' EQ 'B+02050000
               X' OR '&A' EQ 'BDX')                                     02100000
&B(6)    SETB  (&B(6) OR '&A' EQ 'KXH' OR '&A' EQ 'KDXH' OR '&A' EQ 'KC+02150000
               XH' OR '&A' EQ 'KCDXH')                                  02200000
&B(7)    SETB  ('&A' EQ 'KH' OR '&A' EQ 'KDH' OR '&A' EQ 'KCH' OR '&A' +02250000
               EQ 'KCDH' OR '&A' EQ 'KXH' OR '&A' EQ 'KDXH' OR '&A' EQ +02300000
               'KCXH' OR '&A' EQ 'KCDXH')                               02350000
&CTR     SETA  128*&B(1)+64*&B(2)+32*&B(3)+16*&B(4)+8*&B(5)+4*&B(6)     02400000
&CTR     SETA  &CTR+2*&B(7)                                             02450000
         AIF   (&CTR EQ 0).E4                                           02500000
         AIF   (&B(2) AND &B(7)).E4  C AND H INVALID COMB               02550000
         AIF   (&B(4)).EASY                                             02600000
         LA    14,&CTR             PICK UP TYPE BYTE                    02650000
         SLL   14,24               SHIFT TO HI-ORDER BYTE               02700000
         OR    0,14                PACK IN WITH LOW.LIMIT               02750000
         AGO   .FINI                                                    02800000
.EASY    LA    0,&CTR              PICK UP TYPE BYTE                    02850000
         SLL   0,24                SHIFT TO HI-ORDER BYTE               02900000
.FINI    L     15,76(0,1)          LOAD SETL ROUTINE ADDRESS            02950000
         BALR  14,15               LINK TO SETL ROUTINE                 03000000
         MEXIT                                                          03050000
.E1      IHBERMAC 6                                                     03100000
         MEXIT                                                          03150000
.E2      IHBERMAC 25                                                    03200000
         MEXIT                                                          03250000
.E3      IHBERMAC 10                                                    03300000
         MEXIT                                                          03350000
.E4      IHBERMAC 48,,&TYPE                                             03400000
         MEXIT ,                                                   @L1C 03450000
.E5      IGGERMAC 203,SETL      THE SETL MACRO DOES NOT SUPPORT    @L1A 03500000
         MEXIT                    SYSSTATE AMODE64=YES             @L1A 03550000
.E6      IGGERMAC 204,SETL      THE SETL MACRO DOES NOT SUPPORT    @L1A 03600000
         MEND                     SYSSTATE ASCENV=AR OR ANY        @L1A 03650000
