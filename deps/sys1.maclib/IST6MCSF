         MACRO                                                                  
         IST6MCSF &AREA=,&BRANCH=,&CONVID=,&ECB=,&EXIT=,&INVALP1=,     X        
               &INVALP2=,&OPTCD=,&QUALIFY=,&RECLEN=,&VTRINA=,          X        
               &VTRINL=,&VTROUTA=,&VTROUTL=                        @D1C         
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCSF                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: CONTROL = SENDFMH5 PROCESSOR                */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1994.             */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */         
.* /*      CONTROL=SENDFMH5 PARAMETER ON THE APPCCMD MACRO.          */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS:                                              */         
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCER                       */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: NONE                                     */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*                                                                */         
.* /* $MAC(IST6MCSF),COMP(API),PROD(VTAM): CONTROL = SENDFMH5        */         
.* /*                                                                */         
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */         
.* /* ---- -------- ------- ------ ------  ------------------------- */         
.* /* $L0= KA@@@PLD APPCRR  930722 446638  DCE Security Services     */         
.* /* $L1= KA@DR006 APPCRR  940713 576146  Vector Format             */         
.* /* $D1= ID@@@MST FPAPI   950310 143576  Fast Path API for APPC    */         
.* /*                                                                */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
.*                                                                              
         GBLB  &ISTGEN                                                          
         GBLB  &ISTSF                                                           
         AIF   (&ISTSF EQ 1).SFINVKW                                            
&ISTSF   SETB  1                                                                
.*                                     CHECK INVALID APPCCMD                    
.SFINVKW ANOP                                                                   
         IST6MCUC SENDFMH5,CONTROL,INVALKW=&INVALP1                             
         IST6MCUC SENDFMH5,CONTROL,INVALKW=&INVALP2                             
.SFAREA  ANOP                                                                   
         AIF   ('&AREA' EQ '').SFCNVID                                          
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X        
               KW=DATA_ADDRESS                                                  
.*                                     PUT DATA ADDRESS IN RPL                  
.SFCNVID ANOP                                                                   
         AIF   ('&CONVID' EQ '').SFCOMKW                                        
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X        
               KW=CONVID                                                        
.*                                     PUT CONVID IN APPC AREA                  
.SFCOMKW ANOP                                                                   
         IST6MCUP BRANCH=&BRANCH,ECB=&ECB,EXIT=&EXIT,OPTCD=&OPTCD,     X        
               CKSYN=YES,              ASY/SYN IS VALID FOR OPTCD PARM X        
               CKBUFF=NO               BUFFLST/NBUFFLST IS INVALID              
.*                                     OPTCD PARAMETER                          
.SFQFY   ANOP                                                                   
         AIF   ('&QUALIFY' EQ '').SFRLEN                                        
         AIF   ('&QUALIFY' EQ 'NULL').SFNQUAL                                   
         IST6MCER 5,QUALIFY,&QUALIFY   INVALID QUALIFY VALUE                    
         AGO   .SFRLEN                                                          
.SFNQUAL ANOP                                                                   
         AIF   (NOT &ISTGEN).SFRLEN                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6NQUA SET QUALIFY TO NULL               
.*                                                                              
.SFRLEN  ANOP                                                                   
         AIF   ('&RECLEN' EQ '').SFVECT                                         
         IST6MCUA KWVAL=&RECLEN,LOC=RPLRLEN-IFGRPL,BASEREG=1,          X        
               KW=DATA_LENGTH                                                   
.*                                     PUT DATA LENGTH IN RPL                   
.SFVECT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED                    
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).SFVOUT    @D1C         
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).SFERVI     @D1C         
         IST6MCUA KW=VTAM_TO_APPL_VECTOR_ADDRESS,KWVAL=&VTRINA,        X        
               LOC=RPL6VAIA-ISTRPL6X,BASEREG=14                    @D1C         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                             
         IST6MCUA KW=VTAM_TO_APPL_VECTOR_LENGTH,KWVAL=&VTRINL,LEN=2,   X        
               LOC=RPL6VAIL-ISTRPL6X,BASEREG=14                    @D1C         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                             
         AGO   .SFVOUT                                             @D1C         
.SFERVI  ANOP                                                                   
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C         
.SFVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED       @D1A         
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).SFREQ   @D1A         
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).SFERVO   @D1A         
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X        
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                @D1A         
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X        
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                @D1A         
         AGO   .SFREQ                                              @D1A         
.SFERVO  ANOP                                                      @D1A         
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A         
.SFREQ   ANOP                                                                   
         AIF   (NOT &ISTGEN).SFEXIT                                             
.SFREQ1  MVI   RPL6REQ-ISTRPL6X(14),RPL6SFM5                                    
.*                                    PUT SENDFMH5 REQUEST CODE IN APPC         
.*                                    AREA                                      
.SFEXIT  MEND                                                                   
