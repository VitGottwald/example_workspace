*%/*   START OF PL/X COMMENT ENDING ON %MACRO STATEMENT            @L2A 00010000
         MACRO                                                          00020000
&NAME    NOTE  &DCB,&TYPE=REL,&DECB=                               @L3C 00024400
.* $MAC(NOTE):                                                          00028800
.*PROPRIETARY V3 STATEMENT                                              00031600
.*LICENSED MATERIALS - PROPERTY OF IBM                                  00034400
.*"RESTRICTED MATERIALS OF IBM"                                         00037200
.*5695-DF1                                                              00040000
.*COPYRIGHT 1994, 2011 IBM CORP.                                        00042800
.*END PROPRIETARY V3 STATEMENT                                          00045600
.* STATUS = z/OS 1.13 DFSMS                                        @L4C 00048700
.*                                                                      00051900
.* CHANGE ACTIVITY =                                                    00053000
.*                                                                      00054100
.* $H1=3480,JDP1111,,STLPKH:  3480 SUPPORT                            * 00054600
.* $L1=31BIT,JDZ1110,,SJPLRK:  INVOCABLE IN 31-BIT MODE            @L1A 00055100
.* $L2=PLX  ,JDZ1110,930930,SJPLMET: PL/X SUPPORT                  @L2A 00055600
.* $L3=NOTEDECB,HDZ11B0,940426,SJPLCC: SUPPORT DECB KEYWORD        @L3A 00056100
.* $L4=64BIT,JDZ1D10,20101116,SVLWER: 64 bit message               @L4A 00056600
.*                                                                      00057600
         GBLC  &SYSAM64    AMODE64=YES set by SYSSTATE macro       @L4A 00058000
         GBLC  &SYSASCE    ASCENV=AR or ANY set by SYSSTATE macro  @L4A 00058400
         SYSSTATE TEST     Test how macros are to expand           @L4A 00058800
         AIF   ('&SYSAM64' EQ 'YES').ERR3    GO IF AMODE64=YES     @L4A 00059200
         AIF   (('&SYSASCE' NE '') AND ('&SYSASCE' NE 'P')).ERR4   @L4A 00059600
         AIF   ('&DCB' EQ '').ERR                                       00060000
&NAME    IHBINNRA &DCB,&DECB                                       @L3C 00075000
         AIF   ('&TYPE' NE 'REL').NOTREL                           @H1A 00090000
         SLR   15,15               CLEAR REGISTER                  @L1A 00098300
         ICM   15,7,85(1)          NOTE ROUTINE ADDRESS            @L1C 00106600
         AIF   ('&DECB' NE '').DECBNT                              @L3A 00115000
         BASR  14,15               LINK TO NOTE ROUTINE            @L4C 00127500
         MEXIT                                                          00140000
.DECBNT  BAS   14,8(,15)           LINK TO NOTE RTN                @L3A 00140800
         MEXIT                                                     @L3A 00141600
.NOTREL  AIF   ('&TYPE' NE 'ABS').ERR1                             @H1A 00142500
         AIF   ('&DECB' NE '').ERR2                                @L3A 00143700
         SLR   0,0                 INDICATES NOTE MACRO            @H1A 00145900
         LA    15,32               ROUTER CODE                     @H1A 00148100
         SVC   109                 SUPERVISOR CALL                 @H1A 00150300
         MEXIT                                                     @H1A 00152500
.ERR1    MNOTE 8,'INVALID PARAMETER FOR TYPE'                      @H1A 00155000
         MEXIT                                                     @H1A 00157500
.ERR2    MNOTE 8,'DECB KEYWORD NOT VALID WITH TYPE=ABS'            @L3A 00158300
         MEXIT                                                     @L3A 00159100
.ERR     IHBERMAC 6                                                     00160000
         MEXIT                                                     @L4A 00163300
.ERR3    IGGERMAC 203,NOTE    THE NOTE MACRO DOES NOT SUPPORT      @L4A 00166600
         MEXIT                  SYSSTATE AMODE64=YES               @L4A 00169900
.ERR4    IGGERMAC 204,NOTE    THE NOTE MACRO DOES NOT SUPPORT      @L4A 00173200
         MEXIT                  SYSSTATE ASCENV=AR OR ANY          @L4A 00176500
         MEND                                                           00180000
**/ NOTE: MACRO KEYS(1:1(DCB_NAME,DCB_ADDR),                   /*@L2A*/ 00230000
*                   0:1(CU_BLOCKID_NAME,CU_BLOCKID_ADDR),      /*@L2A*/ 00280000
*                   1:1(BLOCKID_NAME,BLOCKID_ADDR),            /*@L2A*/ 00330000
*                   0:1(BLOCKID_TYPE,ABS_BLOCK,REL_BLOCK),     /*@L2A*/ 00380000
*                       RETCODE,RSNCODE);                      /*@L2A*/ 00430000
* ANS('? '!!MACLABEL!!' NOTEP '!!MACKEYS!!';') RESCAN SKIP;    /*@L2A*/ 00480000
* %END NOTE;                                                   /*@L2A*/ 00530000
