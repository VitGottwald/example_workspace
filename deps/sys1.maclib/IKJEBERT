*/*                                                                     00050000
         MACRO                                                          01000020
&NAME    IKJEBERT &REG,&PARA,&RC=O                                      02000020
.*A612000-988000                                               @Y30NQKC 02050000
.*                                                                      03000020
.*  THIS MACRO IS USED BY ALL EDIT SUBCOMMANDS AND SERVICE ROUTINES     04000020
.*  TO CONTROL EPILOG CODE GENERATED.  THE BASIC SECTION OF THIS        05000020
.*  MACRO IS THE OS/360 RETURN MACRO INSTRUCTION.  IN ADDITION, THIS    06000020
.*  MACRO WILL DEQUEUE AND ZERO THE CURRENT SAVE AREA AND UPDATE THE    07000020
.*  POINTER TO THE CURRENT SAVE AREA IN THE COMMUNICATION AREA.         08000020
.*                                                                      09000020
.*  MACRO LAST UPDATED 06-03-74                                @Y30NQKC 09050000
.*                                                                      11000020
         LCLA  &A                                                       12000020
         AIF   ('&NAME' EQ '').GO                                       13000020
&NAME    DS    0H                                                       14000020
.GO      ST    13,(CANXTSVA-IKJEBECA)(COMMREG) UPDATE NEXT              15000020
*                                  AVAILABLE SAVE AREA POINTER.         16000020
         LR    12,13          COPY OVER SAVE AREA ADDRESS.              17000020
         L     13,4(,13)      ADDRESS OF HIGHER SAVE AREA.              18000020
         XC    0(72,12),0(12) ZERO SAVE AREA.                           19000020
         AIF   ('&RC' EQ '(15)').SPECRT                                 20000020
         AIF   ('&REG' EQ '').CONTA                                     21000020
.COMBACK ANOP                                                           22000020
&A       SETA  &REG(1)*4+20                                             23000020
         AIF   (&A LE 75).CONTB                                         24000020
&A       SETA  &A-64                                                    25000020
.CONTB   AIF   (N'&REG NE 2).CONTC                                      26000020
         LM    &REG(1),&REG(2),&A.(13) RESTORE THE REGISTERS.           27000020
         AGO   .CONTA                                                   28000020
.SPECRT  AIF   ('&REG(1)' NE '14' AND '&REG(1)' NE '15').COMBACK        29000020
         AIF   ('&REG(1)' EQ '14' AND N'&REG EQ 1).COMBACK              30000020
         AIF   ('&REG(1)' EQ '15' AND N'&REG EQ 1).CONTA                31000020
         AIF   ('&REG(1)' EQ '14').SKIP                                 32000020
         AIF   ('&REG(2)' EQ '0').ZTWO                                  33000020
.LM      LM    0,&REG(2),20(13) RESTORE THE REGISTERS.                  34000020
         AGO   .CONTA                                                   35000020
.ZTWO    L     0,20(0,13)     RESTORE REGISTER ZERO.                    36000020
         AGO   .CONTA                                                   37000020
.SKIP    L     14,12(0,13)    RESTORE REGISTER 14.                      38000020
         AIF   ('&REG(2)' EQ '15').CONTA                                39000020
         AIF   ('&REG(2)' EQ '0').ZTWO                                  40000020
         AIF   ('&REG(2)' EQ '0').ZTWO                                  41000020
         AGO   .LM                                                      42000020
.CONTC   AIF   (N'&REG NE 1).ERROR1                                     43000020
         L     &REG(1),&A.(0,13) RESTORE REGISTER.                      44000020
.CONTA   AIF   ('&PARA' EQ '').CONTD                                    45000020
         AIF   ('&PARA' NE 'T').ERROR2                                  46000020
         MVI   12(13),X'FF'   SET RETURN INDICATION.                    47000020
.CONTD   AIF   ('&RC' EQ 'O').CONTE                                     48000020
         AIF   ('&RC'(1,1) EQ '(').ISAREG                               49000020
         LA    15,&RC.(0,0)   LOAD RETURN CODE.                         50000020
         AGO   .CONTE                                                   51000020
.ISAREG  AIF   ('&RC(1)' EQ '15').CONTE                                 52000020
         MNOTE 12,'INVALID REGISTER NOTATION IN RC OPERAND.'            53000020
         MEXIT                                                          54000020
.CONTE   BR    14             RETURN.                                   55000020
         AGO   .END                                                     56000020
.ERROR1  MNOTE 12,'INVALID FIRST OPERAND SPECIFIED.'                    57000020
         MEXIT                                                          58000020
.ERROR2  MNOTE 12,'INVALID SECOND OPERAND SPECIFIED.'                   59000020
.END     MEND                                                           60000020
**/                                                                     60050000
*% IKJEBERT: MACRO KEYS(                                                60100000
*        RC                            /* INDENTIFIES THE VARIABLE      60150000
*                                         WHICH CONTAINS THE RETURN     60200000
*                                         CODE.                      */ 60250000
*        );                                                             60300000
*  DECLARE                                                              60350000
*    EHAREGS CHAR EXT,                 /* REGS DECLARED BY EPILOG    */ 60400000
*    ALINE CHAR,                       /* A LINE OF TEXT             */ 60450000
*    EHAUNIQUE CHAR EXT,               /* MISC VARIABLES FOR EPILOG  */ 60500000
*    DOCCOMCOL FIXED EXT,              /* START COL FOR COMMENTS     */ 60550000
*    DOCCOMCOLALT FIXED EXT;           /* ALTERNATE FOR DOCCOMCOL    */ 60600000
*                                                                       60650000
*        IF DOCCOMCOL < 5              /* COMMENT START AT LEFT      */ 60700000
*          THEN                        /*   YES-DEFAULT AT LEFT      */ 60750000
*            DOCCOMCOL = 40;           /* SET DEFAULT TO RIGHT       */ 60800000
*                                                                       60850000
*        IF INDEX(EHAREGS,'13P') = 0   /* REG 13 NOT DECLARED YET    */ 60900000
*          THEN                        /*   TRUE                     */ 60950000
*            EHAREGS = EHAREGS !! '13P';                                61000000
*                                                                       61050000
*        IF INDEX(EHAREGS,'12P') = 0   /* REG 12 NOT DECLARED YET    */ 61100000
*          THEN                        /*   TRUE                     */ 61150000
*            EHAREGS = EHAREGS !! '12P';                                61200000
*                                                                       61250000
*        IF INDEX(EHAUNIQUE,'CASAVPTR') = 0 /* SA PTR NOT DECLD      */ 61300000
*          THEN                        /*   TRUE                     */ 61350000
*            EHAUNIQUE = EHAUNIQUE !! '#CASAVPTR PTR(31) BASED#' !!     61400000
*              '@SAVE AREA PTR FOR IKJEBERT@';                          61450000
*                                                                       61500000
*        IF INDEX(EHAUNIQUE,'CASAVE') = 0 /* SA PTR NOT DECLD        */ 61550000
*          THEN                        /*   TRUE                     */ 61600000
*            EHAUNIQUE = EHAUNIQUE !! '#CASAVE CHAR(72) BASED#' !!      61650000
*              '@SAVE AREA EDIT MODULE@';                               61700000
*                                                                       61750000
*        ANS('RESPECIFY (GPR12P,GPR13P) RSTD;')                         61800000
*           COL(MACCOL) SKIP(1);                                        61850000
*                                                                       61900000
*        ALINE = 'CANXTSVA = GPR13P;';                                  61950000
*        DOCCOMCOLALT = LENGTH(ALINE) + MACCOL + 1;                     62000000
*        ANS(ALINE !! ' ? DOC (UPDATE NEXT-SAVE-AREA PTR);')            62050000
*          SKIP(1) COL(MACCOL);                                         62100000
*                                                                       62150000
*        ALINE = 'GPR12P = GPR13P;';                                    62200000
*        DOCCOMCOLALT = LENGTH(ALINE) + MACCOL + 1;                     62250000
*        ANS(ALINE !! '? DOC (SAVE PRESENT SAVE-AREA ADDR);')           62300000
*            COL(MACCOL);                                               62350000
*                                                                       62400000
*        ALINE = 'GPR13P = (GPR13P + 4) -> CASAVPTR;';                  62450000
*        DOCCOMCOLALT = LENGTH(ALINE) + MACCOL + 1;                     62500000
*        ANS(ALINE !! '? DOC (GET BACKWARD SAVE-AREA PTR);')            62550000
*            COL(MACCOL);                                               62600000
*                                                                       62650000
*        ALINE = 'RESPECIFY CASAVE BASED(GPR12P);';                     62700000
*        DOCCOMCOLALT = LENGTH(ALINE) + MACCOL + 1;                     62750000
*        ANS(ALINE !! '? DOC (CHANGE BASE FOR SAVE-AREA); ')            62800000
*            COL(MACCOL);                                               62850000
*                                                                       62900000
*        ALINE = 'CASAVE = CASAVE && CASAVE;';                          62950000
*        DOCCOMCOLALT = LENGTH(ALINE) + MACCOL + 1;                     63000000
*        ANS(ALINE !! '? DOC (ZERO OUT THIS SAVE-AREA);') COL(MACCOL);  63050000
*                                                                       63100000
*        ANS('RESPECIFY CASAVE BASED;') COL(MACCOL);                    63150000
*                                                                       63200000
*        IF RC = ''                    /* NO RETURN CODE             */ 63250000
*          THEN DO;                    /*   TRUE                     */ 63300000
*            ALINE = 'RETURN;';                                         63350000
*            DOCCOMCOLALT = LENGTH(ALINE) + MACCOL + 1;                 63400000
*            ANS(ALINE !! '? DOC (RETURN WITHOUT RETURN CODE);')        63450000
*                 COL(MACCOL);                                          63500000
*                                                                       63550000
*             END;                                                      63600000
*          ELSE DO;                    /*   FALSE, WANT RET CODE      */63650000
*            ALINE = 'RETURN CODE(' !! RC(1) !! ');';                   63700000
*            DOCCOMCOLALT = LENGTH(ALINE) + MACCOL + 1;                 63750000
*            ANS(ALINE !! '? DOC (RETURN WITH RETURN CODE);')           63800000
*                 COL(MACCOL);                                          63850000
*             END;                                                      63900000
*                                                                       63950000
*        ANS('RESPECIFY (GPR12P,GPR13P) UNRSTD;')                       64000000
*           COL(MACCOL) SKIP(1);                                        64050000
*         ANS(' ') COL(DOCCOMCOL-1);                                    64100000
*                                                                       64150000
*         DOCCOMCOLALT = 0;            /* RESET TO ZERO              */ 64200000
*% END IKJEBERT;                                                        64250000
