         MACRO                                                                  
         IST6MCRX &AREA=,&AREALEN=,&BRANCH=,&CONMODE=,&CONVID=,        X        
               &CONXMOD=,&ECB=,&EXIT=,&INVALP1=,&INVALP2=,&OPTCD=,     X        
               &QUALIFY=,&VTRINA=,&VTRINL=,&VTROUTA=,&VTROUTL=     @D1C         
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCRX                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: CONTROL = RECEIVE EXPEDITED DATA PROCESSOR  */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1993, 1994        */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */         
.* /*      CONTROL=RECVEXPD PARAMETER ON THE APPCCMD MACRO.          */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS:                                              */         
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCER                       */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: NONE                                     */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*                                                                */         
.* /* $MAC(IST6MCRX),COMP(API),PROD(VTAM): CONTROL=RECVEXPD          */         
.* /*                                                                */         
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */         
.* /* ---- -------- ------- ------ ------  ------------------------- */         
.* /* $I0= FF@@@MST APPCFDX 910418 879315: APPC Full-Duplex support  */         
.* /* $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services     */         
.* /* $Y1= P114810  APPCRR  940518 576146: DCE Security Services     */         
.* /* $L2= KA@DR006 APPCRR  940713 576146: Vector Format             */         
.* /* $D1= ID@@@MST FPAPI   950310 143576: Fast Path API for APPC    */         
.* /*                                                                */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
         GBLB  &ISTGEN                                                          
         GBLB  &ISTRX                                                           
         AIF   (&ISTRX EQ 1).RXINVKW                                            
*                                                                               
*  IST6MCRX - ACF/VTAM VERSION 4 RELEASE 2                                      
*                                                                               
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                              
*                                                                               
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"               
*                                                                               
*             5695-117 (C) COPYRIGHT IBM CORP. 1993.                            
*             ALL RIGHTS RESERVED.                                              
*                                                                               
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                         
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY                      
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                          
*                                                                               
*             SEE COPYRIGHT INSTRUCTIONS.                                       
*                                                                               
&ISTRX   SETB  1                                                                
.RXINVKW ANOP                                                                   
         IST6MCUC RECVEXPD,CONTROL,INVALKW=&INVALP1                             
         IST6MCUC RECVEXPD,CONTROL,INVALKW=&INVALP2                             
.*                                     CHECK INVALID APPCCMD                    
.RXAREA  ANOP                       ** AREA= PROCESSING **                      
         AIF   ('&AREA' EQ '').RXALEN                                           
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X        
               KW=AREA_ADDRESS                                                  
.*                                     PUT AREA ADDRESS IN RPL                  
.RXALEN  ANOP                       ** AREALEN= PROCESSING **                   
         AIF   ('&AREALEN' EQ '').RXQFY                                         
         IST6MCUA KWVAL=&AREALEN,LOC=RPLBUFL-IFGRPL,BASEREG=1,         X        
               KW=AREA_LENGTH                                                   
.*                                     PUT AREA LENGTH IN RPL                   
.RXQFY   ANOP                       ** QUALIFY= PROCESSING **                   
         AIF   ('&QUALIFY' EQ '').RXID                                          
.RXQFY1  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'ANY').RXQFY2                                     
         IST6MCUC ANY,QUALIFY,INVALKW=(CONVID=&CONVID)                          
         AIF   (NOT &ISTGEN).RXCOMKW                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6ANY                                    
*                                      PUT ANY CODE IN APPC AREA                
         AGO   .RXCOMKW                                                         
.RXQFY2  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'IANY').RXQFY3                                    
         IST6MCUC IANY,QUALIFY,INVALKW=(CONVID=&CONVID)                         
         AIF   (NOT &ISTGEN).RXCOMKW                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6IANY                                   
*                                      PUT IANY CODE IN APPC AREA               
         AGO   .RXCOMKW                                                         
.RXQFY3  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'SPEC').RXQFY4                                    
         AIF   (NOT &ISTGEN).RXCOMKW                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6SPEC                                   
*                                      PUT SPEC CODE IN APPC AREA               
         AGO   .RXID                                                            
.RXQFY4  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'ISPEC').RXQFYER                                  
         AIF   (NOT &ISTGEN).RXCOMKW                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6ISPC                                   
*                                      PUT ISPEC CODE IN APPC AREA              
         AGO   .RXID                                                            
.RXQFYER ANOP                                                                   
         IST6MCER 5,QUALIFY,&QUALIFY                                            
.RXID    ANOP                       ** CONVID= PROCESSING **                    
         AIF   ('&CONVID' EQ '').RXCOMKW                                        
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X        
               KW=CONVID                                                        
.RXCOMKW ANOP                       ** COMMON KEYWORDS **                       
         IST6MCUP BRANCH=&BRANCH,CONMODE=&CONMODE,CONXMOD=&CONXMOD,    X        
               ECB=&ECB,EXIT=&EXIT,OPTCD=&OPTCD,                       X        
               CKCMODE=YES,            CHECK CONMODE AND CONXMOD       X        
               CKSYN=YES,              ASY/SYN IS VALID FOR OPTCD      X        
               CKBUFF=NO               BUFFLST/NBUFFLST IS INVALID              
.*                                     VALIDATE & SET COMMON KEYWORDS           
.RXVECT  ANOP                          ENSURE BOTH PARMS CODED OR NOT           
.*                                     CODED                  @L1A @Y1M         
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).RXVOUT    @D1C         
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).RXERVI     @D1C         
.*                                  ** VTRINA = PROCESSING **      @L1A         
         IST6MCUA KWVAL=&VTRINA,LOC=RPL6VAIA-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_ADDRESS                      @D1C         
.*                                     STORE VECTOR AREA INPUT ADDRESS          
.*                                                                 @L1A         
.*                                  ** VTRINL = PROCESSING **      @L1A         
         IST6MCUA KWVAL=&VTRINL,LOC=RPL6VAIL-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2                 @D1C         
.*                                     STORE VECTOR AREA INPUT LENGTH           
.*                                                                 @L1A         
         AGO   .RXVOUT                                             @D1C         
.RXERVI  ANOP                                                 @L1A @Y1M         
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C         
.RXVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED       @D1A         
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).RXREQ   @D1A         
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).RXERVO   @D1A         
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X        
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                @D1A         
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X        
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                @D1A         
         AGO   .RXREQ                                              @D1A         
.RXERVO  ANOP                                                      @D1A         
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A         
.RXREQ   ANOP                       ** MACRO REQUEST TYPE **                    
         AIF   (NOT &ISTGEN).RXEXIT                                             
         MVI   RPL6REQ-ISTRPL6X(14),RPL6RCVX                                    
*                                      PUT RECVEXPD REQUEST IN RPL6             
.RXEXIT  MEND                                                                   
