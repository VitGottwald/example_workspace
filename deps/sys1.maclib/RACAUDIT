*********START OF SPECIFICATIONS*************************************** 00010000
*                                                                       00020000
*  $MAC(RACAUDIT) COMP(SC1BN) PROD(RACF):                               00030000
*                                                                       00040000
*  DESCRIPTIVE NAME = INTERFACE TO RACF AUDITING FUNCTION               00050000
*                                                                       00060000
* *01* PROPRIETARY STATEMENT=                                         * 00070000
*  **PROPRIETARY_STATEMENT********************************************* 00080000
*                                                                     * 00090000
*                                                                     * 00100000
*   Licensed Materials - Property of IBM                              * 00110000
*   5650-ZOS Copyright IBM Corp. 1981, 2019                           * 00120000
*                                                                     * 00130000
*   STATUS= HBB77C0                                                   * 00140000
*                                                                     * 00150000
*  **END_OF_PROPRIETARY_STATEMENT************************************** 00160000
*                                                                    *  00170000
*01* EXTERNAL CLASSIFICATION:                                        *  00180000
*02* NONE:                                                           *  00190000
*01* END OF EXTERNAL CLASSIFICATION:                                 *  00200000
*                                                                    *  00210000
*  FUNCTION:                                                            00220000
*    THE RACAUDIT MACRO PROVIDES A STANDARD INTERFACE TO THE RACF       00230000
*    AUDITING ROUTINE ICHAUD00                                          00240000
*                                                                       00250000
*  INVOCATION:                                                          00260000
*    THE RACF AUDIT ROUTINE IS INVOKED BY THE RACROUTE REQUEST=AUDIT    00270000
*    MACRO.                                                             00280000
*                                                                       00290000
*   REQUIREMENTS FOR USE - USER MUST BE APF AUTHORIZED, SUP STATE       00300000
*                          OR SYSTEM KEY (0-7)                          00310000
*                                                                       00320000
*  MODULE TYPE:                                                         00330000
*    MACRO                                                              00340000
*                                                                       00350000
*  CHANGE ACTIVITY:                                                     00360000
*   $L0= VTAMSLS RACF/190 880803 PDPWB1: VTAM SESSION SECURITY     @L0A 00370000
*   $P1= PGW0043 RACF/190 881102 PDPWB1: PGW0043                   @P1A 00380000
*   $P2= PGW0045 RACF/190 881102 PDPWB1: PGW0045                   @P2A 00390000
*   $P3= PGW0042 RACF/190 881102 PDPWB1: PGW0042                   @P3A 00400000
*   $P4= PGP0408 RACF/190 890721 PDPWB1: PGP0408                   @P4A 00410000
*   $D1= DCR0077 RACF/190 890814 PDJCD1: DCR0077                   @D1A 00420000
*   $P5= PGP0550 RACF/190 890915 PDB_S1: PGP0550                   @P5A 00430000
*   $P6= PI90009 RACF/190 891010 PDB_S1: PI90009                   @P6A 00440000
*   $L1= RELID   RACF/192 900904 PDAHW1: SUPPORT FOR RACF 1.9.2    @L1A 00450000
*   $P7= PJA0029 RACF/192 910524 PDPSB1: PJA0029                   @P7A 00460000
*   $L2= OW04015 RACF/210 930426 PDKAF1: SUPPORT FOR RACF 2.1      @L2A 00470000
*   $L3= OW10709 RACF/220 940509 PDJMV1: SUPPORT FOR RACF 2.2      @L3A 00480000
*   $L4= RELID   RACF/230 960422 PDJMV1: SUPPORT FOR RACF 2.3      @L4A 00490000
*   $P8= PQC1216 RACF/230 960801 PDBCC1: PQC1216                   @P8A 00500000
*   $L5= RELID   RACF/240 960904 PDJMV1: RELEASE ID SUPPORT 2.4    @L5A 00510000
*   $L6= RELID   RACF/260 970728 PDCAM1: RELEASE ID FOR HRF2260    @L6A 00520000
*   $L7= RELID   HRF2608  980624 PDRDS2: RELEASE ID FOR HRF2608    @L7A 00530000
*   $L8= RELID   HRF7703  990429 PDRDS2: RELEASE ID FOR HRF7703    @L8A 00540000
*   $L9= RELID   HRF7705  000404 PDIFS2: RELEASE ID FOR HRF7705    @L9A 00550000
*   $LA= RELID   HRF7706  010430 PDRDC1: RELEASE ID FOR HRF7706    @LAA 00560000
*   $LB= RELID   HRF7707  020109 PDMSF1: RELEASE ID FOR HRF7707    @LBA 00570000
*   $LC= RELID   HRF7708  020118 PDMSF1: RELEASE ID FOR HRF7708    @LCA 00580000
*   $LD= RELID   HRF7709  030430 PDMSD1: RELEASE ID FOR HRF7709    @LDA 00590000
*   $LE= RELID   HRF7720  040224 PDRIV1: SUPPORT SAF FMID HBB7720  @LEA 00600000
*   $LF= RELID   HRF7730  050531 PDRIV1: SUPPORT SAF FMID HBB7730  @LFA 00610000
*   $LG= RELID   HRF7740  060330 PDRIV1: SUPPORT SAF FMID HBB7740  @LGA 00620000
*   $LH= RELID   HRF7750  070302 PDRIV1: SUPPORT SAF FMID HBB7750  @LHA 00630000
*   $LI= RELID   HRF7760  080522 PDJLW1: SUPPORT SAF FMID HBB7760  @LIA 00640000
*   $LJ= RELID   HRF7770  090511 PDJLW1: SUPPORT SAF FMID HBB7770  @LJA 00650000
*   $LK= RELID   HRF7780  100512 PDJLW1: SUPPORT SAF FMID HBB7780  @LKA 00660000
*   $LL= RELID   HRF7790  110117 PDJLW1: SUPPORT SAF FMID HBB7790  @LLA 00670000
*   $LM= RELID   HRF77A0  130115 PDAWS1: SUPPORT SAF FMID HBB77A0  @LMA 00680000
*   $LN= RELID   HRF77B0  150508 PDPGP1: Support SAF FMID HBB77B0  @LNA 00690000
*   $LO= RELID   HBB77C0  180530 PDGTM1: ParmList Version Support  @LOA 00700000
*                                                                       00710000
*                                                                       00720000
*                                                                       00730000
*  CHANGE DESCRIPTIONS:                                                 00740000
*   A000000-999999 ORIGINAL CODE                                   @L0A 00750000
*   A - START PLIST ON A WORD BOUNDARY                             @P1A 00760000
*   A - START BAL ON A WORD BOUNDARY                               @P2A 00770000
*   A - ASSEMBLER F COMPATIBILITY                                  @P3A 00780000
*   A - GENERALIZED AUDIT FUNCTION, LOGSTR & RESULT KEYWORDS ADDED @D1A 00790000
*   A - ALLOW PARENS AROUND RELEASE                                @P4A 00800000
*   D - DELETE REFERENCES TO ERTN070 FOR ASSEMBLER F COMPATIBILITY @P5A 00810000
*   C - CORRECT THE PARAMETER LIST RESERVED STORAGE                @P6A 00820000
*   C - ALLOW RACF RELEASE 1.9.2 TO BE SPECIFIED                   @L1A 00830000
*   C - STATUS TO CORRECTLY CORRESPOND TO MVS LEVEL                @P7A 00840000
*   C - ALLOW RACF RELEASE 2.1 TO BE SPECIFIED                     @L2A 00850000
*   C - ALLOW RACF RELEASE 2.2 TO BE SPECIFIED                     @L3A 00860000
*   C - ALLOW RACF RELEASE 2.3 TO BE SPECIFIED                     @L4A 00870000
*   C - CDPI CLASSIFICATION NONE                                   @P8A 00880000
*   A - ALLOW RACF RELEASE 2.4 TO BE SPECIFIED                     @L5A 00890000
*   A - ALLOW RACF RELEASE 2.6 TO BE SPECIFIED FOR HRF2260         @L6A 00900000
*   A - ALLOW RACF RELEASE 2608 TO BE SPECIFIED FOR HRF2608        @L7A 00910000
*   A - ALLOW RACF RELEASE 7703 TO BE SPECIFIED FOR HRF7703        @L8A 00920000
*   A - ALLOW RACF RELEASE 7705 TO BE SPECIFIED FOR HRF7705        @L9A 00930000
*   A - ALLOW RACF RELEASE 7706 TO BE SPECIFIED FOR HRF7706        @LAA 00940000
*   A - ALLOW RACF RELEASE 7707 TO BE SPECIFIED FOR HRF7707        @LBA 00950000
*   A - ALLOW RACF RELEASE 7708 TO BE SPECIFIED FOR HRF7708        @LCA 00960000
*   A - ALLOW RACF RELEASE 7709 TO BE SPECIFIED FOR HRF7709        @LDA 00970000
*   A - ADD SUPPORT FOR SAF RELEASE FMID HBB7720                   @LEA 00980000
*   A - ADD SUPPORT FOR SAF RELEASE FMID HBB7730                   @LFA 00990000
*   A - ADD SUPPORT FOR SAF RELEASE FMID HBB7740                   @LGA 01000000
*   A - ADD SUPPORT FOR SAF RELEASE FMID HBB7750                   @LHA 01010000
*   A - ADD SUPPORT FOR SAF RELEASE FMID HBB7760                   @LIA 01020000
*   A - ADD SUPPORT FOR SAF RELEASE FMID HBB7770                   @LJA 01030000
*   A - ADD SUPPORT FOR SAF RELEASE FMID HBB7780                   @LKA 01040000
*   A - ADD SUPPORT FOR SAF RELEASE FMID HBB7790                   @LLA 01050000
*   A - ADD SUPPORT FOR SAF RELEASE FMID HBB77A0                   @LMA 01060000
*   A - Add support for SAF release FMID HBB77B0                   @LNA 01070000
*   A - Support for Parameter List Version                              01080000
*       - Specify the parameter list version instead of                 01090000
*         the last 4 characters of the FMID by specifying               01100000
*         RELEASE=PLV0001                                          @LOA 01110000
*                                                                       01120000
*                                                                       01130000
*                                                                       01140000
*********END OF SPECIFICATIONS***************************************** 01150000
         MACRO                                                          01160000
&SYMBOL RACAUDIT &ACEE=,                                           @D1C*01170000
               &CLASS=,                                                *01180000
               &ENTITYX=,                                              *01190000
               &EVENT=,                                                *01200000
               &EVQUAL=,                                               *01210000
               &RELEASE=,                                              *01220000
               &RESULT=,                                               *01230000
               &LOGSTR=,                                               *01240000
               &MF=S                                                    01250000
.*  DEFINE LOCAL VARIABLES                                              01260000
         LCLA  &AUDVERS            USED TO CONTAIN VERSION NUMBER       01270000
         LCLA  &BIT0,&BIT1,&BIT2,&BIT3,&BIT4,&BIT5,&BIT6,&BIT7          01280000
         LCLA  &BITA0,&BITA1,&BITA2,&BITA3                              01290000
         LCLA  &BITA4,&BITA5,&BITA6,&BITA7                              01300000
         LCLC  &NX                                                 @P3A 01310000
         LCLC  &EVN000                                             @P3A 01320000
         LCLC  &CLS000                                             @P3A 01330000
.*  DEFINE GLOBAL VARIABLES                                             01340000
         GBLC  &ICHL               LABEL TO START RACAUDIT PLIST        01350000
         GBLC  &ICHMF              AN INDICATOR OF THE MACRO            01360000
.*                                 FORMAT - L, E, S OR M                01370000
         GBLA  &ICHCT              ICHCT - A COUNTER USED TO KEEP       01380000
.*                                 TRACK OF WHERE IN THE PARAMETER      01390000
.*                                 LIST THE PARAMETER IS TO BE          01400000
.*                                 PLACED. ICHCT IS INITIALIZED TO      01410000
.*                                 THE FIRST FULLWORD IN THE LIST       01420000
.*                                 TO BE FILLED IN BY ICHMM,            01430000
.*                                 AND INCREMENTED BY THE INNER         01440000
.*                                 MACRO (ICHMM).                       01450000
         GBLB  &ICHLIST            USED TO TELL THE ICHMM MACRO    @L0A 01460000
.*                                 WHETHER LIST OR EXECUTE PASS    @L0A 01470000
.*                                 IS BEING PROCESSED              @L0A 01480000
         GBLB  &ICHRAC             ICHRAC - A FLAG WHICH IS             01490000
.*                                 INITIALIZED IN THE RACROUTE MACRO    01500000
.*                                 TO COMMUNICATE TO RACXTRT THAT IT    01510000
.*                                 IS BEING INVOKED AS AN INNER MACRO   01520000
.*                                 TO THE RACROUTE MACRO.               01530000
.*                                 EXECUTE PASS.                        01540000
         GBLB  &ICHNTFY            ICHNTFY - A GLOBAL INDICATOR         01550000
.*                                 WHICH IS USED TO COMMUNICATE WITH    01560000
.*                                 ICHMM - THE INNER MACRO. IF ICHNTFY  01570000
.*                                 IS ON, THE INNER MACRO WILL NOT      01580000
.*                                 ISSUE ITS OWN MNOTE IF IT FINDS AN   01590000
.*                                 ERROR-IT WILL MERELY TURN ON THE     01600000
.*                                 ICHINER FLAG AS NOTIFICATION TO THE  01610000
.*                                 OUTER MACRO, WHICH WILL TAKE THE     01620000
.*                                 APPROPRIATE ACTION.                  01630000
.*                                 IF ICHNTFY IS OFF, THE INNER MACRO   01640000
.*                                 WILL TAKE ACTION ON ITS OWN,         01650000
.*                                 IE. ISSUE AN MNOTE WITH A SEVERITY   01660000
.*                                 CODE OF ITS CHOOSING.                01670000
         GBLB  &ICHINER            ICHINER - A GLOBAL INDICATOR         01680000
.*                                 WHICH IS USED BY ICHMM - AN INNER    01690000
.*                                 MACRO - TO TELL THE OUTER MACRO IF   01700000
.*                                 AN ERROR HAS BEEN DETECTED IN A      01710000
.*                                 PARAMETER.  IF ICHINER IS ON, AN     01720000
.*                                 ERROR HAS BEEN DETECTED.             01730000
.********************************************************************** 01740000
.*                     INITIALIZE VARIABLES                           * 01750000
.********************************************************************** 01760000
&NX      SETC  '&SYSNDX'                                                01770000
&ICHNTFY SETB  0                   WANT ICHMM TO ISSUE ERROR MSGS       01780000
&ICHINER SETB  0                   NO ERRORS YET FROM ICHMM             01790000
&AUDVERS SETA  0                   VERSION 0 OF AUDIT PLIST             01800000
.*                                 NOTE: THE MAXIMUM PLIST VERSION      01810000
.*                                   SHOULD BE BE INCREMENTED WHEN      01820000
.*                                   A NEW FIELD IS ADDED (I.E. AS      01830000
.*                                   NEW KEYWORDS ARE ADDED TO THE      01840000
.*                                   MACRO). THE VERSION OF THE PLIST   01850000
.*                                   WILL BE DETERMINED BY THE KEYWORDS 01860000
.*                                   SPECIFIED. FOR EXAMPLE IF THE      01870000
.*                                   MAXIMUM CURRENT VERSION OF THE     01880000
.*                                   PLIST IS 1 BUT NO VERSION 1        01890000
.*                                   KEYWORDS ARE CODED THEN A VERSION  01900000
.*                                   0 PLIST WILL BE GENERATED.         01910000
         AIF   (NOT &ICHRAC).ERR010 RACAUDIT MUST BE INVOKED VIA        01920000
.*                                   RACROUTE REQUEST=AUDIT             01930000
.ERRTN10 ANOP                      RETURN POINT FROM ERROR 10      @P3C 01940000
.********************************************************************** 01950000
.*  VALIDATE RELEASE AND MF                                           * 01960000
.********************************************************************** 01970000
.****  ERROR INVALID RELEASE                                       @L2C 01980000
         AIF   ('&RELEASE(1)' NE '1.9' AND '&RELEASE(1)' NE            X01990000
                '1.9.2' AND '&RELEASE(1)' NE '2.1'                     X02000000
                AND '&RELEASE(1)' NE '2.2'                             X02010000
                AND '&RELEASE(1)' NE '2.3'                             X02020000
                AND '&RELEASE(1)' NE '2.4'                             X02030000
                AND '&RELEASE(1)' NE '2.6'                             X02040000
                AND '&RELEASE(1)' NE '2608'                            X02050000
                AND '&RELEASE(1)' NE '7703'                            X02060000
                AND '&RELEASE(1)' NE '7705'                            X02070000
                AND '&RELEASE(1)' NE '7706'                            X02080000
                AND '&RELEASE(1)' NE '7707'                            X02090000
                AND '&RELEASE(1)' NE '7708'                            X02100000
                AND '&RELEASE(1)' NE '7709'                            X02110000
                AND '&RELEASE(1)' NE '7720'                            X02120000
                AND '&RELEASE(1)' NE '7730'                            X02130000
                AND '&RELEASE(1)' NE '7740'                            X02140000
                AND '&RELEASE(1)' NE '7750'                            X02150000
                AND '&RELEASE(1)' NE '7760'                            X02160000
                AND '&RELEASE(1)' NE '7770'                            X02170000
                AND '&RELEASE(1)' NE '7780'                            X02180000
                AND '&RELEASE(1)' NE '7790'                            X02190000
                AND '&RELEASE(1)' NE '77A0'                            X02200000
                AND '&RELEASE(1)' NE '77B0'                            X02210000
                AND '&RELEASE(1)' NE 'PLV0001').ERR015             @LOC 02220000
.ERRTN15 ANOP                                                      @P3C 02230000
         AIF   ('&MF' NE 'L' AND '&MF' NE 'S' AND '&MF(1)' NE 'M' AND  X02240000
               '&MF(1)' NE 'E').ERR020      MF MUST BE S, L, M OR E     02250000
.*                                    NOTE: RACROUTE MACRO WILL CONVERT 02260000
.*                                          MF=M TO MF=E IN &ICHMF      02270000
.*                                          BEFORE CALLING RACAUDIT     02280000
.ERRTN20 ANOP                      RETURN POINT FROM ERROR 20      @P3C 02290000
         AIF   ('&ICHMF' EQ 'E' AND (N'&MF NE 2 OR                     X02300000
               '&MF(2)' EQ '')).ERR030   MF=E MUST HAVE PLIST ADDR PARM 02310000
.ERRTN30 ANOP                      RETURN POINT FROM ERROR 30      @P3C 02320000
         AIF   ('&ICHMF' NE 'S').SKIPCHK SKIP CHECK FOR REQUIRED   @P3C 02330000
.*                                      MF=S PARMS IF NOT MF=S          02340000
.********************************************************************** 02350000
.*        MAKE SURE ALL REQUIRED PARMS SUPPLIED FOR MF=S              * 02360000
.********************************************************************** 02370000
.*                                                                1@P5D 02380000
         AIF   ('&EVENT' EQ '').ERR050    EVENT REQUIRED                02390000
         AIF   ('&EVQUAL' EQ '').ERR060   EVQUAL REQUIRED               02400000
.*                                                                1@P5D 02410000
.ERRTN50 ANOP                             RETURN FROM ERROR 50     @P3C 02420000
.ERRTN60 ANOP                             RETURN FROM ERROR 60     @P3C 02430000
.*                                                                 @D1D 02440000
         CNOP  0,4                        ALIGN BAL TO WORD BNDY   @P2C 02450000
         BAL   1,ICH&NX.A                 BRANCH AROUND PLIST, SET R1   02460000
.SKIPCHK ANOP                                                      @P3C 02470000
.********************************************************************** 02480000
.*        SET UP FOR LITERALS IF ANY SUPPLIED                         * 02490000
.********************************************************************** 02500000
&EVN000  SETC  '&EVENT'                                                 02510000
         AIF   ('&EVENT' EQ '').SKIPLT1          SKIP IF NO EVENT  @P3C 02520000
         AIF   ('&EVENT'(1,1) NE '''').SKIPLT1   SKIP NOT LITERAL  @P3C 02530000
&EVN000  SETC  'ICH&NX.E'                 SUBSTITUTE LABEL OF LITERAL   02540000
.*                                         THAT WILL BE GENERATED       02550000
.SKIPLT1 ANOP                                                      @P3C 02560000
&CLS000  SETC  '&CLASS'                                                 02570000
         AIF   ('&CLASS' EQ '').SKIPLT2          SKIP IF NO CLASS  @P3C 02580000
         AIF   ('&CLASS'(1,1) NE '''').SKIPLT2   SKIP NOT LITERAL  @P3C 02590000
&CLS000  SETC  'ICH&NX.C'                 SUBSTITUTE LABEL OF LITERAL   02600000
.*                                         THAT WILL BE GENERATED       02610000
.SKIPLT2 ANOP                                                      @P3C 02620000
         AIF   ('&ICHMF' EQ 'E').MFECONS IF MF=E GO GENERATE            02630000
.*                                           LITERAL CONSTANTS          02640000
.********************************************************************** 02650000
.*                                                                    * 02660000
.*             GENERATE LIST PASS FOR MF=L AND MF=S                   * 02670000
.*                                                                    * 02680000
.********************************************************************** 02690000
&ICHLIST SETB  1                          INDICATE TO ICHMM THAT LIST   02700000
.*                                        PASS IS BEING PROCESSED       02710000
.*    ***************************************************************** 02720000
.*    **   SET VERSION AND LENGTH                                    ** 02730000
.*    ***************************************************************** 02740000
&ICHL    DS    0F                         START OF RACAUDIT PLIST  @P1C 02750000
ICH&NX.B DC    AL2(&AUDVERS)              VERSION                       02760000
         DC    AL2(ICH&NX.F-ICH&NX.B)     LENGTH                        02770000
.*    ***************************************************************** 02780000
.*    **   EVENT KEYWORD                                             ** 02790000
.*    ***************************************************************** 02800000
          ICHMM &EVN000                                                 02810000
.*    ***************************************************************** 02820000
.*    **   EVQUAL KEYWORD                                            ** 02830000
.*    ***************************************************************** 02840000
         AIF   ('&EVQUAL' NE '').CHKREG1  NO EVQUAL, SKIP DC(0)    @P3C 02850000
         DC    AL2(0)                     EVENT QUALIFIER               02860000
         AGO   .RESFLD                    GO DECLARE RESERVED FIELD     02870000
.CHKREG1 ANOP                             SEE IF REGISTER PASSED   @P3C 02880000
         AIF   ('&EVQUAL'(1,1) EQ '(').REGFRM1  IF REGISTER PASSED @P3C 02890000
.*                                        SKIP OVER LITERAL DC          02900000
         DC    AL2(&EVQUAL)               EVENT QUALIFIER               02910000
         AGO   .RESFLD                    GO DECLARE RESERVED FIELD     02920000
.REGFRM1 ANOP                             REGISTER FORM USED       @P3C 02930000
         AIF   ('&ICHMF' EQ 'L').ERR080   REGISTER ILLEGAL FOR MF=S     02940000
.ERRTN80 ANOP                             RETURN FROM ERROR 75     @P3C 02950000
         DC    AL2(0)                     EVENT QUALIFIER               02960000
.RESFLD  ANOP                                                           02970000
         DC    AL2(0)                     RESERVED                      02980000
.*    ***************************************************************** 02990000
.*    **   CLASS KEYWORD                                             ** 03000000
.*    ***************************************************************** 03010000
         ICHMM &CLS000                                                  03020000
.*    ***************************************************************** 03030000
.*    **   ENTITYX KEYWORD                                           ** 03040000
.*    ***************************************************************** 03050000
         ICHMM &ENTITYX                                                 03060000
.*    ***************************************************************** 03070000
.*    **   ACEE KEYWORD                                              ** 03080000
.*    ***************************************************************** 03090000
         ICHMM &ACEE                                                    03100000
.*    ***************************************************************** 03110000
.*    **   LOGSTR KEYWORD                                        @D1A** 03120000
.*    ***************************************************************** 03130000
         ICHMM &LOGSTR                                           @D1A   03140000
.*    ***************************************************************** 03150000
.*    **   VALIDATE AND GENERATE RESULT BYTE                     @D1A** 03160000
.*    ***************************************************************** 03170000
         AIF   ('&RESULT' EQ '' OR '&RESULT' EQ 'SUCCESS').NORES @D1A   03180000
         AIF   ('&RESULT' NE 'FAILURE').RESERR                   @D1A   03190000
.*                                                                      03200000
         DC    AL1(1)            RESULT OF FAILURE SPECIFIED     @D1A   03210000
         DC    AL3(0)            RESERVED                        @D1A   03220000
         AGO   .CONT1                                            @D1A   03230000
.RESERR  ANOP                                                    @D1A   03240000
         AGO   .ERR090                                           @D1A   03250000
.ERRTN90 ANOP                                                    @D1A   03260000
.NORES   ANOP                                                    @D1A   03270000
         DC    AL1(0)            RESULT BYTE                     @D1A   03280000
         DC    AL3(0)            RESERVED                        @D1A   03290000
         AGO   .CONT1                                            @D1A   03300000
.CONT1   ANOP                                                    @D1A   03310000
         DC    4F'0'                      RESERVED               @P6C   03320000
ICH&NX.F DS    0H                         END OF RACAUDIT PLIST         03330000
         AGO   .MFLCONS           GO GENERATE CONSTANTS FOR MF=L,S @P3C 03340000
.********************************************************************** 03350000
.*        GENERATE LITERAL CONSTANTS IF NEEDED                        * 03360000
.********************************************************************** 03370000
.MFECONS ANOP                        MF=E CONSTANT GENERATION           03380000
         B     ICH&NX.A                   BRANCH AROUND CONSTANTS       03390000
.MFLCONS ANOP                        MF=L,S CONSTANT GENERATION    @P3C 03400000
         AIF   ('&EVENT' EQ '').SKIPLT3          SKIP IF NO EVENT  @P3C 03410000
         AIF   ('&EVENT'(1,1) NE '''').SKIPLT3   SKIP NOT LITERAL  @P3C 03420000
ICH&NX.E DC    CL8&EVENT                  EVENT NAME                    03430000
.SKIPLT3 ANOP                                                      @P3C 03440000
         AIF   ('&CLASS' EQ '').SKIPLT4          SKIP IF NO CLASS  @P3C 03450000
         AIF   ('&CLASS'(1,1) NE '''').SKIPLT4   SKIP IF LITERAL   @P3C 03460000
ICH&NX.C DC    CL8&CLASS                  CLASS NAME                    03470000
.SKIPLT4 ANOP                                                      @P3C 03480000
         AIF   ('&ICHMF' EQ 'L').MEND     FINISHED IF IF MF=L           03490000
.********************************************************************** 03500000
.*                                                                    * 03510000
.*               GENERATE EXECUTE PASS FOR FOR MF=S,M,E               * 03520000
.*                                                                    * 03530000
.********************************************************************** 03540000
         AIF   ('&ICHMF' NE 'S').LOADADR IF MF NOT S GO LOAD ADDR  @P3C 03550000
.*                                       OF PLIST INTO R1               03560000
ICH&NX.A LA    1,0(1)                    CLEAR HIGH BYTE/BIT OF REG 1   03570000
         AGO   .SKPADDR                SKIP LOADING ADDR OF PLIST  @P3C 03580000
.LOADADR ANOP                                                      @P3C 03590000
ICH&NX.A A     1,36(1)                   ADDRESS OF AUDIT PLIST IN R1   03600000
.SKPADDR ANOP                                                      @P3C 03610000
&ICHLIST SETB  0                     INDICATE TO ICHMM THAT EXECUTE     03620000
.*                                   OR MODIFY IS BEING PROCESSED       03630000
&ICHCT   SETA  1                     INDICATE TO ICHMM THAT NEXT        03640000
.*                                   FIELD IS 1 WORD OFF OF R1          03650000
.*    ***************************************************************** 03660000
.*    **   EVENT KEYWORD                                             ** 03670000
.*    ***************************************************************** 03680000
         ICHMM &EVN000                                                  03690000
.*    ***************************************************************** 03700000
.*    **   EVQUAL KEYWORD                                            ** 03710000
.*    ***************************************************************** 03720000
         AIF   ('&EVQUAL' EQ '').CLSKEY   NO EVQUAL, TRY CLASS     @P3C 03730000
         AIF   ('&EVQUAL'(1,1) EQ '(').REGFRM2  IF REGISTER PASSED @P3C 03740000
.*                                        SKIP OVER LITERAL STORE       03750000
         LA    15,&EVQUAL                 EVENT QUALIFIER               03760000
         STH   15,8(1)                    EVENT QUALIFIER               03770000
         AGO   .CLSKEY                    GO PROCESS CLASS KEY     @P3C 03780000
.REGFRM2 ANOP                             REGISTER FORM USED       @P3C 03790000
         STH   &EVQUAL(1),8(1)            EVENT QUALIFIER               03800000
.CLSKEY  ANOP                                                      @P3C 03810000
&ICHCT   SETA  &ICHCT+1                  BUMP ICHMM TO NEXT WORD OFFSET 03820000
.*    ***************************************************************** 03830000
.*    **   CLASS KEYWORD                                             ** 03840000
.*    ***************************************************************** 03850000
         ICHMM &CLS000                                                  03860000
.*    ***************************************************************** 03870000
.*    **   ENTITYX KEYWORD                                           ** 03880000
.*    ***************************************************************** 03890000
         ICHMM &ENTITYX                                                 03900000
.*    ***************************************************************** 03910000
.*    **   ACEE KEYWORD                                              ** 03920000
.*    ***************************************************************** 03930000
         ICHMM &ACEE                                                    03940000
.*    ***************************************************************** 03950000
.*    **   LOGSTR KEYWORD                                        @D1A** 03960000
.*    ***************************************************************** 03970000
         ICHMM &LOGSTR                                                  03980000
.*                                                                      03990000
         AIF   ('&ICHMF' EQ 'S').MEND                            @D1A   04000000
.*                                                                      04010000
         AIF   ('&RESULT' EQ '').CONT1S                          @D1A   04020000
         AIF   ('&RESULT' EQ 'SUCCESS').NORES1                   @D1A   04030000
         AIF   ('&RESULT' NE 'FAILURE').RESERR1                  @D1A   04040000
.*                                                                      04050000
         LA    15,1              RESULT OF FAILURE SPECIFIED     @D1A   04060000
         STC   15,28(1)          RESERVED                        @D1A   04070000
         AGO   .CONT1S                                           @D1A   04080000
.RESERR1 ANOP                                                    @D1A   04090000
         AGO   .ERR095                                           @D1A   04100000
.ERRTN95 ANOP                                                    @D1A   04110000
.NORES1  ANOP                                                    @D1A   04120000
         LA    15,0              RESULT BYTE                     @D1A   04130000
         STC   15,28(1)          RESERVED                        @D1A   04140000
.CONT1S  ANOP                                                    @D1A   04150000
         AGO   .MEND                      FINISHED WITH EXECUTE PATH    04160000
.********************************************************************** 04170000
.*        ERROR PROCESSING                                            * 04180000
.********************************************************************** 04190000
.ERR010  ANOP                                                           04200000
         MNOTE 12,'*** RACAUDIT MUST BE INVOKED VIA RACROUTE'           04210000
         AGO   .ERRTN10                                            @P3C 04220000
.ERR015  ANOP                                                           04230000
         MNOTE 12,'*** INVALID RELEASE SPECIFIED FOR KEYWORDS'          04240000
         AGO   .ERRTN15                                            @P3C 04250000
.ERR020  ANOP                                                           04260000
         MNOTE 12,'*** MF MUST BE S, L, E OR M'                         04270000
         AGO   .MEND                                                    04280000
.ERR030  ANOP                                                           04290000
         MNOTE 12,'*** INVALID PLIST ADDR SUPPLIED FOR MF'              04300000
         AGO   .ERRTN30                                           3@P5D 04310000
.ERR050  ANOP                                                           04320000
         MNOTE 12,'*** EVENT REQUIRED ON MF=S'                          04330000
         AGO   .ERRTN50                                            @P3C 04340000
.ERR060  ANOP                                                           04350000
         MNOTE 12,'*** EVQUAL REQUIRED ON MF=S'                         04360000
         AGO   .ERRTN60                                           4@P5D 04370000
.ERR080  ANOP                                                           04380000
         MNOTE 12,'*** REGISTER NOT ALLOWED ON LIST FORM'               04390000
         AGO   .ERRTN80                                            @P3C 04400000
.ERR090  ANOP                                                      @D1A 04410000
         MNOTE 12,'*** IF RESULT IS SPECIFIED, VALID OPERANDS ARE SUCCEX04420000
               SS, OR FAILURE'                                     @D1A 04430000
         AGO   .ERRTN90                                            @D1A 04440000
.ERR095  ANOP                                                      @D1A 04450000
         MNOTE 12,'*** IF RESULT IS SPECIFIED, VALID OPERANDS ARE SUCCEX04460000
               SS, OR FAILURE'                                     @D1A 04470000
         AGO   .ERRTN95                                            @D1A 04480000
.MEND    MEND                               EL FIN                      04490000
                                                                        04500000
