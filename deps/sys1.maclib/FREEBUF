*/*PROPRIETARY V3 STATEMENT                               */            00004400
*/*LICENSED MATERIALS - PROPERTY OF IBM                   */            00006600
*/*"RESTRICTED MATERIALS OF IBM"                          */            00008800
*/*5695-DF1                                               */            00011000
*/*COPYRIGHT 1992, 2011 IBM CORP.                         */            00013800
*/*END PROPRIETARY V3 STATEMENT                           */            00016600
         MACRO                                                          00020000
.* CHANGE ACTIVITY                                                      00022600
.*$L1=31BIT,JDZ1110,,SJPLRK:  31-BIT CALLER SUPPORT                @L1A 00025200
.*$L2=64BIT,JDZ1D10,20100303,SVLJH: 64 bit message                 @L2A 00027800
&NAME    FREEBUF &DCB,&REG                                         @L2C 00030400
         GBLC  &SYSAM64    GLOBAL VARIABLE SET BY SYSSTATE MACRO   @L2A 00033300
         GBLC  &SYSASCE    GLOBAL VARIABLE SET BY SYSSTATE MACRO   @L2A 00036600
         LCLB  &NOGO                                                    00040000
         SYSSTATE TEST    TEST HOW MACROS ARE TO EXPAND            @L2A 00055800
         AIF   ('&SYSAM64' NE 'YES').ChkAR   Go if not AMODE64     @L2A 00060400
         IGGERMAC 203,FREEBUF     FREEBUF DOES NOT SUPPORT AMODE64 @L2A 00065000
&NOGO    SETB  1                   Set error switch                @L2A 00069600
.ChkAR   AIF   (('&SYSASCE' EQ '') OR ('&SYSASCE' EQ 'P')).ChkDCB  @L2A 00074200
         IGGERMAC 204,FREEBUF      FREEBUF doesn't support non-P   @L2A 00078800
&NOGO    SETB  1                   Set error switch                @L2A 00083400
.ChkDCB  AIF   ('&DCB' NE '').R1   Go if DCB coded                 @L2C 00088000
         IHBERMAC 06                                               @L2M 00092600
&NOGO    SETB  1                   Set error switch                @L2M 00097200
.R1      AIF   ('&REG' NE '').R2   Go if the return register coded @L2C 00101800
         IHBERMAC 18                                               @L2M 00106400
&NOGO    SETB  1                   Set error switch                @L2M 00111000
.R2      AIF   ('&REG(1)' NE '0' AND                                   *00115600
               '&REG(1)' NE '1' AND '&REG(1)' NE '14').R3          @L2C 00120200
         IHBERMAC 37,,&REG                                         @L2M 00124800
&NOGO    SETB  1                   Set error switch                @L2A 00129400
.R3      AIF   (&NOGO EQ 1).TERM   Go if any error found                00134000
&NAME    IHBINNRA &DCB                                                  00140000
         SLR   14,14                    PREPARE FOR ICM            @L1A 00156000
         ICM   14,7,21(1)               LOAD BUFCB ADDRESS         @L1C 00172000
         MVC   0(4,&REG(1)),0(14)       ATTACH BUFFER TO CHAIN          00188000
         ST    &REG(1),0(0,14)          PUT BUFFER ADDRESS IN BUFCB     00204000
         MEXIT                                                          00220000
.TERM    MEND                                                           40320000
