         MACRO                                                                  
&NAME    PICTURE &ID=,&GATHER=,&REPORT=,&MAXRBS=,&RBLEN=,&RPTTYP=,     X        
               &TITLE=,&DGTEXT=,&DRTEXT=,&PFK=,&FBLEN=0,&IOML=0,       X        
               &HELP=,&WLMMODE=0,&STYP=                            @H1A         
.*                                                                 @61A         
.*   PROPRIETARY STATEMENT:                                        @61A         
.*        Licensed Materials - Property of IBM                     @61A         
.*        "Restricted Materials of IBM"                            @61A         
.*        Copyright IBM Corp. 1993, 2013                           @61A         
.*        Status: z/OS V2R1 RMF (HRM7790)                          @61A         
.*                                                                 @61A         
.*   $N1=MON2,HRM3102,,FLS: RMF 3.1 SMF RECORD RESTRUCTURE         @N1A         
.*   $P1=IOMLSP,HRM3302,,WEY: I/O MEASUREMENT LEVEL SUPPORT        @P1A         
.*   $ZA=SPEV4,@WA05515,,HOL: USABILITY ENHANCEMENTS - ISPF        @ZAA         
.*   $C1=R510,HRM5512,,GBO: USE ALIAS NAME                         @C1A         
.*   $ZB=SPEV4,@WA05515,,HOL: USABILITY ENHANCEMENTS V5 - ISPF     @ZBA         
.*   $H1=HFS,HRM6607,,SHR: HFS support                             @H1A         
.*   $61=R790,HRM7790,,MWe: explicit copyright for external macro  @61A         
.*                                                                              
         LCLC  &RBNUM,&IDX,&DESC,&GTXT,&RTXT,&PFKNUM               @LBA         
&IDX     SETC  '&SYSNDX'                                                        
         AIF   ('&RPTTYP' EQ 'T').TYPOK                                         
         AIF   ('&RPTTYP' EQ 'R').TYPOK                                         
         AGO   .TYPNG                                                           
.TYPOK   ANOP                                                                   
&STYPV   SETA  00                Default all sessions              @H1A         
         AIF   ('&STYP' EQ '').ALLSES                              @H1A         
         AIF   ('&STYP' EQ 'ALL').ALLSES                           @H1A         
         AIF   ('&STYP' EQ 'IO').IONLY                             @H1A         
         AGO   .BADSES           Bad session typ                   @H1A         
.IONLY   ANOP                                                      @H1A         
&STYPV   SETA  80                ISPF session only                 @H1A         
.ALLSES  ANOP                    All sessions is default           @H1A         
         AIF   ('&ID' EQ '').MSNGID                                             
         AIF   ('&GATHER' EQ '').NOGATH                                         
         AIF   ('&REPORT' EQ '').NOREPT                                         
         AIF   ('&MAXRBS' NE '').MAXRBDF                                        
         AIF   ('&RPTTYP' EQ 'R').ROWRPT                                        
&RBNUM   SETC  '0'               TABLE REPORT - LET RMF SET DEFAULT             
         AGO   .RBDONE                                                          
.ROWRPT  ANOP                                                                   
&RBNUM   SETC  '1'               ROW REPORT - DEFAULT MAX RB'S TO ONE           
         AGO   .RBDONE                                                          
.MAXRBDF ANOP                                                                   
&RBNUM   SETC  '&MAXRBS'                                                        
.RBDONE  ANOP                                                                   
         AIF   ('&RBLEN' EQ '').NORBLEN                                         
         AIF   ('&TITLE' NE '').SETDESC                                         
&DESC    SETC  ''' '''                                                          
         AGO   .TITLEOK                                                         
.SETDESC ANOP                                                                   
&DESC    SETC  '&TITLE'                                                         
.TITLEOK ANOP                                                                   
         AIF   ('&PFK' NE '').SETPFK  PFK NUMBER - DEFAULT IS 0    @LBA         
&PFKNUM  SETC  '0'                SET DEFAULT PFK NUMBER TO 0      @LBA         
         AGO   .PFKDONE                                            @ZAC         
.SETPFK  ANOP                                                      @LBA         
&PFKNUM  SETC  '&PFK'             ASSIGN PFK NUMBER                @LBA         
.PFKDONE ANOP                                                      @LBA         
         AIF   ('&HELP' NE '').SETHELP   HELP PANEL NAME SPECIFIED @ZAA         
&HELPP   SETC  '*'                       SET DEFAULT PANEL TO '*'  @ZAA         
         AGO   .HELPDON                                            @ZAA         
.SETHELP ANOP                                                      @ZAA         
&HELPP   SETC  '&HELP'                                             @ZAA         
.HELPDON ANOP                                                      @ZAA         
&GTXT    SETC  '&DGTEXT'                                                        
&RTXT    SETC  '&DRTEXT'                                                        
&NAME    DC    AL2(ERBL&IDX-*)    LENGTH OF THIS MENU ENTRY        @ZAC         
         DC    CL8'&ID'           MENU ID FOR PICTURE                           
         DC    CL8'&GATHER'       DATA GATHERER MODULE NAME                     
         DC    CL8'&REPORT'       DATA REPORTER MODULE NAME                     
         DC    AL2(&RBNUM)        MAX NUMBER OF RELOCATE BLOCKS                 
         DC    AL2(&RBLEN)        LENGTH OF EACH RELOCATE BLOCK                 
         DC    AL2(&FBLEN)        LENGTH OF DATA CONTROL SECTION   @N1A         
         DC    AL2(&PFKNUM)       PFK NUMBER                       @LBA         
         DC    C'&RPTTYP'         T - TABLE REPORT,R - ROW REPORT               
         DC    AL1(&IOML)         I/O MESUREMENT LEVEL             @P1C         
         DC    AL1(&WLMMODE)      WORKLOAD MANAGER MODE            @ZBA         
*                                 0 = COMPAT MODE                  @ZBA         
*                                 1 = GOAL MODE                    @ZBA         
         DC    XL1'&STYPV'        Session Type                     @H1A         
*                                 00 = All sessions                @H1A         
*                                 80 = ISPF session only           @H1A         
         DC    AL2(ERB@&IDX-*-2)  PICTURE TITLE LENGTH                          
         DC    C&DESC             PICTURE TITLE                                 
ERB@&IDX EQU   *                                                                
         DC    AL2(ERB#&IDX-*-2)  DATA GATHERER TEXT LENGTH                     
         AIF   ('&GTXT' EQ '').NOGTXT                                           
         DC    C&GTXT             DATA GATHERER COMMAND TEXT                    
.NOGTXT  ANOP                                                                   
ERB#&IDX EQU   *                                                                
         DC    AL2(ERB$&IDX-*-2)  DATA REPORTER TEXT LENGTH                     
         AIF   ('&RTXT' EQ '').NORTXT                                           
         DC    C&RTXT             DATA REPORTER TEXT                            
.NORTXT  ANOP                                                                   
ERB$&IDX EQU   *                                                                
         DC    CL8'&HELPP'        HELP PANEL FOR M-II ISPF SESSION @ZAA         
ERBL&IDX EQU   *                  LABEL TO BUILD TOTAL LENGTH      @ZAA         
         MEXIT                                                                  
.MSNGID  ANOP                                                                   
         MNOTE 8,'PICTURE ID MUST BE SPECIFIED'                                 
         MEXIT                                                                  
.NOGATH  ANOP                                                                   
         MNOTE 8,'DATA GATHERER MODULE NAME MUST BE SPECIFIED'                  
         MEXIT                                                                  
.NOREPT  ANOP                                                                   
         MNOTE 8,'DATA REPORTER MODULE NAME MUST BE SPECIFIED'                  
         MEXIT                                                                  
.TYPNG   ANOP                                                                   
         MNOTE 8,'REPORT TYPE &RPTTYP NOT DEFINED'                              
         MEXIT                                                                  
.NORBLEN ANOP                                                                   
         MNOTE 8,'RELOCATE BLOCK LENGTH NOT DEFINED'                            
         MEXIT                                                 @PQQ0020         
.BADPFK  ANOP                                                                   
         MNOTE 8,'PFK KEYWORD &PFK NOT RECOGNIZED'                              
         MEXIT                                                     @H1A         
.BADSTYP ANOP                                                      @H1A         
         MNOTE 8,'STYP KEYWORD &STYP NOT RECOGNIZED'               @H1A         
         MEND                                                                   
                                                                                
