*/* COPYRIGHT = 5650-ZOS                                             */ 00050000
*/*            COPYRIGHT IBM CORP. 1988, 2015                        */ 00100000
*/*            LICENSED MATERIALS - PROPERTY OF IBM                  */ 00200000
*/*                                                                  */ 00350000
*/* STATUS  = HBB77A0                                                */ 00370000
*/*                                                                  */ 00390000
*/*01* EXTERNAL CLASSIFICATION: PI                                   */ 00410000
*/*01* END OF EXTERNAL CLASSIFICATION:                               */ 00430000
*/*                                                                  */ 00450000
*/* CHANGE ACTIVITY =                                                */ 00500000
*/*   $L1 =    AR    HBB3310 850901 PDXB: AR SUPPORT                 */ 00550000
*/*   $L2= 64BITCSV  HBB7703 981204 PD00XB: ESAME                    */ 00575000
*/*   $01= OW57320   HBB7706 021114 PD00XB: AMODE64OK                */ 00587500
*/*   $L4= ARALETS   HBB7770 090501 PDXB: ALETS FOR AMODE 64 AR-MODE */ 00587600
*/*   $L5= ME26814   HBB77A0 131031 PDXB: PLISTARALETS               */ 00587709
*%/*                                                                    00600000
         MACRO                                                          00650000
&NAME    LINKX &EP=,&EPLOC=,&DE=,&PARAM=,&DCB=,&VL=,&ID=,&SF=I,        X00700000
               &MF=I,&ERRET=,&HIARCHY=,&LSEARCH=,&PLIST8=,&PLIST4=,    X00733300
               &AMODE64OK=,&PLIST8ARALETS=,&PLISTARALETS=SYSTEM    @L5C 00766604
         GBLB  &IHBSWA,&IHBSWB                                @G381P2A  00800000
         GBLC  &SYSAM64              AMODE 64                      @L2A 00801500
         GBLC  &SYSPLIST8            8-BYTE PARAMETER LIST         @L2A 00803000
         GBLC  &SYSAMODE64OK         AMODE64 TARGET IS OK          @L3A 00803700
         GBLC  &SYSPLIST8ARALETS                                   @L4A 00803800
         GBLC  &SYSPLISTARALETS                                    @L5A 00803904
         LCLC  &SAVEPLIST8           SAVED VALUE                   @L2A 00804500
         LCLC  &SAVEAMODE64OK        SAVED VALUE                   @L3A 00805200
         LCLC  &SAVEPLIST8ARALETS                                  @L4A 00805300
         LCLC  &SAVEPLISTARALETS                                   @L5A 00805404
         SYSSTATE TEST                                             @L2A 00806000
&SAVEPLIST8 SETC '&SYSPLIST8'                                      @L2A 00807500
&SYSPLIST8 SETC ''                                                 @L2A 00809000
&SAVEAMODE64OK SETC '&SYSAMODE64OK'                                @L3A 00809500
&SYSAMODE64OK SETC ''                                              @L3A 00810000
&SAVEPLIST8ARALETS SETC '&SYSPLIST8ARALETS'                        @L4A 00810100
&SAVEPLISTARALETS SETC '&SYSPLISTARALETS'                          @L5A 00810204
.********************************************************************** 00811600
.*   VALIDATE PLIST4, PLIST8                                          * 00812000
.********************************************************************** 00813500
         AIF   ('&PLIST8' EQ '').PLIST8END                         @L2A 00815000
         AIF   ('&PLIST8' EQ 'YES' OR '&PLIST8' EQ 'NO').PLIST8END @L2A 00816500
         IHBERMAC 1001,PLIST8,&PLIST8                              @L2A 00818000
         AGO   .EXIT                                               @L2A 00819500
.PLIST8END ANOP                                                    @L2A 00821000
         AIF   ('&PLIST4' EQ '').PLIST4END                         @L2A 00822500
         AIF   ('&PLIST4' EQ 'YES' OR '&PLIST4' EQ 'NO').PLIST4END @L2A 00824000
         IHBERMAC 1001,PLIST4,&PLIST4                              @L2A 00825500
         AGO   .EXIT                                               @L2A 00827000
.PLIST4END ANOP                                                    @L2A 00828500
         AIF   ('&PLIST8' EQ '' OR '&PLIST4' EQ '').PLIST84END     @L2A 00830000
         IHBERMAC 1304,PLIST4,PLIST8                               @L2A 00831500
.PLIST84END ANOP                                                   @L2A 00833000
         AIF   ('&PLIST8' EQ 'YES').YESPL8                         @L2A 00834500
         AIF   ('&PLIST4' EQ 'NO').YESPL8                          @L2A 00836000
         AIF   ('&SYSAM64' NE 'YES').NOPL8                         @L4C 00837500
.YESPL8  ANOP                                                      @L2A 00840500
&SYSPLIST8 SETC 'YES'                                              @L2A 00842000
.NOPL8   ANOP                                                      @L2A 00843500
         AIF   ('&PLIST4' NE 'YES').NOPL4                          @L2A 00845000
&SYSPLIST8 SETC ''                                                 @L2A 00846500
.NOPL4   ANOP                                                      @L2A 00848000
.********************************************************************** 00848100
.*   VALIDATE PLIST8ARALETS                                           * 00848200
.********************************************************************** 00848300
         AIF   ('&PLIST8ARALETS' EQ '').PLIST8ARALETSEND           @L4A 00848400
         AIF   ('&PLIST8ARALETS' EQ 'NO').PLIST8ARALETSEND         @L4A 00848500
         AIF   ('&PLIST8ARALETS' EQ 'YES').PLIST8ARALETSEND        @L4A 00848600
         IHBERMAC 1001,PLIST8ARALETS,&PLIST8ARALETS                @L4A 00848700
         AGO   .EXIT                                               @L4A 00848800
.PLIST8ARALETSEND ANOP                                             @L4A 00848900
&SYSPLIST8ARALETS SETC 'NO'                                        @L4A 00849000
         AIF   ('&SYSPLIST8' NE 'YES').DONEPLIST8ARALETS           @L4A 00849100
         AIF   ('&PLIST8ARALETS' NE 'YES').DONEPLIST8ARALETS       @L4A 00849200
&SYSPLIST8ARALETS SETC 'YES'                                       @L4A 00849300
.DONEPLIST8ARALETS ANOP                                            @L4A 00849400
.********************************************************************** 00849501
.*   VALIDATE PLISTARALETS                                            * 00849604
.********************************************************************** 00849701
&SYSPLISTARALETS SETC '&PLISTARALETS'                              @L5A 00849804
         AIF   ('&PLISTARALETS' EQ 'NO').PLISTARALETSVALID         @L5A 00849906
         AIF   ('&PLISTARALETS' EQ 'SYSTEM').PLISTARALETSVALID     @L5A 00850006
         IHBERMAC 1001,PLISTARALETS,&PLISTARALETS                  @L5A 00850104
         AGO   .EXIT                                               @L5A 00850201
.PLISTARALETSVALID ANOP                                            @L5A 00850306
         AIF   ('&PLISTARALETS' NE 'NO').PLISTARALETSNOTNO         @L5A 00850405
         AIF   ('&PLIST8ARALETS' NE 'YES').PLISTARALETSNOTNO       @L5A 00850505
         IHBERMAC 1020,''PLISTARALETS=NO'',''PLIST8ARALETS=YES''   @L5A 00850608
.PLISTARALETSNOTNO ANOP                                            @L5A 00850705
.********************************************************************** 00850901
.*   VALIDATE AMODE64OK                                               * 00851001
.********************************************************************** 00851101
         AIF   ('&AMODE64OK' EQ '').AMODE64OKEND                   @L2A 00851201
         AIF   ('&AMODE64OK' EQ 'YES').AMODE64OKYES                @L3A 00851301
         AIF   ('&AMODE64OK' EQ 'NO').AMODE64OKNO                  @L3A 00851401
         IHBERMAC 1001,AMODE64OK,&AMODE64OK                        @L2A 00851501
         AGO   .EXIT                                               @L2A 00851601
.AMODE64OKYES ANOP                                                 @L2A 00851701
&SYSAMODE64OK SETC 'YES'                                           @L2A 00851801
         AGO   .AMODE64OKEND                                       @L2A 00851901
.AMODE64OKNO ANOP                                                  @L2A 00852001
&SYSAMODE64OK SETC 'NO'                                            @L2A 00852101
.AMODE64OKEND ANOP                                                 @L2A 00853001
&IHBSWA  SETB  ('&VL' EQ '1')                                 @G381P2A  00860000
&IHBSWB  SETB  ('&SF' EQ '(E,(15))')                          @G381P2A  00900000
         AIF   ('&MF' EQ 'L').E1                                        00950000
         AIF   ('&SF' EQ 'L' AND '&MF&PARAM&ID&VL' NE 'I').E2           01000000
         AIF   ('&NAME' EQ '' OR '&SF' EQ 'L').CONTA                    01050000
&NAME    DS    0H                                                       01100000
.CONTA   AIF   ('&PARAM' EQ '' AND '&MF(1)' NE 'E').CONTB               01150000
         IHBOPLTX  ,&PARAM,MF=&MF                                       01200000
.CONTB   ANOP                                                           01250000
&NAME    IHBINNRY &EP,&EPLOC,&DE,&DCB,&LSEARCH,                @G381P2AX01300000
               LINK,&PARAM,&MF,                                @G381P2AX01350000
               ERRET=&ERRET,SF=&SF,                            @G381P2AX01387500
               PLIST4=&PLIST4,PLIST8=&PLIST8                       @L2C 01425000
         AIF   ('&SF' EQ 'L').EXIT                             @G381P2A 01462500
         LA    15,7           LOAD LINK EXTENDED SVC ROUTING CODE       01500000
.*                                                             @G381P2A 01550000
         SVC   122                 ISSUE EXTENDED SVC          @G381P2A 01600000
         AIF   ('&ID' EQ '').EXIT                                       01650000
         DC    X'4700'             NOP INSTRUCTION WITH                 01700000
         DC    AL2(&ID)            ID IN LAST TWO BYTES                 01750000
         AGO   .EXIT                                                    01800000
.E1      IHBERMAC 84                                                    01850000
         AGO   .EXIT                                                    01900000
.E2      IHBERMAC 75                                                    01950000
.EXIT    ANOP                                                           01975000
&SYSPLIST8 SETC '&SAVEPLIST8'                                      @L2A 02000000
&SYSAMODE64OK SETC '&SAVEAMODE64OK'                                @L3A 02012500
&SYSPLIST8ARALETS SETC '&SAVEPLIST8ARALETS'                        @L4A 02012600
&SYSPLISTARALETS SETC '&SAVEPLISTARALETS'                          @L5A 02012704
         MEND                                                           02025000
**/                                                                     02050000
*LINKX:  MACRO                                                          02100000
*        KEYS(MF,EP,DE,DCB,ID,SF,PARAM,ERRET,VL,LSEARCH,PLIST8,PLIST4   02137500
*            ,AMODE64OK                                                 02175000
*                           );                            /*     @L3C*/ 02212500
*        ANS('?'!!MACLABEL!!'LINKXP '!!MACLIST!!MACKEYS!!';');          02250000
*                                                         /* @G381P2A*/ 02300000
*%END;                                                    /* @G381P2A*/ 02350000
