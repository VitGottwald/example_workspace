         MACRO                                                          00050000
.********************************************************************** 00100000
.*                                                                    * 00150000
.* $MAC(ARCHBDEL) COMP(SERVICE) PROD(DFHSM):                          * 00200000
.* MACRO NAME = ARCHBDEL                                              * 00250000
.* FUNC = SERVICE                                                     * 00300000
.* SUBFUNC = MWE                                                      * 00350000
.*                                                                    * 00400000
.* DESCRIPTIVE NAME = REQUEST DFHSM DELETE SPECIFIC BACKUP VERSIONS.  * 00450000
.*                                                                    * 00500000
.********************************************************************** 00537500
.*PROPRIETARY V3 STATEMENT                                            * 00575000
.*LICENSED MATERIALS - PROPERTY OF IBM                                * 00612500
.*"RESTRICTED MATERIALS OF IBM"                                       * 00650000
.*5650-ZOS                                                            * 00015000
.*COPYRIGHT IBM CORP. 1989, 2018                                      * 00016000
.*END PROPRIETARY V3 STATEMENT                                        * 00734300
.********************************************************************** 00762500
.*                                                                    * 00800000
.* STATUS = VERSION 1, RELEASE 11, LEVEL 0                        @RSC* 00850000
.*                                                                    * 00900000
.* FUNCTION = PERFORM DFHSM SVC 109 TO DELETE BACKUP VERSION(S)       * 00950000
.*            OF THE SPECIFIED DATA SET.                              * 01000000
.*                                                                    * 01050000
.* NOTES = SEE BELOW                                                  * 01100000
.*                                                                    * 01150000
.*   DEPENDENCIES = NONE                                              * 01200000
.*                                                                    * 01250000
.*   RESTRICTIONS = NONE                                              * 01300000
.*                                                                    * 01350000
.*   REGISTER CONVENTIONS =                                           * 01400000
.*                                                                    * 01450000
.*            REGISTER 13 MUST POINT TO A STANDARD SAVE AREA          * 01500000
.*            WHEN MACRO IS ISSUED.                                   * 01550000
.*                                                                    * 01600000
.*            REGISTERS 0, 1, 14 AND 15 WILL BE DESTROYED.            * 01650000
.*                                                                    * 01700000
.*   PATCH LABEL = NONE                                               * 01750000
.*                                                                    * 01800000
.* MODULE TYPE = MACRO                                                * 01850000
.*                                                                    * 01900000
.*   PROCESSOR = BAL                                                  * 01950000
.*                                                                    * 02000000
.* INPUT =                                                            * 02050000
.*          DSN      (OPTIONAL) SPECIFY THE ADDRESS OF A 44-BYTE AREA * 02100000
.*                   CONTAINING THE NAME OF THE DATA SET TO BE ACTED  * 02150000
.*                   ON, LEFT JUSTIFIED AND PADDED WITH BLANKS.       * 02200000
.*                   DSN IS MUTUALLY EXCLUSIVE WITH FILE          @UYC*         
.*                   PARAMETER.                                   @UYA*         
.*                                                                    * 02250000
.*               NOTE:     SPECIFY THE FROMVOL PARAMETER ONLY IF THE  * 02300000
.*                         DATA SET WAS UNCATALOGED WHEN BACKED UP.   * 02350000
.*                                                                    * 02400000
.*          FROMVOL  SPECIFY THE ADDRESS OF A 6 CHARACTER VOLUME      * 02450000
.*                   SERIAL OF THE VOLUME THAT CONTAINED THE DATA     * 02500000
.*                   SET AT THE TIME THE DATA SET WAS BACKED UP,      * 02550000
.*                   LEFT JUSTIFIED AND PADDED WITH BLANKS.           * 02600000
.*                                                                    * 02650000
.*          ALL      (OPTIONAL) SPECIFY YES!NO WHETHER ALL BACKUP @RSA* 02651900
.*                   VERSIONS ARE TO BE DELETED. ALL=YES DENOTES  @RSA* 02653800
.*                   THAT ALL ACTIVE AND RETAINED BACKUP VERSIONS @RSA* 02655700
.*                   ARE TO BE DELETED EXCEPT FOR RETIRED VERSION @RSA* 02657600
.*                   IF IT EXISTS.                                @RSA* 02659500
.*                                                                    * 02661400
.*               NOTE:     ALL, DATE, AND VERS ARE MUTUALLY       @RSA* 02663300
.*                         EXCLUSIVE KEYWORDS. ONE OF THE THREE   @RSA* 02665200
.*                         IS REQUIRED TO BE SPECIFIED>           @RSA* 02667100
.*                                                                    * 02669000
.*          DATE     (OPTIONAL) SPECIFY THE ADDRESS OF A 7        @RSA* 02670900
.*                   CHARACTER FIELD WHICH CONTAINS THE DATE      @RSA* 02672800
.*                   THE BACKUP VERSION WAS CREATED IN THE        @RSA* 02674700
.*                   FORMAT OF YYYYDDD.                           @RSA* 02676600
.*                                                                    * 02678500
.*               NOTE:     DATE AND TIME MUST BE SPECIFIED        @RSA* 02680400
.*                         TOGETHER.                              @RSA* 02682300
.*                                                                    * 02684200
.*          TIME     (OPTIONAL) SPECIFY THE ADDRESS OF A 6        @RSA* 02686100
.*                   CHARACTER FIELD WHICH CONTAINS THE TIME      @RSA* 02688000
.*                   THE BACKUP VERSION WAS CREATED IN THE        @RSA* 02689900
.*                   FORMAT OF HHMMSS.                            @RSA* 02691800
.*                                                                    * 02693700
.*               NOTE:     DATE AND TIME MUST BE SPECIFIED        @RSA* 02695600
.*                         TOGETHER.                              @RSA* 02697500
.*                                                            @YA29429* 02700000
.*          VERS     (OPTIONAL) SPECIFY THE ADDRESS OF A          @RSC* 02750000
.*                   STRUCTURE THAT CONTAINS A 2 BYTE NUMBER OF   @RSC* 02800000
.*                   ENTRIES ON THE LIST, FOLLOWED BY ENTRIES     @RSC* 02850000
.*                   (2 BYTES EACH) CONTAINING THE BACKUP VERSION @RSC* 02900000
.*                   NUMBER TO BE DELETED.                        @RSC* 02950000
.*                                                                    * 03100000
.*          TEST     (OPTIONAL) SPECIFY YES!NO WHETHER MACRO IN TEST  * 03150000
.*                   MODE.  TEST=YES DENOTES THAT THE MWE SHOULD      * 03200000
.*                   BUILD BUT THAT NO DFHSM SVC IS ISSUED.  DEFAULT  * 03250000
.*                   FOR THIS PARAMETER IS NO.                        * 03300000
.*                                                                    * 03350000
.*          SNAPDCB  (OPTIONAL) SPECIFY THE ADDRESS OF AN OPEN DCB    * 03400000
.*                   USED FOR SNAP PROCESSING OF THE MWE AREA UPON    * 03450000
.*                   RETURN FROM DFHSM SVC PROCESSING.                * 03500000
.*                                                                    * 03550000
.*          WKAREA   (REQUIRED) SPECIFY THE ADDRESS OF A 200 BYTE     * 03600000
.*                   WORK AREA TO CONTAIN INFORMATION SPECIFIED IN    * 03650000
.*                   THIS MACRO.                                      * 03700000
.*                                                                    * 03750000
.*          FILE     (OPTIONAL) SPECIFY A BACKUP VERSION OF A     @UYA*         
.*                   UNIX FILE TO DELETE.                         @UYA*         
.*                                                                    *         
.* OUTPUT =   REGISTER 15 WILL CONTAIN MWE RETURN CODE                * 03800000
.*            REGISTER 0  WILL CONTAIN MWE REASON CODE                * 03850000
.*                                                                    * 03900000
.*            INVALID REQUESTS NOT SENT TO DFHSM WILL HAVE            * 03950000
.*            THE FOLLOWING RETURN CODES:                             * 04000000
.*                                                                    * 04050000
.*            806 - LINK FAILURE - UNABLE TO LINK TO ARCGIVER         * 04100000
.*                                                                    * 04150000
.* CHANGE ACTIVITY =                                                  * 04200000
.* $K0=HSMKD,250,090888,THP5: PROGRAMMABLE DFHSM REQUEST          @KDA* 04250000
.*                            PROCESSOR                           @KDA* 04300000
.* CHANGE 01 - 90/01/18 - REMOVE VERS= KEYWORD TO COMPLY WITH @YA28429* 04350000
.*                        CLEARLY-DIFFERENTIATED PROGRAMMING  @YA28429* 04400000
.*                        INTERFACE (CDPI) STANDARDS.         @YA28429* 04450000
.* $02=OY40607,260,021991,THF2: PTM KLN0357-ROLLIN OY30406,       @02A* 04500000
.*                              VERS= KEYWORD SUPPORTED NOW THAT  @02A* 04550000
.*                              ARCXTRCT IS AVAILABLE AS CDPI     @02A* 04600000
.*                              INTERFACE                         @02A* 04650000
.* $RS=HSMRS,B10,051908,THL9: BACKDS RETAINDAYS SUPPORT           @RSA* 04666600
.*                            ADDED ALL=YES, DATE=, TIME= SUPPORT @RSA* 04683200
.* $UY=HSMUY,230,041018,THI9-9: UNIX File backup support          @UYA*         
.*                                                                    * 04700000
.*                                                                    * 04750000
.********************************************************************** 04800000
&NAME    ARCHBDEL &DSN=,&FROMVOL=,&TEST=NO,                            X04850000
               &WKAREA=,&SNAPDCB=,&VERS=,                         @RSC X04883300
               &ALL=,&DATE=,&TIME=,&FILE=                         @UYC          
         LCLA  &A                                                       04950000
         LCLB  &ERROR                                                   05000000
         LCLC  &H                                                       05050000
         LCLC  &L1                                                      05100000
         LCLC  &L2                                                      05150000
&L1      SETC  'ARC'.'&SYSNDX'.'A'                                      05200000
&L2      SETC  'ARC'.'&SYSNDX'.'B'                                      05250000
.*                                                                      05300000
.* CHECK THE PARAMETERS SPECIFIED WITH THE HBDELETE FUNCTION            05350000
.*                                                                      05400000
         AIF   ('&DSN' NE '').BD1                                       05450000
         AIF   ('&FILE' NE '').UA01                                @UYA         
         MNOTE 8,'DSN OR FILE PARAMETER SHOULD BE SPECIFIED'       @UYC         
&ERROR   SETB  1                                                        05550000
.UA01    ANOP                                                      @UYA         
         AIF   ('&FROMVOL' EQ '').UA02                             @UYA         
         MNOTE 8,'FROMVOL IS MUTUALLY EXCLUSIVE WITH FILE PARAMETER'            
&ERROR   SETB  1                                                   @UYA         
.BD1     ANOP                                                           05600000
         AIF   ('&FILE' EQ '').UA02                                @UYA         
         MNOTE 8,'DSN IS MUTUALLY EXCLUSIVE WITH FILE PARAMETER'   @UYA         
&ERROR   SETB  1                                                   @UYA         
.UA02    ANOP                                                      @UYA         
         AIF   ('&WKAREA' NE '').BD2                                    05650000
         MNOTE 8,'REQUIRED PARAMETER, WKAREA, IS NOT SPECIFIED'         05700000
&ERROR   SETB  1                                                        05750000
.BD2     ANOP                                                           05755500
         AIF   ('&ALL' EQ '' AND '&DATE' NE '' AND '&VERS' EQ '').BD3   05777400
         AIF   ('&ALL' EQ '' AND '&DATE' EQ '' AND '&VERS' NE '').BD4   05781500
         AIF   ('&ALL' EQ 'YES' AND '&DATE' EQ '' AND                  X05785600
               '&VERS' EQ '').BD4                                       05789700
         MNOTE 8,'ALL, DATE, AND VERS ARE MUTUALLY EXCLUSIVE. ONE OF THX05794000
               EM IS REQUIRED TO BE SPECIFIED'                   6@RSA  05799500
&ERROR   SETB  1                                                  @RSA  05805000
.BD3     ANOP                                                     @RSA  05810500
         AIF   ('&DATE' NE '' AND '&TIME' NE '').BD4              @RSA  05816000
         AIF   ('&DATE' EQ '' AND '&TIME' EQ '').BD4              @RSA  05821500
         MNOTE 8,'DATE AND TIME MUST BE SPECIFIED TOGETHER'       @RSA  05827000
&ERROR   SETB  1                                                  @RSA  05832500
.BD4     ANOP                                                     @RSA  05838000
         AIF   (NOT &ERROR).BUILD                                 @RSC  05843500
         MEXIT                                                          05850000
.*                                                                      05900000
.* AT THIS POINT ALL THE PARAMETERS HAVE BEEN PROPERLY SPECIFIED.       05950000
.* NOW BUILD THE CONTROL BLOCK AND CALL THE PROCESSING ROUTINE.         06000000
.*                                                                      06050000
.BUILD   ANOP                                                           06100000
         AIF   ('&WKAREA'(1,1) EQ '(').WREG                             06150000
&NAME    LA    15,&WKAREA                                               06200000
         AGO   .CLRWKA                                                  06250000
.WREG    ANOP                                                           06300000
&A       SETA  K'&WKAREA-2                                              06350000
&H       SETC  '&WKAREA'(2,&A)                                          06400000
&NAME    LR    15,&H                                                    06450000
.CLRWKA  ANOP                                                           06500000
         XC    0(200,15),0(15)     CLEAR THE WORK AREA                  06550000
         B     *+12                BRANCH AROUND IDENTIFIER             06600000
         DC    CL8'DFHSMREQ'                                            06650000
         MVC   0(8,15),*-8         SET THE IDENTIFIER                   06700000
         B     *+12                BRANCH AROUND FUNCTION NAME          06750000
         DC    CL8'HBDELETE'       FUNCTION NAME                        06800000
         MVC   8(8,15),*-8                                              06850000
.XTEST   AIF   ('&TEST' NE 'YES').XFIL1                           @UYC          
         OI    16(15),X'02'        TEST WAS SPECIFIED                   06950000
.XFIL1   AIF   ('&FILE' EQ '').XDSN                               @UYA          
         AIF   ('&FILE'(1,1) EQ '(').XFIL2                        @UYA          
         LA    1,&FILE              GET ADDR OF FILE AREA         @UYA          
         ST    1,104(15)            STORE IN PARM LIST            @UYA          
         AGO   .XSNAP                                             @UYA          
.XFIL2   ANOP                                                     @UYA          
&A       SETA  K'&FILE-2                                          @UYA          
&H       SETC  '&FILE'(2,&A)                                      @UYA          
         ST    &H,104(15)           STORE ADDR OF FILE AREA       @UYA          
         AGO   .XSNAP                                             @UYA          
.XDSN    ANOP                                                           07000000
         AIF   ('&DSN'(1,1) EQ '(').DREG                                07050000
         LA    1,&DSN              GET ADDR OF DSN                      07100000
         ST    1,20(15)            STORE IN PARM LIST                   07150000
         AGO   .XSNAP                                                   07200000
.DREG    ANOP                                                           07250000
&A       SETA  K'&DSN-2                                                 07300000
&H       SETC  '&DSN'(2,&A)                                             07350000
         ST    &H,20(15)           STORE ADDR OF DSN                    07400000
.XSNAP   ANOP                                                           07450000
         AIF   ('&SNAPDCB' EQ '').XFROM                                 07500000
         AIF   ('&SNAPDCB'(1,1) EQ '(').SREG                            07550000
         LA    1,&SNAPDCB          GET ADDRESS OF SNAP DCB              07600000
         ST    1,188(15)           STORE IN PARM LIST                   07650000
         AGO   .XFROM                                                   07700000
.SREG    ANOP                                                           07750000
&A       SETA  K'&SNAPDCB-2                                             07800000
&H       SETC  '&SNAPDCB'(2,&A)                                         07850000
         ST    &H,188(15)          STORE DCB ADDRESS IN PARM LIST       07900000
.XFROM   ANOP                                                           07950000
         AIF   ('&FROMVOL' EQ '').XVERS               @02C              08000000
         AIF   ('&FROMVOL'(1,1) EQ '(').FREG                            08050000
         LA    1,&FROMVOL          GET ADDR OF FROM VOLUME              08100000
         ST    1,32(15)            STORE IN PARM LIST                   08150000
         AGO   .XVERS                                 @02C              08200000
.FREG    ANOP                                                           08250000
&A       SETA  K'&FROMVOL-2                                             08300000
&H       SETC  '&FROMVOL'(2,&A)                                         08350000
         ST    &H,32(15)           STORE ADDR OF FROM VOLUME PARM       08400000
.*       SUPPORT FOR VERS KEYWORD ADDED                    @02C         08450000
.XVERS   ANOP                                              @02A         08500000
         AIF   ('&VERS' EQ '').XALL                        @02A         08550000
         AIF   ('&VERS'(1,1) EQ '(').VERREG                @02A         08600000
         LA    1,&VERS             GET ADDR OF VERSIONS    @02A         08650000
         ST    1,36(15)            STORE IN PARM LIST      @02A         08700000
         AGO   .XALL                                              @RSC  08750000
.VERREG  ANOP                                              @02A         08800000
&A       SETA  K'&VERS-2                                   @02A         08850000
&H       SETC  '&VERS'(2,&A)                               @02A         08900000
         ST    &H,36(15)           STORE ADDR OF VERSIONS PARM @02A     08950000
.XALL    AIF   ('&ALL' NE 'YES').XDATE                            @RSA  08952100
         OI    19(15),X'40'        ALL=YES WAS SPECIFIED          @RSA  08954200
.XDATE   ANOP                                                     @RSA  08956300
         AIF   ('&DATE' EQ '').GIVER                              @RSA  08958400
         AIF   ('&DATE'(1,1) EQ '(').DATEREG                      @RSA  08960500
         LA    1,&DATE             GET ADDR OF DATE               @RSA  08962600
         ST    1,44(15)            STORE IN PARM LIST             @RSA  08964700
         AGO   .XTIME                                             @RSA  08966800
.DATEREG ANOP                                                     @RSA  08968900
&A       SETA  K'&DATE-2                                          @RSA  08971000
&H       SETC  '&DATE'(2,&A)                                      @RSA  08973100
         ST    &H,44(15)           STORE ADDR OF DATE PARM        @RSA  08975200
.XTIME   ANOP                                                     @RSA  08977300
         AIF   ('&TIME' EQ '').GIVER                              @RSA  08979400
         AIF   ('&TIME'(1,1) EQ '(').TIMEREG                      @RSA  08981500
         LA    1,&TIME             GET ADDR OF TIME               @RSA  08983600
         ST    1,88(15)            STORE IN PARM LIST             @RSA  08985700
         AGO   .GIVER                                             @RSA  08987800
.TIMEREG ANOP                                                     @RSA  08989900
&A       SETA  K'&TIME-2                                          @RSA  08992000
&H       SETC  '&TIME'(2,&A)                                      @RSA  08994100
         ST    &H,88(15)                                          @RSA  08997000
.GIVER   ANOP                                                           09000000
         LR    1,15                PARAMETER ADDRESS TO REG 1           09050000
         LINK  EP=ARCGIVER,ERRET=&L1                                    09100000
         L     15,192(1)           LOAD RETURN CODE                     09150000
         L     0,196(1)            LOAD REASON CODE                     09200000
         B     &L2                                                      09250000
&L1      LA    15,806              INDICATE LINK FAILED                 09300000
&L2      EQU   *                                                        09350000
         MEXIT                                                          09400000
         MEND                                                           09450000
