*/*                                                                     00010000
         MACRO                                                          00020000
&LABEL   ISITMGD  &DEB=,&DCB=,&CONCAT=,&DATATYPE=,                     X00030000
               &VERSION=1,&MF=S                                    @01C 00040009
.********************************************************************** 00050000
.*                                                                    * 00060000
.* $MAC(ISITMGD) COMP(28473): IS_IT_MANAGED INVOCATION MACRO          * 00070000
.*                                                                    * 00080000
.*PROPRIETARY V3 STATEMENT                                              00090000
.*LICENSED MATERIALS - PROPERTY OF IBM                                  00100000
.*"RESTRICTED MATERIALS OF IBM"                                         00110000
.*5650-ZOS                                                              00120000
.*COPYRIGHT IBM CORP. 1989,2020                                         00130000
.*END PROPRIETARY V3 STATEMENT                                          00140000
.*                                                                    * 00150000
.* STATUS - SEE CHANGE ACTIVITY                                    @L1C 00160000
.*                                                                    * 00170000
.* PL/X INVOCATION: SEE THE INTERNAL PL/X MACRO, ISITMGDP          @L2C 00180000
.*                                                                    * 00190000
.* ASSEMBLER INVOCATION:                                              * 00200000
.*                                                                    * 00210000
.* <LABEL>  ISITMGD <{DEB=DEBADDR,                                 @L1C 00220000
.*                   ! DCB=DCBADDR,}>                              @L1C 00230000
.*                  <CONCAT={NUMBER!ALL},>                         @L1A 00240000
.*                  <DATATYPE={YES!NO},>                           @L1A 00250000
.*                  <VERSION={1!2},>                               @01A 00260000
.*                  <{MF=S !                                          * 00270000
.*                    MF=L !                                          * 00280000
.*                    MF=(E,PARMLIST)}>                               * 00290000
.*                                                                    * 00300000
.*  THE FOLLOWING ARE MUTUALLY EXCLUSIVE KEYWORDS:                    * 00310000
.*      (IN THE MF=S FORM, ONE OF THE KEYWORDS IS REQUIRED, BUT IN    * 00320000
.*       THE MF=L OR MF=E FORM THEY ARE OPTIONAL)                     * 00330000
.*                                                                    * 00340000
.*      DEB                                                           * 00350000
.*         PURPOSE: SPECIFIES THE ADDRESS OF THE DEB TO BE PASSED TO  * 00360000
.*                  THE IS_IT_MANAGED FUNCTION.  IT CAN BE AN ADDRESS * 00370000
.*                  IN MEMORY, OR AN ADDRESS IN A REGISTER (2)-(12).  * 00380000
.*                                                                    * 00390000
.*      DCB                                                           * 00400000
.*         PURPOSE: SPECIFIES THE ADDRESS OF THE DCB TO BE PASSED TO  * 00410000
.*                  THE IS_IT_MANAGED FUNCTION.  IT CAN BE AN ADDRESS * 00420000
.*                  IN MEMORY, OR AN ADDRESS IN A REGISTER (2)-(12).  * 00430000
.*                                                                    * 00440000
.*  OPTIONAL KEYWORDS:                                                * 00450000
.*                                                                    * 00460000
.*      CONCAT={NUMBER!ALL}  -  DEFAULT: 0                         @L1C 00470000
.*         PURPOSE: DEFINES THE DATA SET OR DATA SETS TO BE QUERIED@L1C 00480000
.*                                                                 @L1A 00490000
.*             NUMBER: DEFINES THE CONCATENATION NUMBER OF THE ONE @L1A 00500000
.*                  DATA SET TO BE QUERIED. NUMBER CAN BE EITHER   @L1A 00510000
.*                  AN ABSOLUTE EXPRESSION OR A VALUE IN A         @L1A 00520000
.*                  REGISTER (2)-(12). IF AN EXPRESSION, IT CANNOT @L1A 00530000
.*                  BE THE SINGLE SYMBOL 'ALL' BECAUSE THIS WOULD  @L1A 00540000
.*                  BE TREATED AS THE ALL KEYWORD.                 @L1A 00550000
.*             ALL: INDICATES THE INFORMATION RETURNED IS FOR ALL  @L1A 00560000
.*                  OF THE DATA SETS IN THE CONCATENATION. IF TWO  @L1A 00570000
.*                  DATA SETS HAVE DIFFERENT CHARACTERISTICS       @L1A 00580000
.*                  (EG. ONE SMS AND ONE NON-SMS) THE OUTPUT WILL  @L1A 00590000
.*                  REFLECT BOTH CHARACTERISTICS.                  @L1A 00600000
.*                  IF THE CONCAT=ALL IS REQUESTED ON A LEVEL OF   @L1A 00610000
.*                  DFP WHICH DOES NOT SUPPORT THIS REQUEST,       @L1A 00620000
.*                  INFORMATION WILL BE RETURNED FOR THE FIRST     @L1A 00630000
.*                  DATA SET (CONCAT=0). A PROGRAM CAN DETERMINE IF@L1A 00640000
.*                  THE SYSTEM SUPPORTS A SPECIFICATION OF ALL BY  @L1A 00650000
.*                  INSPECTING THE OUTPUT BITS. IF CONCAT=ALL WAS  @L1A 00660000
.*                  RECOGNIZED, AT LEAST TWO OF THE BITS INDICATING@L1A 00670000
.*                  DATA SET KIND WILL BE ON. IF CONCAT=ALL WAS    @L1A 00680000
.*                  NOT RECOGNIZED, AT MOST THE PDSE BIT WILL BE ON@L1A 00690000
.*                                                                    * 00700000
.*      DATATYPE={YES!NO} - DEFAULT: NO                            @L1A 00710000
.*         PURPOSE: REQUESTS THAT THE DATA TYPE IS TO BE RETURNED. @L1A 00720000
.*         THE SUPPORTED DATA TYPES ARE PDSE RECORD FORMAT LIBRARY @L1A 00730000
.*         AND PDSE LOAD MODULE LIBRARY. IF DATATYPE=YES IS        @L1A 00740000
.*         SPECIFIED ON A LEVEL OF DFP WHICH DOES *NOT* SUPPORT THE@L1A 00750000
.*         DATATYPE KEYWORD, NONE OF THE OUTPUT DATATYPE BITS WILL @L1A 00760000
.*         BE SET ON.  IF DATATYPE=YES IS SPECIFIED ON A LEVEL OF  @L1A 00770000
.*         DFP WHICH DOES SUPPORT DATATYPE, AT LEAST ONE OF THE    @L1A 00780000
.*         DATATYPE OUTPUT BITS WILL BE SET ON.                    @L1A 00790000
.*                                                                 @L1A 00800000
.*      VERSION={1!2} - DEFAULT: 1                                 @01A 00810000
.*         PURPOSE: INDICATES THE VERSION OF THE ISM PARAMETER LIST     00820000
.*         VERSION=1 IS THE ORIGINAL MAPPING OF THE ISM CONTROL BLOCK   00830001
.*         IT HAS A LENGTH OF 28 BYTES.                            @01A 00840000
.*         VERSION=2 PROVIDES ADDITIONAL INFORMATION FOR THE ISM        00850001
.*         CONTROL BLOCK. ITS NEW LENGTH IS 40 BYTES LONG.         @01A 00860001
.*         THE VERSION VALUES IN THE EXECUTE AND LIST FORMS MUST   @01A 00870005
.*         BE THE SAME SO THE LENGTH FIELD IS CORRECT.             @01A 00880005
.*                                                                      00890000
.*      MF=S ! L ! (E,PARMLIST)                                       * 00900000
.*         PURPOSE: SPECIFIES THE MACRO FORM.                         * 00910000
.*         DEFAULT: S                                                 * 00920000
.*                                                                    * 00930000
.*         MF=S                                                       * 00940000
.*           SPECIFIES THE STANDARD FORM OF THE MACRO.  THE "S"       * 00950000
.*           FORM GENERATES CODE TO PUT THE PARAMETERS INTO AN        * 00960000
.*           IN-LINE PARAMETER LIST AND INVOKES IS_IT_MANAGED.        * 00970000
.*                                                                    * 00980000
.*         MF=L                                                       * 00990000
.*           SPECIFIES THE LIST FORM OF THE MACRO.  THE "L" FORM      * 01000000
.*           BUILDS AN IN-LINE IS_IT_MANAGED PARAMETER LIST.          * 01010000
.*           SINCE THE PARAMETER LIST IS BUILT AT ASSEMBLY TIME,      * 01020000
.*           NO PARAMETERS MAY BE SPECIFIED IN REGISTER FORMAT.       * 01030000
.*                                                                    * 01040000
.*         MF=(E,PARMLIST)                                            * 01050000
.*           SPECIFIES THE EXECUTE FORM OF THE MACRO.  THE "E" FORM   * 01060000
.*           GENERATES CODE TO PUT THE PARAMETERS INTO THE            * 01070000
.*           IS_IT_MANAGED PARAMETER LIST SPECIFIED BY PARMLIST AND   * 01080000
.*           INVOKES IS_IT_MANAGED.                                   * 01090000
.*             IF NEITHER DATATYPE OR CONCAT=ALL                   @L1C 01100000
.*           IS SPECIFIED, THE MACRO WILL INITIALIZE THE PARAMETER @L1A 01110000
.*           LIST HEADER IN THE AREA DEFINED BY PARMLIST. (THIS    @L1A 01120000
.*           WAS INITIALLY INTENDED TO ACCOMODATE USERS WHO        @L1A 01130000
.*           OBTAINED DYNAMIC STORAGE FOR THEIR PARAMETER LIST,    @L1A 01140000
.*           RATHER THAN REQUIRING THE CALLER TO COPY A STATIC     @L1A 01150000
.*           VERSION OF THE PARM LIST (OBTAINED BY ISSUING ISITMGD @L1A 01160000
.*           WITH MF(L) PARM) INTO THE DYNAMIC STORAGE. HOWEVER    @L1A 01170000
.*           THIS LIMITS THE ABILITY TO UPDATE VERSIONS OF THE     @L1A 01180000
.*           PARAMTER LIST.)                                       @L1A 01190000
.*             IF EITHER DATATYPE OR CONCAT=ALL IS SPECIFIED, THIS @L1A 01200000
.*           MACRO WILL *NOT* COPY THE PARAMETER LIST HEADER INTO  @L1A 01210000
.*           THE STORAGE DEFINED BY PARMLIST. IF DYNAMIC STORAGE   @L1A 01220000
.*           IS USED BY THE CALLER FOR THE ISITMGD PARAMETER LIST, @L1A 01230000
.*           THE CALLER MUST INITIALIZE THE PARMLIST STORAGE BY    @L1A 01240000
.*           COPYING THE STATIC STORAGE DEFINED BY THE INVOCATION  @L1A 01250000
.*           OF ISITMGD MF(L).                                     @L1A 01260000
.*                                                                    * 01270000
.*           ,PARMLIST                                                * 01280000
.*             IS A REQUIRED INPUT.  IT IS THE NAME (RX-TYPE), OR     * 01290000
.*             ADDRESS IN A REGISTER OF A STORAGE AREA FOR THE        * 01300000
.*             PARAMETER LIST.  THE SPECIAL REGISTER FORM "(1)"       * 01310000
.*             MAY BE USED AND DESIGNATES THAT THE PARAMETER LIST     * 01320000
.*             ADDRESS IS ALREADY IN REGISTER 1.                      * 01330000
.*                                                                    * 01340000
.*  GENERAL PURPOSE REGISTER USAGE:                                   * 01350000
.*    REGISTER  0             REASON CODE.  INITIALIZED TO 0 BY MACRO * 01360000
.*    REGISTER  1             POINTER TO PARAMETER LIST               * 01370000
.*    REGISTER 13             POINTER TO 18 FULLWORD SAVE AREA        * 01380000
.*    REGISTER 14             RETURN ADDRESS                          * 01390000
.*    REGISTER 15             RETURN CODE.  ALSO HOLDS BRANCH ADDR.   * 01400000
.*                                                                    * 01410000
.*  EXTERNAL REFERENCES: NONE                                         * 01420000
.*                                                                    * 01430000
.*  MAPPING MACRO FOR IS_IT_MANAGED PARAMETER LIST: IGWCISM           * 01440000
.*                                                                    * 01450000
.*  CHANGE ACTIVITY:                                                  * 01460000
.*  $L0=PDSE,HDP3320,880206,STLFPC: PDSE SUPPORT                  @L0A* 01470000
.*  $P0=KET1837,HDP3320,890127,STLREB: 24 BIT SUPPORT             @P0A* 01480000
.*  $P1=KET2582,HDP3320,890413,STLREB: FIX KET1837                @P1A* 01490000
.*                                                                    * 01500000
.*  DFP 4.1.0 CHANGES:                                             @L1C 01510000
.*  $L1=PM,HDP4410,910320,SJPLREB: DATATYPE AND CONCAT=ALL PARMS   @L1A 01520000
.*  $L2=64BIT,HDZ1D10,20101124,SVLWER: 64 bit message              @L2A 01530000
.*  $01=OA56622,HDZ2240,20200128,ELATORRE: ADD VERSION PARM        @01A 01540000
.********************************************************************** 01550000
.*               VARIABLE DECLARATIONS                                * 01560000
.********************************************************************** 01570000
         GBLC  &SYSAM64    AMODE64=YES set by SYSSTATE macro       @L3A 01580000
         GBLC  &SYSASCE    ASCENV=AR or ANY set by SYSSTATE macro  @L3A 01590000
         LCLC     &BLKPTR                HOLDS CONTENTS OF DEB OR DCB   01600000
         LCLC     &BLKREG                BLKPTR MINUS ENCLOSING PARENS  01610000
         LCLB     &ISDCB                 WAS A DCB ADDRESS SUPPLIED?    01620000
         LCLA     &SEVCODE               THIS MACRO'S ERROR CODE        01630000
         LCLA     &VERSN   Version to be used for this expansion   @01A 01640000
&SEVCODE SETA  0                         INITIALIZE TO SEVERITY 0       01650000
&VERSN   SETA  1           Version 1 ISM                           @01A 01660000
&ISMLEN  SETA  28          Version 1 ISM Length (28)               @01A 01670001
         LCLC     &C                     SCRATCH CHARACTER STRING  @L1A 01680000
.*-----------------------------------------------------------------@L1A 01690000
.*      OR MASKS FOR SETTING ISMIFLG1 BITS - CONTAINS 1'S FOR ALL  @L1A 01700000
.*      BITS TO BE SET ON.                                         @L1A 01710000
.*-----------------------------------------------------------------@L1A 01720000
         LCLC     &ODEBDCB       OR MASK-BITS 0 AND 1 TO INDICATE  @L1A 01730000
.*                                       IF DCB OR DEB WAS INPUT   @L1A 01740000
         LCLC     &OALL          OR MASK-BIT 2 INDICATES IF ALL    @L1A 01750000
.*                                       PARM SPECIFIED            @L1A 01760000
         LCLC     &ODTYPE        OR MASK-BIT 3 INDICATES DATATYPE  @L1A 01770000
.*                                       PARM SPECIFIED            @L1A 01780000
.*-----------------------------------------------------------------@L1A 01790000
.*     AND MASKS FOR SETTING ISMIFLG1 BITS - CONTAINS 0'S FOR BITS @L1A 01800000
.*     TO BE SET OFF.                                              @L1A 01810000
.*-----------------------------------------------------------------@L1A 01820000
         LCLC     &ADEBDCB      AND MASK-BITS 0 AND 1 TO INDICATE  @L1A 01830000
.*                                       IF DCB OR DEB WAS INPUT   @L1A 01840000
         LCLC     &AALL         AND MASK-BIT 2 INDICATES IF ALL    @L1A 01850000
.*                                       PARM SPECIFIED            @L1A 01860000
         LCLC     &ADTYPE       AND MASK-BIT 3 INDICATES DATATYPE  @L1A 01870000
.*                                       PARM SPECIFIED            @L1A 01880000
.********************************************************************** 01890000
.*               CONSTANT DECLARATIONS                                * 01900000
.********************************************************************** 01910000
.*    PARAMETER LIST OFFSETS:                                           01920000
.*-----------------------------------------------------------------@L1A 01930000
         LCLC  &ISMID                    THE ISMID FIELD                01940000
&ISMID   SETC  '0'                                                      01950000
         LCLC  &ISMLVL                   THE ISMLVL FIELD               01971010
&ISMLVL  SETC  '12'                                                     01972010
         LCLC  &ISMADDR                  THE ISMADDR FIELD              01980000
&ISMADDR SETC  '16'                                                     01990000
         LCLC  &ISMCON                   THE ISMCON FIELD               02000000
&ISMCON  SETC  '20'                                                     02010000
         LCLC  &ISMIFL1                  THE ISMIFLG1 FIELD             02020000
&ISMIFL1 SETC  '21'                                                     02030000
.*-----------------------------------------------------------------@L1A 02040000
.*    OFFSETS AND CONSTANTS USED DURING THE LINKAGE:                    02050000
.*-----------------------------------------------------------------@L1A 02060000
         LCLC  &PSACVT                   CVT POINTER IN THE PSA         02070000
&PSACVT  SETC  '16'                                                     02080000
         LCLC  &CVTSCR1                  POINTER TO SYSTEM CONVERT @L1C 02090000
.*                                         ROUTINE BRANCH TABLE    @L1A 02100000
&CVTSCR1 SETC  '232'                                                    02110000
         LCLC  &BITOFF                   OFFSET OF BITMASK              02120000
&BITOFF  SETC  '4'                                                      02130000
         LCLC  &BITMASK                  BITMASK WHICH INDICATES        02140000
&BITMASK SETC  'B''00000010'''             EXISTENCE OF IS_IT_MANAGED   02150000
         LCLC  &ISITOFF                  OFFSET IN BRANCH TABLE OF      02160000
&ISITOFF SETC  '24'                        IS_IT_MANAGED'S ADDRESS      02170000
         LCLC  &NOTSUPP                  FUNCTION_NOT_SUPPORTED         02180000
&NOTSUPP SETC  '8'                         ERROR CODE                   02190000
.********************************************************************** 02200000
.*                 START OF THE MAIN MACRO BODY                       * 02210000
.********************************************************************** 02220000
.*           SET ISDCB TO TRUE IF A DCB WAS SUPPLIED.                 * 02230000
.********************************************************************** 02240000
&ISDCB   SETB  ('&DCB' NE '')                                           02250000
.********************************************************************** 02260000
.*         SET BLKPTR TO CONTAIN EITHER THE DEB OR DCB INPUT.         * 02270000
.*       SET BLKREG TO CONTAIN THE REGISTER NUMBER (USED LATER,       * 02280000
.*                ONLY IF A REGISTER WAS SUPPLIED).                   * 02290000
.********************************************************************** 02300000
&BLKPTR  SETC  '&DCB'                                                   02310000
&BLKREG  SETC  '&DCB(1)'                                                02320000
         AIF   ('&BLKPTR' NE '').ERR1                                   02330000
&BLKPTR  SETC  '&DEB'                                                   02340000
&BLKREG  SETC  '&DEB(1)'                                                02350000
.********************************************************************** 02360000
.*                                                                    * 02370000
.*               PERFORM ALL ERROR CHECKING UP FRONT                  * 02380000
.*                                                                    * 02390000
.********************************************************************** 02400000
.*         THE DEB AND DCB PARAMETERS ARE MUTUALLY EXCLUSIVE          * 02410000
.*                    ENFORCE THIS RESTRICTION.                       * 02420000
.********************************************************************** 02430000
.ERR1    AIF   (('&DCB' EQ '') OR ('&DEB' EQ '')).ERR2                  02440000
         MNOTE 12,'*** BOTH A DCB AND DEB CAN NOT BE SUPPLIED'          02450000
&SEVCODE SETA  12                                                       02460000
.********************************************************************** 02470000
.*            CHECK THE FORMAT OF THE MF= PARAMETER.                  * 02480000
.********************************************************************** 02490000
.* Fail the expansion if 64-bit or AR mode.                        @L2A 02500000
.ERR2    SYSSTATE TEST     Test how macros are to expand           @L2A 02510000
         AIF   ('&SYSAM64' NE 'YES').CHKAR   Go if not AMODE64     @L2A 02520000
         IGGERMAC 203,ISITMGD    ISITMGD doesn't support AMODE64   @L2A 02530000
&SEVCODE SETA  8                 Fail the macro expansion          @L2A 02540000
.CHKAR   AIF   (('&SYSASCE' EQ '') OR ('&SYSASCE' EQ 'P')).CHKMF   @L2A 02550000
         IGGERMAC 204,ISITMGD    ISITMGD doesn't support AR or ANY @L2A 02560000
&SEVCODE SETA  8                 Fail the macro expansion          @L2A 02570000
.CHKMF   AIF   ('&MF' EQ 'S').ERR3    Resume checking              @L2C 02580000
         AIF   ('&MF' EQ 'L').ERR3                                      02590000
         AIF   (N'&MF NE 2).ERR2A                                       02600000
         AIF   ('&MF(1)' NE 'E').ERR2A                                  02610000
         AIF   ('&MF(2)' NE '').ERR3  Resume checking                   02620000
.ERR2A   MNOTE 12,'*** THE MF PARAMETER HAS AN INVALID FORMAT'          02630000
&SEVCODE SETA  12                                                       02640000
.********************************************************************** 02650000
.*    THE DEB/DCB PARAMETER MAY NOT BE IN A REGISTER FOR THE L-FORM.  * 02660000
.*   CHECK BOTH THE DEB AND DCB PARAMETERS IN CASE BOTH WERE SUPPLIED * 02670000
.********************************************************************** 02680000
.ERR3    AIF   ('&MF' NE 'L').ERR4                                      02690000
         AIF   ('&DCB' EQ '').ERR3A                                     02700000
         AIF   ('&DCB'(1,1) EQ '(').ERR3B                               02710000
.ERR3A   AIF   ('&DEB' EQ '').ERR4                                      02720000
         AIF   ('&DEB'(1,1) NE '(').ERR4                                02730000
.ERR3B   MNOTE 12,'*** THE DCB/DEB PARAMETER MAY NOT BE IN A REGISTER'  02740000
&SEVCODE SETA  12                                                       02750000
.********************************************************************** 02760000
.*    THE CONCAT PARAMETER MAY NOT BE IN A REGISTER FOR THE L-FORM.   * 02770000
.*                 ENFORCE THIS RESTRICTION                           * 02780000
.********************************************************************** 02790000
.ERR4    AIF   ('&MF' NE 'L').ERR5                                      02800000
         AIF   ('&CONCAT' EQ '').ERR5                                   02810000
         AIF   ('&CONCAT'(1,1) NE '(').ERR5                             02820000
         MNOTE 12,'*** THE CONCAT PARAMETER MAY NOT BE IN A REGISTER'   02830000
&SEVCODE SETA  12                                                       02840000
.********************************************************************** 02850000
.*    THE S-FORM REQUIRES THAT EITHER A DEB OR DCB BE SUPPLIED.       * 02860000
.*                 ENFORCE THIS REQUIREMENT                           * 02870000
.********************************************************************** 02880000
.ERR5    AIF   ('&MF' NE 'S').ERR6                                      02890000
         AIF   (('&DEB' NE '') OR ('&DCB' NE '')).ERR6                  02900000
         MNOTE 12,'*** A DCB OR DEB MUST BE SUPPLIED'                   02910000
&SEVCODE SETA  12                                                       02920000
.********************************************************************** 02930000
.*     TEST TO ENSURE THAT DATATYPE PARAMETERS ARE VALID           @L1A 02940000
.********************************************************************** 02950000
.ERR6    AIF   (('&DATATYPE' EQ '') OR ('&DATATYPE' EQ 'NO')).ERR7 @L1A 02960000
         AIF   ('&DATATYPE' EQ 'YES').ERR7                         @L1A 02970000
       MNOTE 12,'*** THE DATATYPE PARAMETER HAS AN INVALID FORMAT' @L1A 02980000
&SEVCODE SETA  12                                                  @L1A 02990000
.********************************************************************** 03000000
.*     TEST TO ENSURE THAT VERSION PARAMETERS ARE VALID            @01A 03010000
.********************************************************************** 03020000
.ERR7    AIF   ('&VERSION' EQ '2').SETVER2                         @01A 03030007
         AIF   ('&VERSION' EQ '' OR '&VERSION' EQ '1').SETVER1     @01A 03040000
       IGGERMAC 205,'VERSION','1 OR 2 OR NOT CODED'OF 1 OR 2 '     @01A 03050005
&SEVCODE SETA  12                                                  @01A 03060000
         AGO   .ERR8                                               @01A 03070007
.SETVER2 ANOP                                                      @01A 03080000
&VERSN   SETA  2                                                   @01A 03090000
&ISMLEN  SETA  40              VERSION 2 ISM Length                @01A 03100001
.SETVER1 ANOP                                                      @01A 03110000
.********************************************************************** 03120000
.*     ANY ERROR CODE OVER 4 IS SEVERE ENOUGH TO STOP EXPANSION.      * 03130000
.*               LEAVE THE MACRO IF THIS IS THE CASE.                 * 03140000
.********************************************************************** 03150000
.ERR8    AIF   (&SEVCODE EQ 0).ERREND                              @L1C 03160007
         MEXIT                                                          03170000
.ERREND  ANOP                                                      @L1A 03180000
.********************************************************************** 03190000
.*         SET UP ISMIFLG1                                         @L1A 03200000
.********************************************************************** 03210000
.*         SETUP THE DCB OR DEB SUPPLIED BITS                      @L1A 03220000
.*-----------------------------------------------------------------@L1A 03230000
         AIF   ('&DEB' NE '').IFLG1   DEB SPECIFIED                @L1A 03240000
         AIF   ('&DCB' NE '').IFLG2   DCB SPECIFIED                @L1A 03250000
.*-----------------------------------------------------------------@L1A 03260000
.*       NEITHER DCB NOR DEB SPECIFIED                             @L1A 03270000
.*-----------------------------------------------------------------@L1A 03280000
&ODEBDCB SETC  '00'                DON'T SET BITS ON               @L1A 03290000
&ADEBDCB SETC  '11'                DON'T SET BITS OFF              @L1A 03300000
         AGO   .IFLG3                                              @L1A 03310000
.*-----------------------------------------------------------------@L1A 03320000
.*         DEB INPUT                                               @L1A 03330000
.*-----------------------------------------------------------------@L1A 03340000
.IFLG1   ANOP                                                      @L1A 03350000
&ODEBDCB SETC  '10'                SET ON  ISMDEB                  @L1A 03360000
&ADEBDCB SETC  '10'                SET OFF ISMDCB                  @L1A 03370000
         AGO   .IFLG3                                              @L1A 03380000
.*-----------------------------------------------------------------@L1A 03390000
.*         DCB INPUT                                               @L1A 03400000
.*-----------------------------------------------------------------@L1A 03410000
.IFLG2   ANOP                                                      @L1A 03420000
&ODEBDCB SETC  '01'                SET ON ISMDCB                   @L1A 03430000
&ADEBDCB SETC  '01'                SET OFF ISMDEB                  @L1A 03440000
.*-----------------------------------------------------------------@L1A 03450000
.*         SETUP THE ALL KEYWORD SPECIFIED BIT                     @L1A 03460000
.*-----------------------------------------------------------------@L1A 03470000
.IFLG3   AIF   ('&CONCAT' EQ 'ALL').IFLG4                          @L1A 03480000
         AIF   ('&CONCAT' EQ '').IFLG5                             @L1A 03490000
.*-----------------------------------------------------------------@L1A 03500000
.*       CONCAT  EQ  EXPRESSION                                    @L1A 03510000
.*-----------------------------------------------------------------@L1A 03520000
&OALL    SETC  '0'                 DON'T SET ON ISMALL             @L1A 03530000
&AALL    SETC  '0'                 SET OFF ISMALL                  @L1A 03540000
         AGO   .IFLG6                                              @L1A 03550000
.*-----------------------------------------------------------------@L1A 03560000
.*       CONCAT  EQ  'ALL'                                         @L1A 03570000
.*-----------------------------------------------------------------@L1A 03580000
.IFLG4   ANOP                                                      @L1A 03590000
&OALL    SETC  '1'                 SET ON ISMALL                   @L1A 03600000
&AALL    SETC  '1'                 DON'T SET OFF ISMALL            @L1A 03610000
         AGO   .IFLG6                                              @L1A 03620000
.*-----------------------------------------------------------------@L1A 03630000
.*       CONCAT NOT SPECIFIED                                      @L1A 03640000
.*-----------------------------------------------------------------@L1A 03650000
.IFLG5   ANOP                                                      @L1A 03660000
&OALL    SETC  '0'                 DON'T SET ON ISMALL             @L1A 03670000
&AALL    SETC  '1'                 DON'T SET OFF ISMALL            @L1A 03680000
.*-----------------------------------------------------------------@L1A 03690000
.*         SETUP THE DATATYPE KEYWORD SPECIFIED BIT                @L1A 03700000
.*-----------------------------------------------------------------@L1A 03710000
.IFLG6   AIF   ('&DATATYPE' EQ 'YES').IFLG7                        @L1A 03720000
         AIF   ('&DATATYPE' EQ '').IFLG7A                          @L1A 03730000
.*-----------------------------------------------------------------@L1A 03740000
.*       DATATYPE EQ 'NO'                                          @L1A 03750000
.*-----------------------------------------------------------------@L1A 03760000
&ODTYPE  SETC  '0'                 DON'T SET ON ISMDTYPE           @L1A 03770000
&ADTYPE  SETC  '0'                 SET OFF ISMDTYPE                @L1A 03780000
         AGO   .IFLGEND                                            @L1A 03790000
.*-----------------------------------------------------------------@L1A 03800000
.*       DATATYPE EQ 'YES'                                         @L1A 03810000
.*-----------------------------------------------------------------@L1A 03820000
.IFLG7   ANOP                                                      @L1A 03830000
&ODTYPE  SETC  '1'                 SET ON ISMDTYPE                 @L1A 03840000
&ADTYPE  SETC  '1'                 DON'T SET OFF ISMDTYPE          @L1A 03850000
         AGO   .IFLGEND                                            @L1A 03860000
.*-----------------------------------------------------------------@L1A 03870000
.*       DATATYPE NOT SPECIFIED                                    @L1A 03880000
.*-----------------------------------------------------------------@L1A 03890000
.IFLG7A  ANOP                                                      @L1A 03900000
&ODTYPE  SETC  '0'                 DON'T SET ON ISMDTYPE           @L1A 03910000
&ADTYPE  SETC  '1'                 DON'T SET OFF ISMDTYPE          @L1A 03920000
.*-----------------------------------------------------------------@L1A 03930000
.*         SETUP OF ISMIFLG1 BITS COMPLETE                         @L1A 03940000
.*-----------------------------------------------------------------@L1A 03950000
.IFLGEND ANOP                                                      @L1A 03960000
.********************************************************************** 03970000
.*                                                                    * 03980000
.*               BRANCH TO THE APPROPRIATE MF= HANDLER                * 03990000
.*                                                                    * 04000000
.********************************************************************** 04010000
         AIF   ('&MF' EQ 'S').MFS                                  @L1C 04020000
         AIF   ('&MF' EQ 'L').MFL                                       04030000
.********************************************************************** 04040000
.*                                                                    * 04050000
.*                HANDLE EXECUTE FORM (MF=E)                          * 04060000
.*                                                                    * 04070000
.********************************************************************** 04080000
.MFE     ANOP                                                           04090000
.********************************************************************** 04100000
.*             SETUP ALIGNMENT AND CHECK THE REGISTERS                * 04110000
.********************************************************************** 04120000
         CNOP  0,4                                                      04130000
         AIF   ('&CONCAT' EQ '' OR '&CONCAT' EQ 'ALL').E10        @L1C  04140000
         AIF   ('&CONCAT'(1,1) NE '(').E10                              04150000
         DS    0XL(&CONCAT-1)      CHECK CONCAT REG LOWER BOUND         04160000
         DS    0XL(13-&CONCAT)     CHECK CONCAT REG UPPER BOUND         04170000
.E10     AIF   ('&BLKPTR' EQ '').E20                                    04180000
         AIF   ('&BLKPTR'(1,1) NE '(').E20                              04190000
         DS    0XL(&BLKPTR-1)      CHECK DCB/DEB REG LOWER BOUND        04200000
         DS    0XL(13-&BLKPTR)     CHECK DCB/DEB REG UPPER BOUND        04210000
.********************************************************************** 04220000
.*    MOVE THE PARAMETER LIST ADDRESS INTO REGISTER 1 AND PLACE THE   * 04230000
.*                       PASSED LABEL DOWN.                           * 04240000
.********************************************************************** 04250000
.E20     AIF   ('&MF(2)' EQ '(1)').E40                                  04260000
         AIF   ('&MF(2)'(1,1) EQ '(').E30                               04270000
&LABEL   LA    1,&MF(2)            GET PARAMETER LIST ADDRESS           04280000
         AGO   .E50                                                     04290000
.E30     ANOP                                                           04300000
&LABEL   LA    1,0(,&MF(2,1))      TRANSFER PARM LIST ADDRESS      @L2C 04310000
         AGO   .E50                                                     04320000
.E40     ANOP                                                           04330000
&LABEL   LA    1,0(0,1)            CLEAR HIGH BIT/BYTE                  04340000
.********************************************************************** 04350000
.*     SET THE PARAMETER LIST HEADER IF NEW PARMS NOT SPECIFIED    @L1C 04360000
.********************************************************************** 04370000
.E50     AIF   ('&ODTYPE' EQ '1' OR '&OALL' EQ '1').E55            @L1A 04380000
         JAS   15,*+20             BRANCH AROUND HEADER            @01C 04390005
         DC    CL8'ISITMGD '       PARAMETER LIST ID                    04400000
         DC    FL4'&ISMLEN'        PARAMETER LIST LENGTH           @01C 04410001
         DC    X'01'               PARAMETER LIST LEVEL                 04420000
         DC    3X'00'              RESERVED BYTES                       04430000
         MVC   0(16,1),0(15)       MOVE HEADER TO PARAMETER LIST.       04440000
.E55     ANOP                        DO NOT COPY HEADER. THIS      @L1A 04450000
.*                                   SHOULD NEVER HAVE BEEN DONE   @L1A 04460000
.*                                   IN INITIAL VERSION OF THIS    @L1A 04470000
.*                                   MACRO. THIS IS THE CALLER'S   @L1A 04480000
.*                                   RESPONSIBILITY.               @L1A 04490000
.********************************************************************** 04500000
.*        SET THE ISMADDR PARAMETER AND CHECK REGISTER BOUNDS         * 04510000
.********************************************************************** 04520000
         AIF   ('&BLKPTR' EQ '').E70                               @L1C 04530000
         AIF   ('&BLKPTR'(1,1) EQ '(').E60                              04540000
         LA    0,&BLKPTR           GET DCB/DEB ADDRESS                  04550000
         ST    0,&ISMADDR.(0,1)    STORE DCB/DEB ADDESS                 04560000
         AGO   .E70                                                     04570000
.E60     LA    15,0(0,&BLKREG)     CLEAR HIGH BIT/BYTE                  04580000
         ST    15,&ISMADDR.(0,1)   STORE DCB/DEB ADDRESS                04590000
.********************************************************************** 04600000
.*       SET ISMCON                                                @L1C 04610000
.********************************************************************** 04620000
.E70     AIF   ('&CONCAT' EQ '').E100                              @L1C 04630000
         AIF   ('&CONCAT'(1,1) EQ '(').E80                         @L1C 04640000
         AIF   ('&CONCAT' EQ 'ALL').E90                            @L1A 04650000
         MVI   &ISMCON.(1),&CONCAT       STORE CONCATENATION NUMBER     04660000
         AGO   .E100                                               @L1C 04670000
.E80     STC   &CONCAT(1),&ISMCON.(0,1)  STORE CONCATENATION NUMBER     04680000
         AGO   .E100                                               @L1A 04690000
.E90     MVI   &ISMCON.(1),0             CLEAR CONCATENATION NUMBER     04700000
.********************************************************************** 04710000
.*       SET ISMIFLG1                                              @L1A 04720000
.********************************************************************** 04730000
.E100    AIF   ('&ODEBDCB.&OALL.&ODTYPE' EQ '0000').E110           @L1A 04740000
&C       SETC  '&ODEBDCB.&OALL.&ODTYPE.0000'                       @L1A 04750000
         OI    &ISMIFL1.(1),B'&C.'       SET ON INPUT BITS              04760000
.E110    AIF   ('&ADEBDCB.&AALL.&ADTYPE' EQ '1111').EEND           @L1A 04770000
&C       SETC  '&ADEBDCB.&AALL.&ADTYPE.1111'                       @L1A 04780000
         NI    &ISMIFL1.(1),B'&C.'       SET OFF INPUT BITS             04790000
.********************************************************************** 04800000
.*               CALL THE IS_IT_MANAGED ROUTINE.                      * 04810000
.*       USE THE CODE SHARED BY THE MF=E AND MF=S FORMATS             * 04820000
.********************************************************************** 04830000
.EEND    AGO   .SE10                                               @L1C 04840000
.********************************************************************** 04850000
.*                                                                    * 04860000
.*               HANDLE STANDARD FORM (MF=S)                          * 04870000
.*                                                                    * 04880000
.********************************************************************** 04890000
.MFS     ANOP                                                           04900000
.********************************************************************** 04910000
.*             SETUP ALIGNMENT AND CHECK THE REGISTERS                * 04920000
.********************************************************************** 04930000
         CNOP  0,4                                                      04940000
         AIF   ('&CONCAT' EQ '' OR '&CONCAT' EQ 'ALL').S10         @L1C 04950000
         AIF   ('&CONCAT'(1,1) NE '(').S10                              04960000
         DS    0XL(&CONCAT-1)            CHECK CONCAT REG LOWER BOUND   04970000
         DS    0XL(13-&CONCAT)           CHECK CONCAT REG UPPER BOUND   04980000
.S10     AIF   ('&BLKPTR' EQ '').S20                                    04990000
         AIF   ('&BLKPTR'(1,1) NE '(').S20                              05000000
         DS    0XL(&BLKPTR-1)            CHECK DCB/DEB REG LOWER BOUND  05010000
         DS    0XL(13-&BLKPTR)           CHECK DCB/DEB REG UPPER BOUND  05020000
.********************************************************************** 05030000
.*             CREATE THE PARAMETER LIST HEADER.                      * 05040000
.********************************************************************** 05050000
.S20     ANOP                                                           05060000
&LABEL   JAS   1,*+&ISMLEN+4             BRANCH AROUND PARAMETER LIST   05070001
         DC    CL8'ISITMGD '             PARAMETER LIST ID              05080000
         DC    F'&ISMLEN'                PARAMETER LIST LENGTH @01C     05090001
         DC    X'01'                     PARAMETER LIST LEVEL           05100000
         DC    3X'00'                    RESERVED BYTES                 05110000
.********************************************************************** 05120000
.*               SET THE ISMADDR PARAMETER                            * 05130000
.********************************************************************** 05140000
         AIF   ('&BLKPTR'(1,1) EQ '(').S30                              05150000
         DC    A(&BLKPTR)                INPUT PTR TO DCB OR DEB        05160000
         AGO   .S40                                                     05170000
.S30     DC    A(0)                      INPUT PTR TO DCB OR DEB        05180000
.********************************************************************** 05190000
.*              SET THE CONCATENATION NUMBER                          * 05200000
.********************************************************************** 05210000
.S40     AIF   ('&CONCAT' EQ '').S50                                    05220000
         AIF   ('&CONCAT'(1,1) EQ '(').S50                              05230000
         AIF   ('&CONCAT' EQ 'ALL').S50                            @L1A 05240000
         DC    AL1(&CONCAT)              CONCATENATION NUMBER           05250000
         AGO   .S60                                                     05260000
.S50     DC    AL1(0)                    CONCATENATION NUMBER           05270000
.********************************************************************** 05280000
.*            SET THE ISIFLG1 FIELD                                @L1C 05290000
.********************************************************************** 05300000
.S60     ANOP                                                      @L1A 05310000
&C       SETC  '&ODEBDCB.&OALL.&ODTYPE.0000'                       @L1A 05320000
         DC    B'&C.'                    INPUT FLAGS                    05330000
.********************************************************************** 05340000
.*              CREATE THE PARAMETER LIST TAIL.                       * 05350000
.********************************************************************** 05360000
         DC    6X'00'                    OUTPUT BYTES              @01C 05370001
         AIF   ('&VERSION' EQ '' OR '&VERSION' EQ '1').S70         @01A 05380004
         DC   12X'00'                    OUTPUT/RESERVED BYTES     @01A 05390003
.S70     ANOP                                                      @01A 05400004
.*                                                                 @L1D 05410000
.********************************************************************** 05420000
.*        DYNAMICALLY ALTER THE PARAMETER LIST AS IS NECESSARY.       * 05430000
.********************************************************************** 05440000
.*                MODIFY THE ISMADDR PARAMETER                        * 05450000
.********************************************************************** 05460000
         AIF   ('&BLKPTR'(1,1) NE '(').S90                              05470000
         LA    15,0(0,&BLKREG)           CLEAR HIGH BIT/BYTE            05480000
         ST    15,&ISMADDR.(0,1)         SET DCB/DEB ADDRESS            05490000
.********************************************************************** 05500000
.*                 MODIFY THE ISMCON PARAMETER                        * 05510000
.********************************************************************** 05520000
.S90     AIF   ('&CONCAT' EQ '' OR '&CONCAT' EQ 'ALL').SEND        @L1A 05530000
         AIF   ('&CONCAT'(1,1) NE '(').SEND                        @L1A 05540000
         STC   &CONCAT(1),&ISMCON.(0,1)  SET CONCATENATION NUMBER       05550000
.SEND    ANOP                                                      @L1A 05560000
.********************************************************************** 05570000
.*            COMMON CODE SECTION TO CALL IS_IT_MANAGED               * 05580000
.*     BOTH THE MF=E AND MF=S FORMATS USE THIS INVOCATION SECTION     * 05590000
.********************************************************************** 05600000
.SE10    SR    0,0                       ZERO REASON CODE REGISTER      05610000
         L     15,&PSACVT.(0,0)          GET CVT POINTER                05620000
         L     15,&CVTSCR1.(0,15)        GET POINTER TO BRANCH TABLE    05630000
         TM    &BITOFF.(15),&BITMASK     DOES ROUTINE EXIST?            05640000
         BNZ   *+12                      YES - GO CALL ROUTINE          05650000
         LA    15,&NOTSUPP.(0,0)         SET RETURN CODE 8              05660000
         B     *+10                      BRANCH AROUND ROUTINE CALL     05670000
         L     15,&ISITOFF.(0,15)        GET ROUTINE ADDRESS            05680000
         BASSM 14,15                     CALL ROUTINE                   05690000
         MEXIT                                                          05700000
.********************************************************************** 05710000
.*                                                                    * 05720000
.*                 HANDLE LIST FORM (MF=L)                            * 05730000
.*                                                                    * 05740000
.********************************************************************** 05750000
.MFL     ANOP                                                           05760000
.********************************************************************** 05770000
.*             CREATE THE PARAMETER LIST HEADER.                      * 05780000
.********************************************************************** 05790000
&LABEL   DS    0F                        BUILD LIST ON WORD BOUNDARY    05800000
         DC    CL8'ISITMGD '             PARAMETER LIST ID              05810000
         DC    F'&ISMLEN'                PARAMETER LIST LENGTH @01C     05820001
         DC    X'01'                     PARAMETER LIST LEVEL           05830000
         DC    3X'00'                    RESERVED BYTES                 05840000
.********************************************************************** 05850000
.*               SET THE ISMADDR PARAMETER                            * 05860000
.********************************************************************** 05870000
         AIF   ('&BLKPTR' EQ '').L10                                    05880000
         DC    A(&BLKPTR)                INPUT PTR TO DCB OR DEB        05890000
         AGO   .L20                                                     05900000
.L10     DC    A(0)                      INPUT PTR TO DCB OR DEB        05910000
.********************************************************************** 05920000
.*              SET THE CONCATENATION NUMBER                          * 05930000
.********************************************************************** 05940000
.L20     AIF   ('&CONCAT' EQ '' OR '&CONCAT' EQ 'ALL').L30         @L1C 05950000
         DC    AL1(&CONCAT)              CONCATENATION NUMBER           05960000
         AGO   .L40                                                     05970000
.L30     DC    AL1(0)                    CONCATENATION NUMBER           05980000
.********************************************************************** 05990000
.*            SET THE ISIFLG1 BYTE                                 @L1C 06000000
.********************************************************************** 06010000
.L40     ANOP                                                      @L1C 06020000
&C       SETC  '&ODEBDCB.&OALL.&ODTYPE.0000'                       @L1C 06030000
         DC    B'&C.'                    ISIFLG1 BYTE                   06040000
.********************************************************************** 06050000
.*              CREATE THE PARAMETER LIST TAIL.                       * 06060000
.********************************************************************** 06070000
.L70     DC     6X'00'                   OUTPUT BYTES              @01C 06080003
         AIF   ('&VERSION' EQ '' OR '&VERSION' EQ '1').L80         @01A 06090004
         DC    12X'00'                   OUTPUT AND RESERVED BYTES @01A 06100003
.L80     MEND                                                           06120008
**/                                                                     06130000
*/********************************************************************/ 06140000
*/* INVOKE THE INTERNAL PL/X VERSION OF THE MACRO                    */ 06150000
*/********************************************************************/ 06160000
*%ISITMGD:  MACRO KEYS(DEB, DCB, CONCAT, DATATYPE, VERSION, MF);/*@01C*/06170000
*   ANS ('?' !! MACLABEL !! ' ISITMGDP ' !! MACLIST !!                  06180000
*        ' ' !! MACKEYS  !! ';') COL(MACLMAR) RESCAN;                   06190000
*%END ISITMGD;                                                          06200000
