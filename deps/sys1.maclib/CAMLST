*/********************************************************************/ 00000500
*/*                                                                  */ 00001000
*/*PROPRIETARY V3 STATEMENT                                          */ 00002000
*/*LICENSED MATERIALS - PROPERTY OF IBM                              */ 00003000
*/*"RESTRICTED MATERIALS OF IBM"                                     */ 00004000
*/*5695-DF1                                                          */ 00005000
*/*(C) COPYRIGHT 2006  IBM CORP.                                     */ 00006200
*/*END PROPRIETARY V3 STATEMENT                                      */ 00007500
*/*                                                                  */ 00009000
*/* FUNCTION: The CAMLST macro is a parameter list mapping for       */ 00009200
*/*     several Catalog and DADSM functions.                         */ 00009400
*/*                                                                  */ 00009600
*/*     - The first parameter indicates the function requested and   */ 00009800
*/*       is required for all CAMLST invocations. For Catalog, the   */ 00010000
*/*       supported functions are NAME, CAT, CATBX, UNCAT, UCATDX    */ 00010200
*/*       and RECAT. For DADSM, the supported functions are SEARCH,  */ 00010400
*/*       SEEK, SCRATCH and RENAME. Functions no longer supported    */ 00010600
*/*       are BLDA, BLDG, BLDX, BLOCK, DLTA, DLTX, DRPX, and LNKX.   */ 00010800
*/*     - The second parameter is the object of the function         */ 00011000
*/*       specified and is required.                                 */ 00011200
*/*     - The 3rd parameter is not used with Catalog-related calls   */ 00011400
*/*       (only DADSM calls). It must be specified with the SEARCH,  */ 00011600
*/*       SEEK and RENAME functions.  For RENAME, this parm          */ 00011800
*/*       specifies the new name.  For SEARCH and SEEK, this parm    */ 00012000
*/*       specifies the VOLSER on which the DSCB is located.         */ 00012200
*/*     - The 4th parameter specifies the workarea and is required   */ 00012400
*/*       for CAT, CATBX, RECAT, NAME, SEARCH, SEEK, SCRATCH, and    */ 00012600
*/*       RENAME.  The workarea must not be specified for UNCAT and  */ 00012800
*/*       UCATDX. For the SCRATCH function, this parm specifies a    */ 00013000
*/*       volume list.                                               */ 00013200
*/*     - The first option is no longer used.                        */ 00013400
*/*     - The second option has 1 supported value: OVRD. OVRD        */ 00013600
*/*       can only be specified with function SCRATCH.               */ 00013800
*/*     - The third option is no longer used                         */ 00014000
*/*     - The DSCBTTR keyword is optional but can only be specified  */ 00014200
*/*       with the CAT, CATBX and RECAT functions.                   */ 00014400
*/*     - The NUMBERDSCB keyword specifies an absolute value or      */ 00014600
*/*       (register) with a value between 0-255 that designates the  */ 00014800
*/*       number of consecutive 140-byte workareas provided in the   */ 00015000
*/*       4th parameter. The system treats a value of 0 as 1.  When  */ 00015200
*/*       more than one 140-byte area is provided, OBTAIN processing */ 00015400
*/*       will return DSCBs in logical VTOC order until all the      */ 00015600
*/*       140-byte areas are used.  The keyword is only valid with   */ 00015800
*/*       the OBTAIN SEARCH and SEEK functions.                      */ 00016000
*/*     - The EADSCB keyword indicates whether the caller understands*/ 00016200
*/*       format 8/9 DSCBs and is valid only with OBTAIN SEARCH and  */ 00016400
*/*       SEEK requests.                                             */ 00016600
*/*                                                                  */ 00016800
*/* MACRO ERRORS DETECTED:                                           */ 00017000
*/*    12,***  IHB001  FIRST OPERAND REQ'D-NOT SPECIFIED             */ 00017200
*/*    12,***  IHB001  THIRD OPERAND REQ'D-NOT SPECIFIED             */ 00017400
*/*    12,***  IHB001  FOURTH OPERAND REQ'D-NOT SPECIFIED            */ 00017600
*/*    12,***  IHB002  INVALID KEYWORD OPERAND SPECIFIED-NUMBERDSCB  */ 00017800
*/*    12,***  IHB002  INVALID KEYWORD OPERAND SPECIFIED-EADSCB      */ 00018000
*/*    12,***  IHB243 INVALID OPERAND FOR EADSCB OPTION.             */ 00018200
*/*                                                                  */ 00018400
*/* CHANGE ACTIVITY:                                                 */ 00018600
*/* $L1=EAVSUPP,HDZ1A10,06JUN15,SJPLPDC: ADD NEW PARAMETERS      @L1A*/ 00018800
*/*     NUMBERDSCB AND EADSCB                                    @L1A*/ 00019000
*/*                                                                  */ 00019200
*/***************************************************************@L1A*/ 00019400
         MACRO                                                          00019600
&NAME    CAMLST &PAR1,&PAR2,&PAR3,&PAR4,&OPT1,&OPT2,&OPT3,&DSCBTTR=,   *00020000
               &NUMBERDSCB=,&EADSCB=,&DUMMY=                       @L1C 00030000
         LCLA  &B1,&B2,&B3,&CTR                                         00040000
         LCLB  &P(19),&OP(3),&D(4),&NOGO                            TSO 00042000
         AIF   ('&PAR1' EQ '').E1                                       00044000
         AIF   ('&PAR2' EQ '').E2                                       00046000
.R1      ANOP                                                           00048000
&P(1)    SETB  ('&PAR1' EQ 'CAT' OR '&PAR1' EQ 'CATBX')             TSO 00048400
&P(2)    SETB  ('&PAR1' EQ 'RECAT')                                     00048800
&P(3)    SETB  ('&PAR1' EQ 'UNCAT' OR '&PAR1' EQ 'UCATDX')          TSO 00049200
&P(4)    SETB  ('&PAR1' EQ 'BLDX' OR '&PAR1' EQ 'CATBX')            TSO 00049600
&P(5)    SETB  ('&PAR1' EQ 'BLDG')                                      00049700
&P(6)    SETB  ('&PAR1' EQ 'BLDA')                                      00049800
&P(9)    SETB  ('&PAR1' EQ 'DLTA')                                      00109900
&P(10)   SETB  ('&PAR1' EQ 'DRPX')                                      00119900
&P(11)   SETB  ('&PAR1' EQ 'NAME')                                      00129900
&P(13)   SETB  ('&PAR1' EQ 'SEARCH')                                    00141900
&P(14)   SETB  ('&PAR1' EQ 'SEEK')                                      00143900
&P(15)   SETB  ('&PAR1' EQ 'SCRATCH')                                   00145900
&P(16)   SETB  ('&PAR1' EQ 'RENAME')                                    00147900
&P(17)   SETB  ('&DSCBTTR' NE'')                                        00148300
&P(18)   SETB  ('&NUMBERDSCB' NE'')                                @L1A 00148400
&P(19)   SETB  ('&EADSCB' NE'')                                    @L1A 00148500
&CTR     SETA  &P(1)+&P(2)+&P(3)+&P(4)+&P(5)+&P(6)+&P(9)           @L1C 00148600
&CTR     SETA  &CTR+&P(10)+&P(11)+&P(13)+&P(14)+&P(15)+&P(16)           00148700
.*                                                                      00148800
.* First check for errors                                               00148900
.*                                                                      00149000
.* Check for no positional parameters specified (error)                 00149100
.*                                                                      00149200
         AIF   (&CTR EQ 0).E3                                       TSO 00149500
&D(1)    SETB  ('&PAR3' NE '')                                          00149600
&D(2)    SETB  ('&PAR4' NE '')                                          00149700
.*                                                                      00155900
.* Check for parm 3 not specified with SEARCH, SEEK or RENAME (error)   00162100
.*                                                                      00168300
         AIF   (NOT &D(1) AND (&P(13) OR &P(14) OR &P(16))).E4          00174500
.*                                                                      00180700
.* Check for parm 3 specified with SCRATCH (error)                      00186900
.*                                                                      00193100
.R3      AIF   (&D(1) AND &P(15)).E5                                    00199800
.*                                                                      00204000
.* Check for parm 4 not specified with CAT, CATBX, RECAT, BLDA, BLDX,   00208200
.* NAME, BLOCK, SEARCH, SEEK, SCRATCH or RENAME (error)                 00212400
.*                                                                      00216600
         AIF   (NOT &D(2) AND (&P(1) OR &P(2) OR &P(6) OR &P(11) OR &P(X00220800
               12) OR &P(13) OR &P(14) OR &P(15) OR &P(16))).E          00225000
         AIF   (&P(1) AND &P(4)).R2                                 TSO 00229800
.*                                                                      00232000
.* Check for parm 4 specified with UNCAT, UCATDX, DLTA, DRPX,           00234200
.* BLDX, CATBX or BLDG (error)                                          00236400
.*                                                                      00238600
         AIF   (&D(2) AND (&P(3) OR &P(9) OR &P(10))).E6           @L1C 00240800
         AIF   (&D(2) AND (&P(4) OR &P(5))).E6                          00243000
.R2      AIF   ('&OPT1' EQ '').CONTA                                    00245800
.*                                                                      00246200
.* Check for option 1 not equal to DELETE and not specified with        00246600
.* BLDG  (both errors)                                                  00247000
.*                                                                      00247400
         AIF   ('&OPT1' NE 'DELETE').E7                                 00247800
&OP(1)   SETB  1                                                        00248200
         AIF   (NOT &P(5)).E7                                           00248600
.CONTA   AIF   ('&OPT2' EQ '').CONTB                                    00249000
&OP(3)   SETB  ('&OPT2' EQ 'OVRD')                                      00249500
.*                                                                      00255700
.* Check that OVRD is specified with SCRATCH only                       00261900
.*                                                                      00268100
         AIF   (NOT &P(15) AND &OP(3)).E8                               00274300
.*                                                                      00280500
.* Check that option 3 is only specified with BLDG and vice versa       00286700
.*                                                                      00292900
.CONTB   AIF   ('&OPT3' EQ '' AND &P(5)).E10                            00299700
         AIF   ('&OPT3' NE '' AND NOT &P(5)).E11                        00309700
.CONTB1  AIF   (&P(17) AND NOT (&P(1) OR &P(2))).E12                    00319700
.*                                                                      00320800
.* Check that EADSCB and NUMBERDSCB are only specified with SEARCH and  00321900
.* SEEK functions. Also check for valid values of EADSCB           @L1A 00323000
.*                                                                      00324100
         AIF   (&P(18) AND NOT(&P(13) OR &P(14))).E13              @L1A 00325200
         AIF   (&P(19) AND NOT(&P(13) OR &P(14))).E14              @L1A 00326300
         AIF   (&P(19) AND NOT(('&EADSCB'EQ'OK')OR('&EADSCB'EQ'NOTOK'))X00327400
               ).E15                                               @L1A 00328500
         AIF   (&NOGO EQ 1).END                                         00329700
         AIF   (NOT(&P(19))).GENPL   SKIP WHEN EADSCB not specd    @L1A 00331100
         AIF   ('&EADSCB' EQ 'OK').GENPL                           @L1A 00332500
&P(19)   SETB  0                     Turn off when EADSCB=NOTOK    @L1A 00333900
.*                                                                      00335300
.* Generate the parameter list                                          00336700
.*                                                                      00338100
.GENPL   ANOP                                                      @L1A 00339500
&B1      SETA  128*&D(1)+64*&D(2)+32*&P(1)+20*&P(2)+8*&P(3)        @L1C 00340900
&D(4)    SETB  (&P(3))                                             @L1C 00342300
&B1      SETA  &B1+4*(&P(11)+&P(5)+&P(6)+&D(4)+&P(9)+&P(10))       @L1C 00343700
&D(3)    SETB  (&P(1) OR &P(4))                                     TSO 00345700
&B1      SETA  &B1+4*&D(3)+&P(13)+&P(15)+&P(16)                     TSO 00347700
&B2      SETA  128*&P(14)+64*&P(4)+32*&P(5)+16*&P(6)+2*&P(17)+&P(9) @L1 00348100
&B3      SETA  128*&P(10)+64*&P(15)+32*&P(16)+64*&OP(1)+16*&OP(3)+8*&P(X00348500
               19)+4*&P(18)                                             00348900
&NAME    DS    0F                                ALIGN ON FULL WORD     00349400
         DC    AL1(&B1)                          THREE BYTES OF FLAGS   00349500
         DC    AL1(&B2)                          INDICATING THE FUNC-   00349600
         DC    AL1(&B3)                          TION TO BE PERFORMED   00399600
         AIF   (&P(5)).CONTC                                            00409600
         AIF   (&P(18)).CONTC1                   NUMBERDSCB?       @L1A 00414600
         DC    AL1(0)                            NO OPTION THREE        00419600
         AGO   .CONTD                                                   00429600
.CONTC   DC    AL1(&OPT3)                        NO. OF ENTRIES         00439600
         AGO   .CONTD                                              @L1A 00440200
.CONTC1  DC    AL1(&NUMBERDSCB)                  NUMBER OF DSCBS   @L1A 00440800
.CONTD   DC    A(&PAR2)                          PARAMETER TWO          00441600
         AIF   (&D(1)).CONTE                                            00443600
         DC    A(0)                              PARAM. THREE OMMITTED  00445600
         AGO   .CONTF                                                   00447600
.CONTE   DC    A(&PAR3)                          PARAMETER THREE        00448000
.CONTF   AIF   (NOT &D(2)).END                                          00448400
         DC    A(&PAR4)                          PARAMETER FOUR         00448800
         AIF   (NOT &P(17)).END                                         00449200
         DC    A(&DSCBTTR)                       PARAMETER FIVE         00449300
         MEXIT                                                          00449400
.E       IHBERMAC 5                     PAR4 MISSING                    00449500
         MEXIT                                                          00499500
.E1      IHBERMAC 2                     PAR1 MISSING                    00509500
         MEXIT                                                          00519500
.E2      IHBERMAC 3                     PAR2 MISSING                    00529500
&NOGO    SETB  1                                                        00539500
         AGO   .R1                                                      00541500
.E3      IHBERMAC 36,,&PAR1             PAR1 INVALID                    00543500
         MEXIT                                                          00545500
.E4      IHBERMAC 4                     PAR3 MISSING                    00547500
&NOGO    SETB  1                                                        00547900
         AGO   .R3                                                      00548300
         MEXIT                                                          00548700
.E5      IHBERMAC 60,,&PAR3             PAR3 W/SCRATCH                  00549100
         MEXIT                                                          00549200
.E6      IHBERMAC 39,,&PAR4             PAR4 NOT ALLOWED                00549300
&NOGO    SETB  1                                                        00549400
         AGO   .R2                                                      00599400
.E7      IHBERMAC 50,,&OPT1             OPTN1 INVALID                   00609400
&NOGO    SETB  1                                                        00619400
         AGO   .CONTA                                                   00629400
.E8      IHBERMAC 51,,&OPT2             OPTN2 INVALID                   00639400
&NOGO    SETB  1                                                        00641400
         AGO   .CONTB                                                   00643400
.E10     IHBERMAC 52                    OPTN3 MISSING                   00645400
         MEXIT                                                          00647400
.E11     IHBERMAC 53,,&OPT3             OPTN3 INVALID                   00647800
&NOGO    SETB  1                                                        00648200
         AGO  .CONTB1                                                   00648600
.E12     IHBERMAC 54,,DSCBTTR           DSCBTTR NOT ALLOWED.            00649000
         MEXIT                                                    @L1A  00699000
.E13     IHBERMAC 54,,NUMBERDSCB        NUMBERDSCB not allowed    @L1A  00749000
         MEXIT                                                    @L1A  00799000
.E14     IHBERMAC 54,,EADSCB            EADSCB not allowed        @L1A  00849000
         MEXIT                                                    @L1A  00899000
.E15     IHBERMAC 1007,EADSCB           EADSCB value invalid      @L1A  00949000
.END     MEND                                                           00999000
