*/*PROPRIETARY V3 STATEMENT                                          */ 00010000
*/*LICENSED MATERIALS - PROPERTY OF IBM                              */ 00015000
*/*"RESTRICTED MATERIALS OF IBM"                                     */ 00020000
*/*5695-DF1                                                          */ 00025000
*/*COPYRIGHT 1992, 2011 IBM CORP.                                    */ 00031200
*/*END PROPRIETARY V3 STATEMENT                                      */ 00037500
*%/*                                                               @L1A 00047500
         MACRO                                                          00050000
&NAME    CHECK &DECB,&DSORG=,&RPL=                                      00100000
.*$01=OY46548,HDP3330,901204,SJPLCC:  BDAM DATA ABOVE THE LINE     @01A 00183300
.*$L1=XXXXXXX,JDZ1110,930930,SJPLMET: PL/X SUPPORT                 @L1A 00199900
.*$L2=64BIT,JDZ1D10,20100303,SVLWER: 64 BIT MESSAGE                @L2A 00207000
         GBLC  &SYSAM64    GLOBAL VARIABLE SET BY SYSSTATE MACRO   @L2A 00214100
         GBLC  &SYSASCE    GLOBAL VARIABLE SET BY SYSSTATE MACRO   @L2A 00221200
         SYSSTATE TEST    TEST HOW MACROS ARE TO EXPAND            @L2A 00228300
         AIF   ('&SYSAM64' EQ 'YES').E2    GO IF AMODE64=YES       @L2A 00235400
         AIF   (('&SYSASCE' NE '') AND ('&SYSASCE' NE 'P')).E3     @L2A 00242500
         AIF   ('&RPL' NE '').JES  Go if VSAM, VTAM or subsystem   @L2C 00271200
         AIF   ('&DECB' EQ '').E1                                       00300000
         AIF   ('&DSORG' EQ '').A1                                      00350000
         AIF   ('&DSORG' EQ 'IS').A2                                    00400000
         AIF   ('&DSORG' EQ 'ALL').A3                                   00450000
         IHBERMAC 180,DSORG,ALL                                         00500000
         AGO   .A3                                                      00550000
.A1      ANOP       Generate call for BSAM or BPAM.                     00600000
&NAME    IHBINNRA &DECB                                                 00650000
         L     14,8(0,1)           PICK UP DCB ADDRESS                  00691600
         SR    15,15                                               @01A 00733300
         ICM   15,B'0111',53(14)   LOAD CHECK ROUTINE ADDRESS      @01C 00772200
         BASR  14,15               CALL THE CHECK ROUTINE          @L2C 00811100
         MEXIT                                                          00850000
.A2      ANOP       Generate call only for ISAM.                        00900000
&NAME    IHBINNRA &DECB                                                 00950000
         L     14,8(0,1)           PICK UP DCB ADDRESS                  01000000
         L     15,76(0,14)         LOAD CHECK RTN ADDRESS FOR ISAM      01050000
         BASR  14,15               CALL THE CHECK ROUTINE          @L2C 01100000
         MEXIT                                                          01150000
.A3      ANOP       Generate call for BSAM, BPAM or ISAM.               01200000
&NAME    IHBINNRA &DECB                                                 01250000
         L     14,8(0,1)           PICK UP DCB ADDRESS                  01295000
         L     15,76(0,14)         LOAD CHECK RTN ADDRESS FOR ISAM      01340000
         TM    26(14),X'80'        CHECK FOR ISAM                       01385000
         BO    *+10                GO TO BALR IF ISAM              @01C 01430000
         SR    15,15                                               @01A 01475000
         ICM   15,B'0111',53(14)   LOAD CHECK ROUTINE              @01C 01516600
         BALR  14,15               CALL CHECK ROUTINE                   01558200
         MEXIT                                                          01600000
.E1      IHBERMAC 7                DECB OPERAND MISSING                 01650000
         MEXIT                                                          01700000
.E2      IGGERMAC 203,CHECK       THE CHECK MACRO DOES NOT SUPPORT @L2A 01725000
         MEXIT                      SYSSTATE AMODE64=YES           @L2A 01750000
.E3      IGGERMAC 204,CHECK       THE CHECK MACRO DOES NOT SUPPORT @L2A 01775000
         MEXIT                      SYSSTATE ASCENV=AR OR ANY      @L2A 01800000
.JES     ANOP       Expansion for VSAM, VTAM or subsystem               01825000
         AIF   ('&RPL'(1,1) EQ '(').RPLR                                01850000
&NAME    LA    1,&RPL              LOAD RPL ADDRESS                @L2C 01900000
         AGO   .AREG                                                    01950000
.RPLR    AIF   ('&RPL' EQ '(1)').MakeL Go if already in register 1 @L2C 02000000
&NAME    LR    1,&RPL(1)           LOAD RPL ADDRESS FROM REGISTER  @L2C 02050000
         AGO   .AREG                                               @L2A 02100000
.MakeL   AIF   ('&NAME' EQ '').AREG  Go if no label                @L2A 02150000
&NAME    DS    0H                                                  @L2A 02200000
.AREG    LA    0,X'02'             INDICATE CHECK IN REGISTER           02250000
         L     15,24(1)            LOAD ACB ADDR FROM RPL               02300000
         L     15,8(15)            LOAD ACB INTERFACE MODULE ADDR       02350000
         BASR  14,15               CALL INTERFACE ROUTINE          @L2C 02400000
         MEND                                                           02550000
* */ CHECK: MACRO KEYS(1:1(DECB_ADDR,DECB_NAME),SYNAD_NAME,    /*@L1A*/ 02600000
*          EODAD_NAME);                                        /*@L1A*/ 02650000
*   ANS('? '!!MACLABEL!!' CHECKP '!!MACKEYS!!';') RESCAN SKIP; /*@L1A*/ 02700000
* %END CHECK;                                                  /*@L1A*/ 02750000
