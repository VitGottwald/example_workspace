         MACRO                                                                  
         IST6MCSX &AREA=,&BRANCH=,&CONMODE=,&CONVID=,&CONXMOD=,        X        
               &CRYPT=,&ECB=,&EXIT=,&INVALP1=,&INVALP2=,&OPTCD=,       X        
               &QUALIFY=,&RECLEN=,&VTRINA=,&VTRINL=,&VTROUTA=,         X        
               &VTROUTL=                                           @D1C         
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCSX                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: CONTROL = SEND EXPEDITED DATA PROCESSOR     */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1993, 1994.       */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */         
.* /*      CONTROL=SENDEXPD PARAMETER ON THE APPCCMD MACRO.          */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS:                                              */         
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCUI,IST6MCER              */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: NONE                                     */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*                                                                */         
.* /* $MAC(IST6MCSX),COMP(API),PROD(VTAM): SENDEXPD PROCESSOR MACRO  */         
.* /*                                                                */         
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */         
.* /* ---- -------- ------- ------ ------  -----------------         */         
.* /* $I0= FF@@@MST APPCFDX 911126 879315: APPC Full-Duplex support  */         
.* /* $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services     */         
.* /* $L2= KA@DR006 APPCRR  940713 576146: Vector Format             */         
.* /* $D1= ID@@@MST FPAPI   950310 143576: Fast Path API for APPC    */         
.* /*                                                                */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
.*                                                                              
         GBLB  &ISTGEN                                                          
         GBLB  &ISTSX                                                           
         AIF   (&ISTSX EQ 1).SXINVKW                                            
*                                                                               
*  IST6MCSX - ACF/VTAM VERSION 4 RELEASE 2                                      
*                                                                               
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                              
*                                                                               
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"               
*                                                                               
*             5695-117 (C) COPYRIGHT IBM CORP. 1993.                            
*             ALL RIGHTS RESERVED.                                              
*                                                                               
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                         
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY                      
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                          
*                                                                               
*             SEE COPYRIGHT INSTRUCTIONS.                                       
*                                                                               
&ISTSX   SETB  1                                                                
.SXINVKW ANOP                                                                   
         IST6MCUC SENDEXPD,CONTROL,INVALKW=&INVALP1                             
         IST6MCUC SENDEXPD,CONTROL,INVALKW=&INVALP2                             
.*                                          CHECK INVALID APPCCMD               
.SXID    ANOP                                                                   
         AIF   ('&CONVID' EQ '').SXQFY                                          
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X        
               KW=CONVID                                                        
.*                                              PUT CONVID IN APPC AREA         
.SXQFY   ANOP                                                                   
         AIF   ('&QUALIFY' EQ '').SXAREA                                        
         AIF   ('&QUALIFY' NE 'DATA').SXQFYE                                    
         AIF   (NOT &ISTGEN).SXCKPT                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6DATA                                   
*                                       PUT DATA CODE IN APPC AREA              
         AGO   .SXAREA                                                          
.SXQFYE  ANOP                                                                   
         IST6MCER 5,QUALIFY,&QUALIFY                                            
.SXAREA  ANOP                                                                   
         AIF   ('&AREA' EQ '').SXRLEN                                           
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X        
               KW=DATA_ADDRESS                                                  
.*                                           PUT DATA ADDRESS IN RPL            
.SXRLEN  ANOP                                                                   
         AIF   ('&RECLEN' EQ '').SXVECT                            @L1C         
         IST6MCUA KWVAL=&RECLEN,LOC=RPLRLEN-IFGRPL,BASEREG=1,          X        
               KW=DATA_LENGTH                                                   
.*                                           PUT DATA LENGTH IN RPL             
.SXVECT  ANOP                          ENSURE BOTH PARMS ARE CODED @L1A         
.*                                     OR BOTH PARMS ARE NOT CODED @L1A         
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).SXVOUT    @D1C         
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).SXERVI     @D1C         
         IST6MCUA KWVAL=&VTRINA,LOC=RPL6VAIA-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_ADDRESS                      @D1C         
.*                                     STORE VECTOR AREA INPUT ADDRESS          
.*                                                                 @L1A         
         IST6MCUA KWVAL=&VTRINL,LOC=RPL6VAIL-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2                 @D1C         
.*                                     STORE VECTOR AREA INPUT LENGTH           
.*                                                                 @L1A         
         AGO   .SXVOUT                                             @D1C         
.SXERVI  ANOP                                                      @L1A         
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C         
.SXVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED       @D1A         
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).SXCKPT  @D1A         
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).SXERVO   @D1A         
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X        
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                @D1A         
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X        
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                @D1A         
         AGO   .SXCKPT                                             @D1A         
.SXERVO  ANOP                                                      @D1A         
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A         
.SXCKPT  ANOP                                                                   
         IST6MCUP BRANCH=&BRANCH,CONMODE=&CONMODE,CONXMOD=&CONXMOD,    X        
               ECB=&ECB,EXIT=&EXIT,OPTCD=&OPTCD,                       X        
               CKCMODE=YES,        CHECK CONMODE & CONXMOD PARAMETERS  X        
               CKSYN=YES,          ASY/SYN IS VALID FOR OPTCD          X        
               CKBUFF=YES          BUFFLST/NBUFFLST IS VALID FOR                
.*                                 OPTCD PARAMETER                              
         AIF   (NOT &ISTGEN).SXEXIT                                             
         MVI   RPL6REQ-ISTRPL6X(14),RPL6SNDX                                    
*                                      PUT SENDEXPD REQUEST IN RPL6             
.SXEXIT  MEND                                                                   
