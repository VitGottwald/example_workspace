         MACRO                                                                  
         IST6MCOC &QUALIFY=,&LUNAME=,&LOGMODE=,&AREA=,&ECB=,&EXIT=,    X        
               &OPTCD=,&BRANCH=,&RECLEN=,&AREALEN=,&INVALKW=,&ARG=,    X        
               &CONFTXT=,&USERFLD=,&LIST=,&NETID=,&NAMEUSE=,&VTRINA=,  X        
               &VTRINL=,&VTROUTA=,&VTROUTL=,&LUAFFIN=         @D1C @L2C         
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCOC                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: CONTROL = OPRCNTL PROCESSOR                 */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5694-A01 (C) COPYRIGHT IBM CORP. 1987, 2001.       */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */         
.* /*      CONTROL=OPRCNTL PARAMETER ON THE APPCCMD MACRO.           */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS:                                              */         
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCUI,IST6MCER              */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: NONE                                     */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*    09/03/86 - DP0053 ALLOW USERFLD ON OPRCNTL          @P041406*/         
.* /*    11/19/87 - ALLOW RECLEN ON ACTSESS                  @P051842*/         
.* /*                                                                */         
.* /* $MAC(IST6MCOC),COMP(API),PROD(VTAM): CONTROL=OPRCNTL PROCESSOR */         
.* /*                                                                */         
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */         
.* /* ---- -------- ------- ------ ------  ---------------------     */         
.* /* $B1= AP@@@CLD HVT3401 890508 496698: PERSISTENT LU-LU SESSIONS */         
.* /* $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION          */         
.* /* $KF= NQNSTG2  NQNAME2 910605 000000: ENHANCED NQN SUPPORT (2)  */         
.* /* $01= OY43143  VM340   910731 617408: UPDATE RPL6USR WITH       */         
.* /*                                      PROVIDED USRFLD VALUE     */         
.* /* $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services     */         
.* /* $U2= P110887  GENRES  931024 879315: Generic Resource support  */         
.* /* $L2= KA@DR006 APPCRR  940713 576146: Vector Format             */         
.* /* $D1= ID@@@MST FPAPI   950309 143576: Fast Path API for APPC    */         
.* /* Flag Reason   Release  Date   Origin    Description            */         
.* /* ---- -------- -------- ------ --------  ---------------------- */         
.* /* $L2= R12GRAFF CSV1R2   000829 ROTHWELL: LU level GR AFFIN      */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
.* A-000000-999999                                             @R495810         
.*                                                                              
         GBLB  &ISTGEN                                                          
         GBLB  &ISTOC                                                           
         AIF   (&ISTOC EQ 1).OCINVKW                                            
*                                                                               
*  IST6MCOC - ACF/VTAM                                                          
*                                                                               
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                              
*                                                                               
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"               
*                                                                               
*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1993.                      
*             ALL RIGHTS RESERVED.                                              
*                                                                               
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                         
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY                      
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                          
*                                                                               
*             SEE COPYRIGHT INSTRUCTIONS.                                       
*                                                                               
&ISTOC   SETB  1                                                                
.OCINVKW ANOP                                                                   
         IST6MCUC OPRCNTL,CONTROL,INVALKW=&INVALKW                              
.*                                         CHECK INVALID APPCCMD                
.OCQFY   ANOP                                                                   
         AIF   ('&QUALIFY' EQ '').OCCONF1                                       
.OCQFY1  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'ACTSESS').OCQFY2                                 
         IST6MCUC ACTSESS,QUALIFY,INVALKW=(AREALEN=&AREALEN,LOGMODE=&LOX        
               GMODE,LUNAME=&LUNAME,LIST=&LIST,NETID=&NETID,NAMEUSE=&NAX        
               MEUSE,LUAFFIN=&LUAFFIN)                        @U2C @L2C         
         AIF   (NOT &ISTGEN).OCCONF1                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6ACT                                    
*                                         PUT ACTSESS CODE IN APPC AREA         
.OCRLEN1 ANOP                                                  @P051842         
         AIF   ('&RECLEN' EQ '').OCAREA1                       @P051842         
         IST6MCUA KWVAL=&RECLEN,LOC=RPLRLEN-IFGRPL,BASEREG=1,          X        
               KW=DATA_LENGTH                                  @P051842         
.OCAREA1 ANOP                                                                   
         AIF   ('&AREA' EQ '').OCCONF1                                          
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X        
               KW=AREA_ADDRESS                                                  
         AGO   .OCCONF1                                                         
.OCQFY2  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'DACTSESS').OCQFY3                                
         IST6MCUC DACTSESS,QUALIFY,INVALKW=(AREA=&AREA,AREALEN=&AREALENX        
               ,LOGMODE=&LOGMODE,LUNAME=&LUNAME,RECLEN=&RECLEN,CONFTXT=X        
               &CONFTXT,LIST=&LIST,NETID=&NETID,NAMEUSE=&NAMEUSE,      X        
               LUAFFIN=&LUAFFIN)                              @U2C @L2C         
         AIF   (NOT &ISTGEN).OCARG                                              
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6DACT                                   
*                                        PUT DACTSESS CODE IN APPC AREA         
         AGO   .OCARG                                                           
.OCQFY3  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'DISPLAY').OCQFY4                                 
         IST6MCUC DISPLAY,QUALIFY,INVALKW=(RECLEN=&RECLEN,ARG=&ARG,CONFX        
               TXT=&CONFTXT,LIST=&LIST,NAMEUSE=&NAMEUSE,               X        
               LUAFFIN=&LUAFFIN)                              @U2C @L2C         
         AIF   (NOT &ISTGEN).OCLUNAM                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6DSPY                                   
*                                         PUT DISPLAY CODE IN APPC AREA         
.OCALEN1 ANOP                                                                   
         AIF   ('&AREALEN' EQ '').OCAREA2                                       
         IST6MCUA KWVAL=&AREALEN,LOC=RPLBUFL-IFGRPL,BASEREG=1,         X        
               KW=AREA_LENGTH                                                   
         AGO   .OCAREA2                                                         
.OCQFY4  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'CNOS').OCQFY5                                    
         IST6MCUC CNOS,QUALIFY,INVALKW=(AREALEN=&AREALEN,ARG=&ARG,CONFTX        
               XT=&CONFTXT,LIST=&LIST)                             @B1C         
         AIF   (NOT &ISTGEN).OCLUNAM                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6CNOS                                   
*                                          PUT CNOS CODE IN APPC AREA           
         AGO   .OCRLEN                                                          
.OCQFY5  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'DEFINE').OCQFY6                                  
         IST6MCUC DEFINE,QUALIFY,INVALKW=(AREALEN=&AREALEN,ARG=&ARG,CONX        
               FTXT=&CONFTXT,LIST=&LIST,NAMEUSE=&NAMEUSE,              X        
               LUAFFIN=&LUAFFIN)                              @U2C @L2C         
         AIF   (NOT &ISTGEN).OCLUNAM                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6DFIN                                   
*                                         PUT DEFINE CODE IN APPC AREA          
         AGO   .OCRLEN                                                          
.OCQFY6  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'RESTORE').OCQFY7                    @B1A         
         IST6MCUC RESTORE,QUALIFY,INVALKW=(RECLEN=&RECLEN,ARG=&ARG,    X        
               CONFTXT=&CONFTXT,NAMEUSE=&NAMEUSE,                      X        
               LUAFFIN=&LUAFFIN)                              @U2C @L2C         
         AIF   (NOT &ISTGEN).OCLUNAM                               @B1A         
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6REST                      @B1A         
*                                         PUT RESTORE CODE IN APPC AREA         
         AIF   ('&LIST' NE '').OCLINA     LIST= SPECIFIED?         @B1A         
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6LINS-RPL6LIAL       @B1A         
*                                         DEFAULT LIST TO NONE     @B1A         
         AGO   .OCALEN1                   CHECK LENGTH KEYWORD     @B1A         
.OCLINA  ANOP                                                      @B1A         
         AIF   ('&LIST' NE 'ALL').OCLINO  SPECIFY LIST=ALL IN RPL  @B1A         
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6LINS                @B1A         
         OI    RPL6FLG1-ISTRPL6X(14),RPL6LIAL                      @B1A         
         AGO   .OCALEN1                   CHECK LENGTH KEYWORD     @B1A         
.OCLINO  ANOP                                                      @B1A         
         AIF   ('&LIST' NE 'NONE').OCLINS SPECIFY LIST=NONE IN RPL @B1A         
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6LINS-RPL6LIAL       @B1A         
         AGO   .OCALEN1                   CHECK LENGTH KEYWORD     @B1A         
.OCLINS  ANOP                                                      @B1A         
         AIF   ('&LIST' NE 'NOSESS').OCLIER   SPECIFY LIST=NOSESS  @B1A         
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6LIAL                @B1A         
         OI    RPL6FLG1-ISTRPL6X(14),RPL6LINS                      @B1A         
         AGO   .OCALEN1                   CHECK LENGTH KEYWORD     @B1A         
.OCLIER  ANOP                                                      @B1A         
         IST6MCER 5,LIST,&LIST                                     @B1A         
         AGO   .OCALEN1                   CHECK LENGTH KEYWORD     @B1A         
.OCQFY7  ANOP                                                      @B1A         
         IST6MCER 5,QUALIFY,&QUALIFY                                            
.OCCONF1 ANOP                                                                   
         AIF   ('&CONFTXT' EQ '').OCARG                                         
         AIF   ('&CONFTXT' NE 'YES').OCCONF2                                    
         AIF   (NOT &ISTGEN).OCARG                                              
         OI    RPL6FLG1-ISTRPL6X(14),RPL6CFT SET CONFTXT TO YES                 
         AGO   .OCARG                                                           
.OCCONF2 ANOP                                                                   
         AIF   ('&CONFTXT' NE 'NO').OCCONF3                                     
         AIF   (NOT &ISTGEN).OCARG                                              
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6CFT                              
*                                            SET CONFTXT TO NO                  
         AGO   .OCARG                                                           
.OCCONF3 ANOP                                                                   
         IST6MCER 5,CONFTXT,&CONFTXT                                            
.OCARG   ANOP                                                                   
         AIF   ('&ARG' EQ '').OCCKPT                                            
         IST6MCUA KWVAL=&ARG,LOC=RPLARG-IFGRPL,BASEREG=1,KW=CID                 
.OCCKPT  ANOP                                                                   
         AIF   ('&QUALIFY' EQ 'ACTSESS').OCURFLD                   @01A         
         AIF   ('&QUALIFY' EQ 'DACTSESS').OCURFLD                  @01A         
.OCALEN2 ANOP                                                                   
         AIF   ('&AREALEN' EQ '').OCRLEN                                        
         IST6MCUA KWVAL=&AREALEN,LOC=RPLBUFL-IFGRPL,BASEREG=1,         X        
               KW=AREA_LENGTH                                                   
.OCRLEN  ANOP                                                                   
         AIF   ('&RECLEN' EQ '').OCAREA2                                        
         IST6MCUA KWVAL=&RECLEN,LOC=RPLRLEN-IFGRPL,BASEREG=1,          X        
               KW=DATA_LENGTH                                                   
.OCAREA2 ANOP                                                                   
         AIF   ('&AREA' EQ '').OCLUNAM                                          
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X        
               KW=AREA_ADDRESS                                                  
.OCLUNAM ANOP                                                                   
         AIF   ('&LUNAME' EQ '').OCNETID                           @KFC         
         IST6MCUI KW=LUNAME,KWVAL=&LUNAME,LEN=8,                       X        
               LOC=RPL6LU-ISTRPL6X,BASEREG=14                                   
.*                                      PUT LUNAME IN APPC AREA                 
.OCNETID ANOP                                                      @KFA         
         AIF   ('&NETID' EQ '').OCLOGM                             @KFA         
         IST6MCUI KW=NETID,KWVAL=&NETID,LEN=8,                         X        
               LOC=RPL6NET-ISTRPL6X,BASEREG=14                     @KFA         
.*                                      PUT NETID IN APPC AREA                  
.OCLOGM  ANOP                                                                   
         AIF   ('&LOGMODE' EQ '').OCURFLD                      @P041406         
         IST6MCUI KW=LOGMODE,KWVAL=&LOGMODE,LEN=8,                     X        
               LOC=RPL6MODE-ISTRPL6X,BASEREG=14                                 
.*                                     PUT LOGMODE IN APPC AREA                 
.OCURFLD ANOP                                                  @P041406         
         AIF   ('&USERFLD' EQ '').OCNMUSE                          @U2C         
         IST6MCUI KW=USERFLD,KWVAL=&USERFLD,LEN=4,                     X        
               LOC=RPL6USR-ISTRPL6X,BASEREG=14                 @P041406         
.*                                   PUT USERFLD IN APPC AREA  @P041406         
.OCNMUSE ANOP                                                      @U2A         
         AIF   ('&NAMEUSE' EQ '').OCLUAFF   NAMEUSE OMITTED?  @L1C @L2C         
         AIF   ('&NAMEUSE' NE 'APNAME').OCNMU1     NAMEUSE=APNAME? @U2A         
         AIF   (NOT &ISTGEN).OCLUAFF                               @L2A         
         NI    RPL6FLG2-ISTRPL6X(14),X'FF'-RPL6NAMU    RESET BITS  @U2A         
         OI    RPL6FLG2-ISTRPL6X(14),RPL6ANAM  SET VALUE TO APNAME @U2A         
         AGO   .OCLUAFF                                       @L1C @L2C         
.OCNMU1  ANOP                                                      @U2A         
         AIF   ('&NAMEUSE' NE 'GNAME').OCNMUER     NAMEUSE=GNAME?  @U2A         
         AIF   (NOT &ISTGEN).OCLUAFF                               @L2A         
         NI    RPL6FLG2-ISTRPL6X(14),X'FF'-RPL6NAMU    RESET BITS  @U2A         
         OI    RPL6FLG2-ISTRPL6X(14),RPL6GNAM  SET VALUE TO GNAME  @U2A         
         AGO   .OCLUAFF                                       @L1C @L2C         
.OCNMUER ANOP                                                      @U2A         
         IST6MCER 5,NAMEUSE,&NAMEUSE                               @U2A         
.OCLUAFF ANOP                                                      @L2A         
         AIF   ('&LUAFFIN' EQ '').OCVECT          LUAFFIN OMITTED? @L2A         
         AIF   ('&LUAFFIN' NE 'APPL').OCLUA1      LUAFFIN=APPL?    @L2A         
         AIF   (NOT &ISTGEN).OCVECT                                @L2A         
         NI    RPL6FLG4-ISTRPL6X(14),X'FF'-RPL6AFFN    RESET BITS  @L2A         
         OI    RPL6FLG4-ISTRPL6X(14),RPL6AAFF   SET VALUE TO APPL  @L2A         
         AGO   .OCVECT                                             @L2A         
.OCLUA1  ANOP                                                      @L2A         
         AIF   ('&LUAFFIN' NE 'NOTAPPL').OCLUAER  LUAFFIN=NOTAPPL? @L2A         
         AIF   (NOT &ISTGEN).OCVECT                                @L2A         
         NI    RPL6FLG4-ISTRPL6X(14),X'FF'-RPL6AFFN    RESET BITS  @L2A         
         OI    RPL6FLG4-ISTRPL6X(14),RPL6NAAF   SET NOTAPPL VALUE  @L2A         
         AGO   .OCVECT                                             @L2A         
.OCLUAER ANOP                                                      @L2A         
         IST6MCER 5,LUAFFIN,&LUAFFIN                               @L2A         
.OCVECT  ANOP                        ENSURE BOTH PARMS ARE CODED   @L1A         
.*                                   OR BOTH NOT CODED             @L1A         
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).OCVOUT    @D1C         
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).OCERVI     @D1C         
         IST6MCUA KW=VTAM_TO_APPL_VECTOR_ADDRESS,KWVAL=&VTRINA,        X        
               LOC=RPL6VAIA-ISTRPL6X,BASEREG=14                    @D1C         
.*                                     STORE VECTOR AREA INPUT ADDRESS          
.*                                                                 @L1A         
         IST6MCUA KW=VTAM_TO_APPL_VECTOR_LENGTH,KWVAL=&VTRINL,LEN=2,   X        
               LOC=RPL6VAIL-ISTRPL6X,BASEREG=14                    @L1A         
.*                                     STORE VECTOR AREA INPUT LENGTH           
.*                                                                 @L1A         
         AGO   .OCVOUT                                             @D1C         
.OCERVI  ANOP                                                      @L1A         
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C         
.OCVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED       @D1A         
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).OCCOMKW @D1A         
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).OCERVO   @D1A         
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X        
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                @D1A         
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X        
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                @D1A         
         AGO   .OCCOMKW                                            @D1A         
.OCERVO  ANOP                                                      @D1A         
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A         
.OCCOMKW ANOP                                                      @L1C         
         IST6MCUP OPTCD=&OPTCD,ECB=&ECB,EXIT=&EXIT,                    X        
               BRANCH=&BRANCH,                                         X        
               CKCMODE=NO,               DO NOT CHECK CONMODE PARAMETERX        
               CKSYN=YES,          ASY/SYN IS VALID FOR OPTCD PARAMETERX        
               CKBUFF=NO           BUFFLST/NBUFFLST IS INVALID FOR              
.*                                 OPTCD PARAMETER                              
.OCREQ   ANOP                                                                   
         AIF   (NOT &ISTGEN).OCEXIT                                             
         MVI   RPL6REQ-ISTRPL6X(14),RPL6OPER                                    
*                              PUT OPRCNTL REQUEST CODE IN APPC AREA            
.OCEXIT  MEND                                                                   
