         MACRO                                                                  
         IST6MCPA &BRANCH=,&CGID=,&CONMODE=,&CONXMOD=,&ECB=,&EXIT=,    X        
               &INVALP1=,&INVALP2=,&LOGMODE=,&LUNAME=,&NAMEUSE=,       X        
               &NETID=,&OPTCD=,&QUALIFY=,&RTSRTRN=,&USERFLD=,          X        
               &VTRINA=,&VTRINL=,&VTROUTA=,&VTROUTL=,&LUAFFIN=     @L2C         
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCPA                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: CONTROL = PREALLOC PROCESSOR                */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5694-A01 (C) COPYRIGHT IBM CORP. 1994, 2001.       */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */         
.* /*      CONTROL=PREALLOC PARAMETER ON THE APPCCMD MACRO.          */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS:                                              */         
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCUI,IST6MCER              */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: NONE                                     */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*                                                                */         
.* /* $MAC(IST6MCPA),COMP(API),PROD(VTAM): CONTROL = PREALLOC        */         
.* /*                                                                */         
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */         
.* /* ---- -------- ------- ------ ------  ------------------------- */         
.* /* $L0= KA@@@PLD APPCRR  930722 446638  DCE Security Services     */         
.* /* $Y1= P114808  APPCRR  940518 576146  DCE Security Services     */         
.* /* $L1= KA@DR006 APPCRR  940713 576146  Vector Format             */         
.* /* $D1= ID@@@MST FPAPI   950306 143576  Fast Path API for APPC    */         
.* /* Flag Reason   Release  Date   Origin    Description            */         
.* /* ---- -------- -------- ------ --------  ---------------------- */         
.* /* $L2= R12GRAFF CSV1R2   000829 ROTHWELL: LU level GR AFFIN      */         
.* /*                                                                */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
         GBLB  &ISTGEN                                                          
         GBLB  &ISTPA                                                           
         AIF   (&ISTPA EQ 1).PAINVKW                                            
&ISTPA   SETB  1                                                                
.*                                           CHECK INVALID APPCCMD              
.PAINVKW ANOP                                                                   
         IST6MCUC PREALLOC,CONTROL,INVALKW=&INVALP1                             
         IST6MCUC PREALLOC,CONTROL,INVALKW=&INVALP2                             
.PACGID  ANOP                                                                   
         AIF   ('&CGID' EQ '').PALOGM                                           
         IST6MCUA KW=CGID,KWVAL=&CGID,                                 X        
               LOC=RPL6CGID-ISTRPL6X,BASEREG=14                                 
.*                                     PUT CGID IN APPC AREA                    
.PALOGM  ANOP                                                                   
         AIF   ('&LOGMODE' EQ '').PALUNAM                                       
         IST6MCUI KW=LOGMODE,KWVAL=&LOGMODE,LEN=8,                     X        
               LOC=RPL6MODE-ISTRPL6X,BASEREG=14                                 
.*                                     PUT LOGMODE IN APPC AREA                 
.PALUNAM ANOP                                                                   
         AIF   ('&LUNAME' EQ '').PANETID                                        
         IST6MCUI KW=LUNAME,KWVAL=&LUNAME,LEN=8,                       X        
               LOC=RPL6LU-ISTRPL6X,BASEREG=14                                   
.*                                     PUT LUNAME IN APPC AREA                  
.PANETID ANOP                                                                   
         AIF   ('&NETID' EQ '').PACOMKW                                         
         IST6MCUI KW=NETID,KWVAL=&NETID,LEN=8,                         X        
               LOC=RPL6NET-ISTRPL6X,BASEREG=14                                  
.*                                     PUT NETID IN APPC AREA                   
.PACOMKW ANOP                                                                   
         IST6MCUP BRANCH=&BRANCH,CONMODE=&CONMODE,CONXMOD=&CONXMOD,    X        
               ECB=&ECB,EXIT=&EXIT,OPTCD=&OPTCD,                       X        
               CKCMODE=YES,            CHECK CONMODE & CONXMOD PARMS   X        
               CKSYN=YES,              ASY/SYN IS VALID FOR OPTCD PARM X        
               CKBUFF=NO               BUFFLST/NBUFFLST IS INVALID              
.*                                     OPTCD PARAMETER                          
.PAQFY   ANOP                                                                   
         AIF   ('&QUALIFY' EQ '').PARTSR                                        
         AIF   ('&QUALIFY' EQ 'ALLOCD').PAALLD                                  
         AIF   ('&QUALIFY' EQ 'CONWIN').PACONW                                  
         AIF   ('&QUALIFY' EQ 'CONVGRP').PACONV                                 
         AIF   ('&QUALIFY' EQ 'IMMED').PAIMMD                                   
         AIF   ('&QUALIFY' EQ 'WHENFREE').PAWHNF                                
         IST6MCER 5,QUALIFY,&QUALIFY                               @Y1C         
         AGO   .PARTSR                                                          
.*                                                                              
.PAALLD  ANOP                                                                   
         AIF   (NOT &ISTGEN).PARTSR                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6ALCD                                   
.*                                     SET QUALIFY TO ALLOCD                    
         AGO   .PARTSR                                                          
.PACONW  ANOP                                                                   
         AIF   (NOT &ISTGEN).PARTSR                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6CWIN                                   
.*                                     SET QUALIFY TO CONWIN                    
         AGO   .PARTSR                                                          
.PACONV  ANOP                                                                   
         AIF   (NOT &ISTGEN).PARTSR                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6CNGP                                   
.*                                     SET QUALIFY TO CONVGRP                   
         AGO   .PARTSR                                                          
.PAIMMD  ANOP                                                                   
         AIF   (NOT &ISTGEN).PARTSR                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6IMED                                   
.*                                     SET QUALIFY TO IMMED                     
         AGO   .PARTSR                                                          
.PAWHNF  ANOP                                                                   
         AIF   (NOT &ISTGEN).PARTSR                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6SESF                                   
.*                                     SET QUALIFY TO WHENFREE                  
.PARTSR  AIF   ('&RTSRTRN' EQ '').PAURFLD                                       
         AIF   ('&RTSRTRN' NE 'BOTH').PARTSRE                                   
         AIF   (NOT &ISTGEN).PAURFLD                                            
         NI    RPL6FLG2-ISTRPL6X(14),X'FF'-RPL6RTSX  RESET FLAG                 
         AGO   .PAURFLD                                                         
.PARTSRE ANOP                                                                   
         AIF   ('&RTSRTRN' NE 'EXPD').PARTSER                                   
         AIF   (NOT &ISTGEN).PAURFLD                                            
         OI    RPL6FLG2-ISTRPL6X(14),RPL6RTSX  SET FLAG                         
         AGO   .PAURFLD                                                         
.PARTSER ANOP                                                                   
         IST6MCER 5,RTSRTRN,&RTSRTRN   REPORT BAD VALUE                         
.PAURFLD ANOP                                                                   
         AIF   ('&USERFLD' EQ '').PANMUSE                                       
         IST6MCUI KW=USERFLD,KWVAL=&USERFLD,LEN=4,                     X        
               LOC=RPL6USR-ISTRPL6X,BASEREG=14                                  
.*                                     PUT USERFLD IN APPC AREA                 
.PANMUSE ANOP                                                                   
         AIF   ('&NAMEUSE' EQ '').PALUAFF        NAMEUSE OMITTED?  @L2C         
         AIF   ('&NAMEUSE' NE 'APNAME').PANMU1     NAMEUSE=APNAME?              
         AIF   (NOT &ISTGEN).PALUAFF                               @L2C         
         NI    RPL6FLG2-ISTRPL6X(14),X'FF'-RPL6NAMU    RESET BITS               
         OI    RPL6FLG2-ISTRPL6X(14),RPL6ANAM  SET VALUE TO APNAME              
         AGO   .PALUAFF                                            @L2C         
.PANMU1  ANOP                                                                   
         AIF   ('&NAMEUSE' NE 'GNAME').PANMUER     NAMEUSE=GNAME?               
         AIF   (NOT &ISTGEN).PALUAFF                               @L2C         
         NI    RPL6FLG2-ISTRPL6X(14),X'FF'-RPL6NAMU    RESET BITS               
         OI    RPL6FLG2-ISTRPL6X(14),RPL6GNAM  SET VALUE TO GNAME               
         AGO   .PALUAFF                                            @L2C         
.PANMUER ANOP                                                                   
         IST6MCER 5,NAMEUSE,&NAMEUSE                                            
.PALUAFF ANOP                                                      @L2A         
         AIF   ('&LUAFFIN' EQ '').PAVECT          LUAFFIN OMITTED? @L2A         
         AIF   ('&LUAFFIN' NE 'APPL').PALUA1      LUAFFIN=APPL?    @L2A         
         AIF   (NOT &ISTGEN).PAVECT                                @L2A         
         NI    RPL6FLG4-ISTRPL6X(14),X'FF'-RPL6AFFN    RESET BITS  @L2A         
         OI    RPL6FLG4-ISTRPL6X(14),RPL6AAFF   SET VALUE TO APPL  @L2A         
         AGO   .PAVECT                                             @L2A         
.PALUA1  ANOP                                                      @L2A         
         AIF   ('&LUAFFIN' NE 'NOTAPPL').PALUAER  LUAFFIN=NOTAPPL? @L2A         
         AIF   (NOT &ISTGEN).PAVECT                                @L2A         
         NI    RPL6FLG4-ISTRPL6X(14),X'FF'-RPL6AFFN    RESET BITS  @L2A         
         OI    RPL6FLG4-ISTRPL6X(14),RPL6NAAF   SET NOTAPPL VALUE  @L2A         
         AGO   .PAVECT                                             @L2A         
.PALUAER ANOP                                                      @L2A         
         IST6MCER 5,LUAFFIN,&LUAFFIN                               @L2A         
.PAVECT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED                    
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).PAVOUT    @D1C         
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).PAERVI     @D1C         
         IST6MCUA KWVAL=&VTRINA,KW=VTAM_TO_APPL_VECTOR_ADDRESS,        X        
               LOC=RPL6VAIA-ISTRPL6X,BASEREG=14                    @D1C         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                             
         IST6MCUA KWVAL=&VTRINL,KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2,  X0        
               LOC=RPL6VAIL-ISTRPL6X,BASEREG=14                    @D1C         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                             
         AGO   .PAVOUT                                             @D1C         
.PAERVI  ANOP                                                                   
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C         
.PAVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED       @D1A         
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).PAREQ1  @D1A         
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).PAERVO   @D1A         
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X        
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                @D1A         
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X        
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                @D1A         
         AGO   .PAREQ1                                             @D1A         
.PAERVO  ANOP                                                      @D1A         
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A         
.PAREQ1  AIF   (NOT &ISTGEN).PAEXIT                                             
         MVI   RPL6REQ-ISTRPL6X(14),RPL6PLOC                                    
*                                      PUT PREALLOC REQUEST CODE IN             
*                                      APPC AREA                                
.PAEXIT  MEND                                                                   
