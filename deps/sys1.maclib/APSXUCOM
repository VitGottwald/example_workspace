         MACRO                                                          00010000
         APSXUCOM &BUFF=1599,&JAN=JAN,&FEB=FEB,&MAR=MAR,&APR=APR,      X00020000
               &MAY=MAY,&JUN=JUN,&JUL=JUL,&AUG=AUG,&SEP=SEP,           X00030000
               &OCT=OCT,&NOV=NOV,&DEC=DEC                               00040000
.***** START OF SPECIFICATIONS **************************************** 00050000
.*                                                                      00060000
.* 01* MACRO NAME  = APSXUCOM                                           00070000
.* $MAC (APSXUCOM) COMP(APS) PROD(PSF) : VERSION 4.7.0         /*    */ 00080000
.*                                                                      00090000
.* 01* DESCRIPTIVE NAME = GENERATE THE APSUCOM TABLE                    00100000
.*                                                                      00110000
.*01* STATUS = HPRF470                                         /*    */ 00120000
.*                                                                      00130000
.* 01* FUNCTION = THIS MACRO WILL GENERATE A DEFAULT OR A MODIFIED      00140000
.*                COPY OF THE APSUCOM TABLE.  APSUCOM IS AN             00150000
.*                INSTALLATION EXIT TABLE CONSISING OF A FOUR           00160000
.*                BYTE LENGTH FIELD PLUS A JULIAN TO DAY AND            00170000
.*                MONTH CONVERSION TABLE.                               00180000
.*                                                                      00190000
.*                THE LENGTH FIELD IN APSUCOM DESCRIBES THE             00200000
.*                THE TOTAL LENGTH OF THE APSUECA CONTROL BLOCK.        00210000
.*                THE APSUECA CONSISTS OF TWO PARTS, THE FIXED          00220000
.*                HEADER PORTION AND A WORKING BUFFER AREA.             00230000
.*                THE LENGTH OF THE FIXED HEADER IS OBTAINED            00240000
.*                FROM AN EQUATE DECLARED IN APSUECA.                   00250000
.*                THE WORKING BUFFER AREA CAN BE INCREASED FROM         00260000
.*                ITS MINIMUM LENGTH BY USING THE BUFF INPUT            00270000
.*                PARAMETER VARIABLE WHEN INVOKING THIS MACRO.          00280000
.*                                                                      00290000
.*                SHOULD AN INSTALLATION WISH TO CHANGE THE             00300000
.*                THE NAMES OF THE MONTHS THAT APPEAR ON THE            00310000
.*                SEPARATOR PAGES THE MONTH INPUT PARAMETER             00320000
.*                VARIABLES MAY BE USED TO CREATE A NEW APSUCOM         00330000
.*                TABLE.                                                00340000
.*                                                                      00350000
.* 02* OPERATION = INVOCATION OF THIS MACRO WILL CONSTRUCT              00360000
.*                 ASSEMBLER DC STATEMENTS WHICH COMPRISE THE           00370000
.*                 APSUCOM TABLE.                                       00380000
.*                                                                      00390000
.* 01* NOTES =                                                          00400000
.* 02*   DEPENDENCIES = MACRO APSUECA MUST BE AVAILABLE FOR             00410000
.*                      INCLUSION BY THIS MACRO.                        00420000
.*                                                                      00430000
.* 02*   DEPENDENCIES = THE VALUE SPECIFIED FOR THE BUFFER SIZE         00440000
.*                      MUST NOT BE LESS THAN 1599 NOR GREATER          00450000
.*                      THAN 1,048,576.  PSF  UTILIZES  THIS            00460000
.*                      BUFFER FOR WORKING STORAGE.                     00470000
.*                                                                      00480000
.*                      IF THE JULIAN TABLE IS CHANGED. THE INPUT       00490000
.*                      VARIABLE FOR EACH MONTH MUST NOT EXCEED         00500000
.*                      A LENGTH OF THREE.                              00510000
.*                                                                      00520000
.* 01* MODULE TYPE = MACRO                                              00530000
.* 02*   PROCESSOR = ASSEMBLER                                          00540000
.* 02*   MACRO  SIZE = 30 LOC                                           00550000
.*                                                                      00560000
.* 01* ENTRY POINT = APSXUCOM                                           00570000
.* 02*   PURPOSE = GENERATE AN INSTALLATION EXIT TABLE                  00580000
.*                                                                      00590000
.* 01* MACRO USERS = INSTALLATIONS                                      00600000
.*                                                                      00610000
.* 01* INPUT =                                                          00620000
.*            APSXUCOM  (ANY VARIABLE NOT SPECIFIED WILL DEFAULT        00630000
.*                       TO THE STANDARD VALUE)                         00640000
.*                                                                      00650000
.*            APSXUCOM  BUFF=5678,JAN=AAA, ... DEC=ZZZ                  00660000
.*                                                                      00670000
.*       WHERE   BUFF    - IS THE WORKING BUFFER SIZE FOR APSUECA,      00680000
.*                          DECIMAL VALUE OF 1599 TO 1,048,576          00690000
.*                                                                      00700000
.*               JAN     - IS THE VARIABLE NAME FOR JANUARY             00710000
.*               FEB     - IS THE VARIABLE NAME FOR FEBRUARY            00720000
.*               MAR     - IS THE VARIABLE NAME FOR MARCH               00730000
.*               APR     - IS THE VARIABLE NAME FOR APRIL               00740000
.*               MAY     - IS THE VARIABLE NAME FOR MAY                 00750000
.*               JUN     - IS THE VARIABLE NAME FOR JUNE                00760000
.*               JUL     - IS THE VARIABLE NAME FOR JULY                00770000
.*               AUG     - IS THE VARIABLE NAME FOR AUGUST              00780000
.*               SEP     - IS THE VARIABLE NAME FOR SEPTEMBER           00790000
.*               OCT     - IS THE VARIABLE NAME FOR OCTBER              00800000
.*               NOV     - IS THE VARIABLE NAME FOR NOVEMBER            00810000
.*               DEC     - IS THE VARIABLE NAME FOR DECEMBER            00820000
.*                                                                      00830000
.* 01* OUTPUT = ASSEMBLER DC STATEMENTS                                 00840000
.*                                                                      00850000
.* 01* EXIT-NORMAL = MACRO RETURN CODE 0                                00860000
.*                                                                      00870000
.* 01* EXIT-ERROR = MNOTE WITH RETURN CODE 8                            00880000
.*                                                                      00890000
.*      MACRO RETURN     CONDITION                                      00900000
.*           8    -    BUFFER SIZE NOT WITHIN RANGE                     00910000
.*           8    -    MONTH VARIABLE SPECIFIED INCORRECTLY             00920000
.*                                                                      00930000
.* 01* EXTERNAL REFERENCES =                                            00940000
.* 02*   ROUTINES = NONE                                                00950000
.* 02*   DATA AREAS = NONE                                              00960000
.* 02*   CONTROL BLOCKS =                                               00970000
.* 03*       CONTROL BLOCKS REFERENCED: APSUECA                         00980000
.* 03*       CONTROL BLOCKS MODIFIED: NONE                              00990000
.*                                                                      01000000
.* 01* TABLES = NONE                                                    01010000
.*                                                                      01020000
.* 01* MACROS = NONE                                                    01030000
.*                                                                      01040000
.* 01* MESSAGES = ASSEMBLE TIME MACRO MESSAGES                          01050000
.* 02*   INPUT PARAMETER ERROR, BUFFER SIZE LESS THAN 1599 OR           01060000
.*       MORE THAN 1,048,576                                            01070000
.* 02*   INPUT PARAMETER ERROR, MONTH VARIABLE EXCEEDS MANIMUN LENGTH   01080000
.*       OF 3                                                           01090000
.*                                                                      01100000
.* 01* CHANGE ACTIVITY =                                                01110000
.* $B0=LAPS0000, HAF1102, 093083, TR4  : INITIAL VERSION                01120000
.* $H2=LAPS0002, HAF1203, 080186, BJ13DES : RELEASE 2.0                 01130000
.* $H3=LAPS0003, HAF1220, 081787, B51PSM : RELEASE 2.1                  01140000
.* $H4=LAPS0005, HPRF102, 040789, B53KDAR: REL 2.1.0 PLAS CONV /*@H4A*/ 01150000
.* $DU=LAPS0007,HPRF310,980604,BDKURLB: Version 3.1.0            @DUA*/ 01160000
.* $DX=LAPS0008,HPRF320,991117,BUQ4RLB: Version 3.2.0            @DXA*/ 01170000
.* $EV=LAPS0009,HPRF330,010530,BUQ4RLB: Version 3.3.0            @EVA*/ 01180000
.* $FC=LAPS0010,HPRF340,020612,BDKULD : Version 3.4.0            @FCA*/ 01190000
.* $FM=LAPS0011,HPRF410,040220,BDKURDD: Version 4.1.0            @FMA*/ 01200000
.* $FU=LAPS0012,HPRF420,060630,BDKURDD: Version 4.2.0            @FUA*/ 01210000
.* $GD=LAPS0013,HPRF430,080514,BDKURDD: Version 4.3.0            @GDA*/ 01220000
.* $GH=LAPS0014,HPRF440,090922,BDKURDD: Version 4.4.0            @GHA*/ 01230000
.* $HH=LAPS0015,HPRF450,111202,BDKUGLM: Version 4.5.0            @HHA*/ 01240000
.* $IL=LAPS0016,HPRF460,151207,BDKUDWP: Version 4.6.0            @ILA*/ 01250000
.* $JM=LAPS0017,HPRF470,171116,BDKUDWP: Version 4.7.0            @JMA*/ 01260000
.****** END OF SPECIFICATIONS ***************************************** 01270000
.********************************************************************** 01280000
.* VERIFY THE BUFFER SIZE PARAMETER FOR CORRECT LENGTH.                 01290000
.********************************************************************** 01300000
         AIF   (&BUFF LT 1599 OR &BUFF GT 1048576).OUTMSG1  VERIFY SIZE 01310000
.********************************************************************** 01320000
.* VERIFY ALL MONTH PARAMETERS FOR CORRECT LENGTH.                      01330000
.********************************************************************** 01340000
         AIF   (K'&JAN GT 3 OR K'&FEB GT 3).OUTMSG2         VERIFY      01350000
         AIF   (K'&MAR GT 3 OR K'&APR GT 3).OUTMSG2         LENGTH      01360000
         AIF   (K'&MAY GT 3 OR K'&JUN GT 3).OUTMSG2         OF          01370000
         AIF   (K'&JUL GT 3 OR K'&AUG GT 3).OUTMSG2         MONTH       01380000
         AIF   (K'&SEP GT 3 OR K'&OCT GT 3).OUTMSG2         INPUT       01390000
         AIF   (K'&NOV GT 3 OR K'&DEC GT 3).OUTMSG2         PARAMETERS  01400000
*********************************************************************** 01410000
* THE LENGTH OF CONTROL BLOCK APSUECA IS CALCULATED FROM A FIXED        01420000
* HEADER LENGTH, WHICH IS DECLARED IN APSUECA, PLUS A WORK BUFFER SIZE. 01430000
*********************************************************************** 01440000
FIXLEN   DC    A(ECAFXLEN+&BUFF)                     LENGTH OF APSUECA  01450000
 EJECT                                                                  01460000
*********************************************************************** 01470000
*                                                                       01480000
* THIS TABLE CONSISTS OF NUMBER OF DAYS IN EACH MONTH ENTRIES           01490000
* AND EBCDIC CHARACTER ENTRIES FOR EACH MONTH.  LEAP YEAR MUST BE       01500000
* ACCOUNTED FOR EITHER BY MODIFING A COPY OF THIS TABLE OR WRITING      01510000
* CODE TO DYNAMICALY CHANGE THE NUMBER OF DAYS IN FEBRUARY.             01520000
*                                                                       01530000
* THE ENTRY FOR THE NUMBER OF DAYS DURING DECEMBER HAS BEEN             01540000
* SET TO THE MAXIMUM ALLOWED BY THIS ONE BYTE TABLE ENTRY.              01550000
* THIS NUMBER WILL INSURE A BRANCH OUT OF A JULIAN DAY TO               01560000
* STANDARD DAY SEARCH LOOP DURING THE DECEMBER COMPARISON.              01570000
*                                                                       01580000
* THE FOLLOWING EXAMPLE SHOWS HOW THIS TABLE CAN BE USED TO             01590000
* CONVERT THE JULIAN DATE 363 TO DECEMBER 29.  AS CAN BE SEEN           01600000
* IN THE EXAMPLE PROGRAM THE JULIAN DATE, REGISTER 1, IS DECREMENTED    01610000
* BY THE NUMBER OF DAYS IN THAT MONTH, REGISTER 0, UNTIL A              01620000
* EQUAL OR HIGH COMPARISON IS FOUND.  AT THE CONCLUSION OF THIS         01630000
* LOOP REGISTER 1 WILL CONTAIN THE DATE IN BINARY AND REGISTER 2        01640000
* WILL POINT TO THE 3 BYTE MONTH TABLE ENTRY.                           01650000
*                                                                       01660000
*                       ---- EXAMPLE ----                               01670000
*                                                                       01680000
* CONVERSION SEARCH PROGRAM                     REG 0 AND 1 COMPARISON  01690000
*                                                                       01700000
*                                                   MONTH    R0     R1  01710000
*        R0 = 0                                     ------------------  01720000
*        R1 = JULIAN DATE (BINARY)                   JAN     31    363  01730000
*        R2 = ADDRESS OF APSUCOM                     FEB     28    332  01740000
* LOOP   SLR   1,0      SUBTRACT R0 FROM R1          MAR     31    304  01750000
*        LA    2,4(,2)  POINT TO TABLE ENTRY         APR     30    273  01760000
*        IC    0,0(,2)  INSERT # DAYS IN MONTH       MAY     31    243  01770000
*        CLR   0,1      COMPARE DECLINING JULIAN     JUN     30    212  01780000
*                       DATE TO # OF DAYS IN MONTH   JUL     31    181  01790000
*        BL    LOOP     BRANCH IF LOW COMPARISON     AUG     31    151  01800000
*                                                    SEP     30    120  01810000
*                                                    OCT     31     90  01820000
*                                                    NOV     30     59  01830000
*                                                    DEC    255     29  01840000
*                                                                       01850000
*********************************************************************** 01860000
 EJECT                                                                  01870000
         DC    AL1(31),CL3'&JAN',AL1(28),CL3'&FEB'   JULIAN             01880000
         DC    AL1(31),CL3'&MAR',AL1(30),CL3'&APR'   DAY                01890000
         DC    AL1(31),CL3'&MAY',AL1(30),CL3'&JUN'   AND                01900000
         DC    AL1(31),CL3'&JUL',AL1(31),CL3'&AUG'   MONTH              01910000
         DC    AL1(30),CL3'&SEP',AL1(31),CL3'&OCT'   CONVERSION         01920000
         DC    AL1(30),CL3'&NOV',AL1(255),CL3'&DEC'  TABLE              01930000
         APSUECA                                                        01940000
         AGO   .OUT                                                     01950000
.OUTMSG1 MNOTE 8,'INPUT PARAMETER ERROR, BUFFER SIZE LESS THAN 1599 OR X01960000
                                  MORE THAN 1,048,576'                  01970000
         AGO   .OUT                                                     01980000
.OUTMSG2 MNOTE 8,'INPUT PARAMETER, MONTH VARIABLE EXCEEDS MAXIMUM      X01990000
                                  LENGTH OF 3'                          02000000
         AGO   .OUT                                                     02010000
.OUT     ANOP                                                           02020000
         MEND                                                           02030000
