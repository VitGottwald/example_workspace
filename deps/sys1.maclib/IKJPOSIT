         MACRO                                                          00000100
&LABEL   IKJPOSIT &TYPNAM,&OP,&OP1,&OP2,&OP3,&OP4,&OP5,&OP6,           *00000200
               &OP7,&OP8,&OP9,&PROMPT=,&DEFAULT=,&HELP=,               *00000300
               &VALIDCK=                                       @E1502C6 00000400
.**01* PROPRIETARY STATEMENT=                                         * 00000500
.* **PROPRIETARY_STATEMENT********************************************* 00000600
.*                                                                    * 00000700
.*                                                                    * 00000800
.*  LICENSED MATERIALS - PROPERTY OF IBM                              * 00000900
.*  5650-ZOS COPYRIGHT IBM CORP. 1981, 2017                           * 00001000
.*                                                                    * 00001100
.*  STATUS= HTE77B0                                                   * 00001200
.*                                                                    * 00001300
.* **END_OF_PROPRIETARY_STATEMENT************************************** 00001400
.*                                                                      00001500
.**01* EXTERNAL CLASSIFICATION: PI                                 @L1A 00001600
.**01* END OF EXTERNAL CLASSIFICATION:                             @L1A 00001700
.*                                                                      00001800
.*                                                                      00001900
.*  TITLE - IKJPOSIT MACRO (SYMBOLIC DEBUG PROJECT)            @E2115KD 00002000
.*                                                             @E2115KD 00002100
.*  FUNCTION - THE IKJPOSIT MACRO IS USED BY COMMAND           @E2115KD 00002200
.*             PROCESSORS TO DEFINE TO PARSE THE EXPECTED      @E2115KD 00002300
.*             FORMAT OF POSITIONAL PARAMETERS WHICH ARE       @E2115KD 00002400
.*             DELIMITER DEPENDENT.                            @E2115KD 00002500
.*                                                             @E2115KD 00002600
.*  INPUT - THE PROTOTYPE STATEMENT IS AS DEFINED ABOVE. ITS   @E2115KD 00002700
.*          PARAMETERS REPRESENT THE FOLLOWING:                @E2115KD 00002800
.*             &LABEL - STATEMENT LABEL IS REQUIRED            @E2115KD 00002900
.*             &TYPNAM - A PARAMETER TYPE IS REQUIRED          @E2115KD 00003000
.*             &OPT - POSITIONAL PARAMETERS TO SPECIFY         @E2115KD 00003100
.*                       OPTIONS ON THE IKJPOSIT MACRO         @E2115KD 00003200
.*             &PROMPT - SUPPLIES DATA TO BE USED IF PROMPTING @E2115KD 00003300
.*                       FOR A MISSING PARAMETER               @E2115KD 00003400
.*             &DEFAULT - SUPPLIES VALUE TO BE USED IF THE     @E2115KD 00003500
.*                        PARAMETER IS NOT ENTERED             @E2115KD 00003600
.*             &HELP - SUPPLIES SECOND LEVEL MESSAGES          @E2115KD 00003700
.*             &VALIDCK - SPECIFIES THE ADDRESS OF A USER      @E2115KD 00003800
.*                        SUPPLIED VALIDITY CHECK EXIT         @E2115KD 00003900
.*                        ROUTINE                              @E2115KD 00004000
.*                                                             @E2115KD 00004100
.* OUTPUT - THIS MACRO PRODUCES A PARAMETER CONTROL ENTRY      @E2115KD 00004200
.*          (PCE) TO BE PASSED TO THE PARSE SERVICE ROUTINE.   @E2115KD 00004300
.*          IF AN ERROR IS ENCOUNTERED IN PROCESSING THE       @E2115KD 00004400
.*          MACRO INSTRUCTION ENTERED BY THE CP, AN            @E2115KD 00004500
.*          APPROPRIATE ERROR MESSAGE IS PRINTED AND THE PCE   @E2115KD 00004600
.*          IS NOT GENERATED.                                  @E2115KD 00004700
.*                                                             @E2115KD 00004800
.*  CHANGE ACTIVITY --                                         @E2115KD 00004900
.* RELEASE 20 SUPPORT CODE -- 20035                                     00005000
.* RELEASE 2.0 SUPPORT CODE --- Y02666                                  00005100
.*                                                                      00005200
.* FLAG REASON   RELEASE DATE     ORIGIN                           @L1A 00005300
.* ---- ------   ------- -------- ------                           @L1A 00005400
.* $L1= EXTREAL  JBB1326  800926  PDKD: SP1.3 EXTENDED REAL SUPPORT     00005500
.*                  E2115KD - JBB2115 TSO/E FOR MVS/XA         @E2115KD 00005600
.*                                                             @E2115KD 00005700
.*                  E1402C4 - HTE1402 TSO/E RELEASE 4 VECTOR   @E1402C4 00005800
.*                            ARCHITECTURE ENHANCEMENTS        @E1402C4 00005900
.*                  E1502C6 - HTE21D2 TSO/E RELEASE 5 EXTENDED @E1502C6 00006000
.*                            ARCHITECTURE EXTENSIONS          @E1502C6 00006100
.*                            ENHANCEMENTS                     @E1502C6 00006200
.*                  PEI0139 - FLAG FAREG SET BY SEVERAL LOGICAL         00006300
.*                            STATEMENTS INSTEAD OF ONE TO AVOID        00006400
.*                            CONTINUATION ERRORS.             @PEI0139 00006500
.*                                                                      00006600
.* $L2= MG08414  HTE7790 120619   MPK: ALLOW FOR USE WITHIN RSECT  @L2A 00006700
.* $L3= UID8_2   HTE77B0 151208   ER1: ADD NEW POSIT TYPES         @L3A 00006710
.*                                     UID82PWD                    @L3A 00006720
.*                                     USERID8                     @L3A 00006730
.*                                                                      00006820
.********************************************************************** 00006900
         GBLC  &IKJCSNM,&IKJDSNM                                        00007000
         LCLA  &TYPENO,&PCLSIZ,&T,&END                                  00007100
         LCLB  &FLIST,&FASIS,&FPRPT,&FDFLT,&FHELP,&FRANGE,&FVALID,&FEND*00007200
               BIT,&FQSTR,&FUSID,&FVOLSER,&FDDNAM,&FEXTND,&FEXT,&FVLSR  00007300
         LCLB  &FQS                                                     00007400
.*                                                             @E2115KD 00007500
         LCLB  &FVECT,&FVECTB                                           00007600
.*                                                             @E1402C4 00007700
         LCLB  &FAREG          DECLARE A NEW FLAG FOR AR REG   @E1502C6 00007800
&FENDBIT SETB  0                                                        00007900
.*                                                               Y02666 00008000
         AIF   (N'&SYSLIST LT 12).GOOD                         @E1502C6 00008100
         MNOTE 8,'NO MORE THAN 11 POSITIONAL PARAMETERS ALLOWED'        00008200
.*                                                             @E1502C6 00008300
&FENDBIT SETB  1                                                        00008400
.GOOD    ANOP                                                           00008500
         AIF   ('&IKJCSNM' NE '').GOOD2                                 00008600
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'     00008700
&FENDBIT SETB  1                                                        00008800
.GOOD2   ANOP                                                           00008900
&FLIST SETB ('&OP' EQ 'LIST' OR '&OP1' EQ 'LIST' OR '&OP2' EQ 'LIST' OR*00009000
               '&OP3' EQ 'LIST' OR '&OP4' EQ 'LIST' OR '&OP5' EQ 'LIST'*00009100
               OR '&OP6' EQ 'LIST' OR '&OP7' EQ 'LIST')        @E2115KD 00009200
&FLIST SETB ('&OP8' EQ 'LIST' OR '&OP9' EQ 'LIST' OR &FLIST)   @E1502C6 00009300
&FASIS SETB ('&OP' EQ 'ASIS' OR '&OP1' EQ 'ASIS' OR '&OP2' EQ 'ASIS'   *00009400
               OR '&OP3' EQ 'ASIS' OR '&OP4' EQ 'ASIS' OR '&OP5' EQ    *00009500
               'ASIS' OR '&OP6' EQ 'ASIS' OR '&OP7' EQ 'ASIS') @E2115KD 00009600
&FASIS SETB ('&OP8' EQ 'ASIS' OR '&OP9' EQ 'ASIS' OR &FASIS)   @E1502C6 00009700
&FRANGE SETB ('&OP' EQ 'RANGE' OR '&OP1' EQ 'RANGE' OR '&OP2' EQ 'RANGE*00009800
               ' OR '&OP3' EQ 'RANGE' OR '&OP4' EQ 'RANGE' OR 'OP5' EQ *00009900
               'RANGE' OR '&OP6' EQ 'RANGE' OR '&OP7' EQ 'RANGE')       00010000
.*                                                             @E2115KD 00010100
&FRANGE SETB ('&OP8' EQ 'RANGE' OR &FRANGE)                    @E1402C4 00010200
&FRANGE SETB ('&OP9' EQ 'RANGE' OR &FRANGE)                    @E1502C6 00010300
&FEXT   SETB ('&OP' EQ 'EXTENDED' OR '&OP1' EQ 'EXTENDED' OR '&OP2' EQ *00010400
               'EXTENDED' OR '&OP3' EQ 'EXTENDED' OR '&OP4' EQ 'EXTENDE*00010500
               D' OR '&OP5' EQ 'EXTENDED' OR '&OP6' EQ 'EXTENDED')      00010600
.*                                                             @E2115KD 00010700
&FEXTND SETB ('&OP7' EQ 'EXTENDED' OR &FEXT)                   @E2115KD 00010800
&FEXTND SETB ('&OP8' EQ 'EXTENDED' OR &FEXTND)                 @E1402C4 00010900
&FEXTND SETB ('&OP9' EQ 'EXTENDED' OR &FEXTND)                 @E1502C6 00011000
&FVECT  SETB ('&OP' EQ 'VECTOR'   OR '&OP1' EQ 'VECTOR'   OR '&OP2' EQ *00011100
               'VECTOR' OR '&OP3' EQ 'VECTOR'   OR '&OP4' EQ 'VECTOR'  *00011200
                  OR '&OP5' EQ 'VECTOR'   OR '&OP6' EQ 'VECTOR')        00011300
.*                                                             @E1402C4 00011400
&FVECTB SETB ('&OP7' EQ 'VECTOR'   OR &FVECT)                  @E1402C4 00011500
&FVECTB SETB ('&OP8' EQ 'VECTOR'   OR &FVECTB)                 @E1402C4 00011600
&FVECTB SETB ('&OP9' EQ 'VECTOR'   OR &FVECTB)                 @E1502C6 00011700
.*                                                                      00011800
&FAREG  SETB ('&OP' EQ 'AR'   OR '&OP1' EQ 'AR'   OR '&OP2' EQ         *00011900
               'AR' OR '&OP3' EQ 'AR'   OR '&OP4' EQ 'AR')              00012000
&FAREG  SETB ('&OP5' EQ 'AR' OR '&OP6' EQ 'AR' OR '&OP7' EQ            *00012100
               'AR' OR '&OP8' EQ 'AR' OR '&OP9' EQ 'AR' OR &FAREG)      00012200
.*                                                             @PEI0139 00012300
&FVLSR   SETB ('&OP' EQ 'VOLSER' OR '&OP1' EQ 'VOLSER' OR '&OP2' EQ    *00012400
               'VOLSER' OR '&OP3' EQ 'VOLSER' OR '&OP4' EQ 'VOLSER' OR *00012500
               '&OP5' EQ 'VOLSER' OR '&OP6' EQ 'VOLSER')       @E2115KD 00012600
&FVOLSER SETB ('&OP7' EQ 'VOLSER' OR &FVLSR)                   @ZTM5038 00012700
&FVOLSER SETB ('&OP8' EQ 'VOLSER'   OR &FVOLSER)               @E1402C4 00012800
&FVOLSER SETB ('&OP9' EQ 'VOLSER'   OR &FVOLSER)               @E1502C6 00012900
&FQS   SETB ('&OP' EQ 'SQSTRING' OR '&OP1' EQ 'SQSTRING' OR '&OP2' EQ  *00013000
               'SQSTRING' OR '&OP3' EQ 'SQSTRING' OR '&OP4' EQ 'SQSTRIN*00013100
               G' OR '&OP5' EQ 'SQSTRING' OR '&OP6' EQ 'SQSTRING')      00013200
&FQSTR SETB ('&OP7' EQ 'SQSTRING' OR &FQS)                    @E2115KD  00013300
&FQSTR   SETB ('&OP8' EQ 'SQSTRING' OR &FQSTR)                 @E1402C4 00013400
&FQSTR   SETB ('&OP9' EQ 'SQSTRING' OR &FQSTR)                 @E1502C6 00013500
&FUSID   SETB  ('&OP' EQ 'USID' OR '&OP1' EQ 'USID' OR '&OP2' EQ 'USID'*00013600
               OR '&OP3' EQ 'USID' OR '&OP4' EQ 'USID' OR '&OP5' EQ    *00013700
               'USID' OR '&OP6' EQ 'USID' OR '&OP7' EQ 'USID') @E2115KD 00013800
&FUSID   SETB ('&OP8' EQ 'USID'     OR &FUSID)                 @E1402C4 00013900
&FUSID   SETB ('&OP9' EQ 'USID'     OR &FUSID)                 @E1502C6 00014000
&FDDNAM SETB ('&OP' EQ 'DDNAM' OR '&OP1' EQ 'DDNAM' OR '&OP2' EQ 'DDNAM*00014100
               ' OR '&OP3' EQ 'DDNAM' OR '&OP4' EQ 'DDNAM' OR '&OP5' EQ*00014200
               'DDNAM' OR '&OP6' EQ 'DDNAM' OR '&OP7' EQ 'DDNAM')       00014300
.*                                                             @E2115KD 00014400
&FDDNAM  SETB ('&OP8' EQ 'DDNAM'    OR &FDDNAM)                @E1402C4 00014500
&FDDNAM  SETB ('&OP9' EQ 'DDNAM'    OR &FDDNAM)                @E1502C6 00014600
         AIF   ('&OP' EQ '' OR '&OP' EQ 'LIST' OR '&OP' EQ  'USID' OR  *00014700
               '&OP' EQ 'RANGE' OR '&OP' EQ 'ASIS' OR  '&OP' EQ        *00014800
               'UPPERCASE' OR '&OP' EQ 'SQSTRING').OPT                  00014900
         AIF   ('&OP' EQ 'VOLSER' OR '&OP' EQ 'DDNAM'                  *00015000
               OR '&OP' EQ 'EXTENDED').OPT                     @E2115KD 00015100
         AIF   ('&OP' EQ 'VECTOR').OPT                         @E1402C4 00015200
         AIF   ('&OP' EQ 'AR').OPT                             @E1502C6 00015300
         MNOTE 8,'''&OP'' IS AN INVALID POSITIONAL PARAMETER'           00015400
&FENDBIT SETB  1                                                        00015500
.OPT     AIF   ('&OP1' EQ '' OR '&OP1' EQ 'LIST' OR '&OP1'             *00015600
               EQ 'RANGE' OR '&OP1' EQ 'ASIS' OR                       *00015700
               '&OP1' EQ 'USID' OR '&OP1' EQ 'UPPERCASE').OPT2          00015800
         AIF   ('&OP1' EQ 'SQSTRING' OR '&OP1' EQ 'VOLSER'             *00015900
               OR '&OP1' EQ 'DDNAM' OR '&OP1' EQ                       *00016000
               'EXTENDED').OPT2                                @E2115KD 00016100
         AIF   ('&OP1' EQ 'VECTOR').OPT2                       @E1402C4 00016200
         AIF   ('&OP1' EQ 'AR').OPT2                           @E1502C6 00016300
         MNOTE 8,'''&OP1'' IS AN INVALID POSITIONAL PARAMETER'          00016400
&FENDBIT SETB  1                                                        00016500
.OPT2    AIF   ('&OP2' EQ '' OR '&OP2' EQ 'LIST' OR '&OP2'             *00016600
               EQ 'RANGE' OR '&OP2' EQ 'ASIS' OR                       *00016700
               '&OP2' EQ 'USID' OR '&OP2' EQ 'UPPERCASE').OPT3          00016800
.*                                                               Y02666 00016900
         AIF    ('&OP2' EQ 'SQSTRING' OR '&OP2' EQ 'VOLSER' OR         *00017000
               '&OP2' EQ 'DDNAM' OR '&OP2' EQ 'EXTENDED').OPT3 @E2115KD 00017100
         AIF   ('&OP2' EQ 'VECTOR').OPT3                       @E1402C4 00017200
         AIF   ('&OP2' EQ 'AR').OPT3                           @E1502C6 00017300
         MNOTE 8,'''&OP2'' IS AN INVALID POSITIONAL PARAMETER'          00017400
&FENDBIT SETB  1                                                        00017500
.OPT3    AIF   ('&OP3' EQ '' OR '&OP3' EQ 'LIST' OR '&OP3'             *00017600
               EQ 'RANGE' OR '&OP3' EQ 'ASIS' OR                       *00017700
               '&OP3' EQ 'USID' OR '&OP3' EQ 'UPPERCASE').OPT4          00017800
         AIF   ('&OP3' EQ 'SQSTRING').OPT4                              00017900
         AIF   ('&OP3' EQ 'VOLSER' OR '&OP3' EQ 'DDNAM'                *00018000
               OR '&OP3' EQ 'EXTENDED').OPT4                   @E2115KD 00018100
         AIF   ('&OP3' EQ 'VECTOR').OPT4                       @E1402C4 00018200
         AIF   ('&OP3' EQ 'AR').OPT4                           @E1502C6 00018300
         MNOTE 8,'''&OP3'' IS AN INVALID POSITIONAL PARAMETER'          00018400
&FENDBIT SETB  1                                                        00018500
.OPT4    AIF   ('&OP4' EQ '' OR '&OP4' EQ 'LIST' OR '&OP4'             *00018600
               EQ 'RANGE' OR '&OP4' EQ 'ASIS' OR                       *00018700
               '&OP4' EQ 'USID' OR '&OP4' EQ 'UPPERCASE').OPT1          00018800
         AIF   ('&OP4' EQ 'SQSTRING').OPT1                              00018900
         AIF   ('&OP4' EQ 'VOLSER' OR '&OP4' EQ 'DDNAM' OR             *00019000
               '&OP4' EQ 'EXTENDED').OPT1                      @E2115KD 00019100
         AIF   ('&OP4' EQ 'VECTOR').OPT1                       @E1402C4 00019200
         AIF   ('&OP4' EQ 'AR').OPT1                           @E1502C6 00019300
         MNOTE 8,'''&OP4'' IS AN INVALID POSITIONAL PARAMETER'          00019400
&FENDBIT SETB  1                                                        00019500
.OPT1    AIF   ('&OP5' EQ '' OR '&OP5' EQ 'LIST' OR '&OP5'             *00019600
               EQ 'RANGE' OR '&OP5' EQ 'ASIS' OR                       *00019700
               '&OP5' EQ 'USID' OR '&OP5' EQ 'UPPERCASE').OPT5          00019800
         AIF   ('&OP5' EQ 'SQSTRING').OPT5                              00019900
         AIF   ('&OP5' EQ 'VOLSER' OR '&OP5' EQ 'DDNAM'                *00020000
               OR '&OP5' EQ 'EXTENDED').OPT5                   @E2115KD 00020100
         AIF   ('&OP5' EQ 'VECTOR').OPT5                       @E1402C4 00020200
         AIF   ('&OP5' EQ 'AR').OPT5                           @E1502C6 00020300
         MNOTE 8,'''&OP5'' IS AN INVALID POSITIONAL PARAMETER'          00020400
&FENDBIT SETB  1                                                        00020500
.OPT5     AIF   ('&OP6' EQ '' OR '&OP6' EQ 'LIST' OR '&OP6'            *00020600
               EQ 'RANGE' OR '&OP6' EQ 'ASIS' OR                       *00020700
               '&OP6' EQ 'USID' OR '&OP6' EQ 'UPPERCASE').OPT7 @E2115KD 00020800
          AIF   ('&OP6' EQ 'SQSTRING').OPT7                    @E2115KD 00020900
          AIF   ('&OP6' EQ 'VOLSER' OR '&OP6' EQ 'DDNAM'               *00021000
                OR '&OP6' EQ 'EXTENDED').OPT7                  @E2115KD 00021100
         AIF   ('&OP6' EQ 'VECTOR').OPT7                       @E1402C4 00021200
         AIF   ('&OP6' EQ 'AR').OPT7                           @E1502C6 00021300
         MNOTE 8,'''&OP6'' IS AN INVALID POSITIONAL PARAMETER'          00021400
&FENDBIT SETB  1                                                        00021500
.OPT7     AIF   ('&OP7' EQ '' OR '&OP7' EQ 'LIST' OR '&OP7'            *00021600
                EQ 'RANGE' OR '&OP7' EQ 'ASIS' OR                      *00021700
               '&OP7' EQ 'USID' OR '&OP7' EQ 'UPPERCASE').OPT6 @E2115KD 00021800
          AIF   ('&OP7' EQ 'SQSTRING').OPT6                    @E2115KD 00021900
          AIF   ('&OP7' EQ 'VOLSER' OR '&OP7' EQ 'DDNAM'               *00022000
                OR '&OP7' EQ 'EXTENDED').OPT6                  @E2115KD 00022100
         AIF   ('&OP7' EQ 'VECTOR').OPT6                       @E1402C4 00022200
         AIF   ('&OP7' EQ 'AR').OPT6                           @E1502C6 00022300
         MNOTE 8,'''&OP7'' IS AN INVALID POSITIONAL PARAMETER' @E2115KD 00022400
&FENDBIT SETB  1                                               @E1502C6 00022500
.OPT6     AIF   ('&OP9' EQ '' OR '&OP9' EQ 'LIST' OR '&OP9'            *00022600
                EQ 'RANGE' OR '&OP9' EQ 'ASIS' OR                      *00022700
               '&OP9' EQ 'USID' OR '&OP9' EQ 'UPPERCASE').OPT9 @E1502C6 00022800
          AIF   ('&OP9' EQ 'SQSTRING').OPT9                    @E1502C6 00022900
          AIF   ('&OP9' EQ 'VOLSER' OR '&OP9' EQ 'DDNAM'               *00023000
                OR '&OP9' EQ 'EXTENDED').OPT9                  @E1502C6 00023100
         AIF   ('&OP9' EQ 'VECTOR').OPT9                       @E1502C6 00023200
         AIF   ('&OP9' EQ 'AR').OPT9                           @E1502C6 00023300
        MNOTE 10,'''&OP6'' IS AN INVALID POSITIONAL PARAMETER' @E1502C6 00023400
&FENDBIT SETB  1                                               @E2115KD 00023500
.OPT9     AIF   ('&OP8' EQ '' OR '&OP8' EQ 'LIST' OR '&OP8'            *00023600
                EQ 'RANGE' OR '&OP8' EQ 'ASIS' OR                      *00023700
               '&OP8' EQ 'USID' OR '&OP8' EQ 'UPPERCASE').OPT8 @E1402C4 00023800
          AIF   ('&OP8' EQ 'SQSTRING').OPT8                    @E1402C4 00023900
          AIF   ('&OP8' EQ 'VOLSER' OR '&OP8' EQ 'DDNAM'               *00024000
                OR '&OP8' EQ 'EXTENDED').OPT8                  @E1402C4 00024100
         AIF   ('&OP8' EQ 'VECTOR').OPT8                       @E1402C4 00024200
         AIF   ('&OP8' EQ 'AR').OPT8                           @E1502C6 00024300
         MNOTE 8,'''&OP8'' IS AN INVALID POSITIONAL PARAMETER' @E1402C4 00024400
&FENDBIT SETB  1                                               @E1402C4 00024500
.OPT8    AIF   (('&OP' EQ 'UPPERCASE' OR '&OP1' EQ 'UPPERCASE'         *00024600
                OR '&OP2' EQ 'UPPERCASE' OR '&OP3' EQ                  *00024700
                'UPPERCASE') AND &FASIS).ERROR0                         00024800
         AIF   (('&OP4' EQ 'UPPERCASE') AND &FASIS).ERROR0              00024900
         AIF   (('&OP5' EQ 'UPPERCASE' OR '&OP6' EQ                    *00025000
                'UPPERCASE' OR '&OP7' EQ 'UPPERCASE') AND              *00025100
                &FASIS).ERROR0                                          00025200
         AIF   (('&OP8' EQ 'UPPERCASE') AND &FASIS).ERROR0     @E1402C4 00025300
         AIF   (('&OP9' EQ 'UPPERCASE') AND &FASIS).ERROR0     @E1502C6 00025400
         AGO   .A7                                                      00025500
.ERROR0  MNOTE 8,'BOTH ''UPPERCASE'' AND ''ASIS'' CANNOT BE SPECIFIED'  00025600
&FENDBIT SETB  1                                                        00025700
.A7      ANOP                                                           00025800
         AIF   (NOT(&FUSID OR &FVOLSER OR &FDDNAM)).A0                  00025900
         AIF   ('&TYPNAM' EQ 'DSNAME' OR '&TYPNAM' EQ 'DSTHING').A0     00026000
         AIF     (NOT &FUSID).A6                                        00026100
.ERROR7 MNOTE 4,'''USID'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'        00026200
&FUSID  SETB  0                                                         00026300
&FENDBIT SETB  1                                                        00026400
.A6      ANOP                                                           00026500
         AIF     (NOT &FVOLSER).A10                                     00026600
.ERROR8 MNOTE 4,'''VOLSER'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'      00026700
&FVOLSER  SETB  0                                                       00026800
&FENDBIT SETB  1                                                        00026900
.A10     ANOP                                                           00027000
         AIF     (NOT &FDDNAM).A0                                       00027100
.ERROR9 MNOTE 4,'''DDNAM'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'       00027200
&FDDNAM  SETB  0                                                        00027300
&FENDBIT SETB  1                                                        00027400
.A0      ANOP                                                           00027500
         AIF   (('&TYPNAM' EQ 'DELIMITER' OR '&TYPNAM' EQ 'SPACE')     *00027600
               AND ('&PROMPT' NE '' OR '&DEFAULT' NE '' OR '&HELP' NE  *00027700
               '' OR '&VALIDCK' NE '')).ERROR3                          00027800
&FPRPT   SETB  ('&PROMPT' NE '')                                        00027900
&FDFLT   SETB  ('&DEFAULT' NE '')                                       00028000
&FHELP   SETB  ('&HELP' NE '')                                          00028100
&FVALID  SETB  ('&VALIDCK' NE '')                                       00028200
         AGO   .A1                                                      00028300
.ERROR3  MNOTE  4,'PROMPT, DEFAULT, HELP, VALIDCK IGNORED WITH &TYPNAM' 00028400
.A1      AIF   (('&TYPNAM' NE 'ADDRESS' AND '&TYPNAM' NE 'VALUE')      *00028500
               AND &FRANGE).RANGE                                       00028600
         AGO   .A20                                            @E2115KD 00028700
.RANGE   MNOTE 4,'''RANGE'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'      00028800
&FRANGE  SETB  0                                                        00028900
.A20     AIF   ('&TYPNAM' NE 'ADDRESS' AND &FEXTND).EXTND      @E2115KD 00029000
         AGO   .A15                                            @E2115KD 00029100
.EXTND   MNOTE 4,'''EXTENDED'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'   00029200
.*                                                             @E2115KD 00029300
&FEXTND  SETB  0                                               @E2115KD 00029400
.A15     AIF   ('&TYPNAM' NE 'ADDRESS' AND &FVECTB).VECTB      @E1402C4 00029500
         AGO   .A16                                            @E1402C4 00029600
.VECTB   MNOTE 4,'''VECTOR'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'     00029700
.*                                                             @E1402C4 00029800
&FEXTND  SETB  0                                               @E1402C4 00029900
.*                                                             @E1502C6 00030000
.A16     AIF   ('&TYPNAM' NE 'ADDRESS' AND &FAREG).AREGB       @E1502C6 00030100
         AGO   .A17                                            @E1502C6 00030200
.AREGB   MNOTE 4,'''AR'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'         00030300
.*                                                             @E1502C6 00030400
&FEXTND  SETB  0                                               @E1502C6 00030500
.A17     AIF   (&FDFLT AND &FPRPT).ERROR1                      @E1502C6 00030600
         AGO   .A2                                                      00030700
.ERROR1  MNOTE 8,'PARAMETER CANNOT BOTH DEFAULT AND REQUIRE PROMPTING'  00030800
&FENDBIT SETB  1                                                        00030900
.A2      AIF   ('&LABEL' EQ '').ERROR2                                  00031000
         AGO   .A3                                                      00031100
.ERROR2  MNOTE 8,'STATEMENT LABEL MISSING BUT REQUIRED'                 00031200
&FENDBIT SETB  1                                                        00031300
.A3      AIF   (('&TYPNAM' NE 'STRING') AND &FQSTR).ERROR4              00031400
.*                                                               Y02666 00031500
         AGO   .A4                                                      00031600
.ERROR4  MNOTE 8,'''SQSTRING'' OPTION ILLEGAL WITH &TYPNAM'             00031700
.*                                                               Y02666 00031800
&FENDBIT  SETB  1                                                       00031900
.A4      AIF   (NOT &FLIST OR ( '&TYPNAM' NE 'DELIMITER' AND '&TYPNAM' *00032000
               NE 'STRING' AND '&TYPNAM' NE 'QSTRING' AND '&TYPNAM'    *00032100
               NE 'SPACE')).ACCEPT1                                     00032200
         MNOTE 4,'''LIST'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'       00032300
&FLIST   SETB  0                                                        00032400
.ACCEPT1 AIF   (NOT &FASIS OR ( '&TYPNAM' NE  'DELIMITER' AND          *00032500
               '&TYPNAM' NE 'SPACE')).ACCEPT2                           00032600
         MNOTE 4,'''ASIS'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'       00032700
&FASIS   SETB  0                                                        00032800
.ACCEPT2 ANOP                                                           00032900
         AIF   ('&OP' NE 'UPPERCASE' AND '&OP2' NE 'UPPERCASE'         *00033000
               AND '&OP3' NE 'UPPERCASE' OR ('&TYPNAM' NE              *00033100
               'DELIMITER' AND '&TYPNAM' NE 'SPACE')).ACCEPT4           00033200
         AIF   ('&OP4' NE 'UPPERCASE' AND '&OP5' NE 'UPPERCASE' AND '&O*00033300
               P6' NE 'UPPERCASE' AND '&OP7' NE 'UPPERCASE' OR ('&TYPNA*00033400
               M' NE 'DELIMITER' AND '&TYPNAM' NE 'SPACE')).ACCEPT4     00033500
.*                                                             @E2115KD 00033600
         AIF   ('&OP8' NE 'UPPERCASE' OR ('&TYPNAM' NE                 *00033700
               'DELIMITER' AND '&TYPNAM' NE 'SPACE')).ACCEPT4  @E1402C4 00033800
.*                                                             @E1502C6 00033900
         AIF   ('&OP9' NE 'UPPERCASE' OR ('&TYPNAM' NE                 *00034000
               'DELIMITER' AND '&TYPNAM' NE 'SPACE')).ACCEPT4  @E1502C6 00034100
.*                                                             @E1502C6 00034200
         MNOTE 4,'''UPPERCASE'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'  00034300
.ACCEPT4 ANOP                                                           00034400
         AIF   (NOT &FVALID OR ('&TYPNAM' NE 'STRING')).ACCEPT3         00034500
         MNOTE 4,'''VALIDCK'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'    00034600
&FVALID  SETB  0                                                        00034700
.ACCEPT3 ANOP                                                           00034800
&TYPENO  SETA  1                                                        00034900
         AIF   ('&TYPNAM' EQ 'DELIMITER').TYPED                         00035000
&TYPENO  SETA  10                                                       00035100
         AIF   ('&TYPNAM' EQ 'SPACE').TYPED                             00035200
&TYPENO  SETA  3                                                        00035300
&PCLSIZ  SETA  2                                                        00035400
         AIF   ('&TYPNAM' EQ 'VALUE').TYPED                             00035500
&TYPENO  SETA  2                                                        00035600
         AIF   ('&TYPNAM' EQ 'STRING').TYPED                            00035700
&TYPENO  SETA  5                                                        00035800
         AIF   ('&TYPNAM' EQ 'PSTRING').TYPED                           00035900
&TYPENO  SETA  9                                                        00036000
         AIF   ('&TYPNAM' EQ 'QSTRING').TYPED                           00036100
&PCLSIZ  SETA  4                                                        00036200
&TYPENO  SETA  6                                                        00036300
         AIF   ('&TYPNAM' EQ 'USERID').TYPED                            00036400
&TYPENO  SETA  16                                              @L3A     00036410
         AIF   ('&TYPNAM' EQ 'USERID8').TYPED                  @L3A     00036420
&PCLSIZ  SETA  6                                               @Z40RPKQ 00036500
&TYPENO  SETA  12                                              @Z40RPKQ 00036600
         AIF   ('&TYPNAM' EQ 'UID2PSWD').TYPED                 @Z40RPKQ 00036700
&TYPENO  SETA  17                                              @L3A     00036710
         AIF   ('&TYPNAM' EQ 'UID82PWD').TYPED                 @L3A     00036720
.********************************************************************** 00036800
.*  ADD ANOTHER HEX VALUE TO THE PCE TYPE FIELD (BYTE 7)       @E1502C6 00036900
.*  TO INDICATE ADDRESS WAS SPECIFIED WITH THE 'AR' OPTION.    @E1502C6 00037000
.*  NOTE: AR OPTION HAS PRECEDENCE OVER EXTENDED AND VECTOR.   @E1502C6 00037100
.*                                                             @E1502C6 00037200
.*  THE TYPE NUMBER IS 15 (X'0F').                             @E1502C6 00037300
.********************************************************************** 00037400
&PCLSIZ  SETA  9                                               @E1502C6 00037500
&TYPENO  SETA  15                                              @E1502C6 00037600
         AIF   ('&TYPNAM' EQ 'ADDRESS' AND &FAREG).TYPED       @E1502C6 00037700
.********************************************************************** 00037800
.*  ADD ANOTHER HEX VALUE TO THE PCE TYPE FIELD (BYTE 7)       @E1402C4 00037900
.*  TO INDICATE ADDRESS WAS SPECIFIED WITH THE VECTOR   OPTION @E1402C4 00038000
.*  NOTE: VECTOR OPTION HAS PRECEDENCE OVER EXTENDED OPTION    @E1402C4 00038100
&PCLSIZ  SETA  9                                               @E1402C4 00038200
&TYPENO  SETA  14                                              @E1402C4 00038300
         AIF   ('&TYPNAM' EQ 'ADDRESS' AND &FVECTB).TYPED      @E1402C4 00038400
.*  ADD ANOTHER HEX VALUE TO THE PCE TYPE FIELD (BYTE 7)       @E2115KD 00038500
.*  TO INDICATE ADDRESS WAS SPECIFIED WITH THE EXTENDED OPTION @E2115KD 00038600
&PCLSIZ  SETA  9                                               @E2115KD 00038700
&TYPENO  SETA  13                                              @E2115KD 00038800
         AIF   ('&TYPNAM' EQ 'ADDRESS' AND &FEXTND).TYPED      @E2115KD 00038900
&TYPENO  SETA  4                                                        00039000
&PCLSIZ  SETA  9                                                        00039100
         AIF   ('&TYPNAM' EQ 'ADDRESS').TYPED                           00039200
&PCLSIZ  SETA  6                                                        00039300
&TYPENO  SETA  7                                                        00039400
         AIF   ('&TYPNAM' EQ 'DSNAME').TYPED                            00039500
&TYPENO  SETA  8                                                        00039600
         AIF   ('&TYPNAM' EQ 'DSTHING').TYPED                           00039700
.*                                                               Y02666 00039800
&PCLSIZ  SETA  4                                                 YM4738 00039900
.*                                                               Y02666 00040000
&TYPENO  SETA  11                                                       00040100
.*                                                               Y02666 00040200
         AIF   ('&TYPNAM' EQ 'JOBNAME').TYPED                           00040300
         AIF   ('&TYPNAM' EQ '').NULTYPE                                00040400
         AGO   .ILLTYPE                                                 00040500
.NULTYPE MNOTE 8,'POSITIONAL PARAMETER TYPE FIELD MISSING BUT REQUIRED' 00040600
&FENDBIT SETB  1                                                        00040700
         AGO   .TYPED                                                   00040800
.ILLTYPE ANOP                                                           00040900
         MNOTE 8,'ILLEGAL POSITIONAL TYPE - ''&TYPNAM'''                00041000
.END     MEXIT                                                          00041100
.TYPED   AIF   (&FENDBIT).END                                           00041200
&IKJCSNM CSECT ,                       RESUME PCL CSECT                 00041300
* FLAGS -  BIT3=PROMPT, BIT4=DEFAULT, BIT5=NEW FORMAT PCE,     @E2115KD 00041400
*          BIT6=HELP,   BIT7=VALIDCK, BIT8=LIST,               @E2115KD 00041500
*          BIT9=ASIS,   BIT10=RANGE,  BIT12=SQSTRING,          @E2115KD 00041600
*          BIT13=USID,  BIT14=VOLSER, BIT15=DDNAME             @E2115KD 00041700
POST&SYSNDX EQU *                                                       00041800
         DC    BL2'001&FPRPT&FDFLT.1&FHELP&FVALID&FLIST&FASIS&FRANGE.0&*00041900
               FQSTR&FUSID&FVOLSER&FDDNAM'                     @E2115KD 00042000
         DC    AL2(PEND&SYSNDX-POST&SYSNDX) LENGTH OF PCE               00042100
         DC    AL2(&LABEL-&IKJDSNM)    OFFSET TO PDE                    00042200
         DC    AL1(&TYPENO)            TYPE OF POSITIONAL               00042300
         AIF   (&FPRPT).PRPT           PROMPT                           00042400
         AIF   (NOT &FDFLT).NPRDF      NEITHER                          00042500
         DC    AL1(IKJ@&SYSNDX-*-2),C&DEFAULT DEFAULT DATA              00042600
         AGO   .EQUGEN                                                  00042700
.PRPT    DC    AL1(IKJ@&SYSNDX-*-2),C&PROMPT PROMPT DATA                00042800
.EQUGEN  ANOP                                                           00042900
IKJ@&SYSNDX EQU *                                                       00043000
.NPRDF   ANOP                                                           00043100
         AIF   (NOT &FHELP).NOHELP     IS HELP SPECIFIED                00043200
         DC    AL2(IKJ$&SYSNDX-*)      LENGTH OF HELP DATA              00043300
&T       SETA  1                                                        00043400
.HELPLP  ANOP                                                           00043500
&T       SETA  &T+1                    NEXT HELP                        00043600
         AIF   (&T EQ 257).HELPER                                       00043700
         AIF   (&T LE N'&HELP).HELPLP                                   00043800
         AGO   .HELPCT                                                  00043900
.HELPER  MNOTE 4,'ONLY 255 LEVELS OF HELP AVAILABLE. OTHERS IGNORED'    00044000
&T       SETA  &T+1                    PROVIDE FOR DECREMENT            00044100
.HELPCT  ANOP                                                           00044200
&T       SETA  &T-1                                                     00044300
         DC    AL1(&T)                 NUMBER OF HELP MESSAGES          00044400
&END     SETA  &T                                                       00044500
&T       SETA  1                                                        00044600
.HELPLPA ANOP                                                           00044700
         DC    AL2(IKJ&SYSNDX.&T-*),AL2(00),C&HELP(&T) HELP MSG SEGMENT 00044800
IKJ&SYSNDX.&T EQU *                                                     00044900
&T       SETA  &T+1                                                     00045000
         AIF   (&T LE &END).HELPLPA                                     00045100
IKJ$&SYSNDX EQU *                                                       00045200
.NOHELP  AIF   (NOT &FVALID).NOVALID   IS VALIDCK SPECIFIED             00045300
         DC    AL4(&VALIDCK)         VALIDCK ROUTINE ADDRESS  @E2115KD  00045400
.NOVALID ANOP                                                           00045500
PEND&SYSNDX EQU *                                                       00045600
&IKJDSNM DSECT                                                          00045700
&LABEL   DS    &PCLSIZ.A               ALLOCATE PDE SPACE               00045800
         AIF   (NOT &FRANGE).NRNGGEN                                    00045900
         DS    &PCLSIZ.A               ALLOCATE ADDITIONAL PDE SPACE    00046000
.NRNGGEN ANOP                                                           00046100
         AIF   (NOT &FLIST).NLSTGEN                                     00046200
         DS    A                       FOR LIST CHAIN POINTER           00046300
.NLSTGEN ANOP                                                           00046400
&SYSECT  &SYSSTYP ,                    RESUME USERS CSECT OR RSECT @L2A 00046500
         MEXIT                                                          00046600
         MEND                                                           00046700
