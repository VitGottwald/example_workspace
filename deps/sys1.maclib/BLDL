*/*PROPRIETARY V3 STATEMENT                                          */ 00004000
*/*LICENSED MATERIALS - PROPERTY OF IBM                              */ 00006000
*/*"RESTRICTED MATERIALS OF IBM"                                     */ 00008000
*/*5695-DF1                                                          */ 00010000
*/*COPYRIGHT 1992, 2011 IBM CORP.                                    */ 00012500
*/*END PROPRIETARY V3 STATEMENT                                      */ 00015000
*/*                                                                  */ 00018000
*/*       NOTE END DELIMITER MISSING UNTIL BEFORE PL/X INVOCATION @L1A  00019000
         MACRO                                                          00020000
&NAME    BLDL  &DCB,&LIST,&OPTION1,&OPTION2,&OPTION3,             @02C *00021400
               &START=,&STOP=                                      @L4C 00022900
.********************************************************************** 00024400
.*                                                                    * 00026600
.* STATUS = z/OS 1.13 DFSMS                                           * 00034300
.*                                                                    * 00042000
.* CHANGE ACTIVITY =                                                  * 00044200
.*                                                                    * 00046400
.*      VS2 RELEASE 03 CHANGES/DELETIONS                              * 00048600
.*A001100-001140                                                ZA00734 00052000
.*  $L0=PDSE,HDP3320,880206,STLFPC: PDSE SUPPORT                  @L0A* 00054000
.*  $L1=       ,       ,      ,SJPLMET: PLX SUPPORT               @L1A* 00054500
.*  $01=OW01366,HDZ11B0,940112,SJPLREB: BYPASSLLA OPTION          @01A* 00055000
.*  $L2=DYNLINK,HDZ11C0,960430,SJPLREB: NODEBCHK OPTION - OW19701 @L2A* 00055500
.*  $02=OA09654,HDZ11H0,20040830,SJPLREB: START, STOP OPTIONS     @02A* 00055700
.*  $L3=64BIT,JDZ1D10,20100303,SVLJH: 64 BIT MESSAGE              @L3A* 00055800
.*  $L4=64BIT,JDZ1D10,20101110,SVLWER: 64-bit clean up             @L4A 00056900
.********************************************************************** 00058000
         GBLC  &SYSAM64    AMODE64=YES set by SYSSTATE macro       @L3A 00058200
         GBLC  &SYSASCE    ASCENV=AR or ANY set by SYSSTATE macro  @L3A 00058400
         GBLA  &SYSALVL    ARCHLVL set by SYSSTATE macro           @L4A 00058600
         LCLB  &B0,&B1,&B2,&B3,&B4,&B5,&B6,&B7  BITS IN A BYTE     @01A 00058800
         LCLC  &TEMPC      TO DO ALIGNMENT FOR ESTHETICS           @01A 00059000
         SYSSTATE TEST     TEST HOW MACROS ARE TO EXPAND           @L3A 00059200
         AIF   ('&SYSAM64' EQ 'YES').E6    GO IF AMODE64=YES       @L3A 00059600
         AIF   (('&SYSASCE' NE '') AND ('&SYSASCE' NE 'P')).E7     @L3A 00059800
         AIF   ('&DCB' EQ '').E1                                        00060000
         AIF   ('&LIST' EQ '').E2                                       00080000
&NAME    IHBINNRA &DCB,&LIST              LOAD REG 1 AND REG 0          00100000
.*  REGISTER 1 IS MADE POSITIVE SO THE SVC CODE WILL KNOW THIS  ZA00734 00112000
.*  REQUEST IS FROM BLDL AND NOT THE FIND MACRO.                ZA00734 00114000
         LA    1,0(1)                     CLEAR HIGH ORDER BYTE ZA00734 00114200
.*  TEST FOR VALID OPTIONS TO INITIALIZE A PREFIX                  @L0A 00114400
.OPT     AIF   ('&OPTION1' EQ '' AND                                   *00114500
                '&START'   EQ '' AND                                   *00114600
                '&STOP'    EQ '').FINIS                            @02C 00114700
         AIF   ('&OPTION1' NE ''          AND                          *00114800
               '&OPTION1' NE 'NOCONNECT'  AND                          *00114900
               '&OPTION1' NE 'NODEBCHK'   AND                          *00115000
               '&OPTION1' NE 'BYPASSLLA').E3                       @02C 00115100
.OPT2    AIF   ('&OPTION2' NE ''          AND                          *00115200
               '&OPTION2' NE 'NOCONNECT'  AND                          *00115300
               '&OPTION2' NE 'NODEBCHK'   AND                          *00115400
               '&OPTION2' NE 'BYPASSLLA').E4                            00115500
.OPT3    AIF   ('&OPTION3' NE ''          AND                          *00115600
               '&OPTION3' NE 'NOCONNECT'  AND                          *00115700
               '&OPTION3' NE 'NODEBCHK'   AND                          *00115800
               '&OPTION3' NE 'BYPASSLLA').E5                            00115900
.OPTEND  ANOP                                                      @L2A 00116000
.*  REGISTER 15 IS LOADED WITH LIST ADDRESS FOR OPTION PROCESSING. @L0A 00116100
         LR    15,0                LOAD REG 15 TO CLEAR THE        @L0A 00116200
         LA    15,0(,15)           HIGH ORDER BIT                  @L0A 00116300
         AIF   (&SYSALVL GE 2).Lab3 Go if z/Architecture or later  @L4A 00116400
         LA    0,X'80'             SET HIGH ORDER BIT ON           @L0A 00116500
         SLL   0,24                                                @L0A 00116600
         OR    15,0                                                @L0A 00116700
         AGO   .Lab4                                               @L4A 00116800
.Lab3    OILH  15,X'8000'          Set high order bit on           @L4A 00116900
.Lab4    LA    0,8                 SET PREFIX LENGTH               @L4C 00117000
         SLR   15,0                INITIALIZE THE PREFIX           @L0A 00117100
         XC    0(4,15),0(15)       Clear fields in prefix          @L4C 00117200
         ST    0,4(,15)            Set prefix length               @L4C 00117300
         ALR   0,15                                                @L0A 00117400
&B0      SETB  ('&OPTION1' EQ 'NOCONNECT' OR                           *00117500
               '&OPTION2' EQ 'NOCONNECT'  OR                           *00117600
               '&OPTION3' EQ 'NOCONNECT')  NOCONNECT OPTION        @02C 00117700
&B1      SETB  ('&OPTION1' EQ 'BYPASSLLA' OR                           *00117800
               '&OPTION2' EQ 'BYPASSLLA'  OR                           *00118400
               '&OPTION3' EQ 'BYPASSLLA')  BYPASSLLA OPTION        @02C 00119000
&B2      SETB  ('&OPTION1' EQ 'NODEBCHK'  OR                           *00119600
               '&OPTION2' EQ 'NODEBCHK'   OR                           *00120200
               '&OPTION3' EQ 'NODEBCHK')  NODEBCHK OPTION          @02C 00120800
&B3      SETB  ('&START' NE '')                                    @02A 00121400
&B4      SETB  ('&STOP'  NE '')                                    @02A 00122000
&TEMPC   SETC  '&B0&B1&B2&B3&B4.000' TO PRESERVE COMMENT ALIGNMENT @02C 00122600
         MVI   3(15),B'&TEMPC'     SET OPTIONS                     @01C 00123200
.* DEAL WITH START= OPTION                                         @02A 00123800
         AIF   ('&B3' NE '1').TESTB4  BRANCH IF START NOT CODED    @02A 00124400
         AIF   ('&START(1)' NE '&START').STARTREG  BRANCH IF REG   @02A 00125000
         MVC   1(1,15),&START      START CONCAT NUMBER             @02A 00125600
         AGO   .TESTB4                                             @02A 00126200
.STARTREG ANOP                                                     @02A 00126800
         STC   &START(1),1(,15)    START CONCAT NUMBER             @02A 00127400
.* DEAL WITH STOP=  OPTION                                         @02A 00128000
.TESTB4  ANOP                                                      @02A 00128600
         AIF   ('&B4' NE '1').ENDPREFX  BRANCH IF STOP NOT CODED   @02A 00129200
         AIF   ('&STOP(1)' NE '&STOP').STOPREG  BRANCH IF REGISTER @02A 00129800
         MVC   2(1,15),&STOP       END CONCAT NUMBER               @02A 00130400
         AGO  .ENDPREFX                                            @02A 00131000
.STOPREG ANOP                                                      @02A 00131600
         STC   &STOP(1),2(,15)     END CONCAT NUMBER               @02A 00132200
.ENDPREFX ANOP                                                     @02A 00132800
         AGO   .SVC                BYPASS CODE FOR NO OPTION       @L0A 00133400
.FINIS   ANOP                                                      @L0A 00134000
.*  CLEAR REGISTER 15 TO INSURE THAT OPTIONS ARE NOT PROCESSED.    @L0A 00134600
         SLR   15,15               FOR NO OPTION                   @L0A 00135200
.SVC     ANOP                                                      @L0A 00135800
         SVC   18                  LINK TO BLDL ROUTINE                 00137900
         MEXIT                                                          00140000
.E1      IHBERMAC 6                DCB ADDR MISSING                     00150000
         AIF   ('&LIST' EQ '').E2                                       00160000
         AGO .E2A                                                       00210000
.E2      IHBERMAC 34               LIST ADDR MISSING                    00260000
.E2A     AIF   ('&OPTION1' NE '').OPT                              @01A 00310000
         MEXIT                                                     @01A 00360000
.E3      IHBERMAC 50               INVALID OPTION 1                @01A 00410000
         AGO   .OPT2               PROCESS OPTION 2                @01A 00460000
.E4      IHBERMAC 51               INVALID OPTION 2                @01A 00510000
         AGO   .OPT3               PROCESS OPTION 3                @01A 00560000
.E5      IHBERMAC 53               INVALID OPTION 3                @01A 00610000
         AGO   .OPTEND             PROCESS ANY OTHERS              @01A 00660000
.E6      IGGERMAC 203,BLDL    THE BLDL MACRO DOES NOT SUPPORT      @L3A 05630000
         MEXIT                  SYSSTATE AMODE64=YES               @L3A 05680000
.E7      IGGERMAC 204,BLDL    THE BLDL MACRO DOES NOT SUPPORT      @L3A 05730000
         MEXIT                  SYSSTATE ASCENV=AR OR ANY          @L3A 05780000
         MEND                                                           40200000
* */%BLDL: MACRO KEYS(1:1(DCB_NAME,DCB_ADDR),1:1(AREA_NAME,    /*@L1A*/ 40250000
*       AREA_ADDR),RETCODE,RSNCODE,CONNECT,BYPASSLLA           /*@01C*/ 40283300
*       ,DEBCHK);                                              /*@L2C*/ 40316600
*  ANS('? '!!MACLABEL!!' BLDLP '!!MACKEYS!!';') RESCAN SKIP;   /*@L1A*/ 40350000
*  %END BLDL;                                                  /*@L1A*/ 40400000
