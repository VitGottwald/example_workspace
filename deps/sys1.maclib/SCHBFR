         MACRO                                                          00050000
&NAME    SCHBFR &RPL=,&BFRNO=1                                          00100000
.*                                                                      00101000
.*  $MAC(SCHBFR):                                                       00102000
.*                                                                      00102400
.*PROPRIETARY V3 STATEMENT                                     @WA04075 00103600
.*LICENSED MATERIALS - PROPERTY OF IBM                         @WA04075 00104000
.*"RESTRICTED MATERIALS OF IBM"                                @WA04075 00104400
.*5695-DF1                                                     @WA04075 00104800
.*COPYRIGHT 1992, 2011 IBM CORP.                               @WA04075 00105300
.*END PROPRIETARY V3 STATEMENT                                 @WA04075 00105800
.*                                                                      00106400
.* STATUS = z/OS 1.13 DFSMS                                        @L1C 00106800
.*                                                                      00107200
.* CHANGE ACTIVITY:                                                     00107800
.*  PREVENT COPYRIGHT FROM SHOWING WHEN MACRO IS INVOKED       @WA04075 00108400
.* $L1=64BIT,JDZ1D10,20100303,SVLJH: 64 bit message                @L1A 00108700
.*                                                                      00109000
         GBLC  &SYSAM64    AMODE64=YES set by SYSSTATE macro       @L1A 00120000
         GBLC  &SYSASCE    ASCENV=AR or ANY set by SYSSTATE macro  @L1A 00131000
         LCLA  &I          WORK VARIABLE                                00142000
         LCLB  &ERR        ERROR INDICATOR                              00153000
         SYSSTATE TEST     Test how macros are to expand           @L1A 00164000
         AIF   ('&SYSAM64' EQ 'YES').E0    GO IF AMODE64=YES       @L1A 00175000
         AIF   (('&SYSASCE' NE '') AND ('&SYSASCE' NE 'P')).E1     @L1A 00187500
         AIF   (N'&SYSLIST EQ 0).NOPOS  GO IF NO POSITIONAL OPERANDS    00200000
.POSLOOP ANOP                                                           00210000
&I       SETA  &I+1                     POINT TO NEXT POS OPERAND       00220000
         IDAERMAC 1,&SYSLIST(&I)        ISSUE WARNING MNOTE             00250000
         AIF   (&I LT N'&SYSLIST).POSLOOP  GO IF MORE POS OPERANDS      00260000
.NOPOS   ANOP                                                           00300000
         AIF   ('&RPL' NE '').ROKY      GO IF RPL IS SPECIFIED          00350000
         IDAERMAC 2,RPL                 ISSUE ERROR MNOTE               00400000
&ERR     SETB  1                        INDICATE ERROR                  00450000
.ROKY    ANOP                                                           00500000
         AIF   ('&BFRNO' NE '').BOKY    GO IF BFRNO NOT SPECIFIED NULL  00550000
         IDAERMAC 2,BFRNO               ISSUE ERROR MNOTE               00600000
&ERR     SETB  1                        INDICATE ERROR                  00650000
         AGO   .BFRNOB                  GO AROUND BUFFER NO ANALYSIS    00660000
.BOKY    ANOP                                                           00700000
         AIF   ('&BFRNO' EQ '(1)').BFRNOE  GO IF BUFFER NUM IN REG 1    00710000
         AIF   ('&BFRNO' EQ '0').BFRNOE    GO IF BUFFER NUM EQ ZERO     00720000
         AIF   ('&BFRNO' NE '(13)').BFRNOB GO IF BUFFER NUM NOT IN 13   00730000
.BFRNOE  ANOP                                                           00740000
         IDAERMAC 3,BFRNO,&BFRNO        ISSUE ERROR MNOTE               00742000
&ERR     SETB  1                        INDICATE ERROR                  00744000
.BFRNOB  ANOP                                                           00746000
         AIF   (&ERR).END               GO IF ANY ERRORS                00750000
         AIF   ('&NAME' EQ '').NAMEB    GO IF NO NAME                   00760000
&NAME    DS    0H                                                       00800000
.NAMEB   ANOP                                                           00810000
         AIF   ('&RPL'(1,1) EQ '(').RPLR  GO IF RPL SPECIFIED AS REG    00850000
         LA    1,&RPL                   LOAD RPL ADDRESS                00900000
         AGO   .ARREG                   GO AROUND RPL REG CODE          00950000
.RPLR    ANOP                                                           01000000
         AIF   ('&RPL' EQ '(1)').ARREG  GO IF RPL ADDRESS ALREADY IN 1  01050000
         LR    1,&RPL(1)                LOAD RPL ADDRESS                01100000
.ARREG   ANOP                                                           01150000
         AIF   ('&BFRNO'(1,1) EQ '(').BFRNR  GO IF BFRNO SPECD AS REG   01200000
         LA    0,&BFRNO                 LOAD BUFFER NUMBER              01250000
         AGO   .ABREG                   GO AROUND BFRNO REG CODE        01300000
.BFRNR   ANOP                                                           01350000
         AIF   ('&BFRNO' EQ '(0)').ABREG  GO IF BUFFER NO ALREADY IN 0  01400000
         LR    0,&BFRNO(1)              LOAD BUFFER NUMBER              01450000
.ABREG   ANOP                                                           01500000
         SLL   0,8                      SHIFT BUFFER NUMBER             01550000
         IC    0,*+19                   INDICATE SEARCH BUFFER          01600000
         L     15,24(,1)                LOAD ACB ADDRESS FROM RPL       01650000
         L     15,8(,15)                LOAD VSAM INTERFACE RTN ADDR    01700000
         LA    14,*+8                   LOAD RETURN ADDRESS             01750000
         BR    15                       BRANCH TO INTERFACE ROUTINE     01800000
         DC    H'13'                    SEARCH BUFFER CODE              01850000
         MEXIT                                                     @L1A 01858300
.E0      IGGERMAC 203,SCHBFR    THE SCHBFR MACRO DOES NOT SUPPORT  @L1A 01866600
         MEXIT                    SYSSTATE AMODE64=YES             @L1A 01874900
.E1      IGGERMAC 204,SCHBFR    THE SCHBFR MACRO DOES NOT SUPPORT  @L1A 01883200
         MEXIT                    SYSSTATE ASCENV=AR OR ANY        @L1A 01891500
.END     ANOP                                                           01900000
         MEND                                                           01950000
