         MACRO                                                          00050000
.********************************************************************** 00100000
.*                                                                    * 00150000
.* $MAC(ARCXTRCT) COMP(SERVICE) PROD(DFHSM):                          * 00200000
.* MACRO NAME = ARCXTRCT                                              * 00250000
.* FUNC = SERVICE                                                     * 00300000
.* SUBFUNC = MWE                                                      * 00350000
.*                                                                    * 00400000
.* DESCRIPTIVE NAME = REQUEST DFHSM INFORMATION                       * 00450000
.*                                                                    * 00500000
.********************************************************************** 00512500
.*PROPRIETARY V3 STATEMENT                                            * 00525000
.*LICENSED MATERIALS - PROPERTY OF IBM                                * 00537500
.*"RESTRICTED MATERIALS OF IBM"                                       * 00550000
.*5650-ZOS                                                            * 00559300
.*COPYRIGHT IBM CORP. 1989, 2021                                      * 00568600
.*END PROPRIETARY V3 STATEMENT                                        * 00577900
.********************************************************************** 00587500
.*                                                                    * 00600000
.* FUNCTION = PERFORM DFHSM SVC 109 TO REQUEST SPECIFIC INFORMATION   * 00650000
.*            FROM DFHSM INTERNAL CONTROL DATA SETS.                  * 00700000
.*                                                                    * 00750000
.* NOTES = SEE BELOW                                                  * 00800000
.*                                                                    * 00850000
.*   DEPENDENCIES = NONE                                              * 00900000
.*                                                                    * 00950000
.*   RESTRICTIONS = ALL AREAS PASSED AS PARAMETERS TO THIS MACRO      * 01000000
.*                  MUST BE BELOW THE 16MB LINE.  THIS INCLUDES       * 01050000
.*                  DSN=, COPYPOOL=, WKAREA= AND SNAPDCB= VALUES.     * 01100000
.*                                                                    * 01150000
.*   REGISTER CONVENTIONS =                                           * 01200000
.*                                                                    * 01250000
.*            REGISTER 13 MUST POINT TO A STANDARD SAVE AREA          * 01300000
.*            WHEN MACRO IS ISSUED.                                   * 01350000
.*                                                                    * 01400000
.*            REGISTERS 0, 1, 14 AND 15 WILL BE DESTROYED.            * 01450000
.*                                                                    * 01500000
.*   PATCH LABEL = NONE                                               * 01550000
.*                                                                    * 01600000
.* MODULE TYPE = MACRO                                                * 01650000
.*                                                                    * 01700000
.*   PROCESSOR = BAL                                                  * 01750000
.*                                                                    * 01800000
.* INPUT =                                                            * 01850000
.*                   EITHER DATA, FREE OR DEFINE=YES MUST BE SPECIFIED* 01900000
.*                   THEY ARE MUTUALLY EXCLUSIVE PARAMETERS.          * 01950000
.*                                                                    * 02000000
.*          DATA     SPECIFY ONE OF THE FOLLOWING                     * 02050000
.*                   DATA REQUEST TYPES:                              * 02100000
.*                                                                    * 02150000
.*                        BUVERS - INFORMATION ON BACKUP VERSIONS     * 02200000
.*                        (THE DSN= KEYWORD IS REQUIRED)              * 02250000
.*                                                                    * 02300000
.*                        COPYPOOL - INFORMATION ON COPY POOL BACKUP  * 02310000
.*                                   VERSIONS                         * 02320000
.*                        (THE COPYPOOL= KEYWORD IS REQUIRED)         * 02330000
.*                                                                    *         
.*                        CRQREAD - Information on CRQ data      @Q25A*         
.*                                                                    * 02340000
.*          FREE     SPECIFY THE ADDRESS OF THE DATA AREA             * 02350000
.*                   TO BE FREED.  THIS ADDRESS WAS RETURNED IN       * 02400000
.*                   REGISTER 1 WHEN DATA WAS REQUESTED.              * 02450000
.*                                                                    * 02500000
.*          DEFINE   SPECIFY THE DATA DECLARATIONS DSECT. SPECIFY     * 02550000
.*                   YES IF A DSECT IS TO BE CREATED FOR THE EXTRACT  * 02600000
.*                   HEADER AND ENTRY MAPPINGS.  THE DEFAULT IS       * 02650000
.*                   DEFINE=NO.                                       * 02700000
.*                                                                    * 02750000
.*          TIME     Only valid on a BUVERS request.              @RSA* 02760000
.*                                                                    * 02770000
.*          DATE     Only valid on a BUVERS request.              @RSA* 02780000
.*                                                                    * 02790000
.*     NOTE:  THE DSN, COPYPOOL, TEST, SNAPDCB, AND WKAREA KEYWORDS   * 02800000
.*            ONLY APPLY WHEN DATA= IS SPECIFIED.  THEY ARE IGNORED   * 02850000
.*            IF FREE= OR DEFINE=YES ARE SPECIFIED.                   * 02900000
.*                                                                    * 02950000
.*                                                                    * 03000000
.*          DSN      (OPTIONAL) SPECIFY THE ADDRESS OF A 44-BYTE AREA * 03050000
.*                   CONTAINING THE NAME OF THE SOURCE DATA SET.      * 03100000
.*                   > REQUIRED < on a BUVERS request.            @RSA* 03106200
.*                                                                    * 03112500
.*          COPYPOOL (OPTIONAL) SPECIFY THE NAME OF A COPY POOL       * 03125000
.*                   DEFINED TO DFSMS.                                * 03137500
.*                                                                    * 03150000
.*          TEST     (OPTIONAL) SPECIFY YES!NO WHETHER THE REQUEST    * 03200000
.*                   IS TO BE SENT TO DFHSM FOR PROCESSING.  IF YES   * 03250000
.*                   IS SPECIFIED, THE REQUEST IS NOT SENT TO DFHSM.  * 03300000
.*                   THE DEFAULT IS NO.                               * 03350000
.*                                                                    * 03400000
.*          SNAPDCB  (OPTIONAL) SPECIFY THE ADDRESS OF AN OPEN DCB    * 03450000
.*                   USED FOR SNAP PROCESSING OF THE MWE AREA UPON    * 03500000
.*                   RETURN FROM DFHSM SVC PROCESSING.                * 03550000
.*                                                                    * 03600000
.*          WKAREA   (REQUIRED) SPECIFY THE ADDRESS OF A 200 BYTE     * 03650000
.*                   WORK AREA TO CONTAIN INFORMATION SPECIFIED IN    * 03700000
.*                   THIS MACRO.                                      * 03750000
.*                                                                    * 03800000
.* OUTPUT =   REGISTER 15 WILL CONTAIN MWE RETURN CODE                * 03850000
.*            REGISTER 0  WILL CONTAIN MWE REASON CODE                * 03900000
.*            REGISTER 1  WILL CONTAIN THE ADDRESS OF THE AREA        * 03950000
.*                        CONTAINING THE DATA REQUESTED OR ZERO IF    * 04000000
.*                        THE BUFFER WAS NOT OBTAINED.  IT IS THE     * 04050000
.*                        USER'S RESPONSIBILITY TO FREE THIS AREA.    * 04100000
.*                                                                    * 04150000
.*            INVALID REQUESTS NOT SENT TO DFHSM WILL HAVE            * 04200000
.*            THE FOLLOWING RETURN CODES:                             * 04250000
.*                                                                    * 04300000
.*            806 - LINK FAILURE - UNABLE TO LINK TO ARCGIVER         * 04350000
.*                                                                    * 04400000
.* CHANGE ACTIVITY =                                                  * 04450000
.*  $01=OY30406,250,090690,THP5: NEW INVOCATION MACRO.            @01A* 04500000
.*  $02=OY40607,260,021991,THF2: KLN0357 - ROLLIN OY30406 -       @02A* 04550000
.*                               THIS IS NEW MACRO FOR 2.6.0 THAT @02A* 04600000
.*                               WAS CREATED FOR OY30406 IN 2.5.0.@02A* 04650000
.*                               ROLLED INTO 260 LINE FOR LINE AS @02A* 04700000
.*                               IT IS UNDER 2.5.0 UNDER OY30406. @02A* 04750000
.*  $03=OY50981,260,011392,THCB: MOVING MACRO FROM R610 FMID TO   @03A* 04800000
.*                               R602 FMID ONLY, NO CODE CHANGE   @03A* 04850000
.*  $04=OY52541,260,030692,THH8: REMOVE THE ARCCOPYR MACRO AND    @04A* 04860000
.*                               PUT IN THE COPY RIGHT STATEMENT  @04A* 04870000
.*                               AS A COMMENT. ADDED &NAME TO     @04A* 04880000
.*                               THE ARCXTRCT FREE PARAMETER      @04A* 04890000
.*  $05=OY67292,110,100893,THH8: ARCXTRCT WAS NOT SHIPPED IN 110  @05A* 04895000
.*  $SH=HSMSH,130,072094,THBE:   RLS1 SUPPORT                     @SHA* 04897500
.*  $GH=HSMGH,130,091294,THBE:   SUPPORT FOR >13 BACKUP VERSIONS  @GHA* 04898700
.*  $TH=HSMTH,130,102494,THWG: RLS1B SUPPORT                      @THA* 04899300
.*  $06=OW12040,120,032495,THH8: ARCXTRCT WAS NOT SHIPPED IN 120  @06A* 04924600
.*  $07=OW14397,130,080995,THH9: ROLLIN OW11701. SUPPORT          @07A* 04933000
.*                               UNSERIALIZED BACKUP.             @07A* 04941400
.*  $08=OW49899,210,020701,THR7: ADD BUVENRCM FOR RECOVERY        @08A* 04944200
.*                               CONTROL MANAGER NOTIFICATION     @08A* 04947000
.*  $XM=HSMXM,1H0,103101,THGS: FAST REPLICATIN, STAGE 1           @XMA* 04948500
.*  $XN=HSMXN,180,081303,THA4: FAST REPLICATION TAPE SUPPORT      @XNA* 04949200
.*  $09=OA17314,180,060206,THV5:FR FlashCopy Incremental support  @09A* 04949600
.*  $NR=HSMNR,Z1A,061507,THGS: BACKDS NEWNAME Support             @NRA* 04949800
.*  $RS=HSMRS,B10,052008,THA5: BACKDS RETAINDAYS SUPPORT          @RSA* 04949900
.*  $RS-HSMRS,B10,011209,THHF: K1B0832 DSBRP SUPPORT            1P@RSA* 04966600
.*  $RT=HSMRT,C10,051909,THSB: Fast Reverse Restore               @RTA* 04974900
.*  $10=OA30915,B10,103009,THSB: Add CPFCATIN to ARCXTRCT         @10A* 04979100
.*  $11=OA38169,D10,010312,THL9: Update CPFRECVR comment          @11A* 04981200
.*  $XW=HSMXW,210,030312,THL9: FlashCopy consistency group        @XWA* 04981201
.*  $Q25=HSMQ25,250,080820,THI9-15: Extract CRQ data             @Q25A*         
.*  $C9=OA60278,250,032722,THL9: TCT Full Volume Dump SPE         @C9A* 04981300
.********************************************************************** 04983300
&NAME    ARCXTRCT &DSN=,&DATA=,&FREE=,&DEFINE=NO,                      X05000000
               &WKAREA=,&SNAPDCB=,&TEST=NO,&COPYPOOL=,&DATE=,&TIME=     05050000
         LCLA  &A                                                       05100000
         LCLB  &ERROR                                                   05150000
         LCLC  &H                                                       05200000
         LCLC  &L1                                                      05250000
         LCLC  &L2                                                      05300000
&L1      SETC  'ARC'.'&SYSNDX'.'A'                                      05350000
&L2      SETC  'ARC'.'&SYSNDX'.'B'                                      05400000
.*                                                                      05450000
.* CHECK THE PARAMETERS SPECIFIED WITH THE EXTRACT FUNCTION             05500000
.*                                                                      05550000
.CHKREAD ANOP                                                           05600000
         AIF   ('&DEFINE' NE 'YES' AND '&DEFINE' NE 'NO').BIN           05650000
         AIF   ('&DEFINE' EQ 'YES' AND '&DATA' NE '').EXC               05700000
         AIF   ('&DEFINE' EQ 'YES' AND '&FREE' NE '').EXC               05750000
         AIF   ('&DATA' NE '' AND '&FREE' NE '').EXC                    05800000
         AGO   .RD0                                                     05850000
.BIN     ANOP                                                           05900000
         MNOTE 8,'INVALID VALUE FOR DEFINE KEYWORD: SPECIFY YES/NO'     05950000
&ERROR   SETB  1                                                        06000000
         AGO   .RD7                                                     06050000
.EXC     ANOP                                                           06100000
         MNOTE 8,'DATA, FREE AND DEFINE=YES ARE MUTUALLY EXCLUSIVE'     06150000
&ERROR   SETB  1                                                        06200000
         AGO   .RD7                                                     06250000
.RD0     ANOP                                                           06300000
         AIF   ('&DEFINE' EQ 'YES').DEF                                 06350000
         AIF   ('&FREE' NE '').XFREE                                    06400000
         AIF   ('&DATA' NE '').RD1                                      06450000
         MNOTE 8,'DATA, FREE OR DEFINE MUST BE SPECIFIED'               06500000
&ERROR   SETB  1                                                        06550000
         AGO   .RD7                                                     06600000
.RD1     ANOP                                                           06650000
         AIF   ('&DATA' EQ 'BUVERS').RD2                                06700000
         AIF   ('&DATA' EQ 'COPYPOOL').RD4                              06725000
         AIF   ('&DATA' EQ 'CRQREAD').RD8                     @Q25A             
         MNOTE 8,'DATA PARAMETER SPECIFIED WITH INVALID VALUE'          06750000
&ERROR   SETB  1                                                        06800000
         AGO   .RD7                                                     06825000
.RD2     ANOP                                                           06850000
         AIF   ('&DSN' NE '' AND '&DATA' EQ 'BUVERS').RD25     @RSC     06900000
         MNOTE 8,'DSN MUST BE SPECIFIED FOR THIS DATA REQUEST'          06950000
&ERROR   SETB  1                                                        07000000
         AGO   .RD7                                                     07025000
.* Check DATE and TIME parameters                              @RSA     07029100
.RD25    ANOP                                                           07033200
         AIF   ('&DATE' EQ '' AND '&TIME' EQ '').RD3                    07063700
         AIF   ('&DATE' NE '' AND '&TIME' NE '').RD3                    07094200
         MNOTE 8,'DATE AND TIME MUST BE SPECIFIED TOGETHER'             07124700
&ERROR   SETB  1                                                        07155500
         AGO   .RD7                                                     07161000
.RD3     ANOP                                                           07170700
         AIF   ('&WKAREA' NE '').RD7                                    07180400
         MNOTE 8,'REQUIRED PARAMETER, WKAREA, IS NOT SPECIFIED'         07190100
&ERROR   SETB  1                                                        07200000
         AGO   .RD7                                                     07225000
.RD4     ANOP                                                           07250000
         AIF   ('&DSN' EQ '' AND '&DATA' EQ 'COPYPOOL').RD5             07255000
         MNOTE 8,'DSN CANNOT BE SPECIFIED FOR THIS DATA REQUEST'        07260000
&ERROR   SETB  1                                                        07265000
         AGO   .RD7                                                     07270000
.RD5     ANOP                                                           07275000
         AIF   ('&COPYPOOL' NE '').RD6                                  07280000
         MNOTE 8,'REQUIRED PARAMETER, COPYPOOL, IS NOT SPECIFIED'       07285000
&ERROR   SETB  1                                                        07290000
         AGO   .RD7                                                     07292500
.RD8     AIF   ('&DSN' EQ '' AND '&DATA' EQ 'CRQREAD').RD7       @Q25A          
         MNOTE 8,'DSN CANNOT BE SPECIFIED FOR THIS DATA REQUEST' @Q25A          
&ERROR   SETB  1                                                 @Q25A          
         AGO   .RD7                                              @Q25A          
.RD6     ANOP                                                           07295000
         AIF   ('&WKAREA' NE '').RD7                                    07296200
         MNOTE 8,'REQUIRED PARAMETER, WKAREA, IS NOT SPECIFIED'         07297400
.RD7     ANOP                                                           07298600
         AIF   (NOT &ERROR).BUILD                                       07300000
         MEXIT                                                          07350000
.*                                                                      07400000
.* AT THIS POINT ALL THE PARAMETERS HAVE BEEN PROPERLY SPECIFIED.       07450000
.* NOW BUILD THE CONTROL BLOCK AND CALL THE PROCESSING ROUTINE.         07500000
.*                                                                      07550000
.BUILD   ANOP                                                           07600000
         AIF   ('&WKAREA'(1,1) EQ '(').WREG                             07650000
&NAME    LA    15,&WKAREA                                               07700000
         AGO   .CLRWKA                                                  07750000
.WREG    ANOP                                                           07800000
&A       SETA  K'&WKAREA-2                                              07850000
&H       SETC  '&WKAREA'(2,&A)                                          07900000
&NAME    LR    15,&H                                                    07950000
.CLRWKA  ANOP                                                           08000000
         XC    0(200,15),0(15)     CLEAR THE WORK AREA                  08050000
         B     *+12                BRANCH AROUND IDENTIFIER             08100000
         DC    CL8'DFHSMREQ'                                            08150000
         MVC   0(8,15),*-8         SET THE IDENTIFIER                   08200000
         B     *+12                BRANCH AROUND FUNCTION NAME          08250000
         DC    CL8'EXTRACT '       FUNCTION NAME                        08300000
         MVC   8(8,15),*-8         SET THE FUNCTION NAME                08350000
.XTEST   AIF   ('&TEST' NE 'YES').XSNAP                                 08400000
         OI    16(15),X'02'        TEST WAS SPECIFIED                   08450000
.XSNAP   ANOP                                                           08500000
         AIF   ('&SNAPDCB' EQ '').XTYPE                                 08550000
         AIF   ('&SNAPDCB'(1,1) EQ '(').SREG                            08600000
         LA    1,&SNAPDCB          GET ADDRESS OF SNAP DCB              08650000
         ST    1,188(15)           STORE IN PARM LIST                   08700000
         AGO   .XTYPE                                                   08750000
.SREG    ANOP                                                           08800000
&A       SETA  K'&SNAPDCB-2                                             08850000
&H       SETC  '&SNAPDCB'(2,&A)                                         08900000
         ST    &H,188(15)          STORE DCB ADDRESS IN PARM LIST       08950000
.XTYPE   ANOP                                                           09000000
         AIF   ('&DATA' NE 'BUVERS' AND '&DATA' NE 'COPYPOOL' AND      X09050000
               '&DATA' NE 'CRQREAD').XDSN                     @Q25C             
&DREQ    SETC  '&DATA'(1,4)                                             09100000
         B     *+8                 BRANCH AROUND FUNCTION NAME          09150000
         DC    CL4'&DREQ'          DATA REQUESTED                       09200000
         MVC   60(4,15),*-4        SET THE FUNCTION NAME                09250000
.XDSN    ANOP                                                           09300000
         AIF   ('&DATA' EQ 'COPYPOOL').XCPNAM                           09325000
         AIF   ('&DSN' EQ '').GIVER                                     09350000
         AIF   ('&DSN'(1,1) EQ '(').DSNREG                              09400000
         LA    1,&DSN              GET ADDR OF DSN                      09450000
         ST    1,20(15)            STORE IN PARM LIST                   09500000
         AGO   .XDATE                                         @RSC      09504500
.XCPNAM  ANOP                                                           09509000
         AIF   ('&COPYPOOL'(1,1) EQ '(').CPREG                          09513500
         LA    1,&COPYPOOL         GET ADDR OF COPYPOOL NAME            09518000
         ST    1,84(15)            STORE IN PARM LIST                   09522500
         AGO   .GIVER                                                   09527000
.CPREG   ANOP                                                           09531500
&A       SETA  K'&COPYPOOL-2                                            09536000
&H       SETC  '&COPYPOOL'(2,&A)                                        09540500
         ST    &H,84(15)                                                09545000
         AGO   .GIVER                                                   09550000
.DSNREG  ANOP                                                           09600000
&A       SETA  K'&DSN-2                                                 09650000
&H       SETC  '&DSN'(2,&A)                                             09700000
         ST    &H,20(15)           STORE ADDR OF KEY PARM               09750000
.XDATE   ANOP                                                 @RSA      09756200
         AIF   ('&DATE' EQ '').GIVER                          @RSA      09762400
         AIF   ('&DATE'(1,1) EQ '(').DATEREG                  @RSA      09764200
         LA    1,&DATE             GET ADDR OF DATE           @RSA      09766000
         ST    1,44(15)            STORE IN PARM LIST         @RSA      09767800
         AGO   .XTIME                                         @RSA      09769600
.DATEREG ANOP                                                 @RSA      09771400
&A       SETA  K'&DATE-2                                      @RSA      09773200
&H       SETC  '&DATE'(2,&A)                                  @RSA      09775000
         ST    &H,44(15)                                      @RSA      09776800
         AGO   .XTIME                                         @RSA      09778600
.XTIME   ANOP                                                 @RSA      09780400
         AIF   ('&TIME'(1,1) EQ '(').TIMEREG                  @RSA      09782200
         LA    1,&TIME             GET ADDR OF TIME           @RSA      09784000
         ST    1,88(15)            STORE IN PARM LIST         @RSA      09785800
         AGO   .GIVER                                         @RSA      09787600
.TIMEREG ANOP                                                 @RSA      09789400
&A       SETA  K'&TIME-2                                      @RSA      09791200
&H       SETC  '&TIME'(2,&A)                                  @RSA      09793000
         ST    &H,88(15)                                      @RSA      09794800
         AGO   .GIVER                                         @RSA      09796600
.GIVER   ANOP                                                           09800000
         LR    1,15                PARAMETER ADDRESS TO REG 1           09850000
         LINK  EP=ARCGIVER,ERRET=&L1                                    09900000
         L     15,192(1)           LOAD RETURN CODE                     09950000
         L     0,196(1)            LOAD REASON CODE                     10000000
         L     1,76(1)             LOAD ADDRESS OF EXTRACT DATA         10050000
         B     &L2                                                      10100000
&L1      LA    15,806              INDICATE LINK FAILED                 10150000
&L2      EQU   *                                                        10200000
         MEXIT                                                          10250000
.XFREE   ANOP                                                           10300000
         AIF   ('&FREE'(1,1) EQ '(').FREEREG                            10350000
&NAME    LA    1,&FREE             GET ADDR OF FREE                     10400000
         AGO   .XFR                                                     10450000
.FREEREG ANOP                                                           10500000
&A       SETA  K'&FREE-2                                                10550000
&H       SETC  '&FREE'(2,&A)                                            10600000
&NAME    LR    1,&H                                                     10650000
.XFR     ANOP                                                           10700000
         SLR   0,0                                                      10750000
         ICM   0,7,1(1)                                                 10800000
         SLR   15,15                                                    10850000
         IC    15,0(,1)                                                 10900000
         FREEMAIN RU,LV=(0),A=(1),SP=(15)                               10950000
         MEXIT                                                          11000000
.DEF     ANOP                                                           11050000
ARCXTRCT DSECT ,                                                        11100000
.******************************************************************     11150000
.*  DEFINITION FOR EXTRACT AREA HEADER                                  11200000
*  This structure must match the structure in ARCGIVER        @RSA      11225000
.******************************************************************     11250000
XHEADER  DS  0CL8                    EXTRACT AREA HEADER                11300000
XSUBPOOL DS  AL1                     EXTRACT DATA SUBPOOL               11350000
XLENGTH  DS  AL3                     EXTRACT AREA SIZE (TOTAL)          11400000
XENTRIES DS  H                       NUMBER OF ENTRIES THAT FOLLOW      11450000
XENTRLEN DS  H                       SIZE OF EACH ENTRY                 11500000
XHEADERE DS  0C                      END OF EXTRACT HEADER              11550000
.******************************************************************     11600000
.*  DEFINITION FOR ENTRY CREATED BY DATA=BUVERS                         11650000
.*  THIS DEFINES A SINGLE ENTRY IN THE ARRAY OF ENTRIES                 11700000
.******************************************************************     11750000
BUVEENT  DS  0CL108                  ENTRY FOR BACKUP VERSION           11800000
BUVEVER  DS  F                       VERSION NUMBER 001-999             11850000
BUVEGEN  DS  F                       GENERATION NUMBER 0-99      @GHC   11900000
BUVEDATE DS  CL4                     DATE WHEN BACKUP MADE YYYYDDDF     11950000
BUVETIME DS  CL4                     TIME WHEN BACKUP MADE HHMMSSTH     12000000
BUVEOWNR DS  CL16                    RESOURCE OWNER                     12050000
BUVEDATA DS  CL8                     RESOURCE DATA                      12100000
BUVEFLAG DS  XL2                     STATUS FLAGS                @NRC   12150000
BUVEFBWO EQU B'10000000'             BACKUP WHILE OPEN CANDIDATE        12200000
BUVENONQ EQU B'01000000'             NO ENQUEUE SERIALIZATION           12250000
BUVEFCAT EQU B'00100000'             DATA CATALOGED WHEN BACKUP MADE    12300000
BUVEFRRR EQU B'00010000'             RLS RECOVERY REQUIRED       @THA   12325000
BUVENQN1 EQU B'00001000'             ENQ ATTEMPTED, BUT FAILED   @07A   12331200
BUVENQN2 EQU B'00000100'             ENQ ATTEMPTED, BACKUP RETRIED,     12337400
.*                                   AND ENQ FAILED AGAIN        @07A   12343600
BUVENRCM EQU B'00000010'             NOTIFY RECOVERY CONTROL MGR @08A   12346800
BUVEFNN  EQU B'00000001'             NEWNAME SPECIFIED           @NRA   12352400
BUVEFSNO EQU B'10000000'             SPHERE(NO) PROCESSED        @NRA   12358000
BUVEFGVN EQU B'01000000'             GVCN PROCESSED              @NRA   12363600
BUVEFMAS EQU B'00100000'             ON=More entries available   @RSA   12365400
BUVEFRD  EQU B'00010000'             RETAINDAYS specified        @RSA   12367200
BUVEFNEX EQU B'00001000'           ON=The version never expire. Only    12367800
.*                                 valid when BUVEFRD is 1     1P@RSA   12368400
         DS  XL2                     RESERVED                    @NRC   12369200
BUVERLSG DS  CL8                     RLS RECOVERY TIMESTAMP GMT  @SHA   12375000
BUVERLSL DS  CL8                     RLS RECOVERY TIMESTAMP LOCL @SHA   12400000
BUVERDYS DS  CL2                     RETAINDAYS value, only valid       12412500
.*                                   when BUVEFRD is set to 1    @RSA   12425000
         DS  CL46                    RESERVED                    @RSC   12437500
BUVEENTE DS  0C                      END OF BACKUP VERSION ENTRY        12450000
.******************************************************************@XMA 12453100
.*  DEFINITION FOR ENTRY CREATED BY DATA=COPYPOOL                       12456200
.*  THIS DEFINES A SINGLE ENTRY IN THE ARRAY OF ENTRIES                 12459300
.******************************************************************@XMA 12462400
CPENT    DS  0CL108                ENTRY FOR COPYPOOL BACKUP VER   @XMA 12465300
CPVER    DS  F                     VERSION NUMBER 001-999          @XMA 12468200
CPGEN    DS  F                     GENERATION NUMBER 00-99         @XMA 12471100
CPFLAG   DS  XL2                   STATUS FLAGS                    @10C 12474000
CPFVALID EQU B'10000000'           DASD COPY IS VALID (FAST        @XNC 12476900
.*                                    REPLICATION HAS COMPLETED)   @XNA 12479800
CPFNVTOC EQU B'01000000'           NOVTOCENQ WAS SPECIFIED         @XMA 12482700
.*                                                                      12485600
.* A RECOVERABLE DASD COPY IS ONE WHOSE COPY POOL DEFINITION       @XNA 12488500
.* SPECIFIES VERSIONS > 0                                          @XNA 12491400
.*                                                                      12494300
CPFRECVR EQU B'00100000'           DASD COPY IS RECOVERABLE        @XNA 12497200
.*                                                                 @11C 12500100
CPFDMPRC EQU B'00010000'           ALL REQUIRED DUMP COPIES ARE    @XNA 12503000
.*                                    COMPLETE                     @XNA 12505900
CPFDMPAC EQU B'00001000'           ALL DUMP COPIES ARE COMPLETE    @XNA 12508800
CPFDMPPC EQU B'00000100'           DUMP IS PARTIAL                 @XNA 12511700
CPFINC   EQU B'00000010'           INCREMENTAL VERSION             @09A 12513100
CPFINCMP EQU B'00000001'           RECOVERY INCOMPLETE             @RTA 12513800
CPFCATIN EQU B'10000000'           CATALOG INFORMATION CAPTURED    @10A 12515600
CPFFCCG  EQU B'01000000'           FLASHCOPY CONSISTENCY GROUP     @XWA 12515601
.*                                    VERSION                      @XWA 12515602
.* DELETE RESERVED BYTE TO USE IN CPFLAG                          1@10D 12516500
CPDATE   DS  CL4                   DATE BACKUP MADE YYYYDDDF       @XMA 12517500
CPTIME   DS  CL4                   TIME BACKUP MADE HHMMSSTH       @XMA 12520400
CPTOKEN  DS  CL40                  COPY POOL TOKEN                 @XMA 12523300
CPDINFO  DS  0CL45                 INFO FOR UP TO 5 DUMP COPIES    @XNA 12526200
CPDCNAME DS  CL8                   DUMP CLASS NAME                 @XNA 12529100
CPDFLAGS DS  CL1                   FLAGS FOR DUMP                  @XNA 12532000
CPDCMPLT EQU B'10000000'           DUMP FOR THIS CLASS IS COMPLETE @XNA 12534900
CPDREQD  EQU B'01000000'           DUMP CLASS IS REQUIRED          @XNA 12537800
CPDCLOUD EQU B'00100000'           DUMP CLASS TARGETS CLOUD        @C9A 12537900
         DS  CL25                  RESERVED                        @XNC 12540700
CPENTE   DS  0C                    END OF COPY POOL ENTRY          @XMA 12543600
         MEND                                                           12546500
