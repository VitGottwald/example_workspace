         MACRO                                                                  
&NAME    STFSMODE &A,&INITIAL=NO,&RSHWKEY=64,&NOEDIT=NO,&PARTION=NO             
.**********************************************************************         
.*                                                                    *         
.* STATUS - OS/VS2 TSO/VTAM DYNAMIC RESHOW LEVEL 3.0                  *         
.*                                                                    *         
.* NAME - STFSMODE  (SET FULL SCREEN MODE)                            *         
.*                                                                    *         
.*      COPYRIGHT = LICENSED MATERIALS - PROPERTY OF IBM              *         
.*                                                                    *         
.*                  "RESTRICTED MATERIALS OF IBM"                     *         
.*                                                                    *         
.*                  5695-117                                          *         
.*                                                                    *         
.*                  (C) COPYRIGHT IBM CORP. 1979                      *         
.*                                                                    *         
.*                                                                    *         
.* FUNCTION - THE STFSMODE MACRO IS USED TO SPECIFY WHETHER OR NOT    *         
.*            AN APPLICATION IS PRESENTING DISPLAYS TO A TSO/VTAM     *         
.*            DISPLAY TERMINAL IN FULL SCREEN MODE.                   *         
.*                                                                    *         
.*            WHEN INITIALLY SETTING THE MODE ON, INITIAL=YES WILL    *         
.*            PREVENT PAGING FOR THE FIRST FULL SCREEN TPUT ISSUED BY *         
.*            THE APPLICATION.                                        *         
.*                                                                    *         
.*            RSHWKEY IS USED TO SPECIFY WHICH PFKEY IS TO BE USED    *         
.*            AS THE RESHOW KEY.  E.G., RSHWKEY=3 MEANS THAT PFKEY 3  *         
.*            IS TO BE THE RESHOW KEY.  WHEN STFSMODE IS OFF,         *         
.*            RSHWKEY IS NOT TO BE SPECIFIED.  WHEN STFSMODE IS ON    *         
.*            AND NO RSHWKEY HAS BEEN SPECIFIED, RSHWKEY WILL DEFAULT *         
.*            TO ZERO.                                                *         
.*                                                                    *         
.*            NOEDIT=YES WILL CREATE A MODE IN WHICH ALL INPUT        *         
.*            RECEIVED FROM THE TERMINAL WILL PASS THROUGH VTIOC      *         
.*            UNINSPECTED AND UNMODIFIED.  NOEDIT=NO WILL BE THE      *         
.*            DEFAULT.                                                *         
.*                                                                    *         
.*            PARTION=YES WILL CREATE A MODE IN WHICH THE COMMAND     *         
.*            PROCESSOR CAN MAKE USE OF THE 16-BIT ADDRESSING SCHEME. *         
.*            WHEN PARTION=YES IS SPECIFIED, NO EDITING OF THE ADDRESS*         
.*            BYTES WILL OCCUR AND THE WCC WILL BE SENT AS IS (I.E.   *         
.*            IT WILL NOT BE MADE PRINTABLE).  PARTION=NO WILL BE THE *         
.*            DEFAULT.                                                *         
.*                                                                    *         
.*       THE MACRO PROTOTYPE IS ABOVE                                 *         
.*                                                                    *         
.*       THE STANDARD FORM OF THE EXPANSION IS BELOW                  *         
.*&NAME  LA    1,128+64+32+16           SET FLAGS FOR MODE, INITIAL   *         
.**                                     CALL, NOEDIT, AND PARTITION             
.*       SLL   1,24                     SHIFT TO HIGH ORDER BYTE      *         
.*       LA    0,10                     LOAD RSHWKEY                  *         
.*       OR    1,0                      RSHWKEY IN RIGHTMOST BYTE     *         
.*       LA    0,18                     LOAD ENTRY CODE               *         
.*       SLL   0,24                     SHIFT TO HIGH ORDER BYTE      *         
.*       SVC   94                       TERMINAL CONTROL MACRO SVC    *         
.*                                                                    *         
.**********************************************************************         
         LCLA  &SYSFS                                                           
         LCLA  &SYSINIT                                                         
         LCLA  &SYSNEDT                                                         
         LCLA  &SYSPART                                                         
&SYSFS   SETA  128                                                              
&SYSINIT SETA  0                                                                
&SYSNEDT SETA  0                                                                
&SYSPART SETA  0                                                                
         AIF   ('&INITIAL' NE 'YES' AND '&INITIAL' NE 'NO').ERROR1              
         AIF   ('&PARTION' NE 'YES' AND '&PARTION' NE 'NO').ERROR6              
         AIF   ('&NOEDIT' NE 'YES' AND '&NOEDIT' NE 'NO').ERROR5                
         AIF   ('&NOEDIT' EQ 'YES' AND '&A' EQ 'OFF').ERROR4                    
         AIF   (&RSHWKEY GT 0 AND &RSHWKEY LT 25).TESTFS                        
         AIF   (&RSHWKEY NE 64).ERROR2                                          
         AGO   .NEXT                                                            
.TESTFS  ANOP                                                                   
         AIF   ('&A' EQ 'OFF').ERROR3                                           
.NEXT    ANOP                                                                   
         AIF   ('&INITIAL' EQ 'NO').TESTNE                                      
&SYSINIT SETA  64                                                               
.TESTNE  ANOP                                                                   
         AIF   ('&NOEDIT' EQ 'NO').TESTPA                                       
&SYSNEDT SETA  32                                                               
.TESTPA  ANOP                                                                   
         AIF   ('&PARTION' EQ 'NO').TESTA                                       
&SYSPART SETA  16                                                               
.TESTA   ANOP                                                                   
         AIF   ('&A' EQ 'OFF').FSOFF                                            
         AGO   .FSON                                                            
.ERROR   IHBERMAC 36,,&A                                                        
         MEXIT                                                                  
.ERROR1  IHBERMAC 54,,INITIAL                                                   
         MEXIT                                                                  
.ERROR2  IHBERMAC 54,,RSHWKEY                                                   
         MEXIT                                                                  
.ERROR3  IHBERMAC 1020,RSHWKEY,&A                                               
         MEXIT                                                                  
.ERROR4  IHBERMAC 1020,NOEDIT,&A                                                
         MEXIT                                                                  
.ERROR5  IHBERMAC 54,,NOEDIT                                                    
         MEXIT                                                                  
.ERROR6  IHBERMAC 54,,PARTION                                                   
         MEXIT                                                                  
.FSON    ANOP                                                                   
&NAME    LA    1,&SYSFS+&SYSINIT+&SYSNEDT+&SYSPART SET FLAGS FOR MODE,          
*                                       CALL, NOEDIT AND PARTITION              
         SLL   1,24                     SHIFT TO HIGH ORDER BYTE                
         AGO   .PFCD                                                            
.FSOFF   ANOP                                                                   
&NAME    SR    1,1                      CLEAR PARAMETER REGISTER                
         AIF   ('&PARTION' NE 'YES').PFCD                                       
&NAME    LA    1,&SYSPART               FS MODE OFF, PARTION=YES                
.PFCD    ANOP                                                                   
         AIF   ('&A' EQ 'OFF').ENTRYCD                                          
         AIF   (&RSHWKEY EQ 64).ENTRYCD                                         
         LA    0,&RSHWKEY               LOAD RSHWKEY                            
         OR    1,0                      RSHWKEY IN RIGHTMOST BYTE               
.ENTRYCD ANOP                                                                   
         LA    0,18                     LOAD ENTRY CODE                         
         SLL   0,24                     SHIFT TO HIGH ORDER BYTE                
         SVC   94                       TERMINAL CONTROL MACRO SVC              
         MEND                                                                   
