         MACRO                                                                  
         IST6MCSR &CONVID=,&QUALIFY=,&AREA=,&RECLEN=,&OPTCD=,&VTRINA=, X        
               &ECB=,&EXIT=,&BRANCH=,&CD=,&FILL=,&VTRINL=,&VTROUTA=,   X        
               &CONMODE=,&CONXMOD=,&INVALP1=,&INVALP2=,&VTROUTL=   @D1C         
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCSR                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: CONTROL = SENDRCV PROCESSOR                 */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5655-127 (C) COPYRIGHT IBM CORP. 1996.             */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */         
.* /*      CONTROL=SENDRCV PARAMETER ON THE APPCCMD MACRO.           */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS:                                              */         
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCER                       */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: NONE                                     */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*                                                                */         
.* /* $MAC(IST6MCSR),COMP(API),PROD(VTAM): SENDRCV PROCESSOR         */         
.* /*                                                                */         
.* /*  FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS         */         
.* /*  ---- -------- ------- ------ ------  -----------------        */         
.* /*  $D0= IA@@@MST V51APPC 941118 226653: Sendrcv                  */         
.* /*  $D1= ID@@@MST FPAPI   950310 143576: Fast Path API for APPC   */         
.* /*                                                                */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
.*                                                                              
         GBLB  &ISTGEN                                                          
         GBLB  &ISTSR                                                           
         AIF   (&ISTSR EQ 1).SRINVKW                                            
*                                                                               
*  IST6MCSR - ACF/VTAM                                                          
*                                                                               
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                              
*                                                                               
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"               
*                                                                               
*             5655-127 (C) COPYRIGHT IBM CORP. 1996.                            
*             ALL RIGHTS RESERVED.                                              
*                                                                               
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                         
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY                      
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                          
*                                                                               
*             SEE COPYRIGHT INSTRUCTIONS.                                       
*                                                                               
&ISTSR   SETB  1                                                                
.SRINVKW ANOP                                                                   
         IST6MCUC SENDRCV,CONTROL,INVALKW=&INVALP1  CHECK INVALIDS              
         IST6MCUC SENDRCV,CONTROL,INVALKW=&INVALP2  CHECK INVALIDS              
.SRID    ANOP                                                                   
         AIF   ('&CONVID' EQ '').SRQFY                                          
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X        
               KW=CONVID                                                        
.*                                              PUT CONVID IN APPC AREA         
.SRQFY   ANOP                                                                   
         AIF   ('&QUALIFY' EQ '').SRAREA                                        
.SRQFY1  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'DATAFLU').SRQFY2                                 
         AIF   (NOT &ISTGEN).SRAREA                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6DFLU                                   
*                                       PUT DATAFLU CODE IN APPC AREA           
         AGO   .SRAREA                                                          
.SRQFY2  ANOP                                                                   
         IST6MCER 5,QUALIFY,&QUALIFY                                            
.SRAREA  ANOP                                                                   
         AIF   ('&AREA' EQ '').SRRLEN                                           
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X        
               KW=DATA_ADDRESS                                                  
.*                                           PUT DATA ADDRESS IN RPL            
.SRRLEN  ANOP                                                                   
         AIF   ('&RECLEN' EQ '').SRFILL                                         
         IST6MCUA KWVAL=&RECLEN,LOC=RPLRLEN-IFGRPL,BASEREG=1,          X        
               KW=DATA_LENGTH                                                   
.*                                                                              
.SRFILL  ANOP                       ** FILL= PROCESSING **                      
         AIF   ('&FILL' EQ '').SRCD                                             
         AIF   ('&FILL' NE 'LL').SRFIL1                                         
         AIF   (NOT &ISTGEN).SRCD                                               
         OI    RPL6FLG1-ISTRPL6X(14),RPL6LL  SET FILL TO LL                     
         AGO   .SRCD                                                            
.SRFIL1  ANOP                                                                   
         AIF   ('&FILL' NE 'BUFF').SRFIL2                                       
         AIF   (NOT &ISTGEN).SRCD                                               
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6LL                               
*                                            SET FILL TO BUFF                   
         AGO   .SRCD                                                            
.SRFIL2  ANOP                                                                   
         IST6MCER 5,FILL,&FILL                                                  
.SRCD    ANOP                                                                   
         AIF   ('&CD' EQ '').SRCMKW                                             
         AIF   ('&CD' NE 'IMMED').SRCD1                                         
         AIF   (NOT &ISTGEN).SRCMKW                                             
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6CDDE                             
*                                            SET CD TO IMMED                    
         AGO   .SRCMKW                                                          
.SRCD1   ANOP                                                                   
         AIF   ('&CD' NE 'DEFER').SRCD2                                         
         AIF   (NOT &ISTGEN).SRCMKW                                             
         OI    RPL6FLG1-ISTRPL6X(14),RPL6CDDE                                   
*                                            SET CD TO DEFER                    
         AGO   .SRCMKW                                                          
.SRCD2   ANOP                                                                   
         IST6MCER 5,CD,&CD                                                      
.*                                                                              
.SRCMKW  ANOP                  ** COMMON KEYWORDS **                            
         IST6MCUP CONMODE=&CONMODE,OPTCD=&OPTCD,ECB=&ECB,EXIT=&EXIT,   X        
               BRANCH=&BRANCH,CONXMOD=&CONXMOD,                        X        
               CKCMODE=YES,        CHECK CONMODE & CONXMOD PARAMETERS  X        
               CKSYN=YES,          ASY/SYN IS VALID FOR OPTCD PARAMETERX        
               CKBUFF=BBUF         BUFFLST/XBUFFLST IS VALID                    
.*                                 OPTCD PARAMETER                              
.SRVECT  ANOP                          ENSURE BOTH PARMS CODED OR NOT           
.*                                     CODED                                    
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).SRVOUT    @D1C         
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).SRERVI     @D1C         
         IST6MCUA KWVAL=&VTRINA,LOC=RPL6VAIA-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_ADDRESS                      @D1C         
.*                                     STORE VECTOR AREA INPUT ADDRESS          
.*                                                                              
         IST6MCUA KWVAL=&VTRINL,LOC=RPL6VAIL-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2                 @D1C         
.*                                     STORE VECTOR AREA INPUT LENGTH           
.*                                                                              
         AGO   .SRVOUT                                             @D1C         
.SRERVI  ANOP                                                                   
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C         
.SRVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED       @D1A         
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).SRREQ   @D1A         
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).SRERVO   @D1A         
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X        
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                @D1A         
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X        
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                @D1A         
         AGO   .SRREQ                                              @D1A         
.SRERVO  ANOP                                                      @D1A         
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A         
.SRREQ   ANOP                                                                   
         AIF   (NOT &ISTGEN).SREXIT                                             
         MVI   RPL6REQ-ISTRPL6X(14),RPL6SNRC                                    
*                               PUT SEND/RCV REQUEST CODE IN APPC AREA          
.SREXIT  MEND                                                                   
