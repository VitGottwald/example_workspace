         MACRO                                                          00050000
.********************************************************************** 00100000
.*                                                                    * 00150000
.* $MAC(ARCHBACK) COMP(SERVICE) PROD(DFHSM):                          * 00200000
.* MACRO NAME = ARCHBACK                                              * 00250000
.* FUNC = SERVICE                                                     * 00300000
.* SUBFUNC = MWE                                                      * 00350000
.*                                                                    * 00400000
.* DESCRIPTIVE NAME = REQUEST DFHSM TO BACKUP A DATA SET.             * 00450000
.*                                                                    * 00500000
.********************************************************************** 00537500
.*PROPRIETARY V3 STATEMENT                                            * 00575000
.*LICENSED MATERIALS - PROPERTY OF IBM                                * 00592700
.*"RESTRICTED MATERIALS OF IBM"                                       * 00610400
.*5650-ZOS                                                            * 00015000
.*COPYRIGHT IBM CORP. 1988, 2018                                      * 00016000
.*END PROPRIETARY V3 STATEMENT                                        * 00734300
.********************************************************************** 00762500
.*                                                                    * 00800000
.* STATUS = VERSION 3, RELEASE 10                                     * 00850000
.*                                                                    * 00900000
.* FUNCTION = PERFORM DFHSM SVC 109 TO CREATE A BACKUP VERSION        * 00950000
.*            OF THE DATA SET SPECIFIED.                              * 01000000
.*                                                                    * 01050000
.* NOTES = SEE BELOW                                                  * 01100000
.*                                                                    * 01150000
.*   DEPENDENCIES =                                                   * 01200000
.*            IF ASYNCHRONOUS PROCESSING IS REQUESTED, APPLICATION    * 01250000
.*            IS RESPONSIBLE FOR FREEING THE MWE STORAGE IN CSA.      * 01300000
.*            USE THE ARCFMWE MACRO TO FREE THE MWE AFTER THE ECB     * 01350000
.*            IN THE MWE HAS BEEN POSTED.                         @01A* 01400000
.*                                                                    * 01450000
.*                                                                    * 01500000
.*   RESTRICTIONS = NONE                                              * 01550000
.*                                                                    * 01600000
.*   REGISTER CONVENTIONS =                                           * 01650000
.*                                                                    * 01700000
.*            REGISTER 13 MUST POINT TO A STANDARD SAVE AREA          * 01750000
.*            WHEN MACRO IS ISSUED.                                   * 01800000
.*                                                                    * 01850000
.*            REGISTERS 0, 1, 14 AND 15 WILL BE DESTROYED.            * 01900000
.*                                                                    * 01950000
.*   PATCH LABEL = NONE                                               * 02000000
.*                                                                    * 02050000
.* MODULE TYPE = MACRO                                                * 02100000
.*                                                                    * 02150000
.*   PROCESSOR = BAL                                                  * 02200000
.*                                                                    * 02250000
.* INPUT =                                                            * 02300000
.*          DSN      (OPTIONAL) SPECIFY THE ADDRESS OF A 44-BYTE AREA * 02350000
.*                   CONTAINING THE NAME OF THE DATA SET TO BE ACTED  * 02400000
.*                   ON, LEFT JUSTIFIED AND PADDED WITH BLANKS.       * 02450000
.*                   DSN IS MUTUALLY EXCLUSIVE WITH FILE PARAM.   @UYC*         
.*                                                                    * 02500000
.*          WAIT     (OPTIONAL) SPECIFY YES!NO WHETHER APPLICATION    * 02550000
.*                   DESIRES TO WAIT UNTIL DFHSM HAS PROCESSED THIS   * 02600000
.*                   REQUEST.  DEFAULT FOR THIS PARAMETER IS NO.      * 02650000
.*                                                                    * 02700000
.*          ASYNC    (OPTIONAL) SPECIFY YES!NO WHETHER ASYNCHRONOUS   * 02750000
.*                   RECALL PROCESSING IS REQUESTED.  ASYNC=YES       * 02800000
.*                   CAUSES THE MWE TO BE BUILT IN CSA AND ITS ECB    * 02850000
.*                   ADDRESS WILL BE RETURNED AT OFFSET X'48' INTO    * 02883300
.*                   THE WORK AREA THAT REGISTER 1 POINTS TO.     @02C* 02916600
.*                   DEFAULT FOR THIS PARAMETER IS NO.            @01A* 02950000
.*                                                                    * 03000000
.*              NOTE: ASYNC=YES REQUIRES WAIT=YES ALSO BE SPECIFIED.  * 03050000
.*                                                                    * 03100000
.*                NOTE:    VOLUME AND UNIT MUST BOTH BE SPECIFIED     * 03150000
.*                         IF THE DATA SET IS UNCATALOGED.            * 03200000
.*                                                                    * 03250000
.*          VOLUME   SPECIFY THE ADDRESS OF A 6 CHARACTER VOLUME      * 03300000
.*                   SERIAL OF THE VOLUME CONTAINING THE DATA SET,    * 03350000
.*                   LEFT JUSTIFIED AND PADDED WITH BLANKS.           * 03400000
.*                                                                    * 03450000
.*          UNIT     SPECIFY THE ADDRESS OF A 4 CHARACTER UCB DEVICE  * 03500000
.*                   TYPE OF THE DASD VOLUME CONTAINING THE DATA SET. * 03550000
.*                                                                    * 03600000
.*          DSNENQ   (OPTIONAL) SPECIFY YES!NO WHETHER AN ENQUEUE IS  * 03650000
.*                   REQUESTED FOR THE DATA SET DURING PROCESSING.    * 03700000
.*                   DEFAULT FOR THIS PARAMETER IS YES.               * 03750000
.*                                                                    * 03800000
.*          TEST     (OPTIONAL) SPECIFY YES!NO WHETHER MACRO IN TEST  * 03850000
.*                   MODE.  TEST=YES DENOTES THAT THE MWE SHOULD      * 03900000
.*                   BUILD BUT THAT NO DFHSM SVC IS ISSUED.  DEFAULT  * 03950000
.*                   FOR THIS PARAMETER IS NO.                        * 04000000
.*                                                                    * 04050000
.*          SNAPDCB  (OPTIONAL) SPECIFY THE ADDRESS OF AN OPEN DCB    * 04100000
.*                   USED FOR SNAP PROCESSING OF THE MWE AREA UPON    * 04150000
.*                   RETURN FROM DFHSM SVC PROCESSING.                * 04200000
.*                                                                    * 04250000
.*          WKAREA   (REQUIRED) SPECIFY THE ADDRESS OF A 200 BYTE     * 04300000
.*                   WORK AREA TO CONTAIN INFORMATION SPECIFIED IN    * 04350000
.*                   THIS MACRO.                                      * 04400000
.*                                                                    * 04450000
.*          TARGET   (OPTIONAL) SPECIFY THE TARGET DEVICE FOR     @DKA* 04454500
.*                    BACKUP OUTPUT (TAPE OR DASD).               @DKA* 04459000
.*                                                                @DKA* 04463500
.*          CC       (OPTIONAL) SPECIFY THE CONCURRENT COPY AND   @DKA* 04468000
.*                   NOTIFICATIONS OPTIONS.                       @DKA* 04472500
.*                                                                @DKA* 04477000
.*              NOTE: SPECIFICATION OF PHYSICALEND OR LOGICALEND  @DKA* 04481500
.*                    REQUIRES WAIT=YES ALSO BE SPECIFIED.        @DKA* 04486000
.*                                                                    * 04490500
.*          NEWNAME  (OPTIONAL) SPECIFY THE DATA SET NAME TO      @NRA* 04490700
.*                   ASSIGN TO THE BACKUP VERSION.                @NRA* 04490900
.*                                                                @NRA* 04491100
.*          DATE     (OPTIONAL) SPECIFY THE DATE TO ASSIGN TO     @NRA* 04491300
.*                   THE BACKUP VERSION.  (MUST BE SPECIFIED      @NRA* 04491500
.*                   WITH THE NEWNAME PARAMETER.)                 @NRA* 04491700
.*                                                                @NRA* 04491900
.*          TIME     (OPTIONAL) SPECIFY THE TIME TO ASSIGN TO     @NRA* 04492100
.*                   THE BACKUP VERSION.  (MUST BE SPECIFIED      @NRA* 04492300
.*                   WITH THE NEWNAME PARAMETER.)                 @NRA* 04492500
.*                                                                @NRA* 04492700
.*          SPHERE   (OPTIONAL) SPECIFY WHETHER TO BACK UP THE    @NRA* 04492900
.*                   ENTIRE SPHERE OF THE SPECIFIED VSAM DATA     @NRA* 04493100
.*                   SET. (MUST BE SPECIFIED WITH THE NEWNAME     @NRA* 04493300
.*                   PARAMETER.)                                  @NRA* 04493500
.*                                                                @NRA* 04493700
.*          GVCN     (OPTIONAL) SPECIFY WHETHER TO GENERATE       @NRA* 04493900
.*                   DEFAULT NEW NAMES FOR THE DATA AND           @NRA* 04494100
.*                   INDEX COMPONENTS OF THE SPECIFIED VSAM       @NRA* 04494300
.*                   DATA SET.  A SETSYS DEFAULT (SETSYS          @NRA* 04494400
.*                   DSBACKUP(GVCN(YES))) EXISTS FOR THIS         @NRA* 04494500
.*                   SETTING. (MUST BE SPECIFIED WITH THE         @NRA* 04494600
.*                   NEWNAME PARAMETER.)                          @NRA* 04494700
.*                                                                    * 04498600
.*          RETAINDAYS (OPTIONAL) SPECIFY THE RETAINDAYS TO       @RSA* 04498900
.*                   ASSIGN TO THE BACKUP VERSION.                @RSA* 04499200
.*                                                                    * 04499500
.*          FILE     (OPTIONAL) SPECIFY A UNIX FILENAME TO BE     @UYA*         
.*                   BACKED UP.                                   @UYA*         
.*                                                                    *         
.* OUTPUT =   REGISTER 15 WILL CONTAIN MWE RETURN CODE                * 04500000
.*            REGISTER 0  WILL CONTAIN MWE REASON CODE                * 04550000
.*            REGISTER 1  WILL CONTAIN THE ADDRESS OF THE ECB     @03A* 04600000
.*                        THIS VALUE WILL BE NEEDED BY ARCFMWE    @03A* 04650000
.*                                                                    * 04700000
.*                                                                    * 04750000
.*            INVALID REQUESTS NOT SENT TO DFHSM WILL HAVE            * 04800000
.*            THE FOLLOWING RETURN CODES:                             * 04850000
.*                                                                    * 04900000
.*            806 - LINK FAILURE - UNABLE TO LINK TO ARCGIVER         * 04950000
.*                                                                    * 05000000
.* CHANGE ACTIVITY =                                                  * 05050000
.* $ND=HSMND,240,080388,THL2: APAR OY15834 IN-LINE BACKUP SUPPORT @NDA* 05100000
.* $K0=HSMKD,250,090888,THP5: PROGRAMMABLE DFHSM REQUEST          @KDA* 05150000
.*                            PROCESSOR                           @KDA* 05200000
.* $01=DFHSM,250,091289,THP5: PTM KLA0715 ADD ASYNC PARAMETER     @01A* 05250000
.* $02=OW03533,120,032394,THH8: CORRECT THE DEFINITION OF REG1    @02A* 05275000
.* $03=HSMZH,130,062995,THP5: PTM K132870 SET REGISTER 1 TO ECB   @03A* 05287500
.* $DK=HSMDK,310,101398,THB5: DATA SET BACKUP ENHANCEMENTS        @DKA* 05293700
.* $DK-HSMDK,310,072799,THLC: PTM K160247  SET REQUST BITS     P1 @DKA* 05295800
.* $DK-HSMDK,310,080299,THB5: PTM K160255  ADD ERROR CHECKING  P2 @DKA* 05297900
.* $ER=HSMER,Z1A,011807,THI9-7: EAV AND VCC SUPPORT               @ERA* 05298900
.* $NR=HSMNR,Z1A,061307,THGS: BACKDS NEWNAME SUPPORT              @NRA* 05299400
.* $RS=HSMRS,B10,060108,THHF: BACKDS RETAINDAYS SUPPORT           @RSA* 05324700
.* $UY=HSMUY,230,022518,THI9-9: UNIX File backup support          @UYA*         
.********************************************************************** 05350000
&NAME    ARCHBACK &DSN=,&UNIT=,&VOLUME=,&WAIT=NO,&TEST=NO,&ASYNC=NO,   X05400000
               &WKAREA=,&DSNENQ=YES,&SNAPDCB=,&TARGET=,&CC=,           X05433300
               &NEWNAME=,&DATE=,&TIME=,&SPHERE=,&GVCN=,                X05455500
               &RETAINDAYS=,&FILE=                                 @UYC         
         LCLA  &A                                                       05500000
         LCLB  &ERROR                                                   05550000
         LCLC  &H                                                       05600000
         LCLC  &L1                                                      05650000
         LCLC  &L2                                                      05700000
&L1      SETC  'ARC'.'&SYSNDX'.'A'                                      05750000
&L2      SETC  'ARC'.'&SYSNDX'.'B'                                      05800000
.*                                                                      05850000
.* CHECK THE PARAMETERS SPECIFIED WITH THE HBACKDS FUNCTION             05900000
.*                                                                      05950000
         AIF   ('&DSN' NE '').BA1                                       06000000
         AIF   ('&FILE' NE '').UA01                                @UYA         
         MNOTE 8,'DSN OR FILE PARAMETER SHOULD BE SPECIFIED'       @UYC         
&ERROR   SETB  1                                                        06100000
.BA1     ANOP                                                           06150000
         AIF   ('&FILE' EQ '').UA02                                @UYA         
         MNOTE 8,'DSN IS MUTUALLY EXCLUSIVE WITH FILE PARAMETER'   @UYA         
&ERROR   SETB  1                                                   @UYA         
.UA02    ANOP                                                      @UYA         
         AIF   ('&UNIT' EQ '' AND '&VOLUME' EQ '').BA2                  06200000
         AIF   ('&UNIT' NE '' AND '&VOLUME' NE '').BA2                  06250000
         MNOTE 8,'UNIT AND VOLUME MUST EITHER BOTH BE SPECIFIED OR NEITX06300000
               HER SPECIFIED'                                           06350000
&ERROR   SETB  1                                                        06400000
.UA01    ANOP                                                      @UYA         
         AIF   ('&UNIT' EQ '').UA04                                @UYA         
         MNOTE 8,'UNIT IS MUTUALLY EXCLUSIVE WITH FILE PARAMETER'  @UYA         
&ERROR   SETB  1                                                   @UYA         
.UA04    AIF   ('&VOLUME' EQ '').UA05                              @UYA         
         MNOTE 8,'VOLUME IS MUTUALLY EXCLUSIVE WITH FILE PARAMETER'             
&ERROR   SETB  1                                                   @UYA         
.UA05    AIF   ('&SPHERE' EQ '').UA06                              @UYA         
         MNOTE 8,'SPHERE IS MUTUALLY EXCLUSIVE WITH FILE PARAMETER'             
&ERROR   SETB  1                                                   @UYA         
.UA06    AIF   ('&GVCN' EQ '').BA2                                 @UYA         
         MNOTE 8,'GVCN IS MUTUALLY EXCLUSIVE WITH FILE PARAMETER'               
&ERROR   SETB  1                                                   @UYA         
.BA2     ANOP                                                           06450000
         AIF   ('&WKAREA' NE '').BA3                                    06500000
         MNOTE 8,'REQUIRED PARAMETER, WKAREA, IS NOT SPECIFIED'         06550000
&ERROR   SETB  1                                                        06600000
.BA3     ANOP                                                           06650000
         AIF   ('&ASYNC' NE 'YES').BA4                                  06700000
         AIF   ('&WAIT' EQ 'YES').BA4                                   06750000
         MNOTE 8,'ASYNC=YES REQUIRES THAT WAIT=YES ALSO BE SPECIFIED'   06800000
&ERROR   SETB  1                                                        06850000
.BA4     ANOP                                                   @DKC    06855800
         AIF   ('&CC(2)' EQ '').BA41                            @DKA    06861600
         AIF   ('&WAIT' EQ 'YES').BA41                          @DKA    06867400
         MNOTE 8,'CC PHYSICALEND/LOGICALEND REQUIRES THAT WAIT=YES ALSOX06873200
                BE SPECIFIED'                                   @DKA    06879000
&ERROR   SETB  1                                                        06884800
.BA41    ANOP                                                P2 @DKA    06885000
         AIF ('&TARGET' EQ '').BA4B                          P2 @DKA    06885200
         AIF ('&TARGET' EQ 'TAPE').BA4B                      P2 @DKA    06885400
         AIF ('&TARGET' EQ 'DASD').BA4B                      P2 @DKA    06885600
         MNOTE 8,'INVALID PARAMETER, TARGET, IS NOT TAPE OR DASD'       06885800
&ERROR   SETB  1                                                        06886000
.BA4B    ANOP                                                P2 @DKA    06886200
         AIF ('&CC(1)' EQ '').BA4C                           P2 @DKA    06886400
         AIF ('&CC(1)' EQ 'STANDARD').BA4C                   P2 @DKA    06886600
         AIF ('&CC(1)' EQ 'PREFERRED').BA4C                  P2 @DKA    06886800
         AIF ('&CC(1)' EQ 'ANYPREFERRED').BA4C                  @ERA    06886900
         AIF ('&CC(1)' EQ 'PREFER').BA4C                     P2 @DKA    06887000
         AIF ('&CC(1)' EQ 'ANYREQUIRED').BA4C                   @ERA    06887100
         AIF ('&CC(1)' EQ 'REQUIRED').BA4C                   P2 @DKA    06887200
         AIF ('&CC(1)' EQ 'VIRTUALPREFERRED').BA4C              @ERA    06887300
         AIF ('&CC(1)' EQ 'VPREF').BA4C                         @ERA    06887400
         AIF ('&CC(1)' EQ 'VIRTUALREQUIRED').BA4C               @ERA    06887500
         AIF ('&CC(1)' EQ 'VREQ').BA4C                          @ERA    06887600
         AIF ('&CC(1)' EQ 'CACHEPREFERRED').BA4C                @ERA    06887700
         AIF ('&CC(1)' EQ 'CPREF').BA4C                         @ERA    06887800
         AIF ('&CC(1)' EQ 'CACHEREQUIRED').BA4C                 @ERA    06887900
         AIF ('&CC(1)' EQ 'CREQ').BA4C                          @ERA    06888000
         MNOTE 8,'INVALID PARAMETER, CC(1)'                  P2 @DKA    06888100
&ERROR   SETB  1                                             P2 @DKA    06888200
.BA4C    ANOP                                                           06888300
         AIF ('&CC(2)' EQ '').BA5                            P2 @DKA    06888400
         AIF ('&CC(2)' EQ 'PHYSICALEND').BA5                 P2 @DKA    06888500
         AIF ('&CC(2)' EQ 'PE').BA5                          P2 @DKA    06888600
         AIF ('&CC(2)' EQ 'LOGICALEND').BA5                  P2 @DKA    06888700
         AIF ('&CC(2)' EQ 'LE').BA5                          P2 @DKA    06888800
         MNOTE 8,'INVALID PARAMETER, CC(2)'                  P1 @DKA    06889000
.BA5     ANOP                                                   @DKA    06890600
         AIF ('&CC(2)' EQ 'LOGICALEND').BA5A                    @DKA    06896400
         AIF ('&CC(2)' EQ 'LE').BA5A                            @DKA    06902200
         AGO .BA6                                               @DKA    06908000
.BA5A    AIF ('&CC(1)' EQ 'REQUIRED').BA6                       @DKA    06913800
         AIF ('&CC(1)' EQ 'ANYREQUIRED').BA6                    @ERA    06916700
         AIF ('&CC(1)' EQ 'PREFERRED').BA6                      @DKA    06919600
         AIF ('&CC(1)' EQ 'ANYPREFERRED').BA6                   @ERA    06921000
         AIF ('&CC(1)' EQ 'PREFER').BA6                         @DKA    06922500
         AIF ('&CC(1)' EQ 'VIRTUALPREFERRED').BA6               @ERA    06923700
         AIF ('&CC(1)' EQ 'VPREF').BA6                          @ERA    06924900
         AIF ('&CC(1)' EQ 'VIRTUALREQUIRED').BA6                @ERA    06926100
         AIF ('&CC(1)' EQ 'VREQ').BA6                           @ERA    06927300
         AIF ('&CC(1)' EQ 'CACHEPREFERRED').BA6                 @ERA    06928500
         AIF ('&CC(1)' EQ 'CPREF').BA6                          @ERA    06929700
         AIF ('&CC(1)' EQ 'CACHEREQUIRED').BA6                  @ERA    06930900
         AIF ('&CC(1)' EQ 'CREQ').BA6                           @ERA    06932100
         MNOTE 8,'CC REQUIRED/PREFERRED/VIRTUALREQUIRED/VIRTUALPREFERREX06933300
               D/CACHEREQUIRED/CACHEPREFERRED MUST BE SPECIFIED WITH CCX06934500
                (,LOGICALEND/LE)'                               @ERC    06935700
&ERROR   SETB  1                                                @DKA    06937000
.BA6     AIF ('&DATE' EQ '').BA6A                               @NRA    06937500
         AIF ('&NEWNAME' NE '').BA6A                            @NRA    06938000
         MNOTE 8,'NEWNAME MUST BE SPECIFIED WITH DATE'          @NRA    06938500
&ERROR   SETB  1                                                @NRA    06939000
.BA6A    AIF ('&TIME' EQ '').BA6B                               @NRA    06939500
         AIF ('&DATE' NE '').BA6B                               @NRA    06940000
         MNOTE 8,'DATE MUST BE SPECIFIED WITH TIME'             @NRA    06940500
&ERROR   SETB  1                                                @NRA    06941000
.BA6B    AIF ('&SPHERE' EQ '').BA6C                             @NRA    06941500
         AIF ('&NEWNAME' NE '').BA6C                            @NRA    06942000
         MNOTE 8,'NEWNAME MUST BE SPECIFIED WITH SPHERE'        @NRA    06942500
&ERROR   SETB  1                                                @NRA    06943000
.BA6C    AIF ('&GVCN' EQ '').BA6D                               @NRA    06943500
         AIF ('&NEWNAME' NE '').BA6D                            @NRA    06944000
         MNOTE 8,'NEWNAME MUST BE SPECIFIED WITH GVCN'          @NRA    06944500
&ERROR   SETB  1                                                @NRA    06945000
.BA6D    AIF ('&UNIT' EQ '' AND '&VOLUME' EQ '').BA6E           @NRA    06945500
         AIF ('&NEWNAME' EQ '').BA6E                            @NRA    06946000
         MNOTE 8,'NEWNAME CANNOT BE SPECIFIED WITH UNIT OR VOLUME'      06946500
&ERROR   SETB  1                                                @NRA    06946700
.BA6E    AIF ('&SPHERE' EQ '').BA6F                             @NRA    06946900
         AIF ('&SPHERE' EQ 'YES').BA6F                          @NRA    06947100
         AIF ('&SPHERE' EQ 'NO').BA6F                           @NRA    06947300
         MNOTE 8,'YES OR NO MUST BE SPECIFIED WITH SPHERE'      @NRA    06947500
&ERROR   SETB  1                                                @NRA    06947700
.BA6F    AIF ('&GVCN' EQ '').BA7                                @NRA    06947900
         AIF ('&GVCN' EQ 'YES').BA7                             @NRA    06948100
         AIF ('&GVCN' EQ 'NO').BA7                              @NRA    06948300
         MNOTE 8,'YES OR NO MUST BE SPECIFIED WITH GVCN'        @NRA    06948500
&ERROR   SETB  1                                                @NRA    06949500
.BA7     ANOP                                                   @NRC    06949700
         AIF   (NOT &ERROR).BUILD                                       06950000
         MEXIT                                                          07000000
.*                                                                      07050000
.* AT THIS POINT ALL THE PARAMETERS HAVE BEEN PROPERLY SPECIFIED.       07100000
.* NOW BUILD THE CONTROL BLOCK AND CALL THE PROCESSING ROUTINE.         07150000
.*                                                                      07200000
.BUILD   ANOP                                                           07250000
         AIF   ('&WKAREA'(1,1) EQ '(').WREG                             07300000
&NAME    LA    15,&WKAREA                                               07350000
         AGO   .CLRWKA                                                  07400000
.WREG    ANOP                                                           07450000
&A       SETA  K'&WKAREA-2                                              07500000
&H       SETC  '&WKAREA'(2,&A)                                          07550000
&NAME    LR    15,&H                                                    07600000
.CLRWKA  ANOP                                                           07650000
         XC    0(200,15),0(15)     CLEAR THE WORK AREA                  07700000
         B     *+12                BRANCH AROUND IDENTIFIER             07750000
         DC    CL8'DFHSMREQ'                                            07800000
         MVC   0(8,15),*-8         SET THE IDENTIFIER                   07850000
         B     *+12                BRANCH AROUND FUNCTION NAME          07900000
         DC    CL8'HBACKDS '       FUNCTION NAME                        07950000
         MVC   8(8,15),*-8                                              08000000
.XSYNC   AIF   ('&ASYNC' NE 'YES').XWAIT                                08050000
         OI    16(15),X'80'        ASYNC WAS SPECIFIED                  08100000
.XWAIT   AIF   ('&WAIT' NE 'YES').XTEST                                 08150000
         OI    16(15),X'40'        WAIT WAS SPECIFIED                   08200000
.XTEST   AIF   ('&TEST' NE 'YES').XENQ                                  08250000
         OI    16(15),X'02'        TEST WAS SPECIFIED                   08300000
.XENQ    AIF   ('&DSNENQ' NE 'NO').XDSN                                 08350000
         OI    16(15),X'01'        NO DATA SET ENQUEUE                  08400000
.XDSN    AIF   ('&TARGET' EQ '').XNTAR                           @DKC   08404500
         AIF   ('&TARGET' NE 'TAPE').XDASD                       @DKA   08409000
         OI    18(15),X'40'        TARGET WAS SPECIFIED       P1 @DKC   08413500
         OI    18(15),X'10'        TARGET = TAPE SPECIFIED       @DKA   08418000
.XDASD   AIF   ('&TARGET' NE 'DASD').XNTAR                       @DKA   08422500
         OI    18(15),X'40'        TARGET WAS SPECIFIED       P1 @DKC   08427000
         OI    18(15),X'20'        TARGET = DASD SPECIFIED       @DKA   08431500
.XNTAR   AIF   ('&CC(1)' EQ '').XNCC1                            @DKA   08436000
         AIF   ('&CC(1)' EQ 'STANDARD').XSTD                     @ERM   08437500
         AIF   ('&CC(1)' EQ 'ANYPREFERRED').XPRE                 @ERA   08439000
         AIF   ('&CC(1)' EQ 'PREFERRED').XPRE                    @DKA   08440500
         AIF   ('&CC(1)' EQ 'PREFER').XPRE                    P2 @DKA   08442700
         AIF   ('&CC(1)' EQ 'REQUIRED').XREQ                     @DKA   08449500
         AIF   ('&CC(1)' EQ 'ANYREQUIRED').XREQ                  @ERA   08449900
         AIF   ('&CC(1)' EQ 'VIRTUALPREFERRED').XVPRF            @ERA   08450300
         AIF   ('&CC(1)' EQ 'VPREF').XVPRF                       @ERA   08450700
         AIF   ('&CC(1)' EQ 'VIRTUALREQUIRED').XVREQ             @ERA   08451100
         AIF   ('&CC(1)' EQ 'VREQ').XVREQ                        @ERA   08451500
         AIF   ('&CC(1)' EQ 'CACHEPREFERRED').XCPRF              @ERA   08451900
         AIF   ('&CC(1)' EQ 'CPREF').XCPRF                       @ERA   08452300
         AIF   ('&CC(1)' EQ 'CACHEREQUIRED').XCREQ               @ERA   08452700
         AIF   ('&CC(1)' EQ 'CREQ').XCREQ                        @ERA   08453100
         AGO   .XNCC1                                            @DKA   08454000
.XPRE    ANOP                                                    @DKA   08458500
         OI    17(15),X'08'        CC = PREFERRED SPECIFIED   P1 @DKC   08463000
         AGO   .XNCC1                                            @DKA   08467500
.XSTD    ANOP                                                    @DKA   08472000
         OI    17(15),X'02'        CC = STANDARD SPECIFIED    P1 @DKC   08476500
         AGO   .XNCC1                                            @DKA   08481000
.XREQ    ANOP                                                    @DKA   08485500
         OI    17(15),X'04'        CC = REQUIRED SPECIFIED    P1 @DKC   08490000
         AGO   .XNCC1                                            @ERA   08490200
.XVPRF   ANOP                                                    @ERA   08490400
         OI    17(15),X'08'        CC = VIRTUALPREFFERED SPEC    @ERA   08490600
         OI    18(15),X'08'        CC = VIRTUALPREFFERED SPEC    @ERA   08490800
         AGO   .XNCC1                                            @ERA   08491000
.XVREQ   ANOP                                                    @ERA   08491200
         OI    17(15),X'04'        CC = VIRTUALREQUIRED SPEC     @ERA   08491400
         OI    18(15),X'08'        CC = VIRTUALREQUIRED SPEC     @ERA   08491600
         AGO   .XNCC1                                            @ERA   08491800
.XCPRF   ANOP                                                    @ERA   08492000
         OI    17(15),X'08'        CC = CACHEPREFFERED SPEC      @ERA   08492200
         OI    18(15),X'04'        CC = CACHEPREFFERED SPEC      @ERA   08492400
         AGO   .XNCC1                                            @ERA   08492600
.XCREQ   ANOP                                                    @ERA   08492800
         OI    17(15),X'04'        CC = CACHEREQUIRED SPEC       @ERA   08493000
         OI    18(15),X'04'        CC = CACHEREQUIRED SPEC       @ERA   08493200
.XNCC1   AIF   ('&CC(2)' EQ '').XNCC2                            @DKA   08494500
         AIF   ('&CC(2)' EQ 'PHYSICALEND').XPN                   @DKA   08499000
         AIF   ('&CC(2)' EQ 'PE').XPN                            @DKA   08503500
         AIF   ('&CC(2)' EQ 'LOGICALEND').XLN                    @DKA   08508000
         AIF   ('&CC(2)' EQ 'LE').XLN                            @DKA   08512500
         AGO   .XNCC2                                            @DKA   08517000
.XPN     ANOP                                                    @DKA   08521500
         OI    18(15),X'80'        CC = PHYSICALEND SPECIFIED P1 @DKC   08526000
         AGO   .XNCC2                                            @DKA   08530500
.XLN     ANOP                                                    @DKA   08535000
         OI    17(15),X'01'        CC = LOGICALEND SPECIFIED  P1 @DKC   08539500
.XNCC2   AIF   ('&FILE' EQ '').UA10                              @UYA           
         AIF   ('&FILE'(1,1) EQ '(').DREG1                       @UYA           
         LA    1,&FILE              GET ADDR OF FILE AREA        @UYA           
         ST    1,104(15)            STORE IN PARM LIST           @UYA           
         AGO   .UA11                                             @UYA           
.DREG1   ANOP                                                    @UYA           
&A       SETA  K'&FILE-2                                         @UYA           
&H       SETC  '&FILE'(2,&A)                                     @UYA           
         ST    &H,104(15)           STORE ADDR OF FILE AREA      @UYA           
.UA11    AIF   ('&NEWNAME' EQ '').XSNAP                          @UYA           
         AIF   ('&NEWNAME'(1,1) EQ '(').DREG2                    @UYA           
         LA    1,&NEWNAME           GET ADDR OF NEWNAME AREA     @UYA           
         ST    1,108(15)            STORE IN PARM LIST           @UYA           
         AGO   .XSNAP                                            @UYA           
.DREG2   ANOP                                                    @UYA           
&A       SETA  K'&NEWNAME-2                                      @UYA           
&H       SETC  '&NEWNAME'(2,&A)                                  @UYA           
         ST    &H,108(15)           STORE ADDR OF NEWNAME AREA   @UYA           
         AGO   .XSNAP                                            @UYA           
.UA10    AIF   ('&DSN'(1,1) EQ '(').DREG                         @UYC           
         LA    1,&DSN              GET ADDR OF DSN                      08550000
         ST    1,20(15)            STORE IN PARM LIST                   08600000
         AGO   .XSNAP                                                   08650000
.DREG    ANOP                                                           08700000
&A       SETA  K'&DSN-2                                                 08750000
&H       SETC  '&DSN'(2,&A)                                             08800000
         ST    &H,20(15)           STORE ADDR OF DSN                    08850000
.XSNAP   ANOP                                                           08900000
         AIF   ('&SNAPDCB' EQ '').XUNIT                                 08950000
         AIF   ('&SNAPDCB'(1,1) EQ '(').SREG                            09000000
         LA    1,&SNAPDCB          GET ADDRESS OF SNAP DCB              09050000
         ST    1,188(15)           STORE IN PARM LIST                   09100000
         AGO   .XUNIT                                                   09150000
.SREG    ANOP                                                           09200000
&A       SETA  K'&SNAPDCB-2                                             09250000
&H       SETC  '&SNAPDCB'(2,&A)                                         09300000
         ST    &H,188(15)          STORE DCB ADDRESS IN PARM LIST       09350000
.XUNIT   ANOP                                                           09400000
         AIF   ('&UNIT' EQ '').XVOL                                     09450000
         AIF   ('&UNIT'(1,1) EQ '(').UREG                               09500000
         LA    1,&UNIT             GET ADDR OF UNIT                     09550000
         ST    1,24(15)            STORE IN PARM LIST                   09600000
         AGO   .XVOL                                                    09650000
.UREG    ANOP                                                           09700000
&A       SETA  K'&UNIT-2                                                09750000
&H       SETC  '&UNIT'(2,&A)                                            09800000
         ST    &H,24(15)           STORE ADDR OF UNIT PARM              09850000
.XVOL    ANOP                                                           09900000
         AIF   ('&VOLUME' EQ '').XNN                              @NRC  09950000
         AIF   ('&VOLUME'(1,1) EQ '(').VREG                             10000000
         LA    1,&VOLUME           GET ADDR OF VOLUME                   10050000
         ST    1,28(15)            STORE IN PARM LIST                   10100000
         AGO   .XNN                                               @NRC  10150000
.VREG    ANOP                                                           10200000
&A       SETA  K'&VOLUME-2                                              10250000
&H       SETC  '&VOLUME'(2,&A)                                          10300000
         ST    &H,28(15)           STORE ADDR OF VOLUME PARM            10350000
.XNN     ANOP                                                     @NRA  10351100
         AIF   ('&NEWNAME' EQ '').XRD                             @RSC  10352200
         AIF   ('&NEWNAME'(1,1) EQ '(').NREG                      @NRA  10353300
         LA    1,&NEWNAME          GET ADDR OF NEWNAME            @NRA  10354400
         ST    1,48(15)            STORE ADDR OF NEWNAME          @NRA  10355500
         AGO   .XDATE                                             @NRA  10356600
.NREG    ANOP                                                     @NRA  10357700
&A       SETA  K'&NEWNAME-2                                       @NRA  10358800
&H       SETC  '&NEWNAME'(2,&A)                                   @NRA  10359900
         ST    &H,48(15)                                          @NRA  10361000
.XDATE   ANOP                                                     @NRA  10362100
         AIF   ('&DATE' EQ '').XTIME                              @NRA  10363200
         AIF   ('&DATE'(1,1) EQ '(').DTREG                        @NRA  10364300
         LA    1,&DATE             GET ADDR OF DATE               @NRA  10365400
         ST    1,44(15)            STORE ADDR OF DATE             @NRA  10366500
         AGO   .XTIME                                             @NRA  10367600
.DTREG   ANOP                                                     @NRA  10368700
&A       SETA  K'&DATE-2                                          @NRA  10369800
&H       SETC  '&DATE'(2,&A)                                      @NRA  10370900
         ST    &H,44(15)                                          @NRA  10372000
.XTIME   ANOP                                                     @NRA  10373100
         AIF   ('&TIME' EQ '').XSPH                               @NRA  10374200
         AIF   ('&TIME'(1,1) EQ '(').TREG                         @NRA  10375300
         LA    1,&TIME             GET ADDR OF TIME               @NRA  10376400
         ST    1,88(15)            STORE ADDR OF TIME             @NRA  10377500
         AGO   .XSPH                                              @NRA  10378600
.TREG    ANOP                                                     @NRA  10379700
&A       SETA  K'&TIME-2                                          @NRA  10380800
&H       SETC  '&TIME'(2,&A)                                      @NRA  10381900
         ST    &H,88(15)                                          @NRA  10383000
.XSPH    ANOP                                                     @NRA  10384100
         AIF   ('&SPHERE' NE 'NO').XGVCN                          @NRA  10385800
         OI    18(15),X'02'        SPHERE(NO) WAS SPECIFIED       @NRA  10387500
.XGVCN   ANOP                                                     @NRA  10389200
         AIF   ('&GVCN' EQ '').XRD                                @RSC  10390900
         OI    18(15),X'01'        GVCN WAS SPECIFIED             @NRA  10392600
.XGVCNY  ANOP                                                     @NRA  10394300
         AIF   ('&GVCN' NE 'YES').XRD                             @RSC  10396000
         OI    19(15),X'80'        GVCN(YES) WAS SPECIFIED        @NRA  10397700
.XRD     ANOP                                                     @RSA  10397900
         AIF   ('&RETAINDAYS' EQ '').GIVER                        @RSA  10398100
         AIF   ('&RETAINDAYS'(1,1) EQ '(').RREG                   @RSA  10398300
         LA    1,&RETAINDAYS        GET ADDR OF RETAINDAYS        @RSA  10398500
         ST    1,92(15)             STORE IN PARM LIST            @RSA  10398700
         AGO   .GIVER                                             @RSA  10398900
.RREG    ANOP                                                     @RSA  10399100
&A       SETA  K'&RETAINDAYS-2                                    @RSA  10399300
&H       SETC  '&RETAINDAYS'(2,&A)                                @RSA  10399500
         ST    &H,92(15)            STORE ADDR OF RETAINDAYS PARM @RSA  10399700
.GIVER   ANOP                                                           10400000
         LR    1,15                PARAMETER ADDRESS TO REG 1           10450000
         LINK  EP=ARCGIVER,ERRET=&L1                                    10500000
         L     15,192(1)           LOAD RETURN CODE                     10550000
         L     0,196(1)            LOAD REASON CODE                     10600000
         L     1,72(1)             LOAD ECB ADDRESS FOR ASYNC     @03A  10625000
         B     &L2                                                      10650000
&L1      LA    15,806              INDICATE LINK FAILED                 10700000
&L2      EQU   *                                                        10750000
         MEXIT                                                          10800000
         MEND                                                           10850000
