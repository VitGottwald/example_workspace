         MACRO                                                                  
         IST6MCTE &AREA=,&AREALEN=,&QUALIFY=,&CONVID=,&BRANCH=,        X        
               &ECB=,&EXIT=,&OPTCD=,&INVALP1=,&INVALP2=,&VTRINA=,      X        
               &VTRINL=,&VTROUTA=,&VTROUTL=                        @D1C         
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCTE                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: CONTROL = TEST STATUS PROCESSOR             */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1993, 1994.       */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */         
.* /*      CONTROL=TESTSTAT PARAMETER ON THE APPCCMD MACRO.          */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS:                                              */         
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCER                       */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: NONE                                     */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*                                                                */         
.* /*  $MAC(IST6MCTE),COMP(API),PROD(VTAM): CONTROL=TESTSTAT         */         
.* /*                                                                */         
.* /*  FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS         */         
.* /*  ---- -------- ------- ------ ------  ---------------------    */         
.* /*  $I0= FT@@@MST APPCTST 920212 510689: APPC TESTSTAT            */         
.* /*  $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services    */         
.* /*  $Y1= P114821  APPCRR  940519 576146: DCE Security Services    */         
.* /*  $L2= KA@DR006 APPCRR  940713 576146: Vector Format            */         
.* /*  $D1= ID@@@MST FPAPI   950310 143576: Fast Path API for APPC   */         
.* /*                                                                */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
         GBLB  &ISTGEN                                                          
         GBLB  &ISTTE                                                           
         AIF   (&ISTTE EQ 1).TEINVKW                                            
*                                                                               
*  IST6MCTE - ACF/VTAM                                                          
*                                                                               
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                              
*                                                                               
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"               
*                                                                               
*             5695-117 (C) COPYRIGHT IBM CORP. 1993.                            
*             ALL RIGHTS RESERVED.                                              
*                                                                               
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                         
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY                      
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                          
*                                                                               
*             SEE COPYRIGHT INSTRUCTIONS.                                       
*                                                                               
&ISTTE   SETB  1                                                                
.TEINVKW ANOP                                                                   
         IST6MCUC TESTSTAT,CONTROL,INVALKW=&INVALP1                             
         IST6MCUC TESTSTAT,CONTROL,INVALKW=&INVALP2                             
.*                                     CHECK INVALID APPCCMD                    
.TEAREA  ANOP                       ** AREA= PROCESSING **                      
         AIF   ('&AREA' EQ '').TEALEN                                           
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X        
               KW=AREA_ADDRESS                                                  
.*                                     PUT AREA ADDRESS IN RPL                  
.TEALEN  ANOP                       ** AREALEN= PROCESSING **                   
         AIF   ('&AREALEN' EQ '').TEQFY                                         
         IST6MCUA KWVAL=&AREALEN,LOC=RPLBUFL-IFGRPL,BASEREG=1,         X        
               KW=AREA_LENGTH                                                   
.*                                     PUT AREA LENGTH IN RPL                   
.TEQFY   ANOP                       ** QUALIFY= PROCESSING **                   
         AIF   ('&QUALIFY' EQ '').TEID                                          
.TEQFY1  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'ALL').TEQFY2                                     
         IST6MCUC ALL,QUALIFY,INVALKW=(CONVID=&CONVID)                          
         AIF   (NOT &ISTGEN).TECOMKW                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6QALL                                   
*                                      PUT QALL CODE IN APPC AREA               
         AGO   .TECOMKW                                                         
.TEQFY2  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'IALL').TEQFY3                                    
         IST6MCUC IALL,QUALIFY,INVALKW=(CONVID=&CONVID)                         
         AIF   (NOT &ISTGEN).TECOMKW                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6IALL                                   
*                                      PUT IALL CODE IN APPC AREA               
         AGO   .TECOMKW                                                         
.TEQFY3  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'SPEC').TEQFY4                                    
         AIF   (NOT &ISTGEN).TECOMKW                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6SPEC                                   
*                                      PUT SPEC CODE IN APPC AREA               
         AGO   .TEID                                                            
.TEQFY4  ANOP                                                                   
         AIF   ('&QUALIFY' NE 'ISPEC').TEQFYER                                  
         AIF   (NOT &ISTGEN).TECOMKW                                            
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6ISPC                                   
*                                      PUT ISPEC CODE IN APPC AREA              
         AGO   .TEID                                                            
.TEQFYER ANOP                                                                   
         IST6MCER 5,QUALIFY,&QUALIFY                                            
.TEID    ANOP                       ** CONVID= PROCESSING **                    
         AIF   ('&CONVID' EQ '').TECOMKW                                        
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X        
               KW=CONVID                                                        
.*                                     PUT CONVID IN APPC AREA                  
.TECOMKW ANOP                       ** COMMON KEYWORDS **                       
         IST6MCUP OPTCD=&OPTCD,ECB=&ECB,EXIT=&EXIT,BRANCH=&BRANCH,     X        
               CKCMODE=NO,             CHECK CONMODE AND CONXMOD       X        
               CKSYN=YES,              ASY/SYN IS VALID FOR OPTCD      X        
               CKBUFF=NO               BUFFLST/NBUFFLST IS INVALID              
.*                                     VALIDATE & SET COMMON KEYWORDS           
.TEVECT  ANOP                          ENSURE BOTH PARMS CODED OR NOT           
.*                                     CODED                  @L1A @Y1M         
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).TEVOUT    @D1C         
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).TEERVI     @D1C         
         IST6MCUA KWVAL=&VTRINA,LOC=RPL6VAIA-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_ADDRESS                      @D1C         
.*                                     STORE VECTOR AREA INPUT ADDRESS          
.*                                                                 @L1A         
         IST6MCUA KWVAL=&VTRINL,LOC=RPL6VAIL-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2                 @D1C         
.*                                     STORE VECTOR AREA INPUT LENGTH           
.*                                                                 @L1A         
         AGO   .TEVOUT                                             @D1C         
.TEERVI  ANOP                                                 @L1A @Y1M         
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C         
.TEVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED       @D1A         
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).TEREQ   @D1A         
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).TEERVO   @D1A         
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X        
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                @D1A         
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X        
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                @D1A         
         AGO   .TEREQ                                              @D1A         
.TEERVO  ANOP                                                      @D1A         
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A         
.TEREQ   ANOP                       ** MACRO REQUEST TYPE **                    
         AIF   (NOT &ISTGEN).TEEXIT                                             
         MVI   RPL6REQ-ISTRPL6X(14),RPL6TSTS                                    
*                                      PUT TESTSTAT REQUEST IN RPL6             
.TEEXIT  MEND                                                                   
