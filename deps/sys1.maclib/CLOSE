*%/*                                                               @L2A 00010000
         MACRO                                                          00020000
&CNAME   CLOSE &PAR1,&MF=I,&TYPE=,&MODE=                           @02C 00021500
.*                                                                      00023000
.*CLOSE MACRO                                                      @L1A 00026000
.*                                                                 @L1A 00029000
.*$MAC(CLOSE):                                                     @L1A 00032000
.*                                                                 @L1A 00035000
.*PROPRIETARY V3 STATEMENT                                              00035700
.*LICENSED MATERIALS - PROPERTY OF IBM                                  00036400
.*"RESTRICTED MATERIALS OF IBM"                                         00037100
.*5695-DF1                                                              00037800
.*COPYRIGHT 1987, 2011 IBM CORP.                                        00038700
.*END PROPRIETARY V3 STATEMENT                                          00039700
.*                                                                 @L1A 00041000
.* STATUS = z/OS 1.13 DFSMS                                        @L3C 00042000
.*                                                                 @L1A 00043000
.*CHANGE ACTIVITY - AS FOLLOWS:                                    @L1A 00044000
.*                                                                    * 00046000
.*$L1=ACB31BIT,HDP2230,,STLSS: O/C/E SUPPORT OF 31-BIT VSAM        @L1A 00048000
.*$01=OY05946,HDP2230,,NSDRJV: EXTRA INSTRUCTION WITH MODE=31      @01A 00050000
.*$02=OY07793,HDP2230,870720,STLSS: REMOVE SPLEVEL DEPENDENCE      @02A 00051000
.*$L2=XXXXXXX,JDZ1110,930930,SJPLMET: PL/X SUPPORT                 @L2A 00051500
.*$L3=64BIT,HDZ1D10,20101217,SVLWER: 64 BIT MESSAGE                @L3A 00051700
.*                                                                    * 00052000
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * ** 00054000
.*                                                                 @02D 00056000
         GBLC  &SYSAM64    AMODE64=YES set by SYSSTATE macro       @L3A 00057300
         GBLC  &SYSASCE    ASCENV=AR or ANY set by SYSSTATE macro  @L3A 00058600
         LCLA  &ACTR,&ASUM,&CTR,&CTR1,&LGTH,&NUMBER                     00060000
         LCLB  &MFESW,&MFISW,&MFI2SW,&NULLSW,&B(5),&MSW            @L1C 00080000
         LCLC  &PARA                                                    00100000
         SYSSTATE TEST     Test how macros are to expand           @L3A 00102500
         AIF   ('&SYSAM64' EQ 'YES').ERROR10    Go if AMODE64=YES  @L3A 00105000
         AIF  (('&SYSASCE' NE '') AND ('&SYSASCE' NE 'P')).ERROR11 @L3A 00107500
.*                                                                 @02D 00110000
&NUMBER  SETA  N'&PAR1                                                  00120000
.*                                                                 @02D 00130000
&CTR     SETA  1                                                        00140000
&CTR1    SETA  2                                                        00160000
         AIF   (N'&SYSLIST LE 1).TYPE   IF ONLY 1 (PARMLIST)     A45585 00185000
         IHBERMAC 238                   EXCESSIVE POS. PARMS     A45585 00190000
.TYPE    AIF   (T'&TYPE NE 'O' AND '&TYPE' NE 'T').ERROR8        A45585 00195000
         AIF   (T'&MODE EQ 'O').MFTEST                             @L1A 00199100
         AIF   ('&MODE' NE '31' AND '&MODE' NE '24').ERROR9        @L1A 00203200
         AIF   ('&MODE' EQ '24').MFTEST                            @L1A 00207300
&MSW     SETB  1                        SET MODE=31 SWITCH         @L1A 00211400
.MFTEST  AIF   ('&MF' EQ 'L').RTEL                                 @L1C 00215500
         AIF   ('&MF' EQ 'I').TESTI                                     00220000
         AIF   (N'&MF LE 1).ERROR3                                      00240000
         AIF   ('&MF(1)' NE 'E').ERROR3                                 00260000
.* Begin expansion of the execute form (MF=(E,xxxx)).              @L3A 00270000
&CNAME   IHBINNRA &MF(2)                                                00280000
.*--------------------------------------------------------------------  00293300
         AIF   ('&PAR1' EQ '').RGFLIP                              @L1C 00306600
.* Set &NUMBER to number of things coded, omitting null ones at    @L3A 00311000
.* the end.                                                        @L3A 00315400
.LOOPA   AIF   ('&PAR1(&NUMBER)' NE '').LOOPB                           00320000
&NUMBER  SETA  &NUMBER-1                                                00340000
         AIF   (&NUMBER EQ 0).RGFLIP                                    00360000
         AGO   .LOOPA                                                   00380000
.* Beginning of big loop.                                          @L3A 00390000
.LOOPB   AIF   ('&PAR1(&CTR)' NE '').TESTCTR                            00400000
&NULLSW  SETB  1                        Remember DCB or ACB is null     00415000
.TESTCTR AIF   (&CTR EQ &NUMBER).QUITNOW   Go if last DCB or ACB   @L3C 00430000
.* Branch if option after DCB or ACB is omitted.                   @L3A 00445000
         AIF   ('&PAR1(&CTR1)' EQ '').NULLTWO                           00460000
&MFESW   SETB  1                        Remember doing execute form     00480000
         AGO   .CLRSW                                                   00500000
.ERTRTRN AIF   (&CTR1 EQ &NUMBER).THRUNOW  Go if no more DCB or ACB@L3C 00540000
         AIF   (&NULLSW).NOFIRST        Go if DCB or ACB omitted   @L3C 00580000
         AIF   ('&PAR1(&CTR)'(1,1) EQ '(').REGISTR                      00620000
         LA    14,&PAR1(&CTR)           PICK UP DCB ADDRESS             00633300
         AIF   (NOT &MSW).MODE24A                                  @L1A 00646600
&ACTR    SETA  &ACTR+4                                             @L1A 00653200
.MODE24A ST    14,&ACTR.(,1)            STORE DCB OR ACB ADDRESS   @L3C 00659800
         AIF   (NOT &MSW).NOFIRST                                  @L1A 00666400
&ACTR    SETA  &ACTR-4                                             @L1A 00673000
.NOFIRST MVI   &ACTR.(1),&ASUM          MOVE IN OPTION BYTE             00686500
         AGO   .INCRMT                                                  00700000
.REGISTR ANOP                                                           00720000
&LGTH    SETA  K'&PAR1(&CTR)-2                                          00740000
&PARA    SETC  '&PAR1(&CTR)'(2,&LGTH)                                   00760000
         AIF   (NOT &MSW).MODE24B                                  @L1A 00768500
&ACTR    SETA  &ACTR+4                                             @L1A 00777000
.MODE24B ST    &PARA,&ACTR.(,1)         STORE DCB OR ACB ADDRESS   @L3C 00785500
         AIF   (NOT &MSW).MODE24C                                  @L1A 00794000
&ACTR    SETA  &ACTR-4                                             @L1A 00802500
.MODE24C MVI   &ACTR.(1),&ASUM          MOVE IN OPTION BYTE        @L1C 00811200
         AGO   .INCRMT                                                  00820000
.* Option was omitted.  Do not change the option byte in the list. @L3A 00846600
.NULLTWO AIF   (&NULLSW).INCRMT         Branch if DCB/ACB omitted       00873200
         AIF   ('&PAR1(&CTR)'(1,1) EQ '(').REGSTER                      00900000
         LA    0,&PAR1(&CTR)            PICK UP DCB OR ACB ADDRESS @L3C 00918800
         AIF   (NOT &MSW).MODE24D                                  @L1A 00937600
         ST    0,&ACTR.+4(,1)           STORE DCB OR ACB ADDRESS   @L3C 00954600
         AGO   .INCRMT                                             @L3A 00971600
.MODE24D STCM  0,B'0111',&ACTR.+1(1)    STORE DCB OR ACB ADDRESS   @L3C 00988600
         AGO   .INCRMT                                             @L3C 01005600
.REGSTER ANOP                           DCB or ACB address in reg  @L3C 01022600
&LGTH    SETA  K'&PAR1(&CTR)-2                                          01040000
&PARA    SETC  '&PAR1(&CTR)'(2,&LGTH)                                   01060000
.***?    LA    0,&PAR1(&CTR)            PICK UP DCB OR ACB ADDRESS @L3C 01070000
         AIF   (NOT &MSW).MODE24F       Go if 24-bit               @L1A 01080000
         ST    &PARA,&ACTR.+4(,1)       STORE DCB OR ACB ADDRESS   @L3C 01090000
         AGO   .INCRMT                                             @L3C 01100000
.MODE24F STCM  &PARA,B'0111',&ACTR.+1(1) STORE DCB OR ACB ADDRESS  @L3C 01110000
         AGO   .INCRMT                                                  01120000
.* Handle the last DCB or ACB.                                     @L3A 01137200
.QUITNOW AIF   ('&PAR1(&CTR)'(1,1) EQ '(').REGST                        01154400
         LA    0,&PAR1(&CTR)            PICK UP DCB ADDRESS             01171600
         AIF   (NOT &MSW).MODE24H                                  @L1A 01188800
         ST    0,&ACTR.+4(,1)           STORE DCB OR ACB ADDRESS   @L3C 01204200
         AGO   .RGFLIP                  BRANCH IF MODE=31          @L1A 01219600
.MODE24H STCM  0,B'0111',&ACTR.+1(1)    STORE DCB OR ACB ADDRESS   @L3C 01235000
         AGO   .RGFLIP                                                  01250400
.REGST   ANOP                                                           01260000
&LGTH    SETA  K'&PAR1(&CTR)-2                                          01280000
&PARA    SETC  '&PAR1(&CTR)'(2,&LGTH)                                   01300000
         AIF   (NOT &MSW).MODE24K       Go if doing 24-bit list    @L3C 01314000
.MODE24J ST    &PARA,&ACTR.+4(,1)       STORE DCB OR ACB ADDRESS   @L3C 01328000
         AGO   .RGFLIP                  BRANCH IF MODE=31          @01A 01342000
.MODE24K STCM  &PARA,B'0111',&ACTR.+1(1) SET DCB OR ACB AD IN LIST @L3C 01356000
         AGO   .RGFLIP                                                  01370000
.* Handle last DCB or ACB                                          @L3A 01376600
.THRUNOW NI    &ACTR.(1),X'80'          CLEAR OPTION BITS               01383200
         AIF   (&ASUM EQ 0).CONTA                                       01390000
         OI    &ACTR.(1),&ASUM          SET OPTION BITS                 01405000
.CONTA   AIF   (&NULLSW).RGFLIP                                         01420000
         AGO   .QUITNOW                                                 01440000
.INCRMT  ANOP                                                           01460000
&CTR     SETA  &CTR+2                   Increment to next DCB or ACB    01480000
&CTR1    SETA  &CTR1+2                                                  01500000
         AIF   (&MSW).MODE24L           Go if doing 31-bit list    @L1A 01505700
&ACTR    SETA  &ACTR+4                  Increment offset in list   @L1C 01511400
         AGO   .MODE24M                                            @L1A 01517100
.MODE24L ANOP                                                      @L1A 01522800
&ACTR    SETA  &ACTR+8                  Increment offset in list   @L1A 01528500
.MODE24M ANOP                                                      @L1A 01534200
&NULLSW  SETB  0                        Clear switch for next time      01547100
         AGO   .LOOPB                                                   01560000
.RGFLIP  AIF   (NOT &MSW).SVCSVC        MODE=24..BRANCH            @L1C 01562400
         LR    0,1                      Copy parameter list address@L1A 01564800
         SR    1,1                      Set indicator of 31-bit    @L1A 01567200
.*--------------------------------------------------------------------  01569600
.SVCSVC  AIF   ('&TYPE' NE 'T').ISSST                                   01572000
         SVC   23                       ISSUE CLOSE TYPE=T SVC          01596000
         MEXIT                                                          01620000
.ISSST   SVC   20                       ISSUE CLOSE SVC                 01640000
         MEXIT                                                          01660000
.*--------------------------------------------------------------------  01670000
.* Begin expansion of the list form (MF=L).                        @L3A 01675000
.RTEL    ANOP                                                           01680000
&CNAME   DC    0F'0'                    ALIGN LIST TO FULLWORD          01695000
         AIF   ('&PAR1' EQ '').ERROR5                                   01710000
.*--------------------------------------------------------------------- 01713300
.* Handle any form.  Interpret the option and set &ASUM.           @L3A 01716600
.CLRSW   ANOP                                                           01720000
&ASUM    SETA  0                                                        01740000
         AIF   (&CTR1 GT &NUMBER).J                                     01780000
         AIF   ('&PAR1(&CTR1)' EQ '').DC1                               01800000
&B(1)    SETB  ('&PAR1(&CTR1)' EQ 'REREAD')                             01820000
&B(2)    SETB  ('&PAR1(&CTR1)' EQ 'LEAVE')                              01840000
&B(3)    SETB  ('&PAR1(&CTR1)' EQ 'DISP')                               01860000
&B(4)    SETB  ('&PAR1(&CTR1)' EQ 'REWIND')                             01870000
&B(5)    SETB  ('&PAR1(&CTR1)' EQ 'FREE') DYNAMIC UNALLOCATION   Y02134 01872000
         AIF   (&B(1)+&B(2)+&B(3)+&B(4)+&B(5) NE 1).ERROR2       Y02134 01880000
         AIF   ('&TYPE' NE 'T').CLO0100 TEST FOR TYPE=T          Y02134 01882000
         AIF   (&B(3) OR &B(4) OR &B(5)).ERROR2 TYPE=T INVLID OP Y02134 01884000
.CLO0100 ANOP                                                    Y02134 01894000
&ASUM    SETA  16*&B(1)+32*&B(5)+48*&B(2)+64*&B(4)               Y02134 01900000
.DC1     AIF   (&MFESW).ERTRTRN         Go if doing execute form        01935000
.* We are doing the list or standard form.                         @L3A 01970000
         AIF   (&CTR1 LT &NUMBER).K     Go if not last DCB or ACB  @L3C 02005000
.J       ANOP                                                           02040000
&ASUM    SETA  &ASUM+128                Set indicator of last entry     02070000
.K       AIF   (&MFI2SW).I2ROUT                                         02100000
         AIF   (&MFISW).IROUTRN         Go if doing standard form       02113000
.* We are doing the list form.                                     @L3A 02126000
         DC    AL1(&ASUM)               OPTION BYTE                     02139000
         AIF   (NOT &MSW).OLDPARM       Go if doing 24-bit list    @L1A 02152000
         DC    AL3(0)                   RESERVED BYTES             @L1A 02165000
         AIF   ('&PAR1(&CTR)' NE '').NOBLNK2                       @L1A 02178000
         DC    A(0)                     DCB OR ACB ADDRESS         @L1A 02184000
         AGO   .NOTLST                                             @L1A 02190000
.NOBLNK2 AIF   ('&PAR1(&CTR)'(1,1) EQ '(').ERROR6                  @L1A 02196000
         DC    A(&PAR1(&CTR))           DCB OR ACB ADDRESS         @L1A 02202000
         AGO   .NOTLST                                             @L1A 02208000
.OLDPARM AIF   ('&PAR1(&CTR)' NE '').NOBLANK                       @L1C 02214000
         DC    AL3(0)                   DCB OR ACB ADDRESS              02227000
         AGO   .NOTLST                                                  02240000
.NOBLANK AIF   ('&PAR1(&CTR)'(1,1) EQ '(').ERROR6                       02280000
         DC    AL3(&PAR1(&CTR))         DCB OR ACB ADDRESS              02300000
.NOTLST  AIF   (&CTR1 GE &NUMBER).FINI                                  02320000
&CTR     SETA  &CTR+2                                                   02340000
&CTR1    SETA  &CTR1+2                                                  02360000
         AGO   .CLRSW                                                   02380000
.*--------------------------------------------------------------------- 02392000
.* Begin expansion of the standard form (instructions & list).     @L3A 02404000
.TESTI   AIF   (&MSW).LPLIST            Go if doing 31-bit list    @L1A 02416000
         CNOP  0,4                      ALIGN LIST TO FULLWORD          02428000
&LGTH    SETA  ((&NUMBER+1)/2)*4+4                                      02440000
&CNAME   BAS   1,*+&LGTH                LOAD REG1 W/LIST ADDR           02452200
         AGO   .CONT                                               @L1A 02464400
.LPLIST  ANOP                           Generate 31-bit list       @L1A 02468800
         CNOP  0,4                      ALIGN LIST TO WORD              02473200
&LGTH    SETA  ((&NUMBER+1)/2)*8+4                                 @L1A 02477600
&CNAME   BAS   1,*+&LGTH                POINT REGISTER 1 TO LIST   @L1A 02484200
.CONT    ANOP                                                      @L1A 02490800
&MFISW   SETB  1                        MF=I SWITCH                @L1C 02495400
.RTEYE   AIF   ('&PAR1(&CTR)' EQ '').ERROR5                             02500000
         AIF   ('&PAR1(&CTR)'(1,1) NE '(').CLRSW                        02520000
         AIF   (NOT &MSW).SPL1          MODE=24; BRANCH            @L1A 02525700
         DC    A(0)                     OPTION BYTE                @L1A 02531400
         DC    A(0)                     DCB OR ACB ADDRESS         @L1A 02537100
         AGO   .UPCTRS                                             @L1A 02542800
.SPL1    ANOP                           FOR MODE=24                @L1A 02548500
         DC    A(0)                     OPT BYTE AND DCB OR ACB ADDR.   02554200
         AGO   .UPCTRS                                                  02560000
.* Resume standard form.                                           @L3A 02569000
.IROUTRN DC    AL1(&ASUM)               OPTION BYTE                     02578000
         AIF   (&MSW).IROUT3            Branch if 31-bit list      @L1A 02587000
         DC    AL3(&PAR1(&CTR))         DCB OR ACB ADDRESS              02596000
         AGO   .UPCTRS                                             @L1A 02605000
.IROUT3  DC    AL3(0)                   Reserved bytes             @L3C 02610000
         DC    A(&PAR1(&CTR))           DCB OR ACB ADDRESS         @L1A 02615000
.UPCTRS  AIF   (&CTR1 GE &NUMBER).QUITS                                 02620000
&CTR     SETA  &CTR+2                   Go to next DCB or ACB           02640000
&CTR1    SETA  &CTR1+2                                                  02660000
         AGO   .RTEYE                                                   02680000
.* We finished parameter list for standard form. Make instructions.@L3A 02690000
.QUITS   ANOP                                                           02700000
&CTR     SETA  1                        Go back to first DCB or ACB     02720000
&CTR1    SETA  2                                                        02740000
.ILOOP2  AIF   ('&PAR1(&CTR)'(1,1) NE '(').UPCTR2                       02760000
&LGTH    SETA  K'&PAR1(&CTR)-2                                          02780000
&PARA    SETC  '&PAR1(&CTR)'(2,&LGTH)                                   02800000
&MFI2SW  SETB  1                                                   @L3M 02814500
         AIF   (&MSW).PARA2             Go if making 31-bit list   @L3C 02829000
         STCM  &PARA,B'0111',&ACTR.+1(1) STORE DCB OR ACB ADDRESS  @L3C 02843500
         AGO   .CLRSW                   Go to interpret the option @L3A 02858000
.PARA2   ST    &PARA,&ACTR.+4(,1)       STORE DCB OR ACB ADDRESS   @L3C 02872500
         AGO   .CLRSW                   Go to interpret the option      02887000
.I2ROUT  AIF   (&ASUM EQ 0).UPCTR2                                 @L3C 02901500
         MVI   &ACTR.(1),&ASUM          MOVE IN OPTION BYTE        @L1C 02916000
.UPCTR2  AIF   (&CTR1 GE &NUMBER).RGFLIP  Go if no more options    @L3C 02930500
&CTR     SETA  &CTR+2                   Advance to next DCB or ACB      02945000
&CTR1    SETA  &CTR1+2                                                  02960000
&ACTR    SETA  &ACTR+4                  Next offset in list             02971200
         AIF   (NOT &MSW).ILOOP2        Go if 31-bit form          @L3C 02982400
&ACTR    SETA  &ACTR+4                  Next offset in list        @L3M 02993600
         AGO   .ILOOP2                                             @L3M 03004800
.*--------------------------------------------------------------------  03016000
.ERROR2  IHBERMAC 49,,&PAR1(&CTR1)      INVALID OPTION OPERAND          03020000
         MEXIT                                                          03040000
.ERROR3  IHBERMAC 35,,&MF               INVALID MF OPERAND SPECIFIED    03060000
         MEXIT                                                          03080000
.ERROR5  IHBERMAC 06                    DCB MISSING                     03100000
         MEXIT                                                          03120000
.ERROR6  IHBERMAC 69                    INVALID REG NOTATION W/MF=L     03140000
         MEXIT                                                          03146000
.ERROR8  IHBERMAC 63,,&TYPE             INVALID TYPE OPERAND     M0626  03156000
         MEXIT                                                          03157300
.ERROR9  IGGERMAC 201                   INVALID MODE OPERAND      @L1A  03158600
         MEXIT                                                     @L3A 03158700
.ERROR10 IGGERMAC 203,CLOSE    THE CLOSE MACRO DOES NOT SUPPORT    @L3A 03158800
         MEXIT                   SYSSTATE AMODE64=YES              @L3A 03158900
.ERROR11 IGGERMAC 204,CLOSE    THE CLOSE MACRO DOES NOT SUPPORT    @L3A 03159000
         MEXIT                   SYSSTATE ASCENV=AR OR ANY         @L3A 03159100
.*                                                            D 2  @02D 03159300
.FINI    MEND                                                           03160000
**/ CLOSE: MACRO KEYS(DCB_NAME,DCB_ADDR,MODE,TYPE_T,RETCODE,   /*@L2A*/ 03210000
*        MF);                                                  /*@L2A*/ 03260000
* ANS('? '!!MACLABEL!!' CLOSEP '!!MACKEYS!!';') RESCAN SKIP;   /*@L2A*/ 03310000
*  %END CLOSE;                                                 /*@L2A*/ 03360000
