         MACRO                                                                  
         IST6MCSS &BRANCH=,&ECB=,&EXIT=,&OPTCD=,&QUALIFY=,&CONVID=,    X        
               &SESSID=,&SESSIDL=,&INVALP1=,&INVALP2=,&VTRINA=,        X        
               &VTRINL=,&VTROUTA=,&VTROUTL=              @T1C @L1C @D1C         
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCSS                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: CONTROL = SETSESS PROCESSOR                 */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1989, 1994.       */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */         
.* /*      CONTROL=SETSESS PARAMETER ON THE APPCCMD MACRO.           */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS:                                              */         
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCER                       */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: NONE                                     */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*                                                                */         
.* /* $MAC(IST6MCSS),COMP(API),PROD(VTAM): CONTROL = SETSESS         */         
.* /*                                      PROCESSOR                 */         
.* /*                                                                */         
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */         
.* /* ---- -------- ------- ------ ------  ------------------------- */         
.* /* $F0= RCR0004  HVT3302 880913 255471: SYNCPOINT RCR SUPPORT     */         
.* /* $Q1= P062052  HVT3302 881025 255471: skip the MVC of SESSID    */         
.* /*                                      if SESSIDL equals 0       */         
.* /* $Q2= P062674  HVT3302 881104 255471: Check for null SESSID     */         
.* /* $Q3= P062882  HVT3302 881121 255471: Issue error message for   */         
.* /*                                      invalid QUALIFY field     */         
.* /* $Q4= P063295  HVT3302 881217 991843: Allow register notation   */         
.* /*                                      for SESSID and SESSIDL    */         
.* /* $V1= P082013  HVT3401 900827 474203: Add two new qualifiers    */         
.* /*                                      SYNCBEG and SYNCEND       */         
.* /* $T1= P100610  NQNAME2 920228 879315: ASSEMBLY ERRORS ON ASMXF  */         
.* /* $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services     */         
.* /* $L2= KA@DR006 APPCRR  940713 576146: Vector Format             */         
.* /* $D1= ID@@@MST FPAPI   950310 143576: Fast Path API for APPC    */         
.* /*                                                                */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
.*                                                                              
         GBLB  &ISTGEN                                                          
         GBLB  &ISTSS                                                           
         AIF   (&ISTSS EQ 1).SSINVKW                                            
&ISTSS   SETB  1                                                                
.SSINVKW ANOP                                                                   
         IST6MCUC SETSESS,CONTROL,INVALKW=&INVALP1                 @T1C         
         IST6MCUC SETSESS,CONTROL,INVALKW=&INVALP2                 @T1A         
.*                                     CHECK INVALID APPCCMD                    
.SSCOMKW ANOP                                                                   
         IST6MCUP OPTCD=&OPTCD,ECB=&ECB,EXIT=&EXIT,BRANCH=&BRANCH,     X        
               CKCMODE=NO,             DO NOT CHECK CONMODE PARAMETER  X        
               CKSYN=YES,              ASY/SYN IS VALID FOR OPTCD      X        
               CKBUFF=NO               PARAMETER BUFFLST/NBUFFLST IS            
.*                                     INVALID FOR OPTCD PARAMETER              
.SSVECT  ANOP                          ENSURE BOTH PARMS ARE CODED @L1A         
.*                                     OR BOTH ARE NOT CODED       @L1A         
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).SSVOUT    @D1C         
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).SSERVI     @D1C         
         IST6MCUA KWVAL=&VTRINA,LOC=RPL6VAIA-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_ADDRESS                      @D1C         
.*                                     STORE VECTOR AREA INPUT ADDRESS          
.*                                                                 @L1A         
         IST6MCUA KWVAL=&VTRINL,LOC=RPL6VAIL-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2                 @D1C         
.*                                     STORE VECTOR AREA INPUT LENGTH           
.*                                                                 @L1A         
         AGO   .SSVOUT                                             @D1C         
.SSERVI  ANOP                                                      @L1A         
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C         
.*                                                                              
.SSVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED       @D1A         
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).SSQUALI @D1A         
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).SSERVO   @D1A         
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X        
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                @D1A         
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X        
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                @D1A         
         AGO   .SSQUALI                                            @D1A         
.SSERVO  ANOP                                                      @D1A         
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A         
.*                                                                              
.SSQUALI ANOP                                                                   
         AIF   ('&QUALIFY' EQ '').SSID         DEFAULT TO RPL6 VALUES           
         AIF   ('&QUALIFY' EQ 'SUSPEND').SSUSP                                  
         AIF   ('&QUALIFY' EQ 'RESUME').SSRESUM                                 
         AIF   ('&QUALIFY' EQ 'SYNCBEG').SSYNCB                  @V1A           
         AIF   ('&QUALIFY' EQ 'SYNCEND').SSYNCE                  @V1A           
         IST6MCER 5,QUALIFY,&QUALIFY                             @Q3C           
.*                                     PUT QUALIFY IN APPC AREA                 
.SSYNCB  ANOP                                                    @V1A           
         AIF   (NOT &ISTGEN).SSEXIT                              @V1A           
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6SYNB                    @V1A           
         AGO   .SSID                                             @V1A           
.*                                     QUALIFY=SYNCBEG           @V1A           
.SSUSP   ANOP                                                                   
         AIF   (NOT &ISTGEN).SSEXIT                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6SUSP                                   
.*                                     QUALIFY=SUSPEND                          
.SSID    ANOP                                                                   
         AIF   ('&CONVID' EQ '').SSESID                                         
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X        
               KW=CONVID                                                        
.*                                     PUT CONVID IN APPC AREA                  
.SSRESUM ANOP                                                                   
         AIF   (NOT &ISTGEN).SSEXIT                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6RESM                                   
         AGO   .SSESID                                           @V1A           
.*                                     QUALIFY=RESUME                           
.SSYNCE  ANOP                                                    @V1A           
         AIF   (NOT &ISTGEN).SSEXIT                              @V1A           
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6SYNE                    @V1A           
.*                                     QUALIFY=SYNCEND           @V1A           
.SSESID  ANOP                                                                   
         AIF   ('&SESSID' EQ '').SSREQ     DEFAULT TO RPL6 VALUES @Q2A          
         AIF   ('&SESSID'(K'&SESSID,1) EQ '''').SSERROR                         
         AGO   .SSIDL                                                           
.*                                     CHECK INVALID SESSID                     
.SSERROR ANOP                                                                   
         IST6MCER 11,SESSID,&SESSID                                             
         AGO   .SSREQ                  DEFAULT TO RPL6 VALUES                   
.*                                     INVALID DATA TYPE                        
.SSIDL   ANOP                                                                   
         AIF   ('&SESSIDL' EQ '').SSREQ        DEFAULT TO RPL6 VALUES           
         AIF   ('&SESSIDL'(K'&SESSIDL,1) EQ '''').SSLENER                       
         AIF   (NOT &ISTGEN).SSEXIT                                             
         AIF   ('&SESSIDL'(1,1) EQ '(').SLENGTH   REGISTER NOTATION             
.*                                     SPECIFIED                  @Q4A          
         IC    15,&SESSIDL                                        @Q1A          
         AGO   .SETSSL                                            @Q4A          
.SLENGTH ANOP                                                     @Q4A          
         LR    15,&SESSIDL(1)          OBTAIN LENGTH SPECIFIED    @Q4A          
.SETSSL  ANOP                                                     @Q4A          
         STC   15,RPL6SIDL-ISTRPL6X(14)                                         
         LTR   15,15                   SESSIDL=0?                 @Q1A          
         BZ    *+10                    YES, SKIP SESSID           @Q1A          
         AIF   ('&SESSID'(1,1) EQ '(').SSIREGA  REGISTER NOTATION               
.*                                     SPECIFIED FOR SESSID       @Q4A          
         BCTR  15,0                                                             
         EX    15,*+8                                                           
         B     *+10                                                             
         MVC   RPL6SSID-ISTRPL6X(1,14),&SESSID                    @Q1A          
         AGO   .SSREQ                                                           
.SSIREGA ANOP                                                     @Q4A          
         BCTR  15,0                                               @Q4C          
         EX    15,*+8                                             @Q4C          
         B     *+10                                               @Q4C          
         MVC   RPL6SSID-ISTRPL6X(1,14),0(&SESSID(1))              @Q4M          
         AGO   .SSREQ                                                           
.*                                     PUT SESSID AND SESSIDL                   
.*                                     IN APPC AREA                             
.SSLENER ANOP                                                                   
         IST6MCER 11,SESSIDL,&SESSIDL  DEFAULT TO RPL6 VALUES                   
.*                                     INVALID DATA TYPE                        
.SSREQ   ANOP                                                                   
         MVI   RPL6REQ-ISTRPL6X(14),RPL6SETS                                    
*                                      PUT SETSESS REQUEST CODE IN              
*                                      APPC AREA                                
.SSEXIT  MEND                                                                   
