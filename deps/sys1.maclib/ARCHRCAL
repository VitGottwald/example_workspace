         MACRO                                                                  
.**********************************************************************         
.*                                                                    *         
.* $MAC(ARCHRCAL) COMP(SERVICE) PROD(DFHSM):                          *         
.* MACRO NAME = ARCHRCAL                                              *         
.* FUNC = SERVICE                                                     *         
.* SUBFUNC = MWE                                                      *         
.*                                                                    *         
.* DESCRIPTIVE NAME = REQUEST DFHSM RECALL A MIGRATED DATA SET.       *         
.*                                                                    *         
.**********************************************************************         
.*PROPRIETARY V3 STATEMENT                                            *         
.*LICENSED MATERIALS - PROPERTY OF IBM                                *         
.*"RESTRICTED MATERIALS OF IBM"                                       *         
.*5650-ZOS                                                            *         
.*COPYRIGHT IBM CORP. 1989, 2013                                      *         
.*END PROPRIETARY V3 STATEMENT                                        *         
.**********************************************************************         
.*                                                                    *         
.* STATUS = VERSION 2, RELEASE 6, LEVEL 0                             *         
.*                                                                    *         
.* FUNCTION = PERFORM DFHSM SVC 109 TO RECALL THE SPECIFIED           *         
.*            MIGRATED DATA SET.                                      *         
.*                                                                    *         
.* NOTES = SEE BELOW                                                  *         
.*                                                                    *         
.*   DEPENDENCIES =                                                   *         
.*            IF ASYNCHRONOUS PROCESSING IS REQUESTED, APPLICATION    *         
.*            IS RESPONSIBLE FOR FREEING THE MWE STORAGE IN CSA.      *         
.*            USE THE ARCFMWE MACRO TO FREE THE MWE AFTER THE ECB     *         
.*            IN THE MWE HAS BEEN POSTED.                             *         
.*                                                                    *         
.*   RESTRICTIONS = THE APPLICATION MUST BE APF AUTHORIZED TO USE     *         
.*            ASYNCHRONOUS PROCESSING.                                *         
.*                                                                    *         
.*   REGISTER CONVENTIONS =                                           *         
.*                                                                    *         
.*            REGISTER 13 MUST POINT TO A STANDARD SAVE AREA          *         
.*            WHEN MACRO IS ISSUED.                                   *         
.*                                                                    *         
.*            REGISTERS 0, 1, 14 AND 15 WILL BE DESTROYED.            *         
.*                                                                    *         
.*   PATCH LABEL = NONE                                               *         
.*                                                                    *         
.* MODULE TYPE = MACRO                                                *         
.*                                                                    *         
.*   PROCESSOR = BAL                                                  *         
.*                                                                    *         
.* INPUT =                                                            *         
.*          DSN      (REQUIRED) SPECIFY THE ADDRESS OF A 44-BYTE AREA *         
.*                   CONTAINING THE NAME OF THE DATA SET TO BE ACTED  *         
.*                   ON, LEFT JUSTIFIED AND PADDED WITH BLANKS.       *         
.*                                                                    *         
.*               NOTE:     TOVOL AND UNIT MUST BOTH BE SPECIFIED      *         
.*                         IF THE RECALL IS DIRECTED TO A SPECIFIC    *         
.*                         VOLUME.                                    *         
.*                                                                    *         
.*          TOVOL    SPECIFY THE ADDRESS OF A 6 CHARACTER VOLUME      *         
.*                   SERIAL OF THE VOLUME CONTAINING THE DATA SET,    *         
.*                   LEFT JUSTIFIED AND PADDED WITH BLANKS.           *         
.*                                                                    *         
.*          UNIT     SPECIFY THE ADDRESS OF A 4 CHARACTER UCB DEVICE  *         
.*                   TYPE OF THE DASD VOLUME CONTAINING THE DATA SET. *         
.*                                                                    *         
.*          WAIT     (OPTIONAL) SPECIFY YES!NO WHETHER APPLICATION    *         
.*                   DESIRES TO WAIT UNTIL DFHSM HAS PROCESSED THIS   *         
.*                   REQUEST.  DEFAULT FOR THIS PARAMETER IS NO.      *         
.*                                                                    *         
.*          ASYNC    (OPTIONAL) SPECIFY YES!NO WHETHER ASYNCHRONOUS   *         
.*                   RECALL PROCESSING IS REQUESTED.  ASYNC=YES       *         
.*                   CAUSES THE MWE TO BE BUILT IN CSA AND ITS ECB    *         
.*                   ADDRESS WILL BE RETURNED IN REGISTER 1.          *         
.*                   DEFAULT FOR THIS PARAMETER IS NO.                *         
.*                                                                    *         
.*              NOTE: ASYNC=YES REQUIRES WAIT=YES ALSO BE SPECIFIED.  *         
.*                                                                    *         
.*          TCBTOKEN (OPTIONAL) SPECIFY CURRENT FOR ASYNC WAIT        *         
.*                   REQUESTS ONLY. SPECIFIES THE CURRENT TCB TOKEN   *         
.*                   SHOULD BE USED TO VALIDATE THE ECB ON POST.      *         
.*                   WITHOUT THIS PARAMETER, THE JOBSTEP TCB TOKEN    *         
.*                   IS USED. PROVIDED TO SUPPORT CHANGES IN BCP      *         
.*                   FOR BATCH JOB RECALL PROCESSING WHICH REQUIRES   *         
.*                   CURRENT SO THAT AN MWE IS NOT POSTED BACK IF     *         
.*                   THE TASK ABENDS BUT THE JOBSTEP TASK IS STILL    *         
.*                   ACTIVE.                                      @UWA*         
.*                                                                    *         
.*          DLRKEEP  (OPTIONAL) SPECIFY YES!NO:                   @02A*         
.*                   - YES, DLR IS KEPT UNCHANGED                 @02A*         
.*                   - NO,  DLR IS SET TO RECALL DATE (DEFAULT)   @02A*         
.*                                                                    *         
.*          TEST     (OPTIONAL) SPECIFY YES!NO WHETHER MACRO IN TEST  *         
.*                   MODE.  TEST=YES DENOTES THAT THE MWE SHOULD BE   *         
.*                   BUILT BUT THAT NO DFHSM SVC IS ISSUED.  DEFAULT  *         
.*                   FOR THIS PARAMETER IS NO.                        *         
.*                                                                    *         
.*          SNAPDCB  (OPTIONAL) SPECIFY THE ADDRESS OF AN OPEN DCB    *         
.*                   USED FOR SNAP PROCESSING OF THE MWE AREA UPON    *         
.*                   RETURN FROM DFHSM SVC PROCESSING.                *         
.*                                                                    *         
.*          WKAREA   (REQUIRED) SPECIFY THE ADDRESS OF A 200 BYTE     *         
.*                   WORK AREA TO CONTAIN INFORMATION SPECIFIED IN    *         
.*                   THIS MACRO.                                      *         
.*                                                                    *         
.*          DAOPT    (OPTIONAL) SPECIFY SAMETRK!RELTRK!RELBLK     @01A*         
.*                   DEPENDING ON THE VOLUME SELECTION AND DATA   @01A*         
.*                   ACCESSING REQUIRED FOR THE RECALL OF A DA    @01A*         
.*                   DATA SET                                         *         
.*                                                                    *         
.* OUTPUT =   REGISTER 15 WILL CONTAIN MWE RETURN CODE                *         
.*            REGISTER 0  WILL CONTAIN MWE REASON CODE                *         
.*            REGISTER 1  WILL CONTAIN ADDRESS OF ECB IN CSA AREA     *         
.*                        OR ZERO IF ASYNC NOT SPECIFIED              *         
.*                                                                    *         
.*            INVALID REQUESTS NOT SENT TO DFHSM WILL HAVE            *         
.*            THE FOLLOWING RETURN CODES:                             *         
.*                                                                    *         
.*            402 - LOCATE ERROR OCCURRED FOR SPECIFIED DATA SET      *         
.*            806 - LINK FAILURE - UNABLE TO LINK TO ARCGIVER         *         
.*                                                                    *         
.* CHANGE ACTIVITY =                                                  *         
.* $K0=HSMKD,250,090888,THP5: PROGRAMMABLE DFHSM REQUEST          @KDA*         
.*                            PROCESSOR                           @KDA*         
.* $01=OY40118,260,020591,THA4: ROLLIN APAR OY39056 - DA DATA     @01A*         
.*                              SET IMPROVEMENTS SPE              @01A*         
.* $UW=HSMUW,210,052212,THI9-6: Update ARCHRCAL to use            @UWA*         
.*                              different TCB                     @UWA*         
.* $02=OA39884,D10,072012,THI9-9: ADD DLRKEEP OPTION TO ARCHRCAL  @02A*         
.*                                                                    *         
.**********************************************************************         
&NAME    ARCHRCAL &DSN=,&UNIT=,&ASYNC=NO,&WAIT=NO,&TEST=NO,            X        
               &WKAREA=,&TOVOL=,&SNAPDCB=,&DAOPT=,&TCBTOKEN=,     @02C X        
               &DLRKEEP=NO                                        @02A          
         LCLA  &A                                                               
         LCLB  &ERROR                                                           
         LCLC  &H                                                               
         LCLC  &L1                                                              
         LCLC  &L2                                                              
&L1      SETC  'ARC'.'&SYSNDX'.'A'                                              
&L2      SETC  'ARC'.'&SYSNDX'.'B'                                              
.*                                                                              
.* CHECK THE PARAMETERS SPECIFIED WITH THE HRECALL FUNCTION                     
.*                                                                              
.CHKRCL  ANOP                                                                   
         AIF   ('&DSN' NE '').RCL1                                              
         MNOTE 8,'REQUIRED PARAMETER, DSN, NOT SPECIFIED'                       
&ERROR   SETB  1                                                                
.RCL1    ANOP                                                                   
         AIF   ('&WKAREA' NE '').RCL2                                           
         MNOTE 8,'REQUIRED PARAMETER, WKAREA, IS NOT SPECIFIED'                 
&ERROR   SETB  1                                                                
.RCL2    ANOP                                                                   
         AIF   ('&UNIT' EQ '' AND '&TOVOL' EQ '').RCL3                          
         AIF   ('&UNIT' NE '' AND '&TOVOL' NE '').RCL3                          
         MNOTE 8,'UNIT AND TOVOL MUST EITHER BOTH BE SPECIFIED OR NEITHX        
               ER SPECIFIED'                                                    
&ERROR   SETB  1                                                                
.RCL3    ANOP                                                                   
         AIF   ('&ASYNC' NE 'YES').RCL4                                         
         AIF   ('&WAIT' EQ 'YES').RCL4                                          
         MNOTE 8,'ASYNC=YES REQUIRES THAT WAIT=YES ALSO BE SPECIFIED'           
&ERROR   SETB  1                                                                
.RCL4    ANOP                                                     @01A          
         AIF   ('&DAOPT' EQ '').RCL5                              @01A          
         AIF   ('&DAOPT' EQ 'SAMETRK').RCL5                       @01A          
         AIF   ('&DAOPT' EQ 'RELTRK').RCL5                        @01A          
         AIF   ('&DAOPT' EQ 'RELBLK').RCL5                        @01A          
         MNOTE 8,'DAOPT SUBPARAMETER SPECIFIED INCORRECTLY'       @01A          
&ERROR   SETB  1                                                  @01A          
.RCL5    ANOP                                                     @UWA          
         AIF   ('&TCBTOKEN' EQ '').RCL7                           @UWA          
         AIF   ('&TCBTOKEN' EQ 'CURRENT').RCL6                    @UWA          
         MNOTE 8,'TCBTOKEN SUBPARAMETER SPECIFIED INCORRECTLY'    @UWA          
&ERROR   SETB  1                                                  @UWA          
         AGO   .RCL7                                              @UWA          
.RCL6    ANOP                                                     @UWA          
         AIF   ('&ASYNC' EQ 'YES').RCL7                           @UWA          
         AIF   ('&ASYNC' NE 'YES' AND '&WAIT' NE 'YES').RCL61     @UWA          
         MNOTE 8,'TCBTOKEN=CURRENT ALSO REQUIRES ASYNC=YES'       @UWA          
&ERROR   SETB  1                                                  @UWA          
         AGO   .RCL7                                              @UWA          
.RCL61   ANOP                                                     @UWA          
         MNOTE 8,'TCBTOKEN=CURRENT REQUIRES THAT ASYNC=YES AND WAIT=YESX        
                ALSO BE SPECIFIED'                                @UWA          
&ERROR   SETB  1                                                  @UWA          
.RCL7    ANOP                                                     @02A          
         AIF   ('&DLRKEEP' EQ '').RCL8                            @02A          
         AIF   ('&DLRKEEP' EQ 'NO').RCL8                          @02A          
         AIF   ('&DLRKEEP' EQ 'YES').RCL8                         @02A          
         MNOTE 8,'DLRKEEP SUBPARAMETER SPECIFIED INCORRECTLY'     @02A          
&ERROR   SETB  1                                                  @02A          
.RCL8    AIF   (NOT &ERROR).BUILD                                 @02C          
         MEXIT                                                                  
.*                                                                              
.* AT THIS POINT ALL THE PARAMETERS HAVE BEEN PROPERLY SPECIFIED.               
.* NOW BUILD THE CONTROL BLOCK AND CALL THE PROCESSING ROUTINE.                 
.*                                                                              
.BUILD   ANOP                                                                   
         AIF   ('&WKAREA'(1,1) EQ '(').WREG                                     
&NAME    LA    15,&WKAREA                                                       
         AGO   .CLRWKA                                                          
.WREG    ANOP                                                                   
&A       SETA  K'&WKAREA-2                                                      
&H       SETC  '&WKAREA'(2,&A)                                                  
&NAME    LR    15,&H                                                            
.CLRWKA  ANOP                                                                   
         XC    0(200,15),0(15)     CLEAR THE WORK AREA                          
         B     *+12                BRANCH AROUND IDENTIFIER                     
         DC    CL8'DFHSMREQ'                                                    
         MVC   0(8,15),*-8         SET THE IDENTIFIER                           
         B     *+12                BRANCH AROUND FUNCTION NAME                  
         DC    CL8'HRECALL '       FUNCTION NAME                                
         MVC   8(8,15),*-8                                                      
.XSYNC   AIF   ('&ASYNC' NE 'YES').XWAIT                                        
         OI    16(15),X'80'        ASYNC WAS SPECIFIED                          
.XWAIT   AIF   ('&WAIT' NE 'YES').XTEST                                         
         OI    16(15),X'40'        WAIT WAS SPECIFIED                           
.XTEST   AIF   ('&TEST' NE 'YES').XTOKN                           @UWC          
         OI    16(15),X'02'        TEST WAS SPECIFIED                           
.XTOKN   AIF   ('&TCBTOKEN' NE 'CURRENT').XDLRKPT                 @02C          
         OI    19(15),X'20'        TCBTOKEN=CURRENT SPECIFIED     @UWA          
.XDLRKPT AIF  ('&DLRKEEP' NE 'YES').XDSN                          @02A          
         OI    19(15),X'10'        DLRKEEP WAS SPECIFIED          @02A          
.XDSN    ANOP                                                                   
         AIF   ('&DSN'(1,1) EQ '(').DREG                                        
         LA    1,&DSN              GET ADDR OF DSN                              
         ST    1,20(15)            STORE IN PARM LIST                           
         AGO   .XSNAP                                                           
.DREG    ANOP                                                                   
&A       SETA  K'&DSN-2                                                         
&H       SETC  '&DSN'(2,&A)                                                     
         ST    &H,20(15)           STORE ADDR OF DSN                            
.XSNAP   ANOP                                                                   
         AIF   ('&SNAPDCB' EQ '').XUNIT                                         
         AIF   ('&SNAPDCB'(1,1) EQ '(').SREG                                    
         LA    1,&SNAPDCB          GET ADDRESS OF SNAP DCB                      
         ST    1,188(15)           STORE IN PARM LIST                           
         AGO   .XUNIT                                                           
.SREG    ANOP                                                                   
&A       SETA  K'&SNAPDCB-2                                                     
&H       SETC  '&SNAPDCB'(2,&A)                                                 
         ST    &H,188(15)          STORE DCB ADDRESS IN PARM LIST               
.XUNIT   ANOP                                                                   
         AIF   ('&UNIT' EQ '').XTOVOL                                           
         AIF   ('&UNIT'(1,1) EQ '(').UREG                                       
         LA    1,&UNIT             GET ADDR OF UNIT                             
         ST    1,24(15)            STORE IN PARM LIST                           
         AGO   .XTOVOL                                                          
.UREG    ANOP                                                                   
&A       SETA  K'&UNIT-2                                                        
&H       SETC  '&UNIT'(2,&A)                                                    
         ST    &H,24(15)           STORE ADDR OF UNIT PARM                      
.XTOVOL  ANOP                                                                   
         AIF   ('&TOVOL' EQ '').XDAOPT                            @01C          
         AIF   ('&TOVOL'(1,1) EQ '(').TOREG                                     
         LA    1,&TOVOL            GET ADDR OF TOVOL                            
         ST    1,52(15)            STORE IN PARM LIST                           
         AGO   .XDAOPT                                            @01C          
.TOREG   ANOP                                                                   
&A       SETA  K'&TOVOL-2                                                       
&H       SETC  '&TOVOL'(2,&A)                                                   
         ST    &H,52(15)           STORE ADDR OF TOVOL PARM                     
.XDAOPT  ANOP                                                     @01A          
         AIF   ('&DAOPT' EQ '').GIVER                             @01A          
         AIF   ('&DAOPT' NE 'SAMETRK').DAOPT2                     @01A          
         OI    17(15),X'40'        DAOPT=SAMETRK WAS SPECIFIED    @01A          
         AGO   .GIVER                                             @01A          
.DAOPT2  AIF   ('&DAOPT' NE 'RELTRK').DAOPT3                      @01A          
         OI    17(15),X'80'        DAOPT=RELTRK WAS SPECIFIED     @01A          
         AGO   .GIVER                                             @01A          
.DAOPT3  AIF   ('&DAOPT' NE 'RELBLK').GIVER                       @01A          
         OI    17(15),X'C0'        DAOPT=RELBLK WAS SPECIFIED     @01A          
.GIVER   ANOP                                                                   
         LR    1,15                PARAMETER ADDRESS TO REG 1                   
         LINK  EP=ARCGIVER,ERRET=&L1                                            
         L     15,192(1)           LOAD RETURN CODE                             
         L     0,196(1)            LOAD REASON CODE                             
         L     1,72(1)             ADDRESS OF ECB IF ASYNC SPECIFIED            
         B     &L2                                                              
.NOMWE   ANOP                                                                   
&L1      LA    15,806              INDICATE LINK FAILED                         
&L2      EQU   *                                                                
         MEXIT                                                                  
         MEND                                                                   
