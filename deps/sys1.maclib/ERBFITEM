 %ERBFITEM:                                                                     
     MACRO KEYS(FLOAT               /* Floating point precision      */         
               ,BOUNDARY    ,BDY    /* Boundary (default byte)       */         
               );                                                               
 /****  START OF SPECIFICATIONS  *************************************/         
 /*                                                                  */         
 /*01* Proprietary Statement:                                        */         
 /*       Licensed Materials - Property of IBM                   @61A*/         
 /*       "Restricted Materials of IBM"                          @61A*/         
 /*       Copyright IBM Corp. 1994, 2021                             */         
 /*                                                                  */         
 /*       Status: z/OS V2R5 Data Gatherer (HRG77D0)                  */         
 /*                                                                  */         
 /*01* DESCRIPTIVE_NAME: FLOATING POINT ITEM DECLARATION             */         
 /*02*   ACRONYM: N/A                                                */         
 /*                                                                  */         
 /*01* MACRO-NAME:  ERBFITEM                                         */         
 /*                                                                  */         
 /*01* DSECT NAME: N/A                                               */         
 /*                                                                  */         
 /*01* COMPONENT:  zOS Data Gatherer (27401)                         */         
 /*                                                                  */         
 /*01* EYE-CATCHER:  None.                                           */         
 /*                                                                  */         
 /*01* STORAGE ATTRIBUTES:  N/A                                      */         
 /*                                                                  */         
 /*01* SIZE:  N/A                                                    */         
 /*                                                                  */         
 /*01* CREATED BY:                                                   */         
 /*      Issuing routine                                             */         
 /*                                                                  */         
 /*01* POINTED TO BY:  N/A                                           */         
 /*                                                                  */         
 /*01* SERIALIZATION:  None.                                         */         
 /*                                                                  */         
 /*01* FUNCTION:                                                     */         
 /*02*     Generates a standard PL/X declaration for                 */         
 /*        floating point variables. To be used in                   */         
 /*        mapping macros only                                       */         
 /*                                                                  */         
 /*        FLOAT(24)  is generated as CHAR(4)                        */         
 /*        FLOAT(56)  is generated as CHAR(8)                        */         
 /*        FLOAT(112) is generated as CHAR(16)                       */         
 /*      Notes:                                                      */         
 /*         Depending on the external macro variable ERBFLTDCL       */         
 /*         there is an alternative expansion. This expansion is     */         
 /*         NOT INTENDED FOR CUSTOMER USE.                           */         
 /*                                                                  */         
 /*         Begin of part NOT INTENDED FOR CUSTOMER USE              */         
 /*                                                                  */         
 /*         The expansion generates the floating point declarations  */         
 /*         suitable for the floating point macro package using      */         
 /*         the macro FLTITEM.                                       */         
 /*         For this expansion the following must be included in     */         
 /*         the module:                                              */         
 /*         percent DCL ERBFLTDCL CHAR EXTERNAL semicolon            */         
 /*         percent ERBFLTDCL = 'YES' semicolon                      */         
 /*                                                                  */         
 /*         End of part NOT INTENDED FOR CUSTOMER USE                */         
 /*                                                                  */         
 /*01* METHOD OF ACCESS:                                             */         
 /*02*   PL/X:                                                       */         
 /*        ?ERBFITEM(a) FLOAT(b) c semicolon                         */         
 /*        WHERE:                                                    */         
 /*             a - Item to be declared. This is the name if the     */         
 /*                 item optionally preceeded by the PL/X            */         
 /*                 structure level number.                          */         
 /*             b - Floating point precision (24, 56, or 112)        */         
 /*             c - Optional boundary (e.g. BDY(DWORD) )             */         
 /*                 Default is byte (as opposed to flt macro)        */         
 /*        EXAMPLE:                                                  */         
 /*          questionmark ERBFITEM(5 SMF71XYZ) FLOAT(56) semicolon,  */         
 /*                                                                  */         
 /*01* DELETED BY: N/A                                               */         
 /*                                                                  */         
 /*01* FREQUENCY: N/A                                                */         
 /*                                                                  */         
 /*01* DEPENDENCIES: None                                            */         
 /*                                                                  */         
 /*01* DISTRIBUTION LIBRARY:                                         */         
 /*      N/A                                                         */         
 /*                                                                  */         
 /*01* CHANGE ACTIVITY:                                              */         
 /*      $D0=CAP,HRM5520,,JKE: Floating point mapping support        */         
 /*      $61=R790,HRM7790,,MWe: explicit copyright for ext. macro@61A*/         
 /*                                                                  */         
 /****  END OF SPECIFICATIONS  ***************************************/         
 /*                                                                  */         
     DECLARE ERBFLTDCL CHAR EXTERNAL;                                           
     DECLARE ML     CHAR INT;                                                   
     DECLARE FORM   CHAR INT;                                                   
     DECLARE XBDY   CHAR INT;                                                   
     DECLARE I3GAA  CHAR;                                                       
     DECLARE I3GBB  CHAR;                                                       
     DECLARE I3GCOL1 FIXED;                                                     
     DECLARE I3GCOL3 FIXED;                                                     
     DECLARE I3GCOL2 FIXED;                                                     
  I3GAA = MACNAME!!' '!!MACLIST!!' '!!MACKEYS;                                  
  I3GAA = I3GAA!!' MACDATE(94.187) ';                                           
  I3GBB = COMMENT(I3GAA);                                                       
  I3GCOL1 = 5;   /* BEGIN OF ANSWER TEXT */                                     
  I3GCOL3 = 37;  /* BEGIN OF COMMENT TEXT */                                    
  I3GCOL2 = 10;  /* BEGIN OF ASSEMBLER OPCODE */                                
                                                                                
  /* Precision                                                    */            
  SELECT (FLOAT(1));                                                            
     WHEN ('24','FLOAT') DO;                                                    
        FORM = 'CHAR(4)';                                                       
        FLT  = '24';                                                            
     END;                                                                       
     WHEN ('56','DFLOAT') DO;                                                   
        FORM = 'CHAR(8)';                                                       
        FLT  = '56';                                                            
     END;                                                                       
     WHEN ('112','QFLOAT') DO;                                                  
        FORM = 'CHAR(16)';                                                      
        FLT  = '112';                                                           
     END;                                                                       
     OTHERWISE ANSWER MSGLEVEL(8)                                               
               MESSAGE('PARM FLOAT('!! FLOAT(1) !! ') INVALID. '!!              
                       I2GBB);                                                  
  END;                                                                          
                                                                                
  /* Boundary                                                        */         
  XBDY = '';                                                                    
  IF (BDY ^= '') THEN DO;                                                       
     XBDY = XBDY!!'BDY'!!BDY;                                                   
  END;                                                                          
  IF (BOUNDARY ^= '') THEN DO;                                                  
     XBDY = XBDY!!'BOUNDARY'!!BOUNDARY;                                         
  END;                                                                          
  IF (XBDY = '') THEN DO;                /* Default Boundary is BYTE */         
     XBDY = 'BOUNDARY(BYTE)';                                                   
  END;                                                                          
                                                                                
  /* Alternate expansion for floating point macro usage              */         
  IF (ERBFLTDCL = 'YES') THEN DO;                                               
     ANS('?FLTITEM'!!MACLIST!!' FLOAT('!!FLT!!') '!!XBDY!!'; '                  
         !!I3GBB) SKIP COL(I3GCOL1);                                            
  END;                                                                          
  /* Default expansion (not using floating point macros)             */         
  ELSE DO;                                                                      
     /* Item itself                                                  */         
     IF LENGTH(MACLIST) > 1 THEN DO;                                            
        ML = MACLIST(2::LENGTH(MACLIST)-2);                                     
     END;                                                                       
     ELSE DO;                                                                   
        ML = MACLIST;                                                           
     END;                                                                       
     /* Result                                                       */         
     ANS(ML!!' '!!FORM!!' '!!XBDY) SKIP COL(I3GCOL1);                           
  END;                                                                          
 %END ERBFITEM;                                                                 
