*/*PROPRIETARY V3 STATEMENT                                          */ 00010000
*/*LICENSED MATERIALS - PROPERTY OF IBM                              */ 00015000
*/*"RESTRICTED MATERIALS OF IBM"                                     */ 00020000
*/*5695-DF1                                                          */ 00025000
*/*COPYRIGHT 1992, 2011 IBM CORP.                                    */ 00031200
*/*END PROPRIETARY V3 STATEMENT                                      */ 00037500
*/*                                                                  */ 00045000
*%/* START OF PL/X COMMENT ENDING ON %MACRO STATEMENT            @L2A   00047500
         MACRO                                                          00050000
&NAME    GET   &CB,&AREA,&RPL=,&TYPE=                           XL03123 00100000
.*       &TYPE PARAMETER DROPPED, ADDED WITH OZ                         00225000
.* $L1=31BIT,JDZ1110,,SJPLRK:  INVOCABLE IN 31-BIT MODE            @L1A 00243700
.* $L2=PLX,JDZ1110,930930,SJPLMET: PL/X SUPPORT                    @L2A 00253100
.* $L3=64BIT,JDZ1D10,20100303,SVLJH: 64 bit message                @L3A 00263800
         GBLC  &SYSAM64    GLOBAL VARIABLE SET BY SYSSTATE MACRO   @L3A 00274500
         GBLC  &SYSASCE    GLOBAL VARIABLE SET BY SYSSTATE MACRO   @L3A 00285200
         SYSSTATE TEST    TEST HOW MACROS ARE TO EXPAND            @L3A 00295900
         AIF   ('&SYSAM64' EQ 'YES').E3    GO IF AMODE64=YES       @L3A 00306600
         AIF   (('&SYSASCE' NE '') AND ('&SYSASCE' NE 'P')).E4     @L3A 00317300
         AIF   ('&CB' NE '').NJES       NON-VSAM EXPANSION         @L3C 00328000
         AIF   ('&RPL' NE '').JES       VSAM/VTAM MACRO EXPANSION  @L3C 00338700
         IHBERMAC 02                                            XL03123 00350000
         MEXIT                                                          00400000
.NJES    ANOP                      Expand for QSAM                      00437500
&NAME    IHBINNRA &CB,&AREA                                     XL03123 00475000
         AIF   ('&TYPE' EQ '').GET40                            XL03123 00500000
         AIF   ('&TYPE' NE 'P').E2                              XL03123 00525000
         L     15,4(0,1)                LOAD PARALLEL GET RTN ADDR      00550000
         AGO   .GET45                                           XL03123 00575000
.GET40   SLR   15,15                    CLEAR REGISTER             @L1A 00591600
         ICM   15,7,49(1)               LOAD GET ROUTINE ADDR      @L1C 00608200
.GET45   BASR  14,15                    LINK TO GET ROUTINE        @L3C 00629100
         MEXIT                                                          00650000
.JES     ANOP                      Expand for VSAM, VTAM or subsystem   00700000
         AIF   ('&RPL'(1,1) EQ '(').RPLR                                00800000
&NAME    LA    1,&RPL                   LOAD RPL ADDR                   00850000
         AGO   .AREG                                                    00900000
.RPLR    AIF   ('&RPL' EQ '(1)').INR1   Go if already in reg 1     @L3C 00916600
&NAME    LR    1,&RPL(1)                LOAD RPL ADDR VIA REG           00933200
         AGO   .AREG                                               @L3A 00949800
.INR1    AIF   ('&NAME' EQ '').AREG     Go if no label coded       @L3A 00966400
&NAME    DS    0H                                                  @L3A 00983000
.AREG    LA    0,X'00'                  INDICATE GET IN REG             01000000
         L     15,24(1)                 LOAD ACB ADDR FROM RPL          01050000
         L     15,8(,15)             LOAD VSAM INTERFACE MOD ADDR  @L3C 01100000
         BASR  14,15                    LINK TO INTERFACE ROUTINE  @L3C 01150000
         MEXIT                                                  XL03123 01433300
.E2      IHBERMAC 48,,&TYPE             INVALID TYPE OPERAND    XL03123 01452700
         MEXIT                                                     @L3A 01472100
.E3      IGGERMAC 203,GET    THE GET MACRO DOES NOT SUPPORT        @L3A 01477600
         MEXIT                 SYSSTATE AMODE64=YES                @L3A 01483100
.E4      IGGERMAC 204,GET    THE GET MACRO DOES NOT SUPPORT        @L3A 01488600
         MEXIT                 SYSSTATE ASCENV=AR OR ANY           @L3A 01494100
         MEND                                                           01500000
**/ GET:  MACRO KEYS(0:1(DCB_NAME,DCB_ADDR),0:1(AREA_NAME,     /*@L2A*/ 01550000
*             AREA_ADDR),0:1(PDAB_NAME,PDAB_ADDR),RECORD_ADDR, /*@L2A*/ 01600000
*                SYNAD_NAME,EODAD_NAME);                       /*@L2A*/ 01650000
*   ANS('? '!!MACLABEL!!' GETP '!!MACKEYS!!';') RESCAN SKIP;   /*@L2A*/ 01700000
*   %END GET;                                                  /*@L2A*/ 01750000
