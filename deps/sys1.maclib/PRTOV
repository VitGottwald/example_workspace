*********************************************************************** 00004500
*PROPRIETARY V3 STATEMENT                                             * 00009000
*LICENSED MATERIALS - PROPERTY OF IBM                                 * 00013500
*"RESTRICTED MATERIALS OF IBM"                                        * 00018000
*5695-DF1                                                             * 00022500
*COPYRIGHT 1992, 2011 IBM CORP.                                       * 00028100
*END PROPRIETARY V3 STATEMENT                                         * 00033700
.* FUNCTION: Handle end of page with directly-attached printer.    @L2A 00035900
.* STATUS = z/OS 1.13 DFSMS                                        @L2A 00038100
*$L1=31BIT,JDZ1110,,SJPLRK:  INVOCABLE IN 31-BIT MODE              @L1A 00040500
*$01=OY65116,JDZ1110,930625,SJPLPS: INVALID BRANCH FOR NON-PRINTER @01A 00042700
*$L2=64BIT,JDZ1D10,20101117,SVLJH: 64 bit message                  @L2A 00043800
*********************************************************************** 00045000
         MACRO                                                          00050000
&NAME    PRTOV &DCB,&NO,&EXIT                                           00100000
         GBLC  &SYSAM64    AMODE64=YES set by SYSSTATE macro       @L2A 00116600
         GBLC  &SYSASCE    ASCENV=AR or ANY set by SYSSTATE macro  @L2A 00133200
         LCLA  &CTR                                                     00150000
         LCLB  &SWA,&SWB,&ERR                                           00200000
         SYSSTATE TEST    TEST HOW MACROS ARE TO EXPAND            @L2A 00212500
         AIF   ('&SYSAM64' NE 'YES').ChkAR Go if not AMODE64       @L2A 00223400
         IGGERMAC 203,PRTOV        PRTOV doesn't support AMODE64   @L2A 00234300
&ERR     SETB  1                   Indicate error                  @L2A 00245200
.ChkAR   AIF   (('&SYSASCE' EQ '') OR ('&SYSASCE' EQ 'P')).ChkDCB  @L2A 00256100
         IGGERMAC 204,PRTOV        PRTOV doesn't support AR, ANY   @L2A 00267000
&ERR     SETB  1                   Indicate error                  @L2A 00277900
.ChkDCB  AIF   ('&DCB' EQ '').E1   TEST FOR DCB ADDRESS MISSING         00288800
.D1      AIF   ('&NO' EQ '').E3                                         00300000
         AIF   ('&NO' NE '9' AND '&NO' NE '12').E2 TEST FOR INVALID NO. 00350000
         AIF   (&ERR).END                                               00400000
         AIF   ('&EXIT' EQ '').NOEXIT  IF NO EXIT TAKE BRANCH           00450000
&SWA     SETB  ('&DCB'(1,1) NE '(')                                     00500000
&SWB     SETB  ('&EXIT'(1,1) NE '(')                                    00550000
         AIF   ('&DCB'(1,1) EQ '(').REGDCB TEST FOR DCB REG             00600000
&NAME    LA    14,&DCB             LOAD DCB ADDRESS                     00650000
         AGO   .CONTA                                                   00700000
.REGDCB  ANOP                                                           00750000
&NAME    LR    14,&DCB(1)          LOAD DCB ADDRESS                     00800000
.CONTA   ANOP                                                           00850000
&CTR     SETA  54+2*&SWA+2*&SWB                                    @01C 00900000
         TM    17(14),X'48'        IS DEVICE A PRINTER                  00950000
         BC    14,*+&CTR           IF NOT BRANCH AROUND                 01000000
&CTR     SETA  &CTR-8                                                   01050000
         TM    17(14),X'B0'        FURTHER TESTING FOR PRINTER          01095800
         BNZ   *+&CTR              IF NOT PRINTER BRANCH AROUND         01141600
         SLR   1,1                                                 @L1A 01187500
         ICM   1,7,69(14)          LOAD STATUS AREA ADDR FROM DCB  @L2C 01232100
         ICM   1,7,13(1)           LOAD ECB ADDR FROM STATUS AREA  @L2C 01276700
         TM    0(1),X'40'          TEST COMPLETION BIT                  01321300
         BO    *+10                BRANCH IF BIT ON                     01365900
         LA    0,1                 SET WAIT COUNT TO ONE                01410500
         SVC   1                   ISSUE WAIT SVC                       01455100
         AIF   ('&DCB'(1,1) EQ '(').DCBREG   TEST FOR DCB REG           01500000
         LA    1,&DCB              LOAD DCB ADDRESS                     01550000
         AGO   .CONTB                                                   01600000
.DCBREG  LR    1,&DCB(1)           LOAD DCB ADDRESS                     01650000
.CONTB   ANOP                                                           01700000
&CTR     SETA  80-((&NO/3)*16)     COMPUTE MASK FOR TM                  01750000
         TM    44(1),&CTR          TEST BIT FOR OVERFLOW                01800000
&CTR     SETA  255-&CTR                                                 01850000
         AIF   ('&EXIT'(1,1) EQ '(').REGEX                              01900000
         BZ    *+14                IF OFF BRANCH AROUND                 01950000
         NI    44(1),&CTR          IF ON TURN OFF AND                   02000000
         LA    15,&EXIT            LOAD ADDR OF USER RTN                02050000
         AGO   .FINISH                                                  02100000
.REGEX   BZ    *+12                IF OFF BRANCH AROUND                 02150000
         NI    44(1),&CTR          IF ON TURN OFF AND                   02200000
         LR    15,&EXIT(1)         LOAD ADDR OF USER RTN                02250000
.FINISH  BALR  14,15               AND LINK TO IT                       02300000
         AGO   .END                                                     02350000
.NOEXIT  AIF   ('&DCB'(1,1) EQ '(').DCBRG  TEST FOR DCB REG             02400000
&NAME    LA    1,&DCB              LOAD DCB ADDR                        02450000
         AGO   .CONTC                                                   02500000
.DCBRG   ANOP                                                           02550000
&NAME    LR    1,&DCB(1)           LOAD DCB ADDRESS                     02600000
.CONTC   TM    17(1),X'48'         IS DEVICE A PRINTER                  02650000
         BC    14,*+16             IF NOT BRANCH AROUND                 02700000
         TM    17(1),X'B0'         FURTHER TESTING FOR PRINTER          02750000
         BNZ   *+8                 IF NOT BRANCH AROUND                 02800000
&CTR     SETA  80-((&NO/3)*16)     COMPUTE MASK FOR OI                  02850000
         OI    18(1),&CTR          SET BIT ON IN DCB                    02900000
         MEXIT                                                          02950000
.E1      IHBERMAC 6                DCB MISSING                          03000000
&ERR     SETB  1                   INDICATE ERROR                       03050000
         AGO   .D1                 AND CONTINUE DIAGNOSIS               03100000
.E2      IHBERMAC 37,,&NO          &NO INVALID                          03150000
         MEXIT                                                          03200000
.E3      IHBERMAC 3                &NO MISSING                          03250000
.END     MEND                                                           03300000
