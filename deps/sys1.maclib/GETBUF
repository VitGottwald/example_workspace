*/*PROPRIETARY V3 STATEMENT                                          */ 00004000
*/*LICENSED MATERIALS - PROPERTY OF IBM                              */ 00006000
*/*"RESTRICTED MATERIALS OF IBM"                                     */ 00008000
*/*5695-DF1                                                          */ 00010000
*/*COPYRIGHT 1992, 2011 IBM CORP.                                    */ 00012500
*/*END PROPRIETARY V3 STATEMENT                                      */ 00015000
*/*                                                                  */ 00018000
.* FUNCTION: Get a buffer from a BSAM or BPAM buffer pool built    @L3A 00018100
.*      by GETPOOL, BUILD or OPEN.                                 @L3A 00018200
.* NOTES: Call can be in 24-bit or 31-bit but buffer pool is below @L3A 00018300
.*      the line.                                                  @L3A 00018400
.*        You can return the buffer to the pool with FREEBUF or    @L3A 00018500
.*      free the whole pool with FREEPOOL or FREEMAIN.  CLOSE      @L3A 00018600
.*      does not free a BSAM or BPAM buffer pool.                  @L3A 00018700
.* CHANGE ACTIVITY                                                      00018800
.*       DFSMS/MVS 1.1.0                                                00019000
.* $L1=31BIT,JDZ1110,,SJPLRK:  ALLOW INVOCATION IN 31-BIT MODE     @L1A 00019300
.* $L2=64BIT,JDZ1D10,20101116,SVLWER: 64 bit message               @L2A 00019600
         MACRO                                                          00020000
&NAME    GETBUF &DCB,&REG                                               00026600
         GBLC  &SYSAM64    AMODE64=YES set by SYSSTATE macro       @L2A 00033000
         GBLC  &SYSASCE    ASCENV=AR or ANY set by SYSSTATE macro  @L2A 00039400
         LCLB  &ERR(5)     Syntax error switches                        00045800
         SYSSTATE TEST     Test how macros are to expand           @L2A 00055000
&ERR(1)  SETB  ('&SYSAM64' EQ 'YES')        Test AMODE64=YES       @L2A 00064200
&ERR(2)  SETB  (('&SYSASCE' NE '') AND ('&SYSASCE' NE 'P'))        @L2A 00073400
&ERR(3)  SETB  ('&DCB' EQ '')      Set if DCB omitted              @L2A 00082600
&ERR(4)  SETB  ('&REG' EQ '')      Set if register omitted         @L2A 00091800
&ERR(5)  SETB  ('&REG(1)' EQ '0' OR '&REG(1)' EQ '14')             @L2A 00101000
         AIF   (&ERR(1)+&ERR(2)+&ERR(3)+&ERR(4)+&ERR(5) NE 0).TERM @L2A 00110200
&NAME    IHBINNRA &DCB                                                  00120000
         SLR   14,14               CLEAR REGISTER                  @L1A 00140000
         ICM   14,7,21(1)          LOAD BUFCB ADDRESS              @L1C 00160000
         ICM   &REG(1),B'1111',0(14)  IS A BUFFER AVAILABLE        @L2C 00180000
         BZ    *+10                NO,RETURN ZERO                       00200000
         MVC   0(4,14),0(&REG(1))  YES,UPDATE BUFCB ADDR                00220000
         MEXIT                                                          00240000
.TERM    AIF   (NOT &ERR(1)).E2 The GETBUF macro does not support  @L2A 00290000
         IGGERMAC 203,GETBUF      SYSSTATE AMODE64=YES             @L2A 00340000
.E2      AIF   (NOT &ERR(2)).E3 The GETBUF macro does not support  @L2A 00390000
         IGGERMAC 204,GETBUF      SYSSTATE ASCENV=AR or ANY        @L2A 00440000
.E3      AIF   (NOT &ERR(3)).E4 Test for missing DCB               @L3A 00490000
         IHBERMAC 06                                                    00540000
.E4      AIF   (NOT &ERR(4)).E5 Test for missing buffer register   @L2C 00590000
         IHBERMAC 18                                                    00640000
.E5      AIF   (NOT &ERR(5)).EXIT Test for incorrect register      @L2C 00690000
         IHBERMAC 37,,&REG                                              00740000
.EXIT    MEND                                                      @L2C 00790000
