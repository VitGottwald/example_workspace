         MACRO -- IAZTBLE - ADD A TABLE ENTRY                           00010000
&SYMNAM  IAZTBLE &CODE=,           Code to assoc with text             C00020000
               &TEXT=              Text value                           00030000
.* **PROPRIETARY-STATEMENT************************************          00040000
.*                                                           *          00050000
.*   LICENSED MATERIALS-PROPERTY OF IBM                      *          00060000
.*   5650-ZOS Copyright IBM Corp. 2013                       *          00070000
.*                                                           *          00080000
.*   STATUS:  HBB7790                                        *          00090000
.*                                                           *          00100000
.*01* EXTERNAL CLASSIFICATION:  NONE                         *          00110000
.*01* END OF EXTERNAL CLASSIFICATION:                        *          00120000
.*                                                           *          00130000
.* **END-OF-PROPRIETARY-STATEMENT*****************************          00140000
.*************************************************************          00150000
.*                                                           *          00160000
.* FUNCTION:                                                 *          00170000
.*                                                           *          00180000
.*       The IAZTBLE macro is invoked by the IAZTABL macro   *          00190000
.*       to add a table entry value (&CODE) and its          *          00200000
.*       associated text (&TEXTLEN, &TEXT) to the table      *          00210000
.*       being created by the IAZTABL macro.                 *          00220000
.*                                                           *          00230000
.* PARAMETERS: (KEYWORD/POSITIONAL)                          *          00240000
.*                                                           *          00250000
.*       &CODE      - Table code to be added (must be        *          00260000
.*                    from 1 to 255)                         *          00270000
.*       &TEXT      - Text to be associated with the table   *          00280000
.*                    code, enclosed in single quotes        *          00290000
.*       &TEXTLEN   - Length of table code text              *          00300000
.*       &SYMNAM    - Symbolic name for this table code      *          00310000
.*                                                           *          00320000
.* PARAMETERS: (PASSED AS GLOBAL ASSEMBLER SYMBOLS)          *          00330000
.*                                                           *          00340000
.*       &TABLTEXT  - Text array                             *          00350000
.*       &TABLRCT   - Table code count (size of text table)  *          00360000
.*       &TABLINDX  - Table code index array                 *          00370000
.*       &TABLLEN   - Maximum length for table code text     *          00380000
.*                                                           *          00390000
.* NOTES:                                                    *          00400000
.*                                                           *          00410000
.*       The description of the IAZTABL macro should be read *          00420000
.*       first.                                              *          00430000
.*                                                           *          00440000
.* ENVIRONMENT:                                              *          00450000
.*                                                           *          00460000
.*       THIS MACRO IS ONLY INVOKED WITHIN THE IAZTABL MACRO *          00470000
.*       TO BUILD THE ARRAYS USED IN THAT MACRO.             *          00480000
.*                                                           *          00490000
.* REGISTER USAGE (ENTRY/EXIT):                              *          00500000
.*                                                           *          00510000
.*       N/A                                                 *          00520000
.*                                                           *          00530000
.* RETURN CODES (R15 ON EXIT):                               *          00540000
.*                                                           *          00550000
.*       N/A                                                 *          00560000
.*                                                           *          00570000
.* OTHER CONSIDERATIONS:                                     *          00580000
.*                                                           *          00590000
.*       NONE                                                *          00600000
.*                                                           *          00610000
.* CHANGE ACTIVITY:                                          *          00620000
.*                                                           *          00630000
.*                                                           *          00640000
.* A000000-999999    CREATED FOR Z/OS Release 2.1              @Z21LJMO 00650000
.*************************************************************          00660000
.*       DECLARE MACRO VARIABLES.                            *          00670000
.*       (SEE IAZTABL MACRO FOR DESCRIPTIONS OF GLOBALS)     *          00680000
.*************************************************************          00690000
         GBLC  &TABLSCP                                                 00700000
         GBLA  &TABLLEN,&LVL,&NUMLVL,&MAXC                              00710000
         LCLA  &ERRORS,&LVLOC                                           00720000
         LCLC  &TEMPC,&STEXT                                            00730000
.*************************************************************          00740000
.*       INITIALIZE PARAMETER ERROR COUNTER.                 *          00750000
.*************************************************************          00760000
&ERRORS  SETA  0                                                        00770000
.*************************************************************          00780000
.*       Set global variables based on NUMLVL                *          00790000
.*************************************************************          00800000
.*                                                                      00810000
         AIF   (&NUMLVL EQ 1).LVL1,                                    C00820000
               (&NUMLVL EQ 2).LVL2,                                    C00830000
               (&NUMLVL EQ 3).LVL3,                                    C00840000
               (&NUMLVL EQ 4).LVL4                                      00850000
         MNOTE 8,'IAZTABL: PARAMETER ERROR. LVLLEN= GREATER'            00860000
         MNOTE 8,' THAN 4 NOT VALID.'                                   00870000
         AGO   .LVLOAD                                                  00880000
.LVL1    ANOP                                                           00890000
         GBLC  &TABLTXT(256)                                            00900000
         GBLA  &TABLRCT(1),&TABLINDX(256),&TABLLVL(256)                 00910000
         AGO   .LVLOAD                                                  00920000
.LVL2    ANOP                                                           00930000
         GBLC  &TABLTXT(512)                                            00940000
         GBLA  &TABLRCT(2),&TABLINDX(512),&TABLLVL(512)                 00950000
         AGO   .LVLOAD                                                  00960000
.LVL3    ANOP                                                           00970000
         GBLC  &TABLTXT(768)                                            00980000
         GBLA  &TABLRCT(3),&TABLINDX(768),&TABLLVL(768)                 00990000
         AGO   .LVLOAD                                                  01000000
.LVL4    ANOP                                                           01010000
         GBLC  &TABLTXT(1024)                                           01020000
         GBLA  &TABLRCT(4),&TABLINDX(1024),&TABLLVL(1024)               01030000
.LVLOAD  ANOP                                                           01040000
.*************************************************************          01050000
.*       VALIDATE REASON CODE. IF INVALID, ISSUE MESSAGE,    *          01060000
.*       INCREMENT ERROR COUNT, AND SKIP CODE DEFINED CHECK  *          01070000
.*       (WHICH WOULD GIVE AN ARRAY BOUNDS ERROR).           *          01080000
.*************************************************************          01090000
         AIF   (&CODE GE 0 AND &CODE LE 255).HTXTRCV                    01100000
         MNOTE 4,'IAZTBLE: &CODE IS AN INVALID TABLE CODE.'             01110000
         MNOTE 4,'  TABLE CODES MUST BE FROM 1 to 255.'                 01120000
         MNOTE 4,'  TABLE CODE &CODE AND ITS TEXT IGNORED.'             01130000
&ERRORS  SETA  &ERRORS+1                                                01140000
         AGO   .HTXTRCU            SKIP DEFINED CHECK                   01150000
.HTXTRCV ANOP                      TABLE CODE VALID                     01160000
.*************************************************************          01170000
.*       MAKE SURE TABLE CODE HAS NOT BEEN DEFINED BEFORE.   *          01180000
.*       IF IT HAS, ISSUE MESSAGE AND INCREMENT ERROR COUNT. *          01190000
.*************************************************************          01200000
&LVLOC   SETA  (&LVL-1)*&MAXC                                           01210000
         AIF   (&TABLINDX(&LVLOC+&CODE+1) EQ 0).HTXTRCU                 01220000
         MNOTE 4,'IAZTBLE: TABLE CODE &CODE HAS ALREADY BEEN'           01230000
         MNOTE 4,'  DEFINED AS &TABLTXT(&TABLINDX(&CODE+1)+1).'         01240000
         MNOTE 4,'  NEW TEXT FOR TABLE CODE &CODE IGNORED.'             01250000
&ERRORS  SETA  &ERRORS+1                                                01260000
.HTXTRCU ANOP                      TABLE CODE UNDEFINED                 01270000
.*************************************************************          01280000
.*       MAKE SURE &TEXT IS IN SINGLE QUOTES. IF NOT, ISSUE  *          01290000
.*       MESSAGE AND INCREMENT ERROR COUNT.                  *          01300000
.*************************************************************          01310000
         AIF   ('&TEXT'(1,1) EQ '''' AND                               C01320000
                '&TEXT'(K'&TEXT,1) EQ '''').HTXTTOK                     01330000
         MNOTE 4,'IAZTBLE: TEXT FOR TABLE CODE &CODE IS NOT'            01340000
         MNOTE 4,'  ENCLOSED IN SINGLE QUOTES AS REQUIRED.'             01350000
         MNOTE 4,'  TABLE CODE &CODE AND ITS TEXT IGNORED.'             01360000
&ERRORS  SETA  &ERRORS+1                                                01370000
.HTXTTOK ANOP                      TABLE CODE TEXT OK                   01380000
.*************************************************************          01390000
.*       STRIP THE SINGLE QUOTES OFF THE TABLE CODE TEXT AND *          01400000
.*       PUT IT IN &STEXT (THE SINGLE QUOTES ALLOW COMMAS TO *          01410000
.*       BE IN THE TABLE CODE TEXT).                         *          01420000
.*************************************************************          01430000
&STEXT   SETC  '&TEXT'(2,K'&TEXT-2)                                     01440000
.*************************************************************          01450000
.*       MAKE SURE THE LENGTH OF THE TEXT IS NOT GREATER     *          01460000
.*       THAN THE LIMIT (&TABLLEN). IF THE TEXT IS TOO LONG, *          01470000
.*       ISSUE A MESSAGE AND TRUNCATE THE TEXT.              *          01480000
.*************************************************************          01490000
         AIF   (K'&STEXT LE &TABLLEN).HTXTLOK                           01500000
         MNOTE 4,'IAZTBLE: TEXT FOR TABLE CODE &CODE TOO LONG.'         01510000
         MNOTE 4,'  MUST NOT EXCEED &TABLLEN CHARACTERS.'               01520000
         MNOTE 4,'  TEXT TRUNCATED TO &TABLLEN CHARACTERS.'             01530000
&STEXT   SETC  '&STEXT'(1,&TABLLEN)                                     01540000
.HTXTLOK ANOP                      LENGTH OK                            01550000
.*************************************************************          01560000
.*       IF THERE WERE ANY ERRORS, IGNORE THIS TABLE CODE    *          01570000
.*************************************************************          01580000
         AIF   (&ERRORS GT 0).HTXTEND                                   01590000
.*************************************************************          01600000
.*       ADD THIS TABLE CODE TO THE TABLE AS FOLLOWS:        *          01610000
.*         1) INCREMENT THE NUMBER OF TABLE CODES IN THE TEXT*          01620000
.*            ARRAY (&TABLRCT) FOR THE GIVEN LEVEL.          *          01630000
.*         2) STORE TABLE CODE TEXT AT END OF TEXT ARRAY     *          01640000
.*            (I.E. IN &TABLTXT(&TABLRCT)) FOR THAT LEVEL.   *          01650000
.*         3) THE INDEX TO BE GENERATED FOR THIS TABLE CODE  *          01660000
.*            IS THE INDEX OF THE TABLE CODE TEXT IN THE TEXT*          01670000
.*            ARRAY (I.E. &TABLRCT) MINUS ONE. STORE THIS IN *          01680000
.*            THE INDEX ARRAY IN THE SLOT RESERVED FOR THIS  *          01690000
.*            TABLE CODE (INDEX &CODE+1) IN THE GIVEN LEVEL. *          01700000
.*************************************************************          01710000
         LCLC  &SYMNAMLV                                                01720000
&SYMNAMLV SETC '&SYMNAM'.'LV'                                           01730000
&LVLOC   SETA  ((&LVL-1)*&MAXC)                                         01740000
&SYMNAMLV SETC '&SYMNAM'.'LC'                                           01750000
&SYMNAMLV SETC '&SYMNAM'.'RC'                                           01760000
&TABLRCT(&LVL) SETA  &TABLRCT(&LVL)+1                                   01770000
&TABLTXT(&LVLOC+(&TABLRCT(&LVL))) SETC '&STEXT'                         01780000
&TABLINDX(&LVLOC+&CODE+1) SETA (&TABLRCT(&LVL)-1)                       01790000
&TABLLVL(&LVLOC+(&TABLRCT(&LVL))) SETA &LVL                             01800000
.*                                                                      01810000
.HTXTEND ANOP                      END OF IAZTABL                       01820000
         MEND                                                           99999999
