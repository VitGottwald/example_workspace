          MACRO                                                         00050000
&NAME    CALLRTM &TYPE=,&COMPCOD=,&ASID=,&TCB=0,&DUMP=,&STEP=NO,       X00076500
               &DUMPOPT=,&DUMPOPX=,&CCA=,&REASON=,&RETRY=YES,          X00103000
               &TTOKEN=,&SYSTEM=YES,&SRBIDTOKEN=                   @L5C 00120601
.********************************************************************   00138200
.*                                                                      00156000
.*01* MACRO NAME: CALLRTM                                               00159800
.*                                                                      00163600
.*01* DESCRIPTIVE NAME: CALL RECOVERY TERMINATION MANAGER               00167400
.*                                                                      00171200
.*01* PROPRIETARY STATEMENT:                                            00175000
.*                                                                      00178800
.*            5650-ZOS                                                  00182601
.*            LICENSED MATERIALS - PROPERTY OF IBM                      00186401
.*            COPYRIGHT IBM CORP. 1980,2013                             00190201
.*                                                                      00198000
.* STATUS  = HBB7790                                                    00204001
.*                                                                      00210000
.*01* FUNCTION: PROVIDES THE INTERFACE TO REQUEST THE ABNORMAL          00210100
.*              TERMINATION OF A TASK OR ADDRESS SPACE                  00210200
.*                                                                      00210300
.*01* EXTERNAL CLASSIFICATION:                                          00210400
.*                                                                      00210500
.*02* GUPI: BASE                                                        00210600
.*                                                                      00210700
.*02* NONE: FIELDS                                                      00210800
.*          CCA                                                         00210900
.*                                                                      00211000
.*01* END OF EXTERNAL CLASSIFICATION:                                   00211100
.*                                                                      00211200
.*01* MODULE TYPE:  ASSEM MACRO                                         00211300
.*                                                                      00211400
.*02*   PROCESSOR:  ASSEM                                               00211500
.*                                                                      00211600
.*01* COMPONENT:  SCRTM                                                 00211700
.*                                                                      00211800
.*01* DISTRIBUTION LIBRARY: AMACLIB                                     00211900
.*                                                                      00212000
.*01* CHANGE ACTIVITY:                                                  00212100
.*             APAR OZ25000                                    @ZA25000 00214000
.*             MVS/SYSTEM PRODUCT RELEASE 2  6/25/79           @G382P2F 00216000
.*             MVS/SYSTEM PRODUCT HBB2102 03/27/81             @G860P1C 00217000
.*$L1=AR      ,HBB3310,860115,PDPY: ACCESS REGISTER SUPPORT        @L1A 00217500
.*$D1=DCR0060 ,HBB3310,861013,PDT9: SUPPORT FOR SABEND             @D1A 00217600
.*$L2=DATASPAC HBB3310 861029 PDFP: PROPAGATION OF DATA SPACE STORAGE   00217700
.*                                 RANGES FOR ABDUMP                    00217800
.*$P1=PC40728 ,HBB3310,870225,PDT9: FIX DUMP BIT DEFAULT           @P1A 00217900
.*$D2=DCR0263 ,HBB3310,870603,PDFP: ACCESS REGISTER SUPPORT FOR ACR     00221200
.*$D3=DCR0006 ,JBB3320,870715,PDCK: CALLRTM RETRY=NO SUPPORT       @D3A 00222800
.*$L3=COUPLRTM,HBB4410,871113,PDT9: CALLRTM TTOKEN SUPPORT         @L3A 00223600
.*$P2=PR20012 ,HBB6601,950912,PDKD: SYSTEM=NO KEYWORD TO ALLOW USER     00224700
.*                                  COMPLETION CODE                @P2A 00225800
.*$01=OW17397  HBB5520 950119 PDKD: TTOKEN SUPPORT FOR XABTERM     @01A 00226300
.*$P3=PJK1357  HBB7709 031226 PDXB: RELATIVE BRANCH                     00226600
.*$L4=MEXXXXX  HBB7740 060408 PDXB: MORE RELATIVE BRANCH                00226700
.*   =ME10058  HBB7750 070613 PDKD: COPYRIGHT UPDATE                    00226801
.*$L5=SRBABTM  HBB7790 110201 PDKD: TYPE=SRBTERM                        00226901
.********************************************************************   00227000
         LCLA  &CTR,&CTRLNK,&CTRACR,&CTRREAS                   @G860P1C 00228000
         LCLB  &B(26),&C(9),&D(9)                                  @L5C 00238009
         LCLC  &A                                              @G381P2F 00248000
         LCLC  &BR                                                 @P3A 00254500
         GBLA  &SYSALVL                                            @P3A 00261000
         SYSSTATE TEST                                             @P3A 00267500
&BR      SETC  'B'                                                 @P3A 00274000
         AIF   (&SYSALVL LT 1).NOTALVL1                            @P3A 00280500
&BR      SETC  'J'                                                 @P3A 00287000
.NOTALVL1 ANOP                                                     @P3A 00293500
&NAME    DS    0H                                                       00300000
&B(1)    SETB  ('&DUMP' NE 'NO')                                   @P1C 00400000
&B(2)    SETB  ('&STEP' EQ 'YES')                                       00450000
&B(3)    SETB  ('&DUMPOPT' NE '' OR '&DUMPOPX' NE '')              @L2C 00500000
&B(4)    SETB  ('&COMPCOD' EQ '')                                       00550000
&B(5)    SETB  ('&ASID' NE '')                                          00600000
&B(6)    SETB  ('&TYPE' EQ 'DATERR')                                    00650000
&B(7)    SETB  ('&TYPE' EQ 'RESTART')                                   00700000
&B(8)    SETB  ('&TYPE' EQ 'MACHCK')                                    00750000
&B(9)    SETB  ('&TYPE' EQ 'SVCERR')                                    00800000
&B(10)   SETB  ('&TYPE' EQ 'STERM')                            @G382P2F 00850000
&B(11)   SETB  ('&TYPE' EQ 'ABTERM' AND '&ASID' EQ '' AND              X00870000
                '&RETRY' NE 'NO')                                  @D3C 00890000
&B(22)   SETB  ('&TYPE' EQ 'ABTERM' AND '&ASID' EQ '' AND              X00910000
                '&RETRY' EQ 'NO')                                  @D3A 00930000
&B(12)   SETB  ('&TYPE' EQ 'MEMTERM')                                   00950000
&B(13)   SETB  ('&TYPE' EQ 'PROGCK')                                    00960000
&B(14)   SETB  ('&TYPE' EQ 'ACR')                                       00970000
&B(15)   SETB  ('&TYPE' EQ 'ACRDISP')                                   00980000
&B(16)   SETB  ('&TYPE' EQ 'ACRLM')                                     00990000
&B(17)   SETB  ('&TYPE' EQ 'ABTERM' AND '&ASID' NE '' AND              X00992000
                '&RETRY' NE 'NO')                                  @D3C 00994000
&B(23)   SETB  ('&TYPE' EQ 'ABTERM' AND '&ASID' NE '' AND              X00996000
                '&RETRY' EQ 'NO')                                  @D3A 00998000
&B(24)   SETB  ('&TTOKEN' NE '')                                   @L3A 00999000
&B(18)   SETB  ('&TYPE' EQ 'RMGRCML')                          @G381P2F 01000000
&B(19)   SETB  ('&TYPE' EQ 'ITERM')  SET IF ITERM SPECIFIED    @G38EP1F 01010000
&B(20)   SETB  ('&TYPE' EQ 'SABEND') SET IF SABEND SPECIFIED       @D1A 01011000
&B(21)   SETB  ('&REASON' NE '')                                   @D1A 01012000
&B(25)   SETB  ('&TYPE' EQ 'SRBTERM' AND '&RETRY' EQ 'NO')         @L5A 01012104
&B(26)   SETB  ('&TYPE' EQ 'SRBTERM' AND '&RETRY' NE 'NO')         @L5A 01012204
&C(1)    SETB  ('&TYPE' NE '')       SET IF TYPE SPECIFIED     @G38EP1F 01013000
&C(2)    SETB  ('&COMPCOD' NE '')    SET IF COMPCOD SPECIFIED  @G38EP1F 01016000
&C(3)    SETB  ('&ASID' NE '')       SET IF ASID SPECIFIED     @G38EP1F 01019000
&C(4)    SETB  ('&TCB' NE '')        SET IF TCB SPECIFIED      @G38EP1F 01022000
&C(5)    SETB  ('&DUMP' NE '')       SET IF DUMP SPECIFIED     @G38EP1F 01025000
&C(6)    SETB  ('&STEP' NE '')       SET IF STEP SPECIFIED     @G38EP1F 01028000
&C(7)    SETB  ('&DUMPOPT' NE '' OR '&DUMPOPX' NE '')  SET IF DUMPOPT  X01030000
                                     OR DUMPOPX SPECIFIED          @L2C 01032000
&C(8)    SETB  ('&CCA' NE '')        SET IF CCA SPECIFIED      @G38EP1F 01034000
&C(9)    SETB  ('&SRBIDTOKEN' NE '') SET IF SRBIDTOKEN SPECIFIED   @L5A 01035002
&D(1)    SETB  ('&TYPE' EQ '')    SET IF TYPE NOT SPECIFIED    @G38EP1F 01037000
&D(2)    SETB  ('&COMPCOD' EQ '') SET IF COMPCOD NOT SPECIFIED @G38EP1F 01040000
&D(3)    SETB  ('&ASID' EQ '')    SET IF ASID NOT SPECIFIED    @G38EP1F 01043000
&D(4)    SETB  ('&TCB' EQ '')     SET IF TCB NOT SPECIFIED     @G38EP1F 01046000
&D(5)    SETB  ('&DUMP' EQ '')    SET IF DUMP NOT SPECIFIED    @G38EP1F 01049000
&D(6)    SETB  ('&STEP' EQ '')    SET IF STEP NOT SPECIFIED    @G38EP1F 01052000
&D(7)    SETB  ('&DUMPOPT' EQ '') SET IF DUMPOPT NOT SPECIFIED @G38EP1F 01055000
&D(8)    SETB  ('&CCA' EQ '')     SET IF CCA NOT SPECIFIED     @G38EP1F 01058000
&D(9)    SETB  ('&SRBIDTOKEN' EQ '') SET IF SRBIDTOKEN NOT SPEC    @L5A 01058102
.* THE FOLLOWING EXPRESSIONS CALCULATE THE OFFSET INTO THE ENTRY POINT  01059000
.* BRANCH TABLE FOR IEAVTRT1 OR IEAVTRG1 MODULES.                  @D3A 01060000
&CTRLNK  SETA  4+4*&B(6)+8*&B(7)+12*&B(8)+16*&B(9)+20*&B(10)+24*&B(11)+C01061000
               28*&B(12)+32*&B(13)+36*&B(14)+36*&B(15)+36*&B(16)+40*&B(C01064000
               17)+44*&B(18)+48*&B(19)+52*&B(20)+56*&B(22)+60*&B(23)+64C01066001
               *&B(25)+68*&B(26)                                   @L5C 01068004
&CTRACR  SETA  8*&B(14)+2*&B(15)+4*&B(16)                               01070000
         AIF   (&CTRLNK EQ 4).ERR1                                      01100000
.* VALIDATE THE USE OF THE RETRY OPERAND                           @D3A 01110400
.* NOTE THAT AN INVALID RETRY PARM IS TREATED AS IF RETRY=YES WERE      01110510
.* SPECIFIED (ERR19 RETURNS TO OKERR19)                                 01110610
         AIF   ('&RETRY' NE 'NO' AND '&RETRY' NE 'YES').ERR19      @D3A 01131200
         AIF   ('&RETRY' EQ 'YES').OKERR19                         @L5A 01141710
.* RETRY=NO IS ONLY SUPPORTED FOR TYPE=ABTERM AND TYPE=SRBTERM     @L5A 01141806
         AIF   ('&TYPE' NE 'ABTERM' AND '&TYPE' NE 'SRBTERM').ERR18    C01141906
                                                                   @L5C 01142006
.OKERR19 ANOP                                                      @D3A 01142210
.* VALIDATE THE USE OF THE SYSTEM OPERAND                          @P2A 01142300
         AIF   ('&SYSTEM' NE 'YES' AND '&SYSTEM' NE 'NO').ERR22    @P2A 01142400
.* VALIDATE THE USE OF THE TTOKEN OPERAND                          @L3A 01142700
         AIF   ('&TTOKEN' EQ '').NOTTKN                            @L3A 01143800
         AIF   ('&TYPE' NE 'ABTERM').ERR20                         @L3A 01144900
&A       SETC  'TCB'                                               @L3A 01148200
         AIF   ('&TCB' NE '' AND '&TCB' NE '0').ERR21              @L3A 01149300
.NOTTKN  ANOP                                                      @L3A 01150400
         AIF   ('&TYPE' EQ 'MACHCK').LINKAGE                            01152000
         AIF   ('&TYPE' EQ 'ACR').LINKAGE                               01154000
         AIF   ('&TYPE' EQ 'ACRDISP').LINKAGE                           01154400
         AIF   ('&TYPE' EQ 'ACRLM').ACRLM                               01154800
.* ABTERM, MEMTERM, STERM, RMGRCML, AND SRBTERM REQUIRE A COMPLETION    01154903
.* CODE TO BE SPECIFIED.  THEY ALL FLOW TO SKPTST                       01155003
         AIF   ('&TYPE' EQ 'ABTERM' AND &B(4)).ERR2                     01156000
         AIF   ('&TYPE' EQ 'MEMTERM' AND &B(4)).ERR2                    01160000
         AIF   ('&TYPE' EQ 'STERM' AND &B(4)).ERR2             @G381P2F 01160500
         AIF   ('&TYPE' EQ 'RMGRCML' AND &B(4)).ERR2           @G381P2F 01161000
         AIF   ('&TYPE' EQ 'SRBTERM' AND &B(4)).ERR2               @L5A 01161103
         AIF   ('&TYPE' EQ 'DATERR' AND '&ASID' EQ '').ERR11   @G381P2F 01161500
         AIF   ('&TYPE' EQ 'ITERM').ITERM                      @G38EP1F 01161700
         AIF   ('&TYPE' EQ 'SABEND').SABEND                        @D1A 01161800
         AIF   ('&TYPE' NE 'RMGRCML').SKPTST                   @G381P2F 01162000
.*  THIS SEGMENT OF CODE IS PERFORMED IF TYPE=RMGRCML              @P2A 01162200
&A       SETC  'ASID'                                          @G381P2F 01162500
         AIF   (&B(5)).ERR12                                   @G381P2F 01163000
&A       SETC  'TCB'                                           @G381P2F 01163500
         AIF   ('&TCB' NE '0').ERR12                           @G381P2F 01164000
&A       SETC  'DUMP'                                          @G381P2F 01164500
         AIF   ('&DUMP' NE '').ERR12                               @D1C 01165000
&A       SETC  'STEP'                                          @G381P2F 01165500
         AIF   ('&STEP' NE 'NO').ERR12                         @G381P2F 01166000
&A       SETC  'DUMPOPT/DUMPOPX'                                   @L2C 01166500
         AIF   (&B(3)).ERR12                                   @G381P2F 01167000
&A       SETC  'CCA'                                           @G381P2F 01167500
         AIF   ('&CCA' NE '').ERR12                            @G381P2F 01168000
         AIF   ('&COMPCOD'(1,1) EQ '(').CCREG                  @G382P2F 01168500
         AIF   (T'&COMPCOD NE 'N').CCSYM                       @G382P2F 01169100
         AIF   (&COMPCOD GT 4095).ERR3                         @G382P2F 01169700
.CCSYM   ANOP                                                  @G382P2F 01170300
         LA    0,&COMPCOD           LOAD REG0 WITH CC          @G382P2F 01170900
         SLL   0,12(0)              SHIFT COMPCODE TO SYSTEM       @P2C 01171500
         AGO   .LINKAGE                                                 01172100
.CCREG   ANOP                                                  @G382P2F 01172700
         AIF   (T'&COMPCOD(1) NE 'N').CCREG2                   @G382P2F 01173300
         AIF   (&COMPCOD(1) EQ 0).CCREG3                       @G382P2F 01173900
.CCREG2  ANOP                                                  @G382P2F 01174500
         LR    0,&COMPCOD(1)        LOAD REG0 WITH CC          @G382P2F 01175100
.CCREG3  ANOP                                                  @G382P2F 01175700
         SLL   0,20(0)              CLEAR HIGH ORDER BYTE      @G382P2F 01176300
         SRL   0,8(0)               SHIFT COMPCODE TO SYSTEM       @P2C 01176900
         AGO   .LINKAGE                                                 01177500
.*  THIS SEGMENT OF CODE IS PERFORMED IF TYPE=SABEND.  VALID       @D1A 01177600
.*  PARAMETERS ARE DUMP, COMPCOD, AND REASON.                      @D1A 01177700
.SABEND  ANOP                                                      @D1A 01177800
&A       SETC  'ASID'                                              @D1A 01177900
         AIF   (&C(3)).ERR16        ASID NOT A VALID SABEND OPTION @D1A 01178000
&A       SETC  'TCB'                                               @D1A 01178100
         AIF   ('&TCB' NE '0').ERR16   TCB NOT VALID SABEND OPTION @D1A 01178200
&A       SETC  'STEP'                                              @D1A 01178300
         AIF   ('&STEP' NE 'NO').ERR16  STEP INVALID SABEND OPTION @D1A 01178400
&A       SETC  'DUMPOPT'                                           @D1A 01178500
         AIF   (&C(7)).ERR16        DUMPOPT INVALID SABEND OPTION  @D1A 01178600
&A       SETC  'CCA'                                               @D1A 01178700
         AIF   (&C(8)).ERR16        CCA IS INVALID SABEND OPTION   @D1A 01178800
         AIF   ('&DUMP' NE 'YES' AND '&DUMP' NE 'NO'                   X01178900
               AND '&DUMP' NE '').ERR8                             @D1A 01179000
.SABRTRN ANOP                                                      @D1A 01179100
         AIF   ('&DUMP' EQ 'NO' OR '&DUMP' EQ '').SABNODP          @P1A 01179200
&CTR SETA  128                                                     @P1A 01179300
.SABNODP ANOP                                                      @P1A 01179400
&CTR SETA  &CTR+64*&B(2)+4*&B(21)                                  @P1C 01179500
         AIF   (&B(4)).SABDFLG                                     @D1A 01179600
         AIF   (T'&COMPCOD NE 'N').SABDRCC                         @D1A 01179700
         AIF   ('&COMPCOD'(1,1) EQ '(').SABDRCC                    @D1A 01179800
         AIF   (&COMPCOD GT 4095).ERR3                             @D1A 01179900
.SABDRCC ANOP                                                      @D1A 01180000
         LR    15,1                 SAVE ADDRESS OF PARAMETER LIST @D1A 01180100
         IHBINNRA  &COMPCOD         LOAD REG1 WITH CC              @D1A 01180200
         AIF   ('&COMPCOD'(1,1) NE '(').SABDCC                     @D1A 01180300
         SLL   1,20                 CLEAR GARBAGE FROM INPUT           X01180400
                                    REGISTER                       @D1A 01180500
         SRL   1,8                  SHIFT COMPCODE TO SYSTEM       @P2C 01180600
         AGO   .SABDCC0                                            @D1A 01180700
.SABDCC  ANOP  ,                                                   @D1A 01180800
         SLL   1,12                 SHIFT COMPCODE TO SYSTEM       @P2C 01180900
.SABDCC0 ANOP  ,                                                   @D1A 01181000
         AIF   (&CTR EQ 0).NOFLAGS  NO REASON CODE OR DUMP?        @D1A 01181100
         LA    0,&CTR.(,0)          SET FLAGS FROM MACRO PARMS     @D1A 01181200
         SLL   0,24(0)              SHIFT FLAGS TO HIGH ORDER BYTE @D1A 01181300
         OR    1,0                  OR FLAGS BITS WITH COMPCODE    @D1A 01181400
.NOFLAGS ANOP  ,                                                        01181500
         O     1,0(0,15)            OR FLAGS/COMPCOD WITH PARMLIST @D1A 01181600
         ST    1,0(0,15)            STORE INTO PARMLIST            @D1A 01181700
         LR    1,15                 RESET R1 TO POINT TO PARM LIST @D1A 01181800
         AGO   .SABDRC                                             @D1A 01181900
.SABDFLG ANOP                                                      @D1A 01182000
         AIF   (&CTR EQ 0).SABDRC   NO REASON CODE OR DUMP?        @D1A 01182100
         LA    0,&CTR.(,0)          DUMP/STEP/DUMPOPTS/ASID        @D1A 01182200
         SLL   0,24(0)              MOVE FLAGS INTO POSITION       @D1A 01182300
         O     0,0(0,1)             OR FLAGS WITH COMPCODE         @D1A 01182400
         ST    0,0(0,1)             STORE FLAGS, COMPCODE          @D1A 01182500
.SABDRC  ANOP                                                      @D1A 01182600
.*  FIRST PROCESS REASON KEYWORD BEFORE BUILDING THE LINKAGE       @D1A 01182700
         AIF   ('&REASON' EQ '').LINKAGE                           @D1A 01182800
         AIF   ('&REASON'(1,1) EQ '(').SABDRRC                     @D1A 01182900
         AIF   (&SYSALVL GT 0).ALVL1A                              @L4A 01183000
         CNOP  0,4                  ALIGN ON WORD BOUNDARY         @D1A 01183100
         &BR   *+8                  BRANCH AROUND CONSTANT         @P3C 01183200
         DC    AL4(&REASON)         REASON CODE CONSTANT           @D1A 01183300
         L     15,*-4               LOAD REG15 WITH REASON CODE    @D1A 01183400
         AGO   .ALVL1B                                             @L4A 01183500
.ALVL1A  ANOP                                                      @L4A 01183600
         L     15,=A(REASON)        LOAD REG15 WITH REASON CODE    @L4A 01183700
.ALVL1B  ANOP                                                      @L4A 01183800
         ST    15,4(0,1)            STORE REASON CODE IN PARM LIST @D1A 01183900
         AGO   .SARCEND                                            @D1A 01184000
.SABDRRC ANOP                                                      @D1A 01184100
         ST    &REASON(1),4(0,1)    STORE REASON CODE IN PARM LIST @D1A 01184200
.SARCEND ANOP                                                      @D1A 01184300
         AGO   .LINKAGE                                            @D1A 01184400
.* THIS SECTION IS FOR TYPE=ITERM ONLY. ONLY THE COMPCOD PARAMETER      01184510
.* IS ALLOWED (BUT NOT REQUIRED SINCE A CALLER MIGHT HAVE SET UP        01184608
.* THE COMPCODE AND FLAGS IN THE REQUIRED PARM LIST POINTED TO BY       01184708
.* REGISTER 1)                                                     @L5C 01184808
.ITERM   ANOP                                                  @G38EP1F 01184900
&A       SETC  'ASID'                                          @G38EP1F 01185000
         AIF   (&C(3)).ERR14   ASID OPERAND IS INVALID         @G38EP1F 01185100
&A       SETC  'DUMPOPT/DUMPOPX'                                   @L2C 01185200
         AIF   (&C(7)).ERR14   DUMPOPT OPERAND IS INVALID      @G38EP1F 01185300
&A       SETC  'CCA'                                           @G38EP1F 01185400
         AIF   (&C(8)).ERR14   CCA OPERAND IS INVALID          @G38EP1F 01185500
&A       SETC  'REASON'                                            @L1A 01185600
         AIF   ('&REASON' NE '').ERR14 REASON NOT VALID            @L1A 01185700
.* PUT THE COMPLETION CODE IN R1.                              @G38EP1F 01185800
         AIF   ('&COMPCOD' EQ '').LINKAGE                          @L1A 01185900
         AIF   ('&COMPCOD'(1,1) EQ '(').ITREG                  @G38EP1F 01186000
         AIF   (T'&COMPCOD NE 'N').ITSYM                       @G38EP1F 01186100
         AIF   (&COMPCOD GT 4095).ERR3                         @G38EP1F 01186200
.ITSYM   ANOP                                                  @G38EP1F 01186300
         LA    0,&COMPCOD           LOAD REG0 WITH CC              @L1C 01186400
         SLL   0,12(0)              SHIFT COMPCODE TO SYSTEM       @P2C 01186500
         AGO   .ITRC                                           @G860P1C 01186600
.ITREG   ANOP                                                  @G38EP1F 01186700
         AIF   (T'&COMPCOD(1) NE 'N').ITREG2                   @G38EP1F 01186800
         AIF   (&COMPCOD(1) EQ 0).ITREG3                           @D1C 01186900
.ITREG2  ANOP                                                  @G38EP1F 01187000
         LR    0,&COMPCOD(1)        LOAD REG0 WITH CC              @L1C 01187100
.ITREG3  ANOP                                                  @G38EP1F 01187200
         SLL   0,20(0)              CLEAR HIGH ORDER BYTE          @L1C 01187300
         SRL   0,8(0)               SHIFT COMPCODE TO SYSTEM       @P2C 01187400
.ITRC    ANOP                                                  @G860P1C 01187500
         ST    0,0(1,0)             SAVE CC IN CALLER PARMS        @L1A 01187600
         AGO   .LINKAGE                                            @L1C 01187700
.*  THIS SEGMENT OF CODE IS PERFORMED FOR TYPE = ABTERM, SRBTERM,       01187803
.*  MEMTERM, STERM, AND DATERR.  IT SUPPORTS USER (SYSTEM=NO)           01187904
.*  COMPLETION CODES FOR ABTERM, SRBTERM, AND MEMTERM REQUESTS.         01188004
.*  THE FOLLOWING LOGIC DOES NOT CHECK FOR SOME IMPOSSIBLE              01188104
.*  COMBINATIONS SUCH AS SRBTERM OR MEMTERM WITH A DUMP REQUEST.        01188204
.*  SUCH COMBINATIONS ARE IMPLICITLY IGNORED BY THE PROCESSING          01188304
.*  LOGIC                                                               01188404
.SKPTST  ANOP                                                  @G381P2F 01188500
         AIF   ('&DUMP' NE 'YES' AND '&DUMP' NE 'NO'                   X01188600
               AND '&DUMP' NE '').ERR8                             @D1C 01188800
.STPCK   AIF   ('&STEP' NE 'YES' AND '&STEP' NE 'NO').ERR9              01189100
.STPCTN  ANOP                                                           01190000
.* &CTR ACCUMULATES THE FLAGS THAT WILL BE PLACED INTO THE HIGH BYTE    01200003
.* OF REGISTER 1.  THEY INCLUDE: DUMP, STEP, DUMPOPT/DUMPOPTX,          01201003
.* COMPCOD, ASID, REASON, AND TTOKEN.  NOTE THAT RETRY IS NOT           01202003
.* REPRESENTED BY A FLAG HERE AND IS SIGNALED BY THE CHOICE OF          01203004
.* ENTRY POINT IN IEAVTRT1                                              01203104
&CTR     SETA  128*&B(1)+64*&B(2)+32*&B(3)+16*&B(4)+8*&B(5)+4*&B(21)+1*X01205004
               &B(24)                                              @L3C 01216600
         AIF   ('&COMPCOD' EQ ''  AND '&REASON' NE '').ERR15   @G860P1C 01230000
         AIF   ('&DUMPOPT' NE ''  AND '&DUMPOPX' NE '').ERR17      @L2A 01240000
         AIF   (&B(4)).FLAGS                                            01250000
         AIF   (T'&COMPCOD NE 'N').REGCC                                01300000
         AIF   ('&COMPCOD'(1,1) EQ '(').REGCC                           01310000
         AIF   (&COMPCOD GT 4095).ERR3                                  01350000
.REGCC   ANOP                                                           01360000
         IHBINNRA  &COMPCOD         LOAD REG1 WITH CC                   01400000
         AIF   ('&COMPCOD'(1,1) NE '(').CC                              01501300
.* THE FOLLOWING CODE RUNS WHEN THE VALUE WAS ALREADY IN A REG     @P2A 01501400
         SLL   1,20                 CLEAR GARBAGE FROM INPUT           X01501500
                                    REGISTER...                @G860P1S 01501700
         AIF   ('&SYSTEM' EQ 'NO').REGCC2                          @P2A 01501800
         SRL   1,8                  SHIFT COMPCODE TO SYSTEM       @P2C 01501900
         AGO   .CC0                                            @G860P1S 01502300
.REGCC2  ANOP                                                      @P2A 01502400
         SRL   1,20                 COMPCODE BACK TO USER POSITION @P2A 01502500
         AGO   .CC0                                                @P2A 01502600
.CC      ANOP  ,                                               @G860P1S 01502700
.* .CC IS THE CASE WHERE THE VALUE WAS NOT ALREADY IN A REG        @P2A 01502800
         AIF   ('&SYSTEM' EQ 'NO').CLEANCC                         @P2A 01502900
         SLL   1,12                 SHIFT COMPCODE TO SYSTEM       @P2C 01503000
         AGO   .CC0                                                @P2A 01503100
.CLEANCC SLL   1,20                 CLEAR HIGH ORDER 5 NIBBLES     @P2A 01503200
         SRL   1,20                 COMPCODE BACK TO USER POSITION @P2A 01503300
.CC0     ANOP  ,                                               @G860P1S 01503400
         LA    0,&CTR.(,0)    DUMP/STEP/DUMPOPTS/ASID/REASON   @G860P1C 01503500
         SLL   0,24(0)              SHIFT TO HIGH ORDER BYTE            01503700
         OR    1,0                  OR IN WITH COMPCODE                 01503800
         AGO   .CC1                                                     01503900
.FLAGS   ANOP                                                           01504000
         LA    1,&CTR.(,0)          DUMP/STEP/DUMPOPTS/ASID             01506000
         SLL   1,24(0)              SHIFT TO HIGH ORDER BYTE            01506400
.CC1     ANOP                                                           01518700
         AIF   ('&TYPE' EQ 'SRBTERM').SRBTERM                      @L5A 01518804
         AIF   ('&TYPE' NE 'MEMTERM' AND '&TYPE' NE 'ABTERM' AND       C01519100
               '&TYPE' NE 'DATERR').DUMPOPT                    @G381P2F 01520000
.TCB     AIF   ('&TYPE' NE 'ABTERM').ASID                      @ZA25000 01520900
         AIF   ('&TTOKEN' NE '').TTKN                              @L3A 01521300
         AIF   ('&TCB' NE '0' AND '&TCB'(1,1) NE '(').ERR6     @ZA25000 01521800
         AIF   ('&TCB' EQ '0').TCB0                            @ZA25000 01522700
         IHBINNRA  ,&TCB                                       @ZA25000 01523600
         AGO   .ASID                                           @ZA25000 01524500
.TTKN    IHBINNRA ,&TTOKEN                                         @01M 01524600
         LR    15,0                 COPY TTOKEN PTR SINCE REGISTER 0   X01524700
                                      CANNOT BE USED AS A BASE REG @01M 01524800
         MVC   0(16,13),0(15)       MOVE TTOKEN INTO USER-PROVIDED     X01524900
                                      WORK AREA                    @01M 01525000
         AGO   .ASID                                               @01A 01525100
.TCB0    LA    0,0(,0)              LOAD REG0-TCB=0            @ZA25000 01525400
.ASID    AIF   ('&ASID' EQ '(2)').DUMPOPT                      @ZA25000 01526300
         AIF   ('&ASID' EQ '').DUMPOPT                                  01527200
         AIF   ('&ASID'(1,1) NE '(').CKNUM                              01528100
         LR    2,&ASID(1)           LOAD REG2 WITH ASID                 01529000
         AGO   .DUMPOPT                                        @ZA25000 01529900
.CKNUM   AIF   (T'&ASID NE 'N').ASYM                                    01531900
         AIF   (&ASID GT 32765).ERR4                                    01533900
.ASYM    ANOP                                                           01538500
         AIF   (&SYSALVL GT 0).ALVL2A                              @L4A 01538600
         CNOP  0,4                                                      01539000
         &BR   *+6                  BRANCH AROUND CONSTANTS        @P3C 01539100
         DC    AL2(&ASID)                                               01539200
         LH    2,*-2                LOAD REG2 WITH ASID                 01539500
         AGO   .ALVL2B                                             @L4A 01539600
.ALVL2A  ANOP                                                      @L4A 01539700
         LHI   2,&ASID              LOAD REG2 WITH ASID            @L4A 01539800
.ALVL2B  ANOP                                                      @L4A 01539900
         AGO   .DUMPOPT                                                 01550900
.* TYPE=SRBTERM REQUIRES AN SRBIDTOKEN                             @L5A 01551004
.SRBTERM AIF   ('&SRBIDTOKEN' EQ '').ERR23                         @L5A 01551104
         AIF   ('&SRBIDTOKEN' EQ '(0)').RCODE                      @L5A 01552007
         AIF   ('&SRBIDTOKEN'(1,1) NE '(').IDTOKNA                 @L5A 01553005
         LR    0,&SRBIDTOKEN(1)                                    @L5A 01554007
         AGO   .RCODE                                              @L5A 01555004
.IDTOKNA LA    0,&SRBIDTOKEN                                       @L5A 01556007
         AGO   .RCODE                                              @L5A 01557004
.DUMPOPT AIF   ('&DUMPOPT' EQ '').DUMPOPX                          @L2C 01619800
         AIF   ('&DUMPOPT'(1,1) NE '(').ERR7                            01629800
         AIF   ('&DUMPOPT' EQ '(3)').RCODE                     @G860P1C 01630200
         LR    3,&DUMPOPT(1)        LOAD REG3 WITH DUMPOPTS             01631800
         AGO   .RCODE                                              @L2A 01631900
.DUMPOPX AIF   ('&DUMPOPX' EQ '').RCODE                            @L2A 01632000
         AIF   ('&DUMPOPX'(1,1) NE '(').ERR7                       @L2A 01632100
         AIF   ('&DUMPOPX' EQ '(3)').RCODE                         @L2A 01632200
         LR    3,&DUMPOPX(1)        LOAD REG3 WITH DUMPOPXS        @L2A 01632300
.RCODE   ANOP                                                  @G860P1C 01632400
.*  FIRST PROCESS REASON KEYWORD BEFORE BUILDING THE LINKAGE   @G860P1C 01632500
         AIF   ('&REASON' EQ '').RCEND                         @G860P1C 01632600
         AIF   ('&REASON'(1,1) EQ '(').RCREG                   @G860P1C 01632700
         AIF   (&SYSALVL GT 0).ALVL3A                              @L4A 01632800
         CNOP  0,4                  ALIGN ON WORD BOUNDARY     @G860P1C 01633700
         &BR   *+8                  BRANCH AROUND CONSTANT         @P3C 01633800
         DC    AL4(&REASON)         REASON CODE CONSTANT       @G860P1C 01633900
         L     6,*-4                LOAD REG6 WITH REASON CODE @G860P1C 01634000
         AGO   .ALVL3B                                             @L4A 01634100
.ALVL3A  ANOP                                                      @L4A 01634200
         L     6,=A(&REASON)        LOAD REG6 WITH REASON CODE     @L4A 01634300
.ALVL3B  ANOP                                                      @L4A 01634500
         AGO   .RCEND                                          @G860P1C 01634600
.RCREG   ANOP                                                  @G860P1C 01634700
         AIF   ('&REASON' EQ '(6)').RCEND                      @G860P1C 01634800
         LR    6,&REASON(1)         LOAD REG6 WITH REASON CODE @G860P1C 01634900
.RCEND   ANOP                                                  @G860P1C 01635000
.LINKAGE ANOP                                                  @G860P1C 01635100
         L     15,X'10'(,0)         GET CVT ADDRESS            @G860P1C 01635200
         L     15,CVTBTERM-CVTMAP(,15) ADDR OF CVTBTERM TABLE           01635300
         L     15,&CTRLNK.(,15)     ADDRESS OF ENTRY POINT              01635800
         AIF   (&CTRACR EQ 0).LNKCNT                                    01637900
         LA    0,&CTRACR.(,0)       ACR TYPE IN REG 0                   01687900
.LNKCNT  ANOP                                                      @D1C 01712900
         AIF   ('&TYPE' EQ 'SABEND').BSSMLNK                       @D1A 01737900
         AIF   ('&TYPE' NE 'ACR').NRMLNK                           @D1C 01762900
         BR    15                                                       01787900
         AGO   .END                                                     01837900
.NRMLNK  ANOP                                                      @D1C 01871200
         BALR  14,15                                               @D1C 01904500
         AGO   .END                                                     01937900
.BSSMLNK ANOP                                                      @D1A 01950400
         BASSM 14,15                BYPASS GLUE MODULE, IEAVTRT1   @D1A 01962900
         AGO   .END                                                @D1A 01975400
.ACRLM   AIF   ('&CCA'(1,1) NE '(').ERR10                               01987900
         L     10,LCCACPUS-LCCA&CCA      POINTER TO LCCA SAVEAREAS      02037900
         L     10,WSACACR-WSAC(,10)      POINTER TO ACR SAVEAREA        02087900
         STD   0,0(,10)             STORE FLOATING POINT REGS           02137900
         STD   2,8(,10)                                                 02187900
         STD   4,16(,10)                                                02237900
         STD   6,24(,10)                                                02287900
         STM   0,15,32(10)          STORE REGS 0-15                     02337900
         STAM  0,15,96(10)          STORE ARS 0-15                 @D2A 02362900
         L     15,X'10'(,0)         GET CVT ADDRESS                     02387900
         L     15,CVTBTERM-CVTMAP(,15) ADDR OF CVTBTERM TABLE           02417900
         L     15,&CTRLNK.(,15)     ADDRESS OF ENTRY POINT              02447900
         LA    0,&CTRACR.(,0)                                           02477900
         BALR  14,15                                                    02537900
         L     10,LCCACPUS-LCCA(,10)    POINTER TO LCCA SAVEAREAS       02587900
         L     10,WSACACR-WSAC(,10)     POINTER TO ACR SAVEAREA         02637900
         LD    0,0(,10)              RESTORE FLOATING POINT REGS        02687900
         LD    2,8(,10)                                                 02737900
         LD    4,16(,10)                                                02787900
         LD    6,24(,10)                                                02837900
         LAM   0,15,96(10)          RESTORE ARS 0-15               @D2A 02862900
         LM    0,15,32(10)          RESTORE REGS 0-15                   02887900
         AGO   .END                                                     02937900
.ERR1    MNOTE 12,'TYPE OPERAND REQUIRED'                      @G381P2F 02987900
         MEXIT                                                          03017900
.ERR2    MNOTE 12,'COMPCOD OPERAND REQUIRED'                   @G381P2F 03047900
         MEXIT                                                          03137900
.ERR3    MNOTE 12,'COMPCOD OPERAND EXCEEDS SPECIFIED LIMITS'            03187900
         MEXIT                                                          03237900
.ERR4    MNOTE 12,'ASID OPERAND EXCEEDS SPECIFIED LIMITS'               03287900
         MEXIT                                                          03337900
.ERR6    MNOTE 12,'TCB OPERAND INVALID FORM'                            03387900
         MEXIT                                                          03437900
.ERR7    MNOTE 12,'DUMPOPT/DUMPOPX OPERAND INVALID'                @L2C 03487900
         MEXIT                                                          03537900
.ERR8    MNOTE *,'DUMP OPERAND INVALID - YES ASSUMED'                   03587900
         AIF   ('&TYPE' EQ 'SABEND').SABRTRN                       @D1A 03612900
         AGO   .STPCK                                                   03637900
.ERR9    MNOTE *,'STEP OPERAND INVALID - NO ASSUMED'                    03687900
         AGO   .STPCTN                                                  03737900
.ERR10   MNOTE 12,'CCA OPERAND INVALID FORM'                            03787900
         MEXIT                                                          03837900
.ERR11   MNOTE 12,'ASID OPERAND REQUIRED'                      @G381P2F 03887900
         MEXIT                                                          03937900
.ERR12   MNOTE 12,'&A OPERAND INVALID WITH TYPE=RMGRCML'       @G381P2F 03987900
         MEXIT                                                 @G381P2F 04037900
.ERR13   MNOTE 12,'&A OPERAND IS REQUIRED WITH TYPE=ITERM'     @G38EP1F 04087900
         MEXIT                                                 @G38EP1F 04137900
.ERR14   MNOTE 12,'&A OPERAND IS INVALID WITH TYPE=ITERM'      @G38EP1F 04187900
         MEXIT                                                 @G38EP1F 04237900
.ERR15 MNOTE 12,'COMPCOD OPERAND REQUIRED WITH REASON OPERAND' @G860P1C 04254500
         MEXIT                                                 @G860P1C 04271100
.ERR16   MNOTE 12,'&A OPERAND IS INVALID WITH TYPE=SABEND'         @D1A 04274400
         MEXIT                                                     @D1A 04277700
.ERR17   MNOTE 12,'DUMPOPT AND DUMPOPX ARE MUTUALLY EXCLUSIVE'     @L2A 04281000
         MEXIT                                                     @L2A 04284300
.ERR18   MNOTE 12,'RETRY=NO IS INVALID WITH TYPE=&TYPE'            @D3A 04285000
         MEXIT                                                     @D3A 04285700
.ERR19   MNOTE *,'RETRY OPERAND INVALID - YES ASSUMED'             @D3A 04286400
         AGO   .OKERR19                                            @D3A 04287100
.ERR20   MNOTE 12,'THE TTOKEN PARAMETER MAY ONLY BE SPECIFIED WITH TYPEX04287200
               =ABTERM'                                            @L3A 04287300
         MEXIT                                                     @L3A 04287400
.ERR21   MNOTE  12,'THE TTOKEN PARAMETER MAY NOT BE SPECIFIED WITH THE X04287500
               &A PARAMETER'                                       @L3A 04287600
         MEXIT                                                     @L3A 04287700
.ERR22   MNOTE  12,'SYSTEM OPERAND NOT SPECIFIED AS YES OR NO'     @P2A 04337700
         MEXIT                                                     @P2A 04387700
.ERR23   MNOTE  12,'TYPE=SRBTERM REQUIRES SRBIDTOKEN'              @L5A 04387804
         MEXIT                                                     @L5A 04387904
.END     MEND                                                           04437700
