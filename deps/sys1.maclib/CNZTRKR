* %/*  THIS MAKES THE BAL VERSION LOOK LIKE A PL/S MACRO COMMENT        00010000
         MACRO                                                          00020000
.** START OF SPECIFICATIONS ******************************************* 00030000
.*                                                                    * 00040000
.*01* MACRO NAME = CNZTRKR                                            * 00050000
.*                                                                    * 00060000
.*01* DESCRIPTIVE NAME = Tracking Facility Interface Macro        @P3C* 00070000
.*01* ACRONYM = n/a                                                   * 00080000
.*                                                                    * 00090000
.**01* PROPRIETARY STATEMENT=                                         * 00100000
.* **PROPRIETARY_STATEMENT********************************************* 00110000
.*                                                                    * 00120000
.*                                                                    * 00130000
.*  LICENSED MATERIALS - PROPERTY OF IBM                              * 00140000
.*  5694-A01 COPYRIGHT IBM CORP. 2003, 2008                           * 00150000
.*                                                                    * 00160000
.*  STATUS= HBB7750                                                   * 00170000
.*                                                                    * 00180000
.* **END_OF_PROPRIETARY_STATEMENT************************************** 00190000
.*                                                                    * 00200000
.*01* EXTERNAL CLASSIFICATION: PI                                     * 00210000
.*01* END OF EXTERNAL CLASSIFICATION:                                 * 00220000
.*                                                                    * 00230000
.*01* FUNCTION =                                                      * 00240000
.*       The CNZTRKR macro is used to invoke the                  @P3C* 00250000
.*       Tracking facility.                                       @P3C* 00260000
.*                                                                @L1A* 00260100
.*       Note: GTZTRACK is the preferred method of invoking the   @L1A* 00261000
.*       tracker. While CNZTRKR continues to work, it will no     @L1A* 00262000
.*       longer be enhanced and the additional data recorded by   @L1A* 00263000
.*       GTZTRACK is not available via the CNZTRKR interface.     @L1A* 00264000
.*                                                                @L1A* 00266000
.*                                                                    * 00270000
.*01* DSECT NAME:     n/a                                             * 00280000
.*01* COMPONENT:      Consoles (SC1CK)                            @P3C* 00290000
.*                                                                    * 00300000
.*01* EYE CATCHER:    n/a                                             * 00310000
.*02*    OFFSET:      n/a                                             * 00320000
.*02*    LENGTH:      n/a                                             * 00330000
.*                                                                    * 00340000
.*01* STORAGE:                                                        * 00350000
.*02*    SUBPOOL:     n/a                                             * 00360000
.*02*    KEY:         n/a                                             * 00370000
.*02*    DATA SPACE:  n/a                                             * 00380000
.*02*    RESIDENCY:   n/a                                             * 00390000
.*                                                                    * 00400000
.*01* SIZE:           n/a                                             * 00410000
.*                                                                    * 00420000
.*                                                                    * 00430000
.*01* OPERATION =                                                     * 00440000
.*       This macro checks its input parameter for syntactic          * 00450000
.*       correctness, issuing error or warning messages if            * 00460000
.*       necessary. If no parameter error was detected, the           * 00470000
.*       address of the parameter list, mapped by CNZTRPL, is         * 00480000
.*       stored in register one and a PC is performed to invoke       * 00490000
.*       the CNZTRKR processor.                                       * 00500000
.*                                                                    * 00510000
.*                                                                    * 00520000
.*01* INVOCATION =                                                    * 00530000
.*       CNZTRKR is invoked with a single positional parameter        * 00540000
.*       specifying the address of the Tracking Request Parameter     * 00550000
.*       List (TRPL), mapped by CNZTRPL.                              * 00560000
.*                                                                    * 00570000
.*       For BAL programs, CNZTRKR is invoked in one of the           * 00580000
.*       following ways:                                              * 00590000
.*                                                                    * 00600000
.*               1) CNZTRKR (x)     Where x is a resister, or a       * 00610000
.*                                  symbol equated to a register      * 00620000
.*                                                                    * 00630000
.*               2) CNZTRKR label   Where label is the symbolic       * 00640000
.*                                  address of the TRPL parameter     * 00650000
.*                                  list.                             * 00660000
.*                                                                    * 00670000
.*                                                                    * 00680000
.*       For PL/X programs, CNZTRKR is invoked as follows:            * 00690000
.*                                                                    * 00700000
.*               ?CNZTRKR(label)    Where label is the symbolic       * 00710000
.*                                  address of the TRPL parameter     * 00720000
.*                                  list.                             * 00730000
.*                                                                    * 00740000
.*01* DEPENDENCIES = None                                             * 00750000
.*                                                                    * 00760000
.*                                                                    * 00770000
.*01* RETURN And REASON Codes =                                       * 00780000
.*       The return codes will be in register 15 and the reason       * 00790000
.*       codes will be in register 0 and are documented in mapping    * 00800000
.*       macro CNZTRPL.                                               * 00810000
.*                                                                    * 00820000
.*                                                                    * 00830000
.*01* CREATED-BY     = n/a                                            * 00840000
.*                                                                    * 00850000
.*01* POINTED-TO-BY  = n/a                                            * 00860000
.*                                                                    * 00870000
.*01* LOCATION       = n/a                                            * 00880000
.*                                                                    * 00890000
.*01* SERIALIZATION  = n/a                                            * 00900000
.*                                                                    * 00910000
.*01* CHANGE ACTIVITY -                                               * 00920000
.*  $MAC(CNZTRKR),COMP(SC1CK): Tracking Request Invocation            * 00930000
.*  $L0=CNIDTRAK, JBB7727, 011225, PDSS: Console Id Tracking          * 00940000
.*  $P1=PYN0778,  JBB7727, 021204, PDED: Is PC in place?              * 00950000
.*  $P2=ME01663,  HBB7720, 040831, PDSW: Reversioning Cancelled       * 00960000
.*  $P3=ME11994,  HBB7750, 071130, PDSS: Generalize tracker           * 00970000
.*  $L1=GENTRKR,  HBB7790, 110711, PDSS: Generic Tracker              * 00971000
.*                                                                    * 00980000
.*01* MESSAGES=                                                       * 00990000
.*       The CNZTRKR macro will issue the following messages upon     * 01000000
.*       detection of syntax errors:                                  * 01010000
.*                                                                    * 01020000
.*02*  WARNING = (Severity 4)                                         * 01030000
.*                                                                    * 01040000
.*    "CNZTRKR(X,Y) HAS TOO MANY ARGUMENTS. ARGUMENTS IGNORED         * 01050000
.*      AFTER X."                                                     * 01060000
.*                                                                    * 01070000
.*02*  ERROR = (SEVERITY 12)                                          * 01080000
.*                                                                    * 01090000
.*    "NO TEXT GENERATED: CNZTRKR PARAMETER MISSING."                 * 01100000
.*                                                                    * 01110000
.*                                                                    * 01120000
.*01* SYSTEM BUILD INFORMATION:                                       * 01130000
.*  Distribution Library = SYS1.MACLIB                                * 01140000
.*                                                                    * 01150000
.*                                                                    * 01160000
.** END OF SPECIFICATIONS ********************************************* 01170000
.***START OF CHANGE ACTIVITY******************************************* 01180000
.* CHANGE DESCRIPTIONS:                                               * 01190000
.* --------------------                                               * 01200000
.* A - Add                                                            * 01210000
.* D - Delete                                                         * 01220000
.* C - Change                                                         * 01230000
.* M - Move                                                           * 01240000
.* ----------------------------------------------------------------   * 01250000
.* A - Add a check to see if the console PC table is in place before  * 01260000
.*     attempting to PC to the tracker code.                      @P1A* 01270000
.* C - Comment only changes to generalize the tracker function    @P3A* 01271000
.* C - Comment only changes to the prolog                         @L1A* 01271100
.*                                                                    * 01272000
.***END OF CHANGE ACTIVITY********************************************* 01273000
&LABEL   CNZTRKR &PARAM                                                 01274000
.********************************************************************** 01275000
.* Make sure that the parameter was not blank. If blank, issue an     * 01276000
.* MNOTE of severity 12 and exit the macro. If the parameter was not  * 01277000
.* blank, go on to check for a user specified label.                  * 01278000
.********************************************************************** 01279000
         AIF   ('&PARAM' NE '' AND '&PARAM(1)' NE '' AND               +01280000
                '&PARAM(1,1)' NE '').LABEL                              01290000
         MNOTE 12,'NO TEXT GENERATED: CNZTRKR PARAMETER MISSING.'       01300000
         AGO   .EXIT                                                    01310000
.********************************************************************** 01320000
.* See if the user specified a label on the macro invocation. If so,  * 01330000
.* insert the label with an EQU into the source stream. Otherwise     * 01340000
.* just continue.                                                     * 01350000
.********************************************************************** 01360000
.LABEL   ANOP                                                           01370000
         AIF   ('&LABEL' EQ '').NOLABEL                                 01380000
&LABEL   EQU   *                                                        01390000
.********************************************************************** 01400000
.* See if the parameter started with a parenthesis. If it did, go     * 01410000
.* handle the enclosed argument. If not, the user specified a label   * 01420000
.* so generate instructions to perform parameter passing of the TRPL  * 01430000
.* parameter list.                                                    * 01440000
.********************************************************************** 01450000
.NOLABEL ANOP                                                           01460000
         AIF   ('&PARAM'(1,1) EQ '(').REG                               01470000
         LA    1,&PARAM            Get address of TRPL                  01480000
         AGO   .GET@                                                    01490000
.********************************************************************** 01500000
.* CNZTRKR was invoked with a register specified as an argument.      * 01510000
.* Generate instructions to perform parameter passing of the TRPL     * 01520000
.* parameter list.                                                    * 01530000
.********************************************************************** 01540000
.REG     ANOP                                                           01550000
         LR    1,&PARAM            Put address of parameter list       +01560000
                                   in register one                      01570000
.********************************************************************** 01580000
.* See if more than one argument was specified. If so, issue a        * 01590000
.* warning indicating that the extra argument(s) have been ignored.   * 01600000
.* Otherwise, just continue.                                          * 01610000
.********************************************************************** 01620000
         AIF   (N'&PARAM EQ 1 AND N'&PARAM(1) EQ 1).GET@                01630000
         MNOTE 4,'CNZTRKR&PARAM HAS TOO MANY ARGUMENTS. ARGUMENTS IGNOR+01640000
               ED AFTER &PARAM(1,1).'                                   01650000
.********************************************************************** 01660000
.* Generate code to perform a space switch PC to the CNZTRKR          * 01670000
.* processor.                                                         * 01680000
.********************************************************************** 01690000
.GET@    ANOP                                                           01700000
         L     15,16(0,0)          Get address of CVT from PSA    @P1A  01710000
         L     15,100(,15)         Get address of UCM from CVT    @P1A  01720000
         L     15,240(,15)         Get address of UCMF from UCM   @P1A  01730000
         LTR   15,15               Has the UCMF been created?     @P1A  01740000
         BZ    *+12                No, skip tracking              @P1A  01750000
         TM    26(15),X'80'        Test UCMFPCOK                  @P1A  01760000
         BO    *+16                Go do the PC                   @P1A  01770000
         LA    15,4                Set tracking not available     @P1A  01780000
         LA    0,8                 Set facility not active        @P1A  01790000
         B     *+24                Bypass the PC                  @P1A  01800000
         L     15,16(0,0)          Get address of CVT from PSA          01810000
         L     15,772(,15)         Get address of SFT from CVT          01820000
         LA    14,22               Get CNZTRKR index into linkage table 01830000
         AL    14,100(,15)         Point register 14 to CNZTRKR entry   01840000
         PC    0(14)               PC to CNZTRKR processor              01850000
.EXIT    ANOP                                                           01860000
         MEND                                                           01870000
.********************************************************************** 01880000
.*                     PL/X Section                                   * 01890000
.********************************************************************** 01900000
**/                                                                     01910000
*CNZTRKR:  MACRO;                                                       01920000
*            ANS('?'!!'CNZTRKRP'!!MACLIST!!';'); /* Invoke PL/X version 01930000
*                                                   of CNZTRKR       */ 01940000
*          %END;                                                        01950000
