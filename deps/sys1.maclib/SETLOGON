* /* START OF SPECIFICATIONS ****                                               
*                                                                               
*01*  MODULE-NAME = SETLOGON                                                    
*                                                                               
*01*  DESCRIPTIVE-NAME = SETLOGON USER MACRO                                    
*                                                                               
*01*  COPYRIGHT = NONE                                                          
*                                                                               
*                                                                               
*01*  FUNCTION = THE PURPOSE OF THIS MACRO IS TO INDICATE TO THE SYSTEM         
*     THAT NO MORE LOGON REQUESTS ARE TO BE QUEUED FOR THE APPLICATION          
*     VIA THE SPECIFIED ACB                                                     
*                                                                               
*01*  NOTES = THIS OPTION ALLOWS AN APPLICATION ALREADY RECEIVING               
*     REQUESTS FOR LOGON TO CLOSE DOWN IN A WAY THAT IS LEAST LIKELY TO         
*     CAUSE OPERATOR CONCERN.  IF THIS FACILITY IS NOT AVAILABLE, THERE         
*     IS POTENTIALLLY A LONG PERIOD OF TIME (FROM THE TIME THE SYSTEM           
*     ACCEPTS THE LOGON REQUEST TO WHEN THE ACB IS CLOSED) DURING WHICH         
*     FURTHER LOGON REQUESTS ARE QUEUED,  ONLY TO BE REJECTED WHEN THE          
*     ACB IS CLOSED                                                             
*                                                                               
*     THIS MACRO GENERATES NO CODE ITSELF, BUT INVOKES AN INNER                 
*     MACRO (IHBRDWRA) TO GENERATE CODE TO MODIFY THE RPL AND LINK              
*     TO THE INTERFACE ROUTINE THAT WILL BEGIN PROCESSING THE REQUEST.          
*                                                                               
*02*    CHARACTER-CODE-DEPENDENCIES = NONE                                      
*                                                                               
*02*    DEPENDENCIES= THE ACB MUST BE OPENED AND BELONG TO THE JOB STEP         
*       THAT ISSUED THE SETLOGON                                                
*                                                                               
*02*    RESTRICTIONS = NONE                                                     
*                                                                               
*02*    REGISTER-CONVENTIONS = NONE                                             
*                                                                               
*02*    PATCH-LABEL = NONE                                                      
*                                                                               
*01*  MODULE-TYPE = MACRO                                                       
*                                                                               
*02*    PROCESSOR = ASSEM-370R                                                  
*                                                                               
*02*    MACRO-SIZE = FOUR STATEMENTS                                            
*                                                                               
*02*    ATTRIBUTES = LIST-EXEC                                                  
*                                                                               
*01*  ENTRY-POINT = NA                                                          
*                                                                               
*01*  EXIT-NORMAL = NA                                                          
*                                                                               
*01*  EXIT-ERROR = NA                                                           
*                                                                               
*01*  EXTERNAL-REFERENCES = NONE                                                
*                                                                               
*01*  TABLES = NONE                                                             
*                                                                               
*01*  INNER-MACROS = IHBRDWRA                                                   
*                                                                               
*01*  SYSTEM-MACROS = NONE                                                      
*                                                                               
*01*  CHANGE-ACTIVITY = AS-FOLLOWS                                              
*                       NEW MACRO - FIRST RELEASE   31  DOS/VS                  
* APAR OZ67334  OPTCD CODED ON SETLOGON MACRO                 @AZ67334          
*                                                   03  OS/VS1                  
*                                                   03  OS/VS2                  
* $MAC(SETLOGON),COMP(API),PROD(VTAM): SETLOGON User Macro                      
*                                                                               
* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS                         
* ---- -------- ------- ------ ------  -------------------------                
* $B1= AP@@@CLD HVT3401 890502 496698: Persistent LU-LU Sessions                
* $I1= FG@@@MST GENRES  921109 879315: Generic Resource                         
* $H1= VS@@@MST VSE/ESA 930513 544872: VSE/ESA CONVERSION                       
* $D1= IT@@@MST TSOGR   941115 062811: Add PARMS=MAXSESS for GNAMEADD           
*                                                                               
* Flag Reason   Release  Date   Origin    Description                           
* ---- -------- -------- ------ --------  ----------------------                
* $R1= X16MNPRT CSV1R6   031006 GIERLACH: MNPS Predatory Takeover               
*                                                                               
**** END OF SPECIFICATIONS ***/                                                 
*                                                                               
         MACRO                                                                  
&NAME    SETLOGON   &RPL=,&ACB=,&ECB=,&EXIT=,&POST=,                   *        
               &OPTCD=,&AREA=,&AREALEN=,&AAREA=,&AAREALN=,&RECLEN=,    *        
               &ARECLEN=,&OBSQVAL=,&IBSQVAL=,&USENSEO=,                *        
               &RESPOND=,&CONTROL=,&CHNGDIR=,&SSENSEO=,&STYPE=,        *        
               &RTYPE=,&IBSQAC=,&OBSQAC=,&CHAIN=,&NIB=,                *        
               &BRACKET=,&SSENSMO=,&SIGDATA=,&BRANCH=,&PARMS=      @I1C         
.*A-000000-999999                                              @DL03HLQ         
         LCLC  &PSTIMER                    TIMER VALUE        @B1A @H1A         
         LCLC  &MAXSESS                    MAXSESS VALUE           @D1A         
         LCLC  &FORCETK                    FORCETKO VALUE          @R1A         
         LCLA  &I                          LOOP COUNTER       @B1A @H1A         
         LCLA  &L                          LENGTH OF OPERAND  @B1A @H1A         
.*                                                                 @H1A         
         AIF   (N'&SYSLIST EQ 0).BYPASS    POSITIONAL OPERANDS INVALID          
       MNOTE 4,'APPARENT POSITIONAL OPERANDS HAVE BEEN DETECTED '               
       MNOTE 4,'NONE ARE EXPECTED AND ALL WILL BE IGNORED'                      
.BYPASS  ANOP                                                                   
.*                                                                 @B1A         
.*       DIAGNOSE ALL KEYWORDS IN PARMS SUBLIST FOR VTAM VALIDITY  @B1A         
.*                                                                 @B1A         
&I       SETA  0                           INITIALIZE COUNTER      @B1A         
         AIF   ('&PARMS' EQ '').P0DONE     NO PARMS SO BRANCH      @B1A         
.PARMS   ANOP                              LOOP THROUGH PARMS      @B1A         
&I       SETA  &I+1                        INCREMENT LOOP COUNTER  @B1A         
         AIF   (&I GT N'&PARMS).P0DONE     NO MORE PARMS           @B1A         
         AIF   ('&PARMS(&I)'(1,7) EQ 'PSTIMER').PSTIME  PSTIMER?   @B1A         
         AIF   ('&PARMS(&I)'(1,7) EQ 'MAXSESS').MAXSES  MAXSESS?   @D1A         
         AIF   ('&PARMS(&I)'(1,8) EQ 'FORCETKO').FRCTKO FORCETKO?  @R1A         
         MNOTE ,'PARMS SUBLIST KEYWORD IS INVALID '                @B1A         
         AGO   .PARMS                                              @B1A         
.PSTIME  ANOP                                                      @B1A         
&L       SETA  K'&PARMS(&I)-8              LENGTH OF OPERAND       @B1A         
&PSTIMER SETC  '&PARMS(&I)'(9,&L)          STORE TIMER VALUE PARM  @B1A         
         AGO   .PARMS                                              @B1A         
.*                                                                              
.* *************** CHECK FOR MAXSESS KEYWORD **************************         
.MAXSES  ANOP                                                      @D1A         
&L       SETA  K'&PARMS(&I)-8              LENGTH OF OPERAND       @D1A         
&MAXSESS SETC  '&PARMS(&I)'(9,&L)          STORE MAXSESS VALUE     @D1A         
         AGO   .PARMS                                              @D1A         
.*                                                                              
.* *************** CHECK FOR FORCETKO KEYWORD *************************         
.FRCTKO  ANOP                                                      @R1A         
&L       SETA  K'&PARMS(&I)-9              LENGTH OF OPERAND       @R1A         
&FORCETK SETC  '&PARMS(&I)'(10,&L)         STORE FORCETKO VALUE    @R1A         
         AGO   .PARMS                                              @R1A         
.*                                                                              
.P0DONE  ANOP                                                      @B1A         
&NAME    IHBRDWRA   RPL=&RPL,ACB=&ACB,ECB=&ECB,EXIT=&EXIT,             *        
               OPTCD=&OPTCD,AREA=&AREA,POST=&POST,                     *        
               AREALEN=&AREALEN,RECLEN=&RECLEN,ARECLEN=&ARECLEN,       *        
               RESPOND=&RESPOND,CONTROL=&CONTROL,CHNGDIR=&CHNGDIR,     *        
               SSENSEO=&SSENSEO,STYPE=&STYPE,RTYPE=&RTYPE,             *        
               IBSQAC=&IBSQAC,OBSQAC=&OBSQAC,CHAIN=&CHAIN,             *        
               OBSQVAL=&OBSQVAL,IBSQVAL=&IBSQVAL,USENSEO=&USENSEO,     *        
               AAREA=&AAREA,AAREALN=&AAREALN,IHBRW=21,                 *        
               BRACKET=&BRACKET,SSENSMO=&SSENSMO,SIGDATA=&SIGDATA,     *        
               BRANCH=&BRANCH,PSTIMER=&PSTIMER,NIB=&NIB,           @I1C*        
               MAXSESS=&MAXSESS,                                   @D1A*        
               FORCETKO=&FORCETK                                   @R1A         
         MEND                                                                   
