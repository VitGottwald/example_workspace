         MACRO                                                                  
         IST6MCRJ &CONVID=,&SENSE=,&BRANCH=,&ECB=,&EXIT=,&OPTCD=,      X        
               &QUALIFY=,&SESSID=,&SESSIDL=,&DEACTYP=,&CGID=,          X        
               &INVALP1=,&INVALP2=,&VTRINA=,&VTRINL=,&VTROUTA=,        X        
               &VTROUTL=                                           @D1C         
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCRJ                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: CONTROL = REJECT PROCESSOR                  */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1988, 1994.       */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */         
.* /*      CONTROL=REJECT PARAMETER ON THE APPCCMD MACRO.            */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS:                                              */         
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCUI                       */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: NONE                                     */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*    05/27/86 - ZERO OUT THE QUALIFY FIELD               @P030472*/         
.* /*                                                                */         
.* /* $MAC(IST6MCRJ),COMP(API),PROD(VTAM): CONTROL = REJECT PROCESSOR*/         
.* /*                                                                */         
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */         
.* /* ---- -------- ------- ------ ------  ------------------------- */         
.* /* $F1= FS1@@CLD HVT3303 880425 255471: SYNCPOINT SUPPORT         */         
.* /* $Q1= P062052  HVT3303 881025 255471: SKIP THE MVC OF SESSID    */         
.* /*                                      IF SESSIDL EQUALS 0       */         
.* /* $Q2= P063278  HVT3303 881130 255471: IMPLEMENT SESSION         */         
.* /*                                      DEACTIVATION DELAYED      */         
.* /*                                      FUNCTION                  */         
.* /* $Q3= P063295  HVT3303 881217 991843: ALLOW REGISTER NOTATION   */         
.* /*                                      FOR SESSID AND SESSIDL    */         
.* /* $Q4= P064302  HVT3303 890113 255471: ALLOW REGISTER NOTATION   */         
.* /*                                      FOR DEACTYP AND ISSUE     */         
.* /*                                      ERROR MESSAGE IF INVALID  */         
.* /*                                      PARAMETER ISSUED FOR      */         
.* /*                                      QUALIFY=CONV              */         
.* /* $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION          */         
.* /* $T1= P097994  HATAPPN 911002 692313: DO NOT DEFAULT QUALIFY    */         
.* /* $T2= P098263  HATAPPN 911011 692313: Bad fix P097994           */         
.* /* $KF= S4N@@CLD HATAPPN 910326 692313: CP-CP SESSIONS            */         
.* /* $I1= FF@@@MST APPCFDX 920302 879315: APPC Full-Duplex support  */         
.* /* $01= OY55939  HATAPPN 921014 495139: INITIALIZATION OF REG15   */         
.* /* $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services     */         
.* /* $L2= KA@DR006 APPCRR  940713 576146: Vector Format             */         
.* /* $D1= ID@@@MST FPAPI   950316 143576: Fast Path API for APPC    */         
.* /*                                                                */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
.* A-000000-999999                                             @R495810         
.*                                                                              
         GBLB  &ISTGEN                                                          
         GBLB  &ISTRJ                                                           
         AIF   (&ISTRJ EQ 1).RJINVKW                                            
&ISTRJ   SETB  1                                                                
.*                                     CHECK INVALID APPCCMD       @F1A         
.RJINVKW ANOP                                                                   
         IST6MCUC REJECT,CONTROL,INVALKW=&INVALP1                  @I1C         
         IST6MCUC REJECT,CONTROL,INVALKW=&INVALP2                  @I1A         
.*                                     SET CONVID IN APPC AREA     @F1A         
.RJID    ANOP                                                                   
         AIF   ('&CONVID' EQ '').RJCGID                                         
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X        
               KW=CONVID                                                        
.*                                     SET CGID IN APPC AREA       @KFA         
.RJCGID  ANOP                                                                   
         AIF   ('&CGID' EQ '').RJSENSE                                          
         IST6MCUA KWVAL=&CGID,LOC=RPL6CGID-ISTRPL6X,BASEREG=14,        X        
               KW=CGID                                                          
.*                                     SET SENSE IN APPC AREA      @F1A         
.RJSENSE ANOP                                                                   
         AIF   ('&SENSE' EQ '').RJVECT                             @L1C         
         IST6MCUI KW=SENSE,KWVAL=&SENSE,LEN=4,                         X        
               LOC=RPL6SNSO-ISTRPL6X,BASEREG=14                                 
.RJVECT  ANOP                          ENSURE BOTH PARMS ARE CODED @L1A         
.*                                     OR BOTH ARE NOT CODED       @L1A         
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).RJVOUT    @D1C         
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).RJERVI     @D1C         
.*                                     STORE VECTOR AREA INPUT ADDRESS          
.*                                                                 @L1A         
         IST6MCUA KWVAL=&VTRINA,LOC=RPL6VAIA-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_ADDRESS                      @D1C         
.*                                     STORE VECTOR AREA INPUT LENGTH           
.*                                                                 @L1A         
         IST6MCUA KWVAL=&VTRINL,LOC=RPL6VAIL-ISTRPL6X,BASEREG=14,      X        
               KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2                 @D1C         
         AGO   .RJVOUT                                             @D1C         
.RJERVI  ANOP                                                      @L1A         
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C         
.RJVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED              
.*                                     OR BOTH ARE NOT CODED       @D1A         
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).RJCOMKW @D1A         
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).RJERVO   @D1A         
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X        
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT ADDRESS            
.*                                     IN APPC AREA                @D1A         
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X        
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A         
.*                                     PUT VECTOR AREA INPUT LENGTH             
.*                                     IN APPC AREA                @D1A         
         AGO   .RJCOMKW                                            @D1A         
.RJERVO  ANOP                                                      @D1A         
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A         
.*                                     CHECK CONMODE PARAMETER     @F1A         
.RJCOMKW ANOP                                                                   
         IST6MCUP OPTCD=&OPTCD,ECB=&ECB,EXIT=&EXIT,BRANCH=&BRANCH,     X        
               CKCMODE=NO,               DO NOT CHECK CONMODE PARAMETERX        
               CKSYN=YES,          ASY/SYN IS VALID FOR OPTCD PARAMETERX        
               CKBUFF=NO           BUFFLST/NBUFFLST IS INVALID FOR              
.*                                 OPTCD PARAMETER                              
.*                                     CHECK QUALIFY PARAMETER     @F1A         
.RJQUALI ANOP                                                                   
         AIF   ('&QUALIFY' EQ '').RJNQUAL                          @T1A         
         AIF   ('&QUALIFY' EQ 'CONV').RJCONV                       @T1A         
         AIF   ('&QUALIFY' EQ 'CONVGRP').RJCNGP                                 
.*                                     QUALIFY=CONVGRP             @KFA         
         AIF   ('&QUALIFY' EQ 'SESSION').RJSSION                                
         IST6MCER 5,QUALIFY,&QUALIFY   NOT VALID QUALIFY VALUE                  
.*                                     NOT CODE QUALIFY            @T2A         
.RJNQUAL ANOP                                                      @T2A         
         AIF   (NOT &ISTGEN).RJEXIT                                             
         AIF   (NOT '&SESSID' EQ '').RJSESID                       @T2A         
         AIF   (NOT '&SESSIDL' EQ '').RJSSIDL                      @T2A         
         AIF   (NOT '&DEACTYP' EQ '').RJTYPE                       @T2A         
         AGO   .RJREQ                                                           
.*                                     QUALIFY=CONV                @F1A         
.RJCONV  ANOP                                                                   
         AIF   (NOT &ISTGEN).RJEXIT                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6CONV                                   
         AIF   (NOT '&SESSID' EQ '').RJCSSID                       @Q4A         
         AIF   (NOT '&SESSIDL' EQ '').RJCSIDL                      @Q4A         
         AIF   (NOT '&DEACTYP' EQ '').RJCDETP                      @Q4A         
         AGO   .RJSETBL                                                         
.RJCNGP  ANOP                                                      @KFA         
         AIF   (NOT &ISTGEN).RJEXIT                                @KFA         
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6CNGP                      @KFA         
         AIF   (NOT '&SESSID' EQ '').RJCSSID                       @KFA         
         AIF   (NOT '&SESSIDL' EQ '').RJCSIDL                      @KFA         
         AIF   (NOT '&DEACTYP' EQ '').RJTYPE                       @KFA         
         AGO   .RJSETBL                                            @KFA         
.*                                     INVALID SESSID PARAMETER    @Q4A         
.RJCSSID ANOP                                                                   
         IST6MCER 6,SESSID,&QUALIFY,QUALIFY                                     
         AGO   .RJSETBL                                                         
.*                                     INVALID SESSIDL PARAMETER   @Q4A         
.RJCSIDL ANOP                                                                   
         IST6MCER 6,SESSIDL,&QUALIFY,QUALIFY                                    
         AGO   .RJSETBL                                                         
.*                                     INVALID DEACTYP PARAMETER   @Q4A         
.RJCDETP ANOP                                                                   
         IST6MCER 6,DEACTYP,&QUALIFY,QUALIFY                                    
         AGO   .RJSETBL                                                         
.*                                     QUALIFY=SESSION             @F1A         
.RJSSION ANOP                                                                   
         AIF   (NOT &ISTGEN).RJEXIT                                             
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6SESN                                   
.*                                     CHECK SESSID PARAMETER      @F1A         
.RJSESID ANOP                                                                   
         AIF   ('&SESSID' EQ '').RJTYPE                                         
         AIF   ('&SESSID'(K'&SESSID,1) EQ '''').RJERROR                         
         AGO   .RJSSIDL                                                         
.*                                     INVALID SESSID DATA TYPE    @F1A         
.RJERROR ANOP                                                                   
         IST6MCER 11,SESSID,&SESSID                                             
         AGO   .RJREQ                  DEFAULT TO RPL6 VALUES                   
.*                                     CHECK SESSIDL PARAMETER     @F1A         
.RJSSIDL ANOP                                                                   
         AIF   ('&SESSIDL' EQ '').RJTYPE                                        
         AIF   ('&SESSIDL'(K'&SESSIDL,1) EQ '''').RJLENER                       
         AIF   (NOT &ISTGEN).RJEXIT                                             
         AIF   ('&SESSIDL'(1,1) EQ '(').SLENGTH  REGISTER NOTATION              
.*                                     SPECIFIED                   @Q3A         
         SLR   15,15                   INITIALIZE REG15            @01A         
         IC    15,&SESSIDL                                         @Q1A         
         AGO   .SETSSL                                             @Q3A         
.*                                     OBTAIN LENGTH SPECIFIED     @Q3A         
.SLENGTH ANOP                                                      @Q3A         
         LR    15,&SESSIDL(1)                                      @Q3A         
.*                                     SET SESSIDL IN APPC AREA    @F1A         
.SETSSL  ANOP                                                      @Q3A         
         AIF   (NOT &ISTGEN).RJEXIT                                             
         STC   15,RPL6SIDL-ISTRPL6X(14)                                         
         LTR   15,15                   SESSIDL=0 ?                 @Q1A         
         BZ    *+10                                                @Q1A         
         AIF   ('&SESSID'(1,1) EQ '(').RJIREGA   REGISTER NOTATION              
.*                                     SPECIFIED                   @Q3A         
.*                                     SET SESSID IN APPC AREA     @F1A         
         BCTR  15,0                                                             
         EX    15,*+8                                                           
         B     *+10                                                             
         MVC   RPL6SSID-ISTRPL6X(1,14),&SESSID                     @Q1A         
         AGO   .RJTYPE                                             @Q2C         
.*                                     SET SESSID IN APPC AREA     @Q3A         
.RJIREGA ANOP                                                      @Q3A         
         AIF   (NOT &ISTGEN).RJEXIT                                             
         BCTR  15,0                                                @Q3M         
         EX    15,*+8                                              @Q3M         
         B     *+10                                                @Q3M         
         MVC   RPL6SSID-ISTRPL6X(1,14),0(&SESSID(1))               @Q3C         
         AGO   .RJTYPE                                             @Q2C         
.*                                     INVALID SESSIDL DATA TYPE   @F1A         
.RJLENER ANOP                                                                   
         IST6MCER 11,SESSIDL,&SESSIDL                                           
         AGO   .RJREQ                  DEFAULT TO RPL6 VALUES                   
.*                                     CHECK DEACTYP PARAMETER     @Q2A         
.RJTYPE  ANOP                                                                   
         AIF   ('&DEACTYP' EQ '').RJREQ                            @Q2A         
         AIF   ('&DEACTYP'(K'&DEACTYP,1) EQ '''').RJTYPER                       
         AIF   ('&DEACTYP'(1,1) EQ '(').RJTPREG  REGISTER NOTATION              
.*                                     SPECIFIED                   @Q4C         
.*                                     SET DEACTYP IN APPC AREA    @Q4A         
         AIF   (NOT &ISTGEN).RJEXIT                                             
         MVC   RPL6DETP-ISTRPL6X(1,14),&DEACTYP                                 
         AGO   .RJREQ                                                           
.*                                     SET DEACTYP IN APPC AREA    @Q4A         
.RJTPREG ANOP                                                                   
         AIF   (NOT &ISTGEN).RJEXIT                                             
         MVC   RPL6DETP-ISTRPL6X(1,14),0(&DEACTYP(1))                           
         AGO   .RJREQ                                                           
.*                                     INVALID DEACTYP DATA TYPE   @Q2A         
.RJTYPER ANOP                                                                   
         IST6MCER 11,DEACTYP,&DEACTYP                                           
         AGO   .RJREQ                  DEFAULT TO RPL6 VALUES                   
.*                                     ZERO OUT SESSID AND SESSIDL @F1A         
.RJSETBL ANOP                                                                   
         AIF   (NOT &ISTGEN).RJEXIT                                             
         XC    RPL6SSID-ISTRPL6X(8,14),RPL6SSID-ISTRPL6X(14)                    
         MVI   RPL6SIDL-ISTRPL6X(14),0                                          
.*                                     SET REJECT REQUEST CODE IN               
.*                                     APPC AREA                   @F1A         
.RJREQ   ANOP                                                                   
         MVI   RPL6REQ-ISTRPL6X(14),RPL6RJCT                                    
*                                                                               
.RJEXIT  MEND                                                                   
