         MACRO                                                                  
         IST6MCUI &KW=,&KWVAL=,&LOC=,&BASEREG=,&LEN=                            
.*                                                                              
.* /***** START OF SPECIFICATIONS ************************************/         
.* /*                                                                */         
.* /*  MACRO NAME: IST6MCUI                                          */         
.* /*                                                                */         
.* /*  DESCRIPTIVE NAME: IMMEDIATE DATA PARAMETER PROCESSOR          */         
.* /*                                                                */         
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */         
.* /*                                                                */         
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/         
.* /*                                                                */         
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1992.       */         
.* /*             ALL RIGHTS RESERVED.                               */         
.* /*                                                                */         
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */         
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */         
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */         
.* /*                                                                */         
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */         
.* /*                                                                */         
.* /*  FUNCTION:                                                     */         
.* /*      CALLED BY OTHER MACROS TO STORE A PARAMETER VALUE         */         
.* /*      IN APPROPRIATE LOCATION IN RPL OR APPC AREA. THE          */         
.* /*      PARAMETER VALUES ARE IMMEDIATE DATA SPECIFICATION.        */         
.* /*                                                                */         
.* /*  NOTES:                                                        */         
.* /*      KW -- CONTAIN THE PARAMETER NAME                          */         
.* /*      KWVAL -- CONTAIN THE PARAMETER VALUE                      */         
.* /*      LOC -- CONTAIN LOCATION IN APPC AREA FOR THE PARAMETER    */         
.* /*      LEN -- CONTAIN THE PARAMETER LENGTH                       */         
.* /*                                                                */         
.* /*  PROCESSOR: ASSEMBLER                                          */         
.* /*                                                                */         
.* /*  CREATED BY:                                                   */         
.* /*                                                                */         
.* /*  INTERNAL MACROS: IST6MCER                                     */         
.* /*                                                                */         
.* /*  EXTERNAL REFERENCES: NONE                                     */         
.* /*                                                                */         
.* /*  CHANGE ACTIVITY:                                              */         
.* /*    09/03/86 - ALLOW USERFLD IN REGISTER NOTATION       @P041406*/         
.* /*                                                                */         
.* /*  $MAC(IST6MCUI),COMP(API),PROD(VTAM): IMMEDIATE DATA PARAMETER */         
.* /*                                       BLOCK                    */         
.* /*                                                                */         
.* /*  FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS         */         
.* /*  ---- -------- ------- ------ ------  ----------------         */         
.* /*  $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION         */         
.* /*  $KF= NQNSTG2  NQNAME2 910711 007643: ENHANCED NQN SUPPORT (2) */         
.* /*                                                                */         
.* /***** END OF SPECIFICATIONS **************************************/         
.*                                                                              
.* A-000000-999999                                             @R495810         
.*                                                                              
         GBLB  &ISTUI                                                           
         GBLB  &ISTGEN                                                          
         LCLA  &KWLEN,&LLEN                                                     
         LCLC  &CHAR1,&CHAR2,&CHAR3,&CHAR                                       
         AIF   (&ISTUI EQ 1).UISTART                                            
*                                                                               
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                              
*                                                                               
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"               
*                                                                               
*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1992.                      
*             ALL RIGHTS RESERVED.                                              
*                                                                               
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                         
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY                      
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                          
*                                                                               
*             SEE COPYRIGHT INSTRUCTIONS.                                       
*                                                                               
&ISTUI   SETB  1                                                                
.UISTART ANOP                                                                   
         AIF   ('&KWVAL'(1,1) NE '(').CKDATA1                                   
.*                                      IF NON REGISTER FORM                    
.*                                      BRANCH TO CHECK DATA TYPE               
         AIF  ('&KW' EQ 'SENSE' OR '&KW' EQ 'USERFLD').REGFORM @P041406         
         IST6MCER 3,&KW                                                         
         MEXIT                                                                  
.REGFORM ANOP                                                                   
         AIF   (NOT &ISTGEN).EXIT                                               
         ST    &KWVAL(1),&LOC.(&BASEREG)                                        
         MNOTE '*                                            STORE &KW X        
               IN APPC AREA'                                                    
         MEXIT                                                                  
.CKDATA1 ANOP                                                                   
&KWLEN   SETA  K'&KWVAL                                                         
&LLEN    SETA  &LEN                                                             
.CKLM    AIF   ('&KW' NE 'LOGMODE').CKLN                                        
         AIF   ('&KWVAL' NE '0').CKLM1                                          
&CHAR    SETC  'XL8''0'''                                                       
         AGO   .MOVDATA                                                         
.CKLM1   AIF   ('&KWVAL' NE 'C''''').CKLM2                                      
&CHAR    SETC  'CL8'' '''                                                       
         AGO   .MOVDATA                                                         
.CKLM2   ANOP                                                                   
         AGO   .CKNAME                                                          
.CKLN    AIF   ('&KW' EQ 'NETID').CKNAME                           @KFA         
         AIF   ('&KW' NE 'LUNAME').CKDATA2                                      
.CKNAME  ANOP                                                                   
&CHAR    SETC  'CL8''&KWVAL'''                                                  
         AGO   .CKLEN                                                           
.CKDATA2 ANOP                                                                   
&CHAR1   SETC  '&KWVAL'(1,2)                                                    
&CHAR2   SETC  '&KWVAL'(2,&KWLEN-1)                                             
&CHAR3   SETC  '&KWVAL'(&KWLEN,1)                                               
&KWLEN   SETA  &KWLEN-3                                                         
         AIF   ('&KW' EQ 'SENSE').XTYPE                                         
         AIF   ('&CHAR1' EQ 'C''').CKAPOS                                       
         AIF   ('&CHAR1' EQ 'F''').CKAPOS                                       
         AIF   ('&CHAR1' EQ 'A(').CKPAREN                                       
         AIF   ('&CHAR1' EQ 'V(').CKPAREN                                       
.XTYPE   AIF   ('&CHAR1' NE 'X''').ERRTYPE                                      
&LLEN    SETA  &LEN*2                                                           
         AGO   .CKAPOS                                                          
.ERRTYPE IST6MCER 11,&KW                                                        
         MEXIT                                                                  
.CKAPOS  ANOP                                                                   
&CHAR1   SETC  '&CHAR1'(1,1)            GET TYPE ATTRIBUTE                      
         AIF   ('&CHAR3' NE '''').OUT   BR IF NOT BALANCED APOSTROPHE           
         AIF   ('&CHAR1' EQ 'F').CKPARE1   BR IF F-TYPE DATA                    
&CHAR    SETC  '&CHAR1.L&LEN&CHAR2'     MAKE UP DATA FIELD                      
         AGO   .CKLEN                                                           
.CKPAREN ANOP                                                                   
         AIF   ('&CHAR3' NE ')').OUT    BR IF NOT BALANCED PARENTHESES          
         AIF   (NOT &ISTGEN).EXIT                                               
.CKPARE1 ANOP                                                  @P030446         
         CNOP  0,4                                             @P030446         
&CHAR    SETC  '&KWVAL'                 DATA FIELD                              
         AGO   .MOVDATA                                                         
.CKLEN   ANOP                                                                   
         AIF   (&KWLEN LE &LLEN).MOVDATA                                        
         IST6MCER 10,&KW,&LEN                                                   
         MEXIT                                                                  
.MOVDATA ANOP                                                                   
         AIF   (NOT &ISTGEN).EXIT                                               
&LLEN    SETA  &LEN+4               DATA LENGTH PLUS INSTRUCTION LENGTH         
         B     *+&LLEN                                                          
         DC    &CHAR                                                            
         MVC   &LOC.(&LEN,&BASEREG),*-&LEN                                      
         MNOTE '*                                       STORE &KW IN APX        
               PC AREA'                                                         
         MEXIT                                                                  
.OUT     ANOP                                                                   
         IST6MCER 12,&KW                                                        
.EXIT    MEND                                                                   
